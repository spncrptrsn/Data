Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_OneShotPlayerTrigger(TRIGGERGUID_S_ARX_DoctorsHouse_Vault_VBTrig_75704d90-ce29-4e07-b460-e6a4845301e1);


//REGION

DB_AD_Dialog(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_01_Sign_5a0094b6-a740-423b-8045-fff175bde482,"ARX_DoctorsHouse_AD_Vault_Case_01_Sign");
DB_AD_Dialog(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_02_Sign_fabf6402-8d8f-4dae-8960-5d78e93dab39,"ARX_DoctorsHouse_AD_Vault_Case_02_Sign");
DB_AD_Dialog(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_03_Sign_ce9eb282-0e4f-40ed-926b-99960c902c3b,"ARX_DoctorsHouse_AD_Vault_Case_03_Sign");
DB_AD_Dialog(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_04_Sign_048d4914-42d1-4004-aa07-f18ee0090a8e,"ARX_DoctorsHouse_AD_Vault_Case_04_Sign");
DB_AD_Dialog(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_05_Sign_4b33fe92-62fd-494f-b9e8-12134f66eddc,"ARX_DoctorsHouse_AD_Vault_Case_05_Sign");
DB_AD_Dialog(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_06_Sign_c46138c0-1363-477e-8d39-e154ab626cf6,"ARX_DoctorsHouse_AD_Vault_Case_06_Sign");
DB_AD_Dialog(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_07_Sign_7c9fab17-dd54-4e38-bcb8-742b079326c8,"ARX_DoctorsHouse_AD_Vault_Case_07_Sign");
//END_REGION

//REGION
DB_ARX_TheDoctor_Vaults((ITEMGUID)ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_01_8faf44ca-6498-4540-9832-67d510092d1e,(TRIGGERGUID)TRIGGERGUID_S_ARX_DoctorsHouse_Vault_Case_01_Trig_3f6f2d3a-884c-4cf7-a663-3c90e7e0ed1f);
DB_ARX_TheDoctor_Vaults(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_02_60d643c4-5d08-4eb7-8c79-49538c76fd9f,TRIGGERGUID_S_ARX_DoctorsHouse_Vault_Case_02_Trig_ac7fbf57-26d0-4f79-9977-35c40bd8dbdf);
DB_ARX_TheDoctor_Vaults(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_03_9e57b27e-0284-4da2-982b-02d641af058a,TRIGGERGUID_S_ARX_DoctorsHouse_Vault_Case_03_Trig_7e1323c6-3961-4b85-aa25-53fbb42fba22);
DB_ARX_TheDoctor_Vaults(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_04_5adcec57-b366-42c0-b06e-e23bc372ee64,TRIGGERGUID_S_ARX_DoctorsHouse_Vault_Case_04_Trig_93f68b5c-0f8b-4885-a6df-daadfa0d7997);
DB_ARX_TheDoctor_Vaults(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_05_7dd3dc7b-c6d2-4169-8b6d-8766b48c4786,TRIGGERGUID_S_ARX_DoctorsHouse_Vault_Case_05_Trig_c0bac0db-9e04-4e6b-85df-b889d31d97e7);
//DB_ARX_TheDoctor_Vaults(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_06_277c274e-ca5b-40ce-8998-4431dd97b154,TRIGGERGUID_S_ARX_DoctorsHouse_Vault_Case_06_Trig_cdb02330-92bc-4811-92de-716681f921fd);
DB_ARX_TheDoctor_Vaults(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_07_9cd4196a-9976-4de8-a8d8-d51d11526574,TRIGGERGUID_S_ARX_DoctorsHouse_Vault_Case_07_Trig_2f35920b-d2c2-42c3-a1a6-e653015f322d);


//END_REGION

DB_ARX_TheDoctor_Vaults_UpdateOnInit((ITEMGUID)ITEMGUID_S_ARX_DoctorsHouse_Vault_Lute_77be3f7e-55fb-48d2-ba19-ea1aa1b86554);
DB_ARX_TheDoctor_Vaults_UpdateOnInit(ITEMGUID_S_ARX_DoctorsHouse_Vault_Tome_1b42fcc8-548b-4ca3-91f2-0005578ef60e);
DB_ARX_TheDoctor_Vaults_UpdateOnInit(ITEMGUID_S_ARX_DoctorsHouse_Vault_Skeleton_ed632b6c-4b90-40f7-9d45-9fb09d193c5f);
DB_ARX_TheDoctor_Vaults_UpdateOnInit(ITEMGUID_S_ARX_DoctorsHouse_Vault_Scroll_00499227-faf4-4b32-9a85-7c525237eaf7);
DB_ARX_TheDoctor_Vaults_UpdateOnInit(ITEMGUID_S_ARX_DoctorsHouse_Vault_Scroll_Pillow_89ba968b-afd7-4944-87d9-8a4c84f6ee28);
//DB_ARX_TheDoctor_Vaults_UpdateOnInit(ITEMGUID_S_ARX_DoctorsHouse_Vault_Figurine_1860b5bc-95e5-4766-9429-637e35abcd88);
DB_ARX_TheDoctor_Vaults_UpdateOnInit(ITEMGUID_S_ARX_DoctorsHouse_Vault_HeavyArms_c40572de-4945-4609-9b19-669546f8ac72);
DB_ARX_TheDoctor_Vaults_UpdateOnInit(ITEMGUID_S_ARX_DoctorsHouse_Vault_HeavyHelmet_f5446956-5830-4e40-8501-cae4c79d79d5);
DB_ARX_TheDoctor_Vaults_UpdateOnInit(ITEMGUID_S_ARX_DoctorsHouse_Vault_HeavyUpperbody_0793c526-3e27-48ef-b009-dce4010f0daa);
DB_ARX_TheDoctor_Vaults_UpdateOnInit(ITEMGUID_S_ARX_DoctorsHouse_Vault_MediumBoots_57b72a8c-3d82-4351-a3ab-0e4d17cf3252);
DB_ARX_TheDoctor_Vaults_UpdateOnInit(ITEMGUID_S_ARX_DoctorsHouse_Vault_MediumLowerbody_19f0a349-fced-42e5-b418-afe98103ed08);
KBSECTION
IF
DB_ARX_TheDoctor_Vaults(_,_Trig)
THEN
TriggerRegisterForItems(_Trig);

IF
DB_ARX_TheDoctor_Vaults_UpdateOnInit(_Item)
AND
DB_ARX_TheDoctor_Vaults(_Vault,_Trig)
AND
ObjectIsInTrigger(_Item,_Trig,1)
THEN
DB_ARX_TheDoctor_Vault_ItemsInTrigger(_Trig,_Item);
SetStoryEvent(_Vault,"vaultUpdate");

IF
ItemEnteredTrigger(_Item,_Trig,_)
AND
DB_ARX_TheDoctor_Vaults(_Vault,_Trig)
THEN
DB_ARX_TheDoctor_Vault_ItemsInTrigger(_Trig,_Item);
SetStoryEvent(_Vault,"vaultUpdate");

IF
ItemLeftTrigger(_Item,_Trig,_)
AND
DB_ARX_TheDoctor_Vaults(_Vault,_Trig)
THEN
ItemSetCanInteract(_Item,1);
NOT DB_ARX_TheDoctor_Vault_ItemsInTrigger(_Trig,_Item);
SetStoryEvent(_Vault,"vaultUpdate");

IF
ItemDestroyed(_Vault)
AND
DB_ARX_TheDoctor_Vaults(_Vault,_Trig)
THEN
SetStoryEvent(_Vault,"vaultUpdate");

IF
StoryEvent((ITEMGUID)_Item,"opened")
AND
DB_ARX_TheDoctor_Vaults(_Item,_)
THEN
SetStoryEvent(_Item,"vaultUpdate");

IF
StoryEvent((ITEMGUID)_Item,"closed")
AND
DB_ARX_TheDoctor_Vaults(_Item,_)
THEN
SetStoryEvent(_Item,"vaultUpdate");


IF
StoryEvent((ITEMGUID)_Item,"vaultUpdate")
AND
DB_ARX_TheDoctor_Vaults(_Item,_Trig)
THEN
Proc_ARX_TheDoctor_VaultUpdate(_Item,_Trig);


PROC //fallback
Proc_ARX_TheDoctor_VaultUpdate((ITEMGUID)_Item,(TRIGGERGUID)_Trig)
AND
DB_ARX_TheDoctor_Vault_ItemsInTrigger(_Trig,_Obj)
AND
ObjectIsInTrigger(_Obj,_Trig,0)
THEN
NOT DB_ARX_TheDoctor_Vault_ItemsInTrigger(_Trig,_Obj);
ItemSetCanInteract(_Obj,1);
DebugText(_Obj,"not in trig");

PROC
Proc_ARX_TheDoctor_VaultUpdate((ITEMGUID)_Item,(TRIGGERGUID)_Trig)
AND
ItemIsDestroyed(_Item,0)
AND
GetVarInteger(_Item,"IsOpen",0)
//ItemIsLocked(_Item,1)
AND
DB_ARX_TheDoctor_Vault_ItemsInTrigger(_Trig,_Obj)
THEN
ItemSetCanInteract(_Obj,0);
DebugText(_Obj,"0");

PROC
Proc_ARX_TheDoctor_VaultUpdate((ITEMGUID)_Item,(TRIGGERGUID)_Trig)
AND
ItemIsDestroyed(_Item,0)
AND
GetVarInteger(_Item,"IsOpen",1)
//ItemIsLocked(_Item,0)
AND
DB_ARX_TheDoctor_Vault_ItemsInTrigger(_Trig,_Obj)
THEN
ItemSetCanInteract(_Obj,1);
DebugText(_Obj,"1");

PROC
Proc_ARX_TheDoctor_VaultUpdate((ITEMGUID)_Item,(TRIGGERGUID)_Trig)
AND
ItemIsDestroyed(_Item,1)
AND
DB_ARX_TheDoctor_Vault_ItemsInTrigger(_Trig,_Obj)
THEN
ItemSetCanInteract(_Obj,1);
DebugText(_Obj,"1 destroyed");

//REGION
IF
CharacterUsedItemFailed(_Char,_item)
AND
DB_ARX_TheDoctor_Vaults(_Item,_)
AND
ItemIsLocked(_Item,1)
THEN
Proc_ARX_TheDoctor_VaultsVB(_Char,_Item);

PROC
Proc_ARX_TheDoctor_VaultsVB((CHARACTERGUID)_Char,(ITEMGUID)_Item)
THEN
ObjectClearFlag(_Char,"ARX_DoctorsHouse_VaultLocked_Paired");
ObjectClearFlag(_Char,"ARX_DoctorsHouse_VaultLocked_PermaLocked");


PROC
Proc_ARX_TheDoctor_VaultsVB((CHARACTERGUID)_Char,(ITEMGUID)_Item)
AND
GetVarInteger(_Item,"permaLocked",1)
THEN
ObjectSetFlag(_Char,"ARX_DoctorsHouse_VaultLocked_PermaLocked");

PROC
Proc_ARX_TheDoctor_VaultsVB((CHARACTERGUID)_Char,ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_04_5adcec57-b366-42c0-b06e-e23bc372ee64)
AND
GetVarInteger(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_04_5adcec57-b366-42c0-b06e-e23bc372ee64,"permaLocked",0)
THEN
ObjectSetFlag(_Char,"ARX_DoctorsHouse_VaultLocked_Paired");

PROC
Proc_ARX_TheDoctor_VaultsVB((CHARACTERGUID)_Char,ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_03_9e57b27e-0284-4da2-982b-02d641af058a)
AND
GetVarInteger(ITEMGUID_S_ARX_DoctorsHouse_Vault_Case_03_9e57b27e-0284-4da2-982b-02d641af058a,"permaLocked",0)
THEN
ObjectSetFlag(_Char,"ARX_DoctorsHouse_VaultLocked_Paired");

PROC
Proc_ARX_TheDoctor_VaultsVB((CHARACTERGUID)_Char,(ITEMGUID)_Item)
AND
QRY_SpeakerIsAvailable(_Char)
THEN
StartVoiceBark("ARX_DoctorsHouse_VB_VaultLocked",_Char);
DB_ARX_TheDoctor_VaultsVB(_Item,_Char);


IF
StoryEvent(_Item,"permaLockedSet")
AND
GetClosestAlivePlayer(_Item,_Char,_)
AND
CharacterCanSee(_Char,_Item,1)
AND
QueryOnlyOnce("ARX_VB_permaLockedSet")
THEN
StartVoiceBark("ARX_DoctorsHouse_VB_VaultPermaLocked",_Char);
//END_REGION

IF
GameBookInterfaceClosed(ITEMGUID_S_ARX_DoctorsHouse_Vault_Scroll_00499227-faf4-4b32-9a85-7c525237eaf7,_Char)
THEN
ApplyStatus(_Char,"FEAR",2.0,1);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ARX"
