Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_LucianStatue_f6ea9ac6-851d-c9f2-c82e-1867f4c3e461,"ARX_Cathedral_DivineTomb_PoB_LucianStatue");

DB_OneShot_VoiceBarkTrigger(TRIGGERGUID_S_ARX_Cathedral_DivineTomb_PoB_DivineStatueVB_Box_ec070412-568c-42d7-8f9b-04a76fb34c2b,"ARX_VB_Cathedral_DivineTomb_PoB_DivineStatue");
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_ARX_Cathedral_DivineTomb_PoB_Room_Box_6c22bc88-0738-4688-8cf6-5a68b7d2faa7);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_ARX_Cathedral_DivineTomb_PoB_CloseDoor_Poly_de035de8-bf1c-4ae8-bf8d-989815c9dc44);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_ARX_Cathedral_DivineTomb_PoB_Button_Box_13a913ae-5ce1-4eef-a72e-7474c571e31b);
Proc_ARX_DivineTomb_PoB_CheckResetLocks();


DB_ARX_Cathedral_DivineTomb_PoB_Amulets((ITEMGUID)ITEMGUID_S_ARX_CreepyCraftsman_SourceAmulet_f9c139b9-bb0f-4027-b8a4-806df5336fd2,"ARX_CreepyCraftsman_Has_SourceAmulet","ARX_DivineTomb_PoB_FullCreepyAmulet","ARX_DivineTomb_PoB_InsertedCreepyAmulet");
//DB_ARX_Cathedral_DivineTomb_PoB_Amulets(,"","ARX_DivineTomb_PoB_FullBraccusAmulet","ARX_DivineTomb_PoB_InsertedBraccusAmulet"); // DELETED

//--- Ghosts
DB_Ghosts(CHARACTERGUID_S_ARX_Cathedral_PoB_DeadPilgrim_000_5b80fb8f-278f-4a36-889e-222ab2b9aff8,CHARACTERGUID_S_ARX_Cathedral_PoB_DeadPilgrim_Ghost_000_6c18cafe-362e-4d68-b09e-4211c898d168,"ARX_Cathedral_DivineTomb_PoB_Ghost_000");
DB_Ghosts(CHARACTERGUID_S_ARX_Cathedral_PoB_DeadPilgrim_001_b90d4b4c-fd71-4ad1-93ca-a690cd08c151,CHARACTERGUID_S_ARX_Cathedral_PoB_DeadPilgrim_Ghost_001_a6b00f0a-d238-48d6-9f13-09619816c4da,"ARX_Cathedral_DivineTomb_PoB_Ghost_001");
DB_Ghosts(CHARACTERGUID_S_ARX_Cathedral_PoB_DeadPilgrim_002_c8070e30-430a-46a5-892f-c11ed841492b,CHARACTERGUID_S_ARX_Cathedral_PoB_DeadPilgrim_Ghost_002_2109e053-93ac-460c-aace-3634f8628674,"ARX_Cathedral_DivineTomb_PoB_Ghost_002");
DB_Ghosts(CHARACTERGUID_S_ARX_Cathedral_PoB_DeadPilgrim_003_3d98bd57-7a24-4905-8810-7b9ff4b8109a,CHARACTERGUID_S_ARX_Cathedral_PoB_DeadPilgrim_Ghost_003_09cd47d0-8b22-4d91-8dbc-343bf739f35e,"ARX_Cathedral_DivineTomb_PoB_Ghost_003");
DB_Ghosts(CHARACTERGUID_S_ARX_Cathedral_PoB_DeadPilgrim_004_4a586b2b-fe66-4a9b-867a-f29ee1be9faf,CHARACTERGUID_S_ARX_Cathedral_PoB_DeadPilgrim_Ghost_004_12f37b6d-5040-477e-ac18-4d19f9dc2d13,"ARX_Cathedral_DivineTomb_PoB_Ghost_004");

DB_ARX_PoB_PassingFlags("ARX_Cathedral_DivineTomb_PoB_PartyMemberAccepted");
DB_ARX_PoB_PassingFlags("ARX_DivineTomb_BypassedPathOfBlood");

DB_ARX_PoB_LearnTrickAmulet("ARX_CreepyCraftsman_ConvincedToRevealAmulet");
DB_ARX_PoB_LearnTrickAmulet("QuestUpdate_ARX_HuntingForDallis_LearnedFromArhu");
DB_ARX_PoB_LearnTrickAmulet("QuestUpdate_ARX_HuntingForDallis_LearnedFromArhusGhost");
KBSECTION
//REGION The Path of Blood - Door and hatch

//--- Initially, door is opened and hatch is locked
PROC
Proc_ARX_DivineTomb_PoB_CheckResetLocks()
AND
Qry_ARX_DivineTomb_PoB_AnyPurePlayerInPoB() //also open if there's any pure player in there
THEN
Proc_ARX_DivineTomb_PoB_ResetLocks();

PROC
Proc_ARX_DivineTomb_PoB_CheckResetLocks()
AND
NOT Qry_ARX_DivineTomb_PoB_AnyPurePlayerInPoB() 
AND
NOT Qry_ARX_DivineTomb_PoB_AnyAlivePlayerInPoB()
THEN
Proc_ARX_DivineTomb_PoB_ResetLocks();

PROC
Proc_ARX_DivineTomb_PoB_ResetLocks()
AND
ItemIsClosed(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Door_313635ac-5ab3-4906-8dcf-21296bf8ba04,1)
THEN
PlaySound(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Door_313635ac-5ab3-4906-8dcf-21296bf8ba04,"SE_ARX_Path_Of_Blood_Door_Open");

PROC
Proc_ARX_DivineTomb_PoB_ResetLocks()
THEN
ItemUnlockAndOpen(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Door_313635ac-5ab3-4906-8dcf-21296bf8ba04);

QRY
Qry_ARX_DivineTomb_PoB_AnyAlivePlayerInPoB()
AND
DB_IsPLayer(_Player)
AND
CharacterIsDead(_Player,0)
AND
DB_InRegion(_Player,TRIGGERGUID_S_ARX_Cathedral_DivineTomb_PoB_Room_Box_6c22bc88-0738-4688-8cf6-5a68b7d2faa7)
THEN
DB_NOOP(1);


QRY
Qry_ARX_DivineTomb_PoB_AnyPurePlayerInPoB()
AND
DB_IsPLayer(_Player)
AND
ObjectGetFlag(_Player,"ARX_Cathedral_DivineTomb_PoB_Accepted",1)
AND
DB_InRegion(_Player,TRIGGERGUID_S_ARX_Cathedral_DivineTomb_PoB_Room_Box_6c22bc88-0738-4688-8cf6-5a68b7d2faa7)
THEN
DB_NOOP(1);



//--- Close door behind player
IF
CharacterEnteredTrigger(_Char,TRIGGERGUID_S_ARX_Cathedral_DivineTomb_PoB_CloseDoor_Poly_de035de8-bf1c-4ae8-bf8d-989815c9dc44)
THEN
Proc_ARX_DivineTomb_PoB_CloseDoor((CHARACTERGUID)_Char);

IF
CharacterResurrected(_Char)
AND
DB_InRegion(_Char,TRIGGERGUID_S_ARX_Cathedral_DivineTomb_PoB_Room_Box_6c22bc88-0738-4688-8cf6-5a68b7d2faa7)
THEN
Proc_ARX_DivineTomb_PoB_CloseDoor(_Char);


PROC
Proc_ARX_DivineTomb_PoB_CloseDoor((CHARACTERGUID)_Char)
AND
NOT Qry_ARX_DivineTomb_PoB_AnyPurePlayerInPoB()
AND
ItemIsOpened(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Door_313635ac-5ab3-4906-8dcf-21296bf8ba04,1)
THEN
ItemCloseAndLock(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Door_313635ac-5ab3-4906-8dcf-21296bf8ba04,"NO_KEY");
PlaySound(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Door_313635ac-5ab3-4906-8dcf-21296bf8ba04,"SE_ARX_Path_Of_Blood_Door_Close");
Proc_ARX_DivineTomb_PoB_CloseDoorReaction((CHARACTERGUID)_Char);

PROC
Proc_ARX_DivineTomb_PoB_CloseDoorReaction((CHARACTERGUID)_Player)
AND
DB_IsPlayer(_Player)
THEN
PartySetFlag(_Player,"QuestUpdate_ARX_HuntingForDallis_EnterPoB");

PROC
Proc_ARX_DivineTomb_PoB_CloseDoorReaction((CHARACTERGUID)_Player)
AND
DB_IsPlayer(_Player)
AND
UserGetFlag(_Player,"ARX_Cathedral_DivineTomb_EnteredPathOfBlood",0)
THEN
UserSetFlag(_Player,"ARX_Cathedral_DivineTomb_EnteredPathOfBlood");
Proc_StartDialog(1,"ARX_AD_Cathedral_DivineTomb_PoB_DoorCloses",_Player);


//--- Reopening the door
IF
CharacterLeftTrigger(_Player,TRIGGERGUID_S_ARX_Cathedral_DivineTomb_PoB_Room_Box_6c22bc88-0738-4688-8cf6-5a68b7d2faa7)
THEN
Proc_ARX_DivineTomb_PoB_CheckResetLocks();

IF
CharacterDied(_Char)
AND
DB_InRegion(_Char,TRIGGERGUID_S_ARX_Cathedral_DivineTomb_PoB_Room_Box_6c22bc88-0738-4688-8cf6-5a68b7d2faa7)
THEN
Proc_ARX_DivineTomb_PoB_CheckResetLocks();


//--- Open hatch
IF
GlobalFlagSet("ARX_DivineTomb_PoB_UnlockHatch")
THEN
GlobalClearFlag("ARX_DivineTomb_PoB_UnlockHatch");
Proc_ARX_DivineTomb_PoB_OpenHatch();


IF
ObjectFlagSet("ARX_Catherdral_PoB_PlayCameraSpline",(CHARACTERGUID)_Char,_)
THEN
FadeToBlack(_Char,0.2,0,"ARX_Catherdral_PoB_PlayCameraSpline_Fade");
DB_ARX_Catherdral_PoB_PlayCameraSpline_Fade(_Char);

IF
FadeOutDone(_,"ARX_Catherdral_PoB_PlayCameraSpline_Fade")
AND
DB_ARX_Catherdral_PoB_PlayCameraSpline_Fade(_Char)
THEN
FadeToBlack(_Char,0.2,1,"");
StartCameraSpline(SPLINEGUID_S_ARX_Cathedral_DivineTomb_PoB_CameraSpline_feb38ac2-cf8f-4b0d-9692-ead2f44f83a3,_Char,0.0,0,1,0);
DB_ARX_Cathedral_DivineTomb_PoB_PlayerInCameraSpline(_Char);
NOT DB_ARX_Catherdral_PoB_PlayCameraSpline_Fade(_Char);

/* Event doesn't get triggered DOS2EE-4338
IF
StoryEvent((CHARACTERGUID)_Player,"ARX_Catherdral_PoB_PlayCameraSpline_Done")
THEN
FadeToBlack(_Player,0.2,0,"ARX_Catherdral_PoB_PlayCameraSpline_Done");

IF
FadeOutDone(_,"ARX_Catherdral_PoB_PlayCameraSpline_Done")
AND
DB_ARX_Cathedral_DivineTomb_PoB_PlayerInCameraSpline(_Player)
THEN
NOT DB_ARX_Cathedral_DivineTomb_PoB_PlayerInCameraSpline(_Player);
FadeToBlack(_Player,0.2,1,"");
*/

PROC
Proc_ARX_DivineTomb_PoB_OpenHatch()
THEN
ItemUnlockAndOpen(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Hatch_59a3160e-b322-4426-b244-6e586611ebe2);

IF
DialogEnded("ARX_Cathedral_DivineTomb_PoB_LucianStatue",_)
AND
DB_ARX_Cathedral_DivineTomb_PoB_PlayerInCameraSpline(_Player)
THEN
StopCameraSpline(SPLINEGUID_S_ARX_Cathedral_DivineTomb_PoB_CameraSpline_feb38ac2-cf8f-4b0d-9692-ead2f44f83a3,_Player);
NOT DB_ARX_Cathedral_DivineTomb_PoB_PlayerInCameraSpline(_Player);

PROC
Proc_ARX_DivineTomb_PoB_OpenHatch()
AND
QueryOnlyOnce("ARX_DT_PoB_RevealHatch")
THEN
SetStoryEvent(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Hatch_59a3160e-b322-4426-b244-6e586611ebe2,"StoryReveal");
PlaySound(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Hatch_59a3160e-b322-4426-b244-6e586611ebe2,"SE_ARX_Cathedral_DivineTomb_PoB_HatchButton_Hatch");
DB_ARX_Cathedral_DivineTomb_PoB_OpenedHatch(1);


//--- Block closing the door
PROC
ProcBlockUseOfItem(_Char,ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Door_313635ac-5ab3-4906-8dcf-21296bf8ba04)
AND
ItemIsOpened(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Door_313635ac-5ab3-4906-8dcf-21296bf8ba04,1)
THEN
DB_CustomUseItemResponse(_Char,ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Door_313635ac-5ab3-4906-8dcf-21296bf8ba04,0);
Proc_StartDialog(1,"ARX_AD_Cathedral_DivineTomb_PoB_BlockedDoor",_Char);


//END_REGION

//REGION The Path of Blood - Button & amulets (tricking PoB)

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_ARX_Cathedral_DivineTomb_PoB_Button_Box_13a913ae-5ce1-4eef-a72e-7474c571e31b)
AND
ObjectGetFlag(_Player,"ARX_CreepyCraftsman_PlayerKnowsCathedralSecret",1) //user flag
THEN
SetStoryEvent(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_HatchButton_37ce22bc-c3b2-4cf9-95da-ae9fd79ff909,"StoryReveal");
StartVoiceBark("ARX_VB_DivineTomb_PoB_RevealSecretButton",_Player);


IF
CharacterUsedItem(_Player,ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_HatchButton_37ce22bc-c3b2-4cf9-95da-ae9fd79ff909)
THEN
Proc_StartDialog(0,"ARX_Cathedral_DivineTomb_PoB_SecretButton",ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_HatchButton_37ce22bc-c3b2-4cf9-95da-ae9fd79ff909,_Player);


IF
DialogStarted("ARX_Cathedral_DivineTomb_PoB_SecretButton",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,_)
THEN
Proc_ARX_DivineTomb_PoB_SetAmuletChargesFlag((CHARACTERGUID)_Player);

PROC
Proc_ARX_DivineTomb_PoB_SetAmuletChargesFlag((CHARACTERGUID)_Player)
AND
DB_ARX_Cathedral_DivineTomb_PoB_Amulets(_Amulet,_HasFlag,_FullFlag,_)
AND
ObjectGetFlag(_Player,_HasFlag,1)
AND
ObjectGetFlag(_Amulet,"GLO_SourceAmuletIsFull",1)
THEN
ObjectSetFlag(_Player,_FullFlag);

IF
DialogEnded("ARX_Cathedral_DivineTomb_PoB_SecretButton",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,_)
AND
DB_ARX_Cathedral_DivineTomb_PoB_Amulets(_Amulet,_HasFlag,_FullFlag,_)
THEN
ObjectClearFlag(_Player,_FullFlag);

IF
DialogEnded("ARX_Cathedral_DivineTomb_PoB_SecretButton",_Inst)
THEN
ObjectClearFlag(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_HatchButton_37ce22bc-c3b2-4cf9-95da-ae9fd79ff909,"ARX_DivineTomb_PoB_HoldsEmptyAmulet");
ObjectClearFlag(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_HatchButton_37ce22bc-c3b2-4cf9-95da-ae9fd79ff909,"ARX_DivineTomb_PoB_HoldsFullAmulet");

IF
ObjectFlagSet(_InsertFlag,_Player,_)
AND
DB_ARX_Cathedral_DivineTomb_PoB_Amulets(_Amulet,_HasFlag,_FullFlag,_InsertFlag)
THEN
ItemDrop(_Amulet);
SetOnStage(_Amulet,0);
DB_ARX_Cathedral_DivineTomb_PoB_AmuletInMechanism(1);
ObjectClearFlag(_Player,_InsertFlag);
DB_ARX_DivineTomb_PoB_AmuletToDrop(_Amulet,_Player);

IF
ObjectFlagSet("ARX_DivineTomb_PoB_HoldsFullAmulet",ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_HatchButton_37ce22bc-c3b2-4cf9-95da-ae9fd79ff909,_)
THEN
PROC_LoopEffect("RS3_FX_GP_ScriptedEvent_ARX_PoB_HatchButton_01",ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_HatchButton_37ce22bc-c3b2-4cf9-95da-ae9fd79ff909,"ARX_DivineTomb_PoB_GlowingButton","__ANY__","");
DB_ARX_DivineTomb_PoB_ActiveEffect(1);

IF
ObjectFlagSet("ARX_Cathedral_DivineTomb_PoB_Accepted",_,_Inst)
AND
IsSpeakerReserved(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Hatch_59a3160e-b322-4426-b244-6e586611ebe2,0)
THEN
DialogAddActor(_Inst,ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Hatch_59a3160e-b322-4426-b244-6e586611ebe2);

IF
ObjectFlagSet("ARX_DivineTomb_BypassedPathOfBlood",_,_Inst)
AND
IsSpeakerReserved(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Hatch_59a3160e-b322-4426-b244-6e586611ebe2,0)
THEN
DialogAddActor(_Inst,ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_Hatch_59a3160e-b322-4426-b244-6e586611ebe2);

IF
GlobalFlagSet("ARX_DivineTomb_PoB_DrainAmulet")
THEN
PlayEffect(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_HatchButton_37ce22bc-c3b2-4cf9-95da-ae9fd79ff909,"RS3_FX_GP_ScriptedEvent_ARX_PoB_HatchButton_01_Consumed");
Proc_CameraShakeAroundCharacter(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_HatchButton_37ce22bc-c3b2-4cf9-95da-ae9fd79ff909,2,4.0);
ItemAddCharges(ITEMGUID_S_ARX_CreepyCraftsman_SourceAmulet_f9c139b9-bb0f-4027-b8a4-806df5336fd2,-5);
proc_SourceAmulet_AddSource(ITEMGUID_S_ARX_CreepyCraftsman_SourceAmulet_f9c139b9-bb0f-4027-b8a4-806df5336fd2,-5);

IF
GlobalFlagSet("ARX_DivineTomb_PoB_DrainAmulet")
AND
DB_ARX_DivineTomb_PoB_ActiveEffect(1)
THEN
NOT DB_ARX_DivineTomb_PoB_ActiveEffect(1);
PROC_StopLoopEffect(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_HatchButton_37ce22bc-c3b2-4cf9-95da-ae9fd79ff909,"ARX_DivineTomb_PoB_GlowingButton");

IF
ObjectFlagCleared("ARX_DivineTomb_PoB_HoldsFullAmulet",ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_HatchButton_37ce22bc-c3b2-4cf9-95da-ae9fd79ff909,_)
AND
DB_ARX_DivineTomb_PoB_ActiveEffect(1)
THEN
NOT DB_ARX_DivineTomb_PoB_ActiveEffect(1);
PROC_StopLoopEffect(ITEMGUID_S_ARX_Cathedral_DivineTomb_PoB_HatchButton_37ce22bc-c3b2-4cf9-95da-ae9fd79ff909,"ARX_DivineTomb_PoB_GlowingButton");


IF
DialogEnded("ARX_Cathedral_DivineTomb_PoB_SecretButton",_Inst)
AND
DB_ARX_DivineTomb_PoB_AmuletToDrop(_Amulet,_Player)
THEN
NOT DB_ARX_Cathedral_DivineTomb_PoB_AmuletInMechanism(1);
NOT DB_ARX_DivineTomb_PoB_AmuletToDrop(_Amulet,_Player);
ItemToInventory(_Amulet,_Player);

//END_REGION

//REGION The Path of Blood - Lucian Statue (rejected / accepted)

//--- REJECTED
IF
DialogEnded("ARX_Cathedral_DivineTomb_PoB_LucianStatue",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,_)
AND
ObjectGetFlag(_Player,"ARX_Cathedral_DivineTomb_PoB_Rejected",1)
THEN
PlayEffect(_Player,"RS3_FX_Skills_Voodoo_Target_Ground_01");
//CharacterSetHitpointsPercentage((CHARACTERGUID)_Player,2.0);
//ApplyStatus(_Player,"MUTED",-1.0,1);
//ApplyStatus(_Player,"PERMANENT_BLIND",-1.0,1);
//Proc_ARX_DivineTomb_PoB_CharacterRejected((CHARACTERGUID)_Player);
PartySetFlag((CHARACTERGUID)_Player,"QuestUpdate_ARX_HuntingForDallis_FailPoB");
CharacterDie(_Player,0,"DoT");


/*
PROC
Proc_ARX_DivineTomb_PoB_CharacterRejected((CHARACTERGUID)_Player)
AND
NOT DB_ARX_DivineTomb_PoB_OncePerCharacterRejected(_Player)
THEN
DB_ARX_DivineTomb_PoB_OncePerCharacterRejected(_Player);
//Proc_StartDialog(1,"ARX_AD_DivineTomb_PoB_Rejected",_Player);*/


//--- ACCEPTED
IF
DialogEnded("ARX_Cathedral_DivineTomb_PoB_LucianStatue",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,_)
AND
ObjectGetFlag(_Player,"ARX_Cathedral_DivineTomb_PoB_Accepted",1)
THEN
GlobalSetFlag("ARX_Cathedral_DivineTomb_PoB_WasPassed");
Proc_ARX_DivineTomb_PoB_CharacterAccepted((CHARACTERGUID)_Player);

PROC
Proc_ARX_DivineTomb_PoB_CharacterAccepted((CHARACTERGUID)_Player)
AND
NOT DB_ARX_PoB_IsPure(_Player)
THEN
DB_ARX_PoB_IsPure(_Player);
Proc_StartDialog(1,"ARX_AD_DivineTomb_PoB_Accepted",_Player);
PlayEffect(_Player,"RS3_FX_Skills_Divine_MassBless_Impact_01");
ApplyStatus(_Player,"PURE",-1.0,1);
CharacterRegisterCrime(_Player,"ARX_PassedPathOfBlood",NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,0);
Proc_ARX_DivineTomb_PoB_CheckResetLocks();


PROC
Proc_ARX_DivineTomb_PoB_CharacterAccepted((CHARACTERGUID)_Player)
THEN
PartySetFlag(_Player,"ARX_Cathedral_DivineTomb_PoB_PartyMemberAccepted");


PROC
Proc_ARX_DivineTomb_PoB_CharacterAccepted((CHARACTERGUID)_Player)
AND
NOT DB_ARX_Cathedral_DivineTomb_PoB_OpenedHatch(1)
THEN
Proc_ARX_DivineTomb_PoB_OpenHatch();


IF
ObjectFlagSet("ARX_Cathedral_PoB_AcceptedOrCheated",_,_)
THEN
Proc_ARX_DivineTomb_PoB_OpenHatch();



// Generic Passing flag
IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Player,_)
AND
DB_ARX_PoB_PassingFlags(_Flag)
THEN
PartySetFlag(_Player,"ARX_PoB_PassedOrCheated");

//END_REGION

//REGION Debug

IF
TextEventSet("PoB_PassLegit")
AND
CharacterGetHostCharacter(_Player)
THEN
ObjectSetFlag(_Player,"ARX_Cathedral_DivineTomb_PoB_Accepted");
GlobalSetFlag("ARX_Cathedral_DivineTomb_PoB_WasPassed");
Proc_ARX_DivineTomb_PoB_CharacterAccepted((CHARACTERGUID)_Player);


IF
TextEventSet("PoB_GiveItems")
AND
DB_IsPlayer(_Player)
THEN
UserSetFlag(_Player,"ARX_CreepyCraftsman_PlayerKnowsCathedralSecret");
ItemToInventory(ITEMGUID_S_ARX_CreepyCraftsman_SourceAmulet_f9c139b9-bb0f-4027-b8a4-806df5336fd2,_Player);
ItemToInventory(ITEMGUID_S_ARX_CreepyCraftsman_ScrollOfAtonment_b5a89b75-bbef-44f1-b22f-148a1b9d13ab,_Player);
Proc_StartDialog(1,"GLO_AD_SourceAmulet_IsFull",_Player);
ProcReplenishedAmulet(_Player);

IF
TextEventSet("PoB_GiveItems")
THEN
ItemAddCharges(ITEMGUID_S_ARX_CreepyCraftsman_SourceAmulet_f9c139b9-bb0f-4027-b8a4-806df5336fd2,5);
ObjectSetFlag(ITEMGUID_S_ARX_CreepyCraftsman_SourceAmulet_f9c139b9-bb0f-4027-b8a4-806df5336fd2,"GLO_SourceAmuletIsFull");

IF
TextEventSet("PoB_CheckFlags")
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"GLO_StoleItem",1)
THEN
DebugText(_Player,"Steal - FAILED");

IF
TextEventSet("PoB_CheckFlags")
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"GLO_StoleItem",0)
THEN
DebugText(_Player,"Steal - PASSED");

IF
TextEventSet("PoB_CheckFlags")
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"GLO_PathOfBlood_MurderedInnocent",1)
THEN
DebugText(_Player,"Kill - FAILED");

IF
TextEventSet("PoB_CheckFlags")
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"GLO_PathOfBlood_MurderedInnocent",0)
THEN
DebugText(_Player,"Kill - PASSED");

IF
TextEventSet("PoB_CheckFlags")
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"GLO_Promise_PromiseBreaker",1)
THEN
DebugText(_Player,"Void - FAILED");

IF
TextEventSet("PoB_CheckFlags")
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"GLO_Promise_PromiseBreaker",0)
THEN
DebugText(_Player,"Void - PASSED");

IF
TextEventSet("PoB_CheckFlags")
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player,"PROMISED",1)
THEN
DebugText(_Player,"Void - FAILED");

IF
TextEventSet("PoB_CheckFlags")
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player,"PROMISED",0)
THEN
DebugText(_Player,"Void - PASSED");

IF
TextEventSet("PoB_CheckFlags")
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"GLO_PathOfBlood_DisrespectedSoul",0)
THEN
DebugText(_Player,"Soul - PASSED");

IF
TextEventSet("PoB_CheckFlags")
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"GLO_PathOfBlood_DisrespectedSoul",1)
THEN
DebugText(_Player,"Soul - FAILED");


IF
TextEventSet("PoB_ResetAll")
AND
DB_IsPlayer(_Player)
THEN
ObjectClearFlag(_Player,"GLO_StoleItem");
ObjectClearFlag(_Player,"GLO_PathOfBlood_MurderedInnocent");
ObjectClearFlag(_Player,"GLO_Promise_PromiseBreaker");
ClearTag(_Player,"PROMISED");
ObjectClearFlag(_Player,"GLO_PathOfBlood_DisrespectedSoul");

//END_REGION

//REGION Journal

IF
GameBookInterfaceClosed(ITEMGUID_S_ARX_CreepyCraftsman_Book_SourceAmuletManual_000_6485696d-bad0-4965-9b40-431ff56040d5,_Player)
THEN
ObjectSetFlag(_Player,"ARX_HuntingForDallis_ReadSourceAmuletManual");

IF
GameBookInterfaceClosed(ITEMGUID_S_ARX_Cathedral_Book_SourceAmuletManual_000_8866aca0-91f9-440a-abcd-0442576d2777,_Player)
THEN
ObjectSetFlag(_Player,"ARX_HuntingForDallis_ReadSourceAmuletManual");

IF
ObjectFlagSet("ARX_HuntingForDallis_ReadSourceAmuletManual",(CHARACTERGUID)_Player,_)
AND
ItemTemplateIsInPartyInventory(_Player,"QUEST_ARX_SourceAmulet_86c59384-3686-4594-b485-507caed669a5",0,_Count)
AND
_Count > 0
AND
DB_ARX_PoB_LearnTrickAmulet(_Flag)
AND
PartyGetFlag(_Player,_Flag,1)
THEN
PartySetFlag(_Player,"QuestUpdate_ARX_HuntingForDallis_SourceAmulet");

IF
ItemTemplateAddedToCharacter(QUEST_ARX_SourceAmulet_86c59384-3686-4594-b485-507caed669a5,_,_Player)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_ARX_HuntingForDallis_SourceAmulet",0)
AND
PartyGetFlag(_Player,"ARX_HuntingForDallis_ReadSourceAmuletManual",1)
AND
DB_ARX_PoB_LearnTrickAmulet(_Flag)
AND
ObjectGetFlag(_Player,_Flag,1)
THEN
PartySetFlag(_Player,"QuestUpdate_ARX_HuntingForDallis_SourceAmulet");

IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Player,_)
AND
DB_IsPlayer(_Player)
AND
DB_ARX_PoB_LearnTrickAmulet(_Flag)
AND
ObjectGetFlag(_Player,"QuestUpdate_ARX_HuntingForDallis_SourceAmulet",0)
AND
ItemTemplateIsInPartyInventory(_Player,"QUEST_ARX_SourceAmulet_86c59384-3686-4594-b485-507caed669a5",0,_Count)
AND
_Count > 0
THEN
PartySetFlag(_Player,"QuestUpdate_ARX_HuntingForDallis_SourceAmulet");

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ARX"
