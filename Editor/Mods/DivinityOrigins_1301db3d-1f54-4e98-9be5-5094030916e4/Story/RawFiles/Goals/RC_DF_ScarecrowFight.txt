Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTERGUID_S_RC_DF_Scarecrow_3bda7938-578f-49fc-a655-50b1b24db065,"RC_DF_Scarecrow");

DB_RC_DF_ScarecrowFight_Scarecrows((CHARACTERGUID)CHARACTERGUID_S_RC_DF_Scarecrow_000_88c4581e-30c9-4dc7-a7a8-eec8244e7531,(ITEMGUID)ITEMGUID_S_RC_DF_Scarecrow_Item_000_dbd8706c-0096-4655-9003-d57706a09fe1);
DB_RC_DF_ScarecrowFight_Scarecrows((CHARACTERGUID)CHARACTERGUID_S_RC_DF_Scarecrow_001_6091c03c-1ca2-474b-8f24-9cb0a1fa0b2c,(ITEMGUID)ITEMGUID_S_RC_DF_Scarecrow_Item_001_147c894e-038b-49cd-aef0-63a42ba5712c);
DB_RC_DF_ScarecrowFight_Scarecrows((CHARACTERGUID)CHARACTERGUID_S_RC_DF_Scarecrow_002_8721af9f-91c3-4b4f-a232-a021f96725c1,(ITEMGUID)ITEMGUID_S_RC_DF_Scarecrow_Item_002_62a0bd3c-6e4d-4d36-91e5-efb05cbb082e);
DB_RC_DF_ScarecrowFight_Scarecrows((CHARACTERGUID)CHARACTERGUID_S_RC_DF_Scarecrow_003_3493ef82-7bf6-45fb-a320-87e1466d83c9,(ITEMGUID)ITEMGUID_S_RC_DF_Scarecrow_Item_003_bcb26cba-a179-403b-bc24-5c6f8d1114c6);

DB_KillCounter_Internal((CHARACTERGUID)CHARACTERGUID_S_RC_DF_Scarecrow_000_88c4581e-30c9-4dc7-a7a8-eec8244e7531,"RC_DF_ScarecrowFight");
DB_KillCounter_Internal((CHARACTERGUID)CHARACTERGUID_S_RC_DF_Scarecrow_001_6091c03c-1ca2-474b-8f24-9cb0a1fa0b2c,"RC_DF_ScarecrowFight");
DB_KillCounter_Internal((CHARACTERGUID)CHARACTERGUID_S_RC_DF_Scarecrow_002_8721af9f-91c3-4b4f-a232-a021f96725c1,"RC_DF_ScarecrowFight");
DB_KillCounter_Internal((CHARACTERGUID)CHARACTERGUID_S_RC_DF_Scarecrow_003_3493ef82-7bf6-45fb-a320-87e1466d83c9,"RC_DF_ScarecrowFight");
DB_KillCounter_Internal((CHARACTERGUID)CHARACTERGUID_S_RC_DF_Scarecrow_3bda7938-578f-49fc-a655-50b1b24db065,"RC_DF_ScarecrowFight");
DB_KillCounter("RC_DF_ScarecrowFight",5);

CharacterSetAnimationSetOverride(CHARACTERGUID_S_RC_DF_Scarecrow_3bda7938-578f-49fc-a655-50b1b24db065,"Totem");

Proc_RC_DF_AddCrittersCrow();
KBSECTION
//REGION Init

IF
DB_RC_DF_ScarecrowFight_Scarecrows(_Scarecrow,_)
THEN
SetOnStage(_Scarecrow,0);


IF
ItemDestroyed(_Item)
AND
DB_RC_DF_ScarecrowFight_Scarecrows(_Scarecrow,_Item)
THEN
NOT DB_KillCounter_Internal(_Scarecrow,"RC_DF_ScarecrowFight");
NOT DB_RC_DF_ScarecrowFight_Scarecrows(_Scarecrow,_Item);
ProcForceStopDialog(CHARACTERGUID_S_RC_DF_Scarecrow_3bda7938-578f-49fc-a655-50b1b24db065);
Proc_RC_DF_ScarecrowFight_Start();

//END_REGION


//REGION AD

IF
CharacterCharacterEvent(CHARACTERGUID_S_RC_DF_Scarecrow_3bda7938-578f-49fc-a655-50b1b24db065,_Player,"RC_DF_ScarecrowSpottedPlayer")
THEN
Proc_StartDialog(1,"RC_DF_AD_ScarecrowCall",CHARACTERGUID_S_RC_DF_Scarecrow_3bda7938-578f-49fc-a655-50b1b24db065,_Player);

//END_REGION


IF
ObjectFlagSet("RC_DF_Scarecrow_SetSleep",_Player,_)
THEN
ApplyStatus(_Player,"SLEEPING",-1.0,1);


IF
ObjectFlagSet("RC_DF_Scarecrow_RemoveSleep",_Player,_)
AND
HasAppliedStatus(_Player,"SLEEPING",1)
THEN
RemoveStatus(_Player,"SLEEPING");


//REGION Start fight

//--- Sleep
IF
DialogEnded("RC_DF_Scarecrow",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,_)
AND
ObjectGetFlag(_Player,"RC_DF_Scarecrow_Succumbed",1)
THEN
CharacterDie((CHARACTERGUID)_Player,0,"Physical");


//--- Triggers
IF
CharacterReceivedDamage(CHARACTERGUID_S_RC_DF_Scarecrow_3bda7938-578f-49fc-a655-50b1b24db065, _, _)
THEN
Proc_RC_DF_ScarecrowFight_Start();


IF
DialogEnded("RC_DF_Scarecrow",_)
AND
GlobalGetFlag("RC_DF_ScarecrowFight_Started",0)
THEN
Proc_RC_DF_ScarecrowFight_Start();

PROC
Proc_RC_DF_ScarecrowFight_Start()
AND
GlobalGetFlag("RC_DF_ScarecrowFight_Started",0) 
AND
DB_RC_DF_ScarecrowFight_Scarecrows(_Scarecrow,_)
AND
GetPosition(_Scarecrow,_X,_Y,_Z)
THEN
PlayEffect(_Scarecrow,"RS3_FX_Char_Animals_Deer_A_VoidTransform_Overlays_01");
PlayEffectAtPosition("RS3_FX_Char_Animals_Deer_A_VoidTransform_01",_X,_Y,_Z);
ProcObjectTimer(_Scarecrow,"RC_DF_Scarecrow_SpawnAnimals",1100);

PROC
ProcObjectTimerFinished(_Scarecrow,"RC_DF_Scarecrow_SpawnAnimals")
AND
DB_RC_DF_ScarecrowFight_Scarecrows((CHARACTERGUID)_Scarecrow,_Item)
THEN
Poof(_Item);
CharacterAppear((CHARACTERGUID)_Scarecrow,1,"");

PROC
Proc_RC_DF_ScarecrowFight_Start()
AND
GlobalGetFlag("RC_DF_ScarecrowFight_Started",0)
THEN
NOT DB_RC_DF_ScarecrowFight_StartAfterDialog(1);
GlobalSetFlag("RC_DF_ScarecrowFight_Started");
CharacterSetCustomName(CHARACTERGUID_S_RC_DF_Scarecrow_3bda7938-578f-49fc-a655-50b1b24db065,"RC_DF_UndeadScarecrow");
SetFaction(CHARACTERGUID_S_RC_DF_Scarecrow_3bda7938-578f-49fc-a655-50b1b24db065,"Evil NPC");
ApplyStatus(CHARACTERGUID_S_RC_DF_Scarecrow_3bda7938-578f-49fc-a655-50b1b24db065,"QUEST_FEAR_AURA",-1.0,1);
CharacterSetAnimationSetOverride(CHARACTERGUID_S_RC_DF_Scarecrow_3bda7938-578f-49fc-a655-50b1b24db065,"");

//END_REGION


//REGION Fight end

PROC
ReactOnKillCounter("RC_DF_ScarecrowFight")
AND
DB_IsPlayer(_Player)
AND
CharacterCanSee(_Player,CHARACTERGUID_S_RC_DF_Scarecrow_3bda7938-578f-49fc-a655-50b1b24db065,1)
AND
QRY_SpeakerIsAvailable(_Player)
AND
QueryOnlyOnce("RC_DF_VB_ScarecrowFight_FightEnded")
THEN
StartVoiceBark("RC_DF_VB_ScarecrowFight_FightEnded",_Player);


//END_REGION


//REGION Crow critters

PROC
Proc_RC_DF_AddCrittersCrow()
THEN
DB_RC_DF_CritterCrow((ITEMGUID)ITEMGUID_S_RC_DF_Critters_Crow_B_002_d9c43a55-c884-4f70-8eb8-9fdcf9c6399c);
DB_RC_DF_CritterCrow((ITEMGUID)ITEMGUID_S_RC_DF_Critters_Crow_B_003_355a6027-0ce0-4434-9dbc-dfeab947f39a);
DB_RC_DF_CritterCrow((ITEMGUID)ITEMGUID_S_RC_DF_Critters_Crow_B_004_78c7fddb-aade-405d-97e2-eedcb38705d0);
DB_RC_DF_CritterCrow((ITEMGUID)ITEMGUID_S_RC_DF_Critters_Crow_B_005_fb2e9ad1-f6c0-4482-aba6-e5ed9a235fc8);
DB_RC_DF_CritterCrow((ITEMGUID)ITEMGUID_S_RC_DF_Critters_Crow_B_006_874d7492-c92d-485f-a28b-d0386045a40c);
DB_RC_DF_CritterCrow((ITEMGUID)ITEMGUID_S_RC_DF_Critters_Crow_B_007_c152c0c8-9f66-4ad6-b3a3-d36aacce6fea);
DB_RC_DF_CritterCrow((ITEMGUID)ITEMGUID_S_RC_DF_Critters_Crow_B_008_f993a938-2dd9-445b-b686-d7061ffc6a3f);
DB_RC_DF_CritterCrow((ITEMGUID)ITEMGUID_S_RC_DF_Critters_Crow_B_009_39aa4643-36bf-4683-909d-707d9b03b851);

IF
GlobalFlagSet("RC_DF_ScarecrowFight_Started")
AND
DB_RC_DF_CritterCrow(_Crow)
THEN
SetStoryEvent(_Crow,"GLO_FleeingItem_DisableReactions");
Proc_RC_DF_MakeCrittersCrowFlee(_Crow);


PROC
Proc_RC_DF_MakeCrittersCrowFlee((ITEMGUID)_Crow)
AND
GetVarInteger(_Crow,"State",1)
THEN
PlayAnimation(_Crow,"open","RC_DF_CritterCrowFled");

PROC
Proc_RC_DF_MakeCrittersCrowFlee((ITEMGUID)_Crow)
AND
GetVarInteger(_Crow,"State",0)
THEN
SetOnStage(_Crow,0);

IF
StoryEvent(_Crow,"RC_DF_CritterCrowFled")
THEN
SetOnStage(_Crow,0);


//END_REGION


PROC
ProcRegionEnded("RC_Main")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
