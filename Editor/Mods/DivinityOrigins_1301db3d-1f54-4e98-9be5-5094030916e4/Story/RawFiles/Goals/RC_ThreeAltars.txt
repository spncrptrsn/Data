Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs((ITEMGUID)ITEMGUID_S_RC_DF_ThreeAltars_BeetleAltar_fc34069e-e4c3-403d-9e27-6361a86b4a4e,"RC_ThreeAltars");
DB_Dialogs((ITEMGUID)ITEMGUID_S_RC_MIL_ThreeAltars_DeerAltar_ade1c767-9901-455d-a1e1-4c99e30710e4,"RC_ThreeAltars");
DB_Dialogs((ITEMGUID)ITEMGUID_S_RC_BF_ThreeAltars_WolfAltar_0b4505eb-7e2b-40d6-b6c3-eed66f8de0ea,"RC_ThreeAltars");

DB_RC_ThreeAltars((ITEMGUID)ITEMGUID_S_RC_DF_ThreeAltars_BeetleAltar_fc34069e-e4c3-403d-9e27-6361a86b4a4e,"RC_ThreeAltars_VisitedBeetle");
DB_RC_ThreeAltars((ITEMGUID)ITEMGUID_S_RC_MIL_ThreeAltars_DeerAltar_ade1c767-9901-455d-a1e1-4c99e30710e4,"RC_ThreeAltars_VisitedDeer");
DB_RC_ThreeAltars((ITEMGUID)ITEMGUID_S_RC_BF_ThreeAltars_WolfAltar_0b4505eb-7e2b-40d6-b6c3-eed66f8de0ea,"RC_ThreeAltars_VisitedWolf");
Proc_RC_ThreeAltars_SetUpAltars();


//--- Altar enemies
// Wolf
DB_Dialogs(CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466,"RC_BF_ThreeAltars_Wolf");
DB_DoNotFace(CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466);
DB_RC_BF_Altar_Wolf((CHARACTERGUID)CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466);
DB_RC_BF_Altar_Wolf((CHARACTERGUID)CHARACTERGUID_S_RC_BF_Altar_Wolf_Black_000_d3f23369-9e96-47f8-bf1c-4b2307537552);
DB_RC_BF_Altar_Wolf((CHARACTERGUID)CHARACTERGUID_S_RC_BF_Altar_Wolf_Black_001_0d22cb95-db90-4678-b6c7-ce528ed672d1);
DB_RC_BF_Altar_Wolf((CHARACTERGUID)CHARACTERGUID_S_RC_BF_Altar_Wolf_Black_002_8a34ce03-d69f-46d9-aa6b-b8d31b022d6a);
DB_RC_BF_Altar_Wolf((CHARACTERGUID)CHARACTERGUID_S_RC_BF_Altar_Wolf_Black_003_3f7d0a8e-be6b-4259-9d98-95b3dc29c7a0);
DB_RC_BF_Altar_Wolf((CHARACTERGUID)CHARACTERGUID_S_RC_BF_Altar_Wolf_Black_004_0fea5dc4-2b31-426c-adbf-f1eaaf4ef588);
DB_KillCounter("RC_ThreeAltars_WolfFight",6);
DB_CrimeWarningIsAD("RC_BF_AD_ThreeAltars_WolfAttack");
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_RC_BF_ThreeAltars_SpotWolf_Box_f37f1de1-9b38-49f0-99ba-908f301192ac);

// Deer
DB_RC_MIL_Altar_Deer((CHARACTERGUID)CHARACTERGUID_S_RC_MIL_Altar_Deer_Void_000_7cce0a94-e072-4f19-9c04-0eaf2cdd1358,"");
DB_RC_MIL_Altar_Deer((CHARACTERGUID)CHARACTERGUID_S_RC_MIL_Altar_Deer_000_6007bb7b-9d8c-49ba-a431-47ce82387fc1,"RC_MIL_ThreeAltars_SavedDeers");
DB_RC_MIL_Altar_Deer((CHARACTERGUID)CHARACTERGUID_S_RC_MIL_Altar_Deer_001_a4002c4f-eb36-4e8d-9c73-ee1d2778897e,"RC_MIL_ThreeAltars_SavedDeers_001");
DB_RC_MIL_Altar_Deer((CHARACTERGUID)CHARACTERGUID_S_RC_MIL_Altar_Deer_002_ff53f004-dbaa-4343-bdf8-e1366fce5ef6,"RC_MIL_ThreeAltars_SavedDeers_002");
DB_KillCounter("RC_ThreeAltars_DeerFight",4);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_RC_MIL_ThreeAltars_DeerDialog_Box_8730a786-0a8d-4b2f-9be3-bc22bc1220c1);
KBSECTION
//REGION Debug & init

IF
TextEventSet("KillAltarWolf")
AND
DB_RC_BF_Altar_Wolf(_Wolf)
AND
NOT DB_Dead(_Wolf)
THEN
CharacterDie(_Wolf,1,"Physical");

IF
TextEventSet("KillAltarDeer")
AND
DB_RC_MIL_Altar_Deer(_Deer,_)
AND
NOT DB_Dead(_Deer)
THEN
CharacterDie(_Deer,1,"Physical");

IF
TextEventSet("BecameZombieChicken")
AND
DB_IsPlayer(_Player)
THEN
SetTag(_Player,"PROMISED");
ObjectSetFlag(_Player,"GLO_Promise_BecameZombieChicken");


PROC
Proc_RC_ThreeAltars_SetUpAltars()
THEN
ObjectSetFlag(ITEMGUID_RC_DF_ThreeAltars_BeetleAltar_fc34069e-e4c3-403d-9e27-6361a86b4a4e,"RC_ThreeAltars_BeetleAltar");
ObjectSetFlag(ITEMGUID_RC_MIL_ThreeAltars_DeerAltar_ade1c767-9901-455d-a1e1-4c99e30710e4,"RC_ThreeAltars_DeerAltar");
ObjectSetFlag(ITEMGUID_RC_BF_ThreeAltars_WolfAltar_0b4505eb-7e2b-40d6-b6c3-eed66f8de0ea,"RC_ThreeAltars_WolfAltar");
//ItemSetCanInteract(ITEMGUID_RC_DF_ThreeAltars_BeetleAltar_fc34069e-e4c3-403d-9e27-6361a86b4a4e,0);
//ItemSetCanInteract(ITEMGUID_RC_MIL_ThreeAltars_DeerAltar_ade1c767-9901-455d-a1e1-4c99e30710e4,0);
//ItemSetCanInteract(ITEMGUID_RC_BF_ThreeAltars_WolfAltar_0b4505eb-7e2b-40d6-b6c3-eed66f8de0ea,0);


//--- Wolf DB setup
IF
DB_RC_BF_Altar_Wolf(_Wolf)
THEN
DB_KillCounter_Internal(_Wolf,"RC_ThreeAltars_WolfFight");
DB_DontCreateMurder(_Wolf);

IF
DB_RC_BF_Altar_Wolf(_Wolf)
AND
_Wolf != CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466
THEN
SetOnStage(_Wolf,0);


//--- Deer DB setup
PROC
Proc_RC_MIL_ThreeAltars_DeerCombat_SetUp()
AND
QueryOnlyOnce("RC_MIL_ThreeAltars_DeerCombat_SetUp")
AND
DB_RC_MIL_Altar_Deer(_Deer,_)
THEN
DB_KillCounter_Internal(_Deer,"RC_ThreeAltars_DeerFight");
DB_KillCounter("RC_ThreeAltars_DeerFight",4);
DB_DontCreateMurder(_Deer);
Proc_RC_MIL_ThreeAltars_DeerCombat_SetUpTransform(_Deer);

PROC
Proc_RC_MIL_ThreeAltars_DeerCombat_SetUpTransform((CHARACTERGUID)_Deer)
AND
_Deer != CHARACTERGUID_S_RC_MIL_Altar_Deer_Void_000_7cce0a94-e072-4f19-9c04-0eaf2cdd1358
AND
GetPosition(_Deer,_X,_Y,_Z)
THEN
PlayEffect(_Deer,"RS3_FX_Char_Animals_Deer_A_VoidTransform_Overlays_01");
PlayEffectAtPosition("RS3_FX_Char_Animals_Deer_A_VoidTransform_01",_X,_Y,_Z);
ProcObjectTimer(_Deer,"RC_MIL_ThreeAltars_SwapDeerModel",1100);

PROC
ProcObjectTimerFinished(_Deer,"RC_MIL_ThreeAltars_SwapDeerModel")
AND
NOT DB_Dead((CHARACTERGUID)_Deer)
THEN
SetFaction(_Deer,"Evil NPC");
Transform(_Deer,"Animals_Deer_A_Void_A 4ac54c7d-1cf3-4adb-868a-9291c56e7afe",1);


IF
DB_RC_MIL_Altar_Deer(_Deer,_)
THEN
DB_DoNotFace(_Deer);

//END_REGION


//REGION Wolf Altar combat

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_RC_BF_ThreeAltars_SpotWolf_Box_f37f1de1-9b38-49f0-99ba-908f301192ac)
AND
NOT DB_Dead(CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466)
THEN
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_RC_BF_ThreeAltars_SpotWolf_Box_f37f1de1-9b38-49f0-99ba-908f301192ac);
StartVoiceBark("RC_BF_VB_ThreeAltars_SpotVoidwolf",_Player);


IF
CharacterCharacterEvent(CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466,_Player,"RC_BF_ThreeAltars_VoidwolfSpottedPlayer")
THEN
Proc_StartDialog(0,"RC_BF_ThreeAltars_Wolf",CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466,_Player);
ObjectSetFlag(CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466,"RC_BF_ThreeAltars_VoidwolfStopSpot");


IF
ObjectFlagSet("RC_BF_ThreeAltars_HostileWolf",CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466,_)
THEN
SetFaction(CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466,"Evil NPC");

IF
CombatStarted(_CombatID)
AND
DB_CombatCharacters(CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466,_CombatID)
AND
GlobalGetFlag("RC_BF_ThreeAltars_WolfCombatStarted",0)
THEN
ObjectSetFlag(CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466,"RC_BF_ThreeAltars_VoidwolfStopSpot");
//JumpToTurn(CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466);
GlobalSetFlag("RC_BF_ThreeAltars_WolfCombatStarted");
SetFaction(CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466,"Evil NPC");
SetHasDialog(CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466,0);
Proc_RC_BF_ThreeAltars_StartWolf();

PROC
Proc_RC_BF_ThreeAltars_StartWolf()
AND
DB_RC_BF_Altar_Wolf(_Wolf)
AND
_Wolf != CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466
THEN
CharacterAppear(_Wolf,1,"");

PROC
ReactOnKillCounter("RC_ThreeAltars_WolfFight")
THEN
ItemClearOwner(ITEMGUID_S_RC_BF_ThreeAltars_WolfAltar_0b4505eb-7e2b-40d6-b6c3-eed66f8de0ea);
//DB_Dialogs((ITEMGUID)ITEMGUID_S_RC_BF_ThreeAltars_WolfAltar_0b4505eb-7e2b-40d6-b6c3-eed66f8de0ea,"RC_ThreeAltars");
//ItemSetCanInteract(ITEMGUID_S_RC_BF_ThreeAltars_WolfAltar_0b4505eb-7e2b-40d6-b6c3-eed66f8de0ea,1);


IF
CharacterDied(CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466)
AND
DB_RC_BF_Altar_Wolf(_Wolf)
AND
_Wolf != CHARACTERGUID_S_RC_BF_Altar_Werewolf_000_8fe2d805-39b6-42ad-a5ef-bc0b9d647466
AND
_Wolf != CHARACTERGUID_S_RC_BF_Altar_Wolf_Black_000_d3f23369-9e96-47f8-bf1c-4b2307537552
AND
NOT DB_Dead(_Wolf)
THEN
SetStoryEvent(_Wolf,"RC_BF_Altar_BlackWolf_ChangeArchetype");

//END_REGION


//REGION Deer Altar combat

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_RC_MIL_ThreeAltars_DeerDialog_Box_8730a786-0a8d-4b2f-9be3-bc22bc1220c1)
AND
NOT DB_Dead(CHARACTERGUID_S_RC_MIL_Altar_Deer_Void_000_7cce0a94-e072-4f19-9c04-0eaf2cdd1358)
THEN
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_RC_MIL_ThreeAltars_DeerDialog_Box_8730a786-0a8d-4b2f-9be3-bc22bc1220c1);
Proc_StartDialog(0,"RC_MIL_ThreeAltars_DeersPrayer",
			CHARACTERGUID_S_RC_MIL_Altar_Deer_Void_000_7cce0a94-e072-4f19-9c04-0eaf2cdd1358,
			CHARACTERGUID_S_RC_MIL_Altar_Deer_000_6007bb7b-9d8c-49ba-a431-47ce82387fc1,
			CHARACTERGUID_S_RC_MIL_Altar_Deer_001_a4002c4f-eb36-4e8d-9c73-ee1d2778897e,
			CHARACTERGUID_S_RC_MIL_Altar_Deer_002_ff53f004-dbaa-4343-bdf8-e1366fce5ef6,
			ITEMGUID_S_RC_MIL_ThreeAltars_DeerAltar_ade1c767-9901-455d-a1e1-4c99e30710e4,_Player);
Proc_RC_MIL_ThreeAltars_DeersLookAtAltar();


IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_RC_MIL_ThreeAltars_DeerDialog_Box_8730a786-0a8d-4b2f-9be3-bc22bc1220c1)
AND
Qry_RC_MIL_ThreeAltars_AnyDeerDead()
THEN
SetFaction(CHARACTERGUID_S_RC_MIL_Altar_Deer_Void_000_7cce0a94-e072-4f19-9c04-0eaf2cdd1358,"Evil NPC");
Proc_RC_MIL_ThreeAltars_DeerCombat_SetUp();


QRY
Qry_RC_MIL_ThreeAltars_AnyDeerDead()
AND
DB_Dead(CHARACTERGUID_S_RC_MIL_Altar_Deer_000_6007bb7b-9d8c-49ba-a431-47ce82387fc1)
THEN
DB_NOOP(1);

QRY
Qry_RC_MIL_ThreeAltars_AnyDeerDead()
AND
DB_Dead(CHARACTERGUID_S_RC_MIL_Altar_Deer_001_a4002c4f-eb36-4e8d-9c73-ee1d2778897e)
THEN
DB_NOOP(1);

QRY
Qry_RC_MIL_ThreeAltars_AnyDeerDead()
AND
DB_Dead(CHARACTERGUID_S_RC_MIL_Altar_Deer_002_ff53f004-dbaa-4343-bdf8-e1366fce5ef6)
THEN
DB_NOOP(1);


PROC
Proc_RC_MIL_ThreeAltars_DeersLookAtAltar()
AND
DB_RC_MIL_Altar_Deer(_Deer,_)
THEN
CharacterLookAt(_Deer,ITEMGUID_S_RC_MIL_ThreeAltars_DeerAltar_ade1c767-9901-455d-a1e1-4c99e30710e4);

IF
DialogStarted("RC_MIL_ThreeAltars_DeersPrayer",_)
THEN
SetFaction(CHARACTERGUID_S_RC_MIL_Altar_Deer_Void_000_7cce0a94-e072-4f19-9c04-0eaf2cdd1358,"Evil NPC");


IF
ObjectFlagSet("RC_MIL_ThreeAltars_CrashedDeerRitual",_,_)
THEN
DB_RC_MIL_Altar_DeerRitualCrashed(1);


//--- Ritual succeeded
IF
DialogEnded("RC_MIL_ThreeAltars_DeersPrayer",_Inst)
AND
NOT DB_RC_MIL_Altar_DeerRitualCrashed(1)
AND
DB_RC_MIL_Altar_Deer(_Deer,_)
THEN
Proc_RC_MIL_ThreeAltars_DeerCombat_SetUp();

IF
GlobalFlagSet("RC_MIL_ThreeAltars_DeersTransform")
THEN
Proc_RC_MIL_ThreeAltars_DeerCombat_SetUp();


//--- Crashed ritual
IF
DialogEnded("RC_MIL_ThreeAltars_DeersPrayer",_Inst)
AND
DB_RC_MIL_Altar_DeerRitualCrashed(1)
AND
DB_DialogPlayers(_Inst,_Player,_)
AND
DB_RC_MIL_Altar_Deer(_Deer,_Dialog)
AND
_Deer != CHARACTERGUID_S_RC_MIL_Altar_Deer_Void_000_7cce0a94-e072-4f19-9c04-0eaf2cdd1358
THEN
DB_Dialogs(_Deer,_Dialog);
DB_KillCounter_Internal(CHARACTERGUID_S_RC_MIL_Altar_Deer_Void_000_7cce0a94-e072-4f19-9c04-0eaf2cdd1358,"RC_ThreeAltars_DeerFight");
DB_KillCounter("RC_ThreeAltars_DeerFight",1);
NOT DB_DoNotFace(_Deer);
CharacterSetRelationIndivFactionToIndivFaction((CHARACTERGUID)_Player,(CHARACTERGUID)_Deer,100);
CharacterSetRelationIndivFactionToIndivFaction((CHARACTERGUID)_Deer,(CHARACTERGUID)_Player,100);
CharacterSetTemporaryHostileRelation(CHARACTERGUID_S_RC_MIL_Altar_Deer_Void_000_7cce0a94-e072-4f19-9c04-0eaf2cdd1358,(CHARACTERGUID)_Deer);


IF
CombatStarted(_CombatID)
AND
Qry_RC_MIL_ThreeAltars_AnyDeerIsInCombat(_CombatID)
AND
NOT DB_RC_MIL_Altar_DeerRitualCrashed(1)
AND
DB_RC_MIL_Altar_Deer(_Deer,_)
THEN
Proc_RC_MIL_ThreeAltars_DeerCombat_SetUp();


QRY
Qry_RC_MIL_ThreeAltars_AnyDeerIsInCombat((INTEGER)_CombatID)
AND
DB_RC_MIL_Altar_Deer(_Deer,_)
AND
DB_CombatCharacters(_Deer,_CombatID)
THEN
DB_NOOP(1);


PROC
ReactOnKillCounter("RC_ThreeAltars_DeerFight")
THEN
ItemSetCanInteract(ITEMGUID_S_RC_MIL_ThreeAltars_DeerAltar_ade1c767-9901-455d-a1e1-4c99e30710e4,1);
NOT DB_RC_MIL_Altar_DeerRitualCrashed(1);


IF
DB_Dead(_Deer)
AND
DB_RC_MIL_Altar_Deer(_Deer,_Dialog)
THEN
NOT DB_RC_MIL_Altar_Deer(_Deer,_Dialog);

//END_REGION


//REGION Set specific flags & rewards

//--- Visited Altar
IF
ObjectFlagSet("RC_ThreeAltars_SetVisitedFlag",_Player,_Inst)
AND
DB_DialogNPCs(_Inst,_Altar,1)
AND
DB_RC_ThreeAltars((ITEMGUID)_Altar,_VisitedFlag)
THEN
ObjectSetFlag(_Player,_VisitedFlag);

IF
ObjectFlagSet("RC_ThreeAltars_SetVisitedFlag",_Player,_Inst)
THEN
ObjectClearFlag(_Player,"RC_ThreeAltars_SetVisitedFlag");


//--- Refused Promise
IF
ObjectFlagSet("RC_ThreeAltars_SetTemptedByPromise",(CHARACTERGUID)_Player,_Inst)
THEN
ObjectClearFlag(_Player,"RC_ThreeAltars_SetTemptedByPromise");
Proc_RC_ThreeAltars_CountRefusedPromise(_Player);


//--- Receive source points
IF
ObjectFlagSet("RC_ThreeAltars_AddOneSourcePoint",(CHARACTERGUID)_Player,_)
THEN
CharacterAddSourcePoints(_Player,1);
ObjectClearFlag(_Player,"RC_ThreeAltars_AddOneSourcePoint");

IF
ObjectFlagSet("RC_ThreeAltars_FillSourcePoints",(CHARACTERGUID)_Player,_)
THEN
CharacterAddSourcePoints(_Player,3);
ObjectClearFlag(_Player,"RC_ThreeAltars_FillSourcePoints");


//--- Count visits for non-undead
IF
ObjectFlagSet("RC_ThreeAltars_SetVisitedFlag",(CHARACTERGUID)_Player,_Inst)
AND
IsTagged(_Player,"UNDEAD",0)
THEN
Proc_RC_ThreeAltars_CountVisit(_Player);

PROC
Proc_RC_ThreeAltars_CountVisit((CHARACTERGUID)_Player)
AND
DB_ThreeAltars_Visit(_Player,_PrevNum,_PrevFlag)
AND
IntegerSum(_PrevNum,1,_Num)
AND
IntegertoString(_Num,_Visit)
AND
StringConcatenate("RC_ThreeAltars_Visit_",_Visit,_Flag)
THEN
NOT DB_ThreeAltars_Visits(_Player,_PrevNum,_PrevFlag);
ObjectClearFlag(_Player,_PrevFlag);
DB_ThreeAltars_Visit(_Player,_Num,_Flag);
ObjectSetFlag(_Player,_Flag);

PROC
Proc_RC_ThreeAltars_CountVisit((CHARACTERGUID)_Player)
AND
NOT DB_ThreeAltars_Visit(_Player,_,_)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_ThreeAltars_LivingFirst");
DB_ThreeAltars_Visit(_Player,2,"RC_ThreeAltars_Visit_2");
ObjectSetFlag(_Player,"RC_ThreeAltars_Visit_2");


//--- Count PROMISE proposals for undead
PROC
Proc_RC_ThreeAltars_CountRefusedPromise((CHARACTERGUID)_Player)
AND
DB_ThreeAltars_RefusedPromise(_Player,_PrevNum,_PrevFlag)
AND
IntegerSum(_PrevNum,1,_Num)
AND
IntegertoString(_Num,_Visit)
AND
StringConcatenate("RC_ThreeAltars_Refused_",_Visit,_Flag)
THEN
NOT DB_ThreeAltars_RefusedPromise(_Player,_PrevNum,_PrevFlag);
ObjectClearFlag(_Player,_PrevFlag);
DB_ThreeAltars_RefusedPromise(_Player,_Num,_Flag);
ObjectSetFlag(_Player,_Flag);

PROC
Proc_RC_ThreeAltars_CountRefusedPromise((CHARACTERGUID)_Player)
AND
NOT DB_ThreeAltars_RefusedPromise(_Player,_,_)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_ThreeAltars_UndeadRefusedFirst");
DB_ThreeAltars_RefusedPromise(_Player,2,"RC_ThreeAltars_Refused_2");
ObjectSetFlag(_Player,"RC_ThreeAltars_Refused_2");

//END_REGION



EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
