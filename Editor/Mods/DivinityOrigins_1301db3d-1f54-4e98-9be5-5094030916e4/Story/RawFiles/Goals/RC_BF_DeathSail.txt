Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_IsNotMessingAround(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703);

DB_Dialogs(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,"RC_BF_DeathSail");
DB_Dialogs(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703,"RC_BF_Ferryman");

DB_RC_BF_DeathBoat_Dialogs("RC_BF_DeathSail");
DB_RC_BF_DeathBoat_Dialogs("RC_BF_Ferryman");

DB_RC_BF_DeathBoat_PlayerPoints(0,(TRIGGERGUID)TRIGGERGUID_S_RC_BF_DeathLake_AtIsland_8fa57310-343c-4f2d-b7de-180c7f34e03e);
DB_RC_BF_DeathBoat_PlayerPoints(1,TRIGGERGUID_S_RC_BF_DeathLake_AtLand_c6c9f5e7-ef78-4db0-abe3-5d75f5445598);

DB_RC_BF_DeathBoat_FerrymanPoints(0,(TRIGGERGUID)TRIGGERGUID_S_RC_BF_Ferryman_AtIsland_be187703-9064-4c4c-81dd-9a4e87c7218f);
DB_RC_BF_DeathBoat_FerrymanPoints(1,TRIGGERGUID_S_RC_BF_Ferryman_AtLand_c34c49b9-72fd-4892-9d37-e1458ef70836);

DB_RC_BF_DeathBoat_Points(0,(TRIGGERGUID)TRIGGERGUID_S_RC_BF_DeathBoat_AtIsland_3a65aa4e-8b8b-4a4a-800f-768194134c17);
DB_RC_BF_DeathBoat_Points(1,TRIGGERGUID_S_RC_BF_DeathBoat_AtLand_c114dbf0-3b49-44d6-8841-397485448a09);

DB_RC_BF_DeathBoat_Price(100);
//DB_RC_BF_DeathBoat_Teleport(_Player,_Tp)
//DB_RC_BF_DeathBoat_FerryTeleport(_Ferry,_Tp)
//DB_RC_BF_DeathBoat_BoatTeleport(_Boat,_Tp)

DB_OneTimeEventFlag("RC_BF_DeathBoat_Pay");
//DB_OneTimeEventFlag("RC_BF_DeathBoat_Sail");

TriggerRegisterForItems(TRIGGERGUID_S_RC_BF_DeathBoat_AtLand_Box_05c7007e-fe2f-41a4-8e81-9728102d23df);
TriggerRegisterForItems(TRIGGERGUID_S_RC_BF_DeathBoat_AtIsland_Box_4d2f16bf-c9b9-4283-844f-c0c28f85494e);

DB_RC_BF_DeathBoat_VBTriggers((TRIGGERGUID)TRIGGERGUID_S_RC_BF_DeathBoat_VB_AtLand_45eca41e-29aa-48ad-82d6-deb6e953ddf5,0);
DB_RC_BF_DeathBoat_VBTriggers(TRIGGERGUID_S_RC_BF_DeathBoat_VB_AtIsland_2c00e22d-0cdf-4909-8fa4-527bb33be027,1);
KBSECTION
//REGION Debug
IF
TextEventSet("rcbfboatreset")
THEN
TeleportTo(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,TRIGGERGUID_S_RC_BF_DeathBoat_AtLand_c114dbf0-3b49-44d6-8841-397485448a09);
TeleportTo(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703,TRIGGERGUID_S_RC_BF_Ferryman_AtLand_c34c49b9-72fd-4892-9d37-e1458ef70836);
Foop(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703);
ItemSetCanInteract(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,1);
GlobalClearFlag("RC_BF_DeathBoat_AtIsland");
SysClear("RC_BF_SailTeleport",2);

IF
TextEventSet("rcbfboatreset")
AND
DB_IsPlayer(_Player)
THEN
SetVisible(_Player,1);
ProcSetInvulnerable(_Player,0);
CharacterUnfreeze(_Player);

IF
TextEventSet("rcbftrip")
THEN
SetStoryEvent(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,"Trip");

IF
TextEventSet("rcbftrip1")
THEN
SetStoryEvent(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,"Trip1");
//END_REGION


//REGION Voice Bark
IF
DB_RC_BF_DeathBoat_VBTriggers(_Trigger,_)
THEN
ProcTriggerRegisterForPlayers(_Trigger);

IF
CharacterEnteredTrigger(_Player,_Trigger)
AND
DB_RC_BF_DeathBoat_VBTriggers(_Trigger,_AtIsland)
AND
DB_IsPlayer(_Player)
AND
GlobalGetFlag("RC_BF_DeathBoat_AtIsland",_AtIsland)
AND
UserGetFlag(_Player,"RC_BF_VB_FoundDeathBoat",0)
THEN
StartVoiceBark("RC_BF_VB_FoundDeathBoat",_Player);
UserSetFlag(_Player,"RC_BF_VB_FoundDeathBoat");
//END_REGION


IF
DB_RC_BF_DeathBoat_Price(_Amount)
THEN
DB_DialogMoneyTransfer(1,"RC_BF_Ferryman",_Amount);


PROC
ProcBlockUseOfItem(_Player,ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444)
AND
DB_IsPlayer(_Player)
AND
NOT DB_Dead(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703)
AND
QRY_IsInRange(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703,ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,15.0)
THEN
DB_CustomUseItemResponse(_Player,ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,0);
Proc_RC_BF_DeathBoat_Unauthorized(_Player);
Proc_StartDialog(0,"RC_BF_Ferryman",CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703,_Player);

PROC
Proc_RC_BF_DeathBoat_Unauthorized((CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"RC_BF_DeathBoat_UnauthorizedUse",1)
THEN
ObjectSetFlag(_Player,"RC_BF_DeathBoat_UnauthorizedUse_Twice");

PROC
Proc_RC_BF_DeathBoat_Unauthorized((CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"RC_BF_DeathBoat_UnauthorizedUse",0)
THEN
ObjectSetFlag(_Player,"RC_BF_DeathBoat_UnauthorizedUse");



//REGION Boat interaction and flag
IF
ItemEnteredTrigger(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,TRIGGERGUID_S_RC_BF_DeathBoat_AtIsland_Box_4d2f16bf-c9b9-4283-844f-c0c28f85494e,_)
THEN
GlobalSetFlag("RC_BF_DeathBoat_AtIsland");
ItemSetCanInteract(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,1);

IF
ItemEnteredTrigger(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,TRIGGERGUID_S_RC_BF_DeathBoat_AtLand_Box_05c7007e-fe2f-41a4-8e81-9728102d23df,_)
THEN
ItemSetCanInteract(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,1);

IF
ItemLeftTrigger(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,TRIGGERGUID_S_RC_BF_DeathBoat_AtIsland_Box_4d2f16bf-c9b9-4283-844f-c0c28f85494e,_)
THEN
GlobalClearFlag("RC_BF_DeathBoat_AtIsland");
//END_REGION


//REGION Departure
PROC
Proc_RC_BF_DeathBoat_Poof((CHARACTERGUID)_Char)
THEN
Proc_TeleportSmoke(_Char);
SetVisible(_Char,0);
ProcSetInvulnerable(_Char,1);
CharacterFreeze(_Char);

PROC
Proc_RC_BF_DeathBoat_PoofFerry()
AND
NOT DB_Dead(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703)
THEN
Proc_TeleportSmoke(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703);
SetVisible(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703,0);
ProcSetInvulnerable(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703,1);
CharacterFreeze(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703);

PROC
Proc_RC_BF_DeathBoat_Foop((CHARACTERGUID)_Char)
THEN
CharacterUnfreeze(_Char);
ProcSetInvulnerable(_Char,0);
SetVisible(_Char,1);
Proc_TeleportSmoke(_Char);

IF
DialogEnded(_SailDialog,_ID)
AND
DB_RC_BF_DeathBoat_Dialogs(_SailDialog)
AND
DB_GlobalFlag("RC_BF_DeathBoat_Sail")
AND
DB_DialogPlayers(_ID,_Player,1)
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
GlobalGetFlag("RC_BF_DeathBoat_AtIsland",_AtIsland)
THEN
DB_RC_BF_DeathBoat_SailFromIsland(_AtIsland);
Proc_RC_BF_DeathBoat_PoofFerry();
DB_RC_BF_DeathBoat_PlayersInTrip(_Player);
Proc_RC_BF_DeathBoat_PoofAll(_Player);
Proc_RC_BF_DeathBoat_BoatMove(_AtIsland);

IF
DialogEnded(_SailDialog,_ID)
AND
DB_RC_BF_DeathBoat_Dialogs(_SailDialog)
THEN
GlobalClearFlag("RC_BF_DeathBoat_Sail");


PROC
Proc_RC_BF_DeathBoat_PoofAll((CHARACTERGUID)_Player)
AND
DB_IsPlayer((CHARACTERGUID)_Player2)
AND
CharacterIsInPartyWith(_Player2,_Player,1)
AND
CharactersAreGrouped(_Player2,_Player,1)
THEN
Proc_RC_BF_DeathBoat_ChooseWhoToPoof(_Player2);

PROC
Proc_RC_BF_DeathBoat_ChooseWhoToPoof((CHARACTERGUID)_Player2)
AND
QRY_IsInRange(_Player2,ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,14.0)
THEN
DB_RC_BF_DeathBoat_PlayersInTrip(_Player2);
Proc_RC_BF_DeathBoat_Poof(_Player2);
FadeToBlack(_Player2,1.0,0,"RC_BF_DeathBoat_Fade");

PROC
Proc_RC_BF_DeathBoat_ChooseWhoToPoof((CHARACTERGUID)_Player2)
AND
NOT QRY_IsInRange(_Player2,ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,14.0)
THEN
CharacterDetachFromGroup(_Player2);

PROC
Proc_RC_BF_DeathBoat_BoatMove((INTEGER)_FerryAtIsland)
THEN
ItemSetCanInteract(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,0);
SetVarInteger(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,"AtIsland",_FerryAtIsland);
SetStoryEvent(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,"StartSail");
ProcObjectTimer(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,"RC_BF_DeathBoat_SailTimer",1100);

IF
DB_RC_BF_DeathBoat_PlayersInTrip(_Player)
AND
CharacterIsDead(_Player,0)
AND
IsTagged(_Player,"UNDEAD",1)
THEN
GlobalSetFlag("RC_BF_DeathBoat_Survivers");

IF
DB_RC_BF_DeathBoat_PlayersInTrip(_Player)
AND
CharacterIsDead(_Player,0)
AND
IsTagged(_Player,"UNDEAD",0)
THEN
GlobalSetFlag("RC_BF_DeathBoat_Harvest");
//END_REGION


//REGION Arrival
PROC
ProcObjectTimerFinished(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,"RC_BF_DeathBoat_SailTimer")
THEN
Proc_RC_BF_DeathBoat_Arrived();

PROC
ProcObjectTimerCancel(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,"RC_BF_DeathBoat_SailTimer")
THEN
Proc_RC_BF_DeathBoat_Arrived();

PROC
Proc_RC_BF_DeathBoat_Arrived()
AND
DB_RC_BF_DeathBoat_PlayersInTrip(_Player)
THEN
FadeToBlack(_Player,1.0,1,"RC_BF_DeathBoat_Unfade");

PROC
Proc_RC_BF_DeathBoat_Arrived()
THEN
Proc_RC_BF_DeathBoat_BoatFinishMove();
Proc_RC_BF_DeathBoat_FoopFerry();
Proc_RC_BF_DeathBoat_FoopAll();


PROC
Proc_RC_BF_DeathBoat_BoatFinishMove()
AND
DB_RC_BF_DeathBoat_SailFromIsland(_FromIsland)
AND
DB_RC_BF_DeathBoat_Points(_FromIsland,_Tp)
THEN
TeleportTo(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,_Tp);
ItemMoveToTrigger(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,_Tp,1.0,1.0,1,"");
ItemSetCanInteract(ITEMGUID_S_RC_BF_DeathBoat_1ca15059-2465-4f15-a520-375a36cb1444,1);

PROC
Proc_RC_BF_DeathBoat_FoopFerry()
AND
NOT DB_Dead(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703)
AND
DB_RC_BF_DeathBoat_SailFromIsland(_FromIsland)
AND
DB_RC_BF_DeathBoat_FerrymanPoints(_FromIsland,_Tp)
THEN
TeleportTo(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703,_Tp);
Proc_RC_BF_DeathBoat_Foop(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703);

PROC
Proc_RC_BF_DeathBoat_FoopAll()
AND
DB_RC_BF_DeathBoat_SailFromIsland(_FromIsland)
AND
DB_RC_BF_DeathBoat_PlayerPoints(_FromIsland,_Tp)
AND
DB_RC_BF_DeathBoat_PlayersInTrip(_Player)
THEN
TeleportTo(_Player,_Tp);
Proc_RC_BF_DeathBoat_Foop(_Player);

PROC
Proc_RC_BF_DeathBoat_Arrived()
AND
NOT DB_Dead(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703)
THEN
Proc_RC_BF_DeathBoat_ArrivedWithFerry();

PROC
Proc_RC_BF_DeathBoat_Arrived()
AND
DB_Dead(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703)
THEN
Proc_RC_BF_DeathBoat_ArrivedSolely();
//END_REGION


//REGION Arrived without Ferryman
PROC
Proc_RC_BF_DeathBoat_ArrivedSolely()
AND
DB_RC_BF_DeathBoat_PlayersInTrip(_Player)
THEN
Proc_RC_BF_DeathBoat_DieFromDeathFog(_Player);

PROC
Proc_RC_BF_DeathBoat_ArrivedSolely()
THEN
Proc_RC_BF_DeathBoat_ArriveCleanUp();
//END_REGION


//REGION Arrived with Ferryman
PROC
Proc_RC_BF_DeathBoat_ArrivedWithFerry()
AND
DB_RC_BF_DeathBoat_PlayersInTrip(_Player)
AND
CharacterIsDead(_Player,0)
THEN
ApplyStatus(_Player,"KNOCKED_DOWN",-1.0,1);
CharacterSetAnimationOverride(_Player,"knockdown_loop");

PROC
Proc_RC_BF_DeathBoat_ArrivedWithFerry()
AND
DB_GlobalFlag("RC_BF_DeathBoat_Harvest")
AND
DB_RC_BF_DeathBoat_PlayersInTrip(_Player)
THEN
PartySetFlag(_Player,"RC_BF_DeathBoat_CompanionDied");

PROC
Proc_RC_BF_DeathBoat_ArrivedWithFerry()
AND
DB_RC_BF_DeathBoat_PlayersInTrip(_Player)
AND
IsTagged(_Player,"UNDEAD",0)
THEN
CharacterSetHitpointsPercentage(_Player,1.0);


PROC
Proc_RC_BF_DeathBoat_ArrivedWithFerry()
AND
DB_RC_BF_DeathBoat_PlayersInTrip(_Player)
AND
GlobalGetFlag("RC_BF_DeathBoat_DialogAfterStarting",0)
AND
QRY_SpeakerIsAvailable(CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703,1)
THEN
GlobalSetFlag("RC_BF_DeathBoat_DialogAfterStarting");
Proc_StartDialog(0,"RC_BF_Ferryman",CHARACTERGUID_S_RC_BF_Ferryman_6d4270d3-2f01-4674-8668-396ac0a4c703,_Player);

IF
DialogStarted("RC_BF_Ferryman",_)
AND
GlobalGetFlag("RC_BF_DeathBoat_DialogAfterStarting",1)
THEN
GlobalSetFlag("RC_BF_DeathBoat_DialogAfterStarted");

IF
DialogRequestFailed("RC_BF_Ferryman",_)
AND
GlobalGetFlag("RC_BF_DeathBoat_DialogAfterStarting",1)
THEN
Proc_RC_BF_DeathBoat_AfterTrip();

IF
DialogEnded("RC_BF_Ferryman",_)
AND
GlobalGetFlag("RC_BF_DeathBoat_DialogAfterStarted",1)
THEN
Proc_RC_BF_DeathBoat_AfterTrip();

PROC
Proc_RC_BF_DeathBoat_ArrivedWithFerry()
AND
GlobalGetFlag("RC_BF_DeathBoat_DialogAfterStarting",0)
THEN
Proc_RC_BF_DeathBoat_AfterTrip();

PROC
Proc_RC_BF_DeathBoat_AfterTrip()
AND
DB_RC_BF_DeathBoat_PlayersInTrip(_Player)
AND
CharacterIsDead(_Player,0)
THEN
CharacterSetAnimationOverride(_Player,"");
RemoveStatus(_Player,"KNOCKED_DOWN");
Proc_RC_BF_DeathBoat_DieFromDeathFog(_Player);

PROC
Proc_RC_BF_DeathBoat_AfterTrip()
THEN
Proc_RC_BF_DeathBoat_ArriveCleanUp();
//END_REGION


//REGION Arrival finalizing
PROC
Proc_RC_BF_DeathBoat_DieFromDeathFog((CHARACTERGUID)_Player)
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player,"UNDEAD",0)
THEN
CharacterDie(_Player,0,"DoT");

PROC
Proc_RC_BF_DeathBoat_ArriveCleanUp()
THEN
GlobalClearFlag("RC_BF_DeathBoat_Harvest");
GlobalClearFlag("RC_BF_DeathBoat_Survivers");
GlobalClearFlag("RC_BF_DeathBoat_DialogAfterStarting");
GlobalClearFlag("RC_BF_DeathBoat_DialogAfterStarted");
SysClear("DB_RC_BF_DeathBoat_SailFromIsland",1);
SysClear("DB_RC_BF_DeathBoat_PlayersInTrip",1);
//END_REGION
EXITSECTION
SysClear("DB_RC_BF_DeathBoat_Dialogs",1);
SysClear("DB_RC_BF_DeathBoat_PlayerPoints",2);
SysClear("DB_RC_BF_DeathBoat_FerrymanPoints",2);
SysClear("DB_RC_BF_DeathBoat_Points",2);
SysClear("DB_RC_BF_DeathBoat_Price",1);
NOT DB_OneTimeEventFlag("RC_BF_DeathBoat_Pay");
SysClear("DB_RC_BF_DeathBoat_VBTriggers",2);
ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
