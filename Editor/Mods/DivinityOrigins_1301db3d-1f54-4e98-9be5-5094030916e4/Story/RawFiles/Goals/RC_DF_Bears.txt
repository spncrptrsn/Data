Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_RC_DF_Bear((CHARACTERGUID)CHARACTERGUID_S_RC_DF_Bear_000_549586fc-3b50-402b-bf00-4389890f66e2);
DB_RC_DF_Bear((CHARACTERGUID)CHARACTERGUID_S_RC_DF_Bear_001_759e95f0-4f86-48bc-bf24-025f1cdf0b90);
DB_Dialogs(CHARACTERGUID_S_RC_DF_Bear_000_549586fc-3b50-402b-bf00-4389890f66e2,"RC_DF_Bear_000");
DB_Dialogs(CHARACTERGUID_S_RC_DF_Bear_001_759e95f0-4f86-48bc-bf24-025f1cdf0b90,"RC_DF_Bear_001");

DB_OneShotPlayerTrigger(TRIGGERGUID_S_RC_DF_Bears_SpotBox_d237ad85-f176-4ea3-aea5-27eba6284bbe);

DB_Ghosts(CHARACTERGUID_S_RC_DF_Bear_DeadSourcerer_000_9e591955-b590-42ff-adc8-80df06c0b1af,CHARACTERGUID_S_RC_DF_Bear_DeadSourcerer_Ghost_000_692af522-be6b-4746-8e39-fed0089a9767,"RC_DF_Bear_DeadSourcererGhost");

DB_RC_DF_Beehive((ITEMGUID)ITEMGUID_S_RC_DF_Bears_Beehive_A_000_7133c85f-aaa5-4aff-bd30-717079a27ef3);
DB_RC_DF_Beehive((ITEMGUID)ITEMGUID_S_RC_DF_Bears_Beehive_A_001_06dee3bb-a58b-424c-a801-d60585901b9c);
DB_RC_DF_Beehive((ITEMGUID)ITEMGUID_S_RC_DF_Bears_Beehive_A_002_6a65ef59-1705-4c4f-900f-48dd32a8c2e3);
DB_RC_DF_Beehive((ITEMGUID)ITEMGUID_S_RC_DF_Bears_Beehive_A_003_84cd8158-68ec-4dd8-bd6f-3ff9550296ed);
DB_RC_DF_Beehive((ITEMGUID)ITEMGUID_S_RC_DF_Bears_Beehive_A_004_603dfe8d-c484-4f9f-a2f3-0bd9ece21bb5);
DB_RC_DF_Beehive((ITEMGUID)ITEMGUID_S_RC_DF_Bears_Beehive_A_005_09bfb725-7a34-4256-b37f-00ceb2f8906e);

Proc_RC_DF_Bear_SetupCrimes();
KBSECTION
//REGION Init & debug
IF
TextEventSet("BearTF")
AND
QueryOnlyOnce("RC_DF_BearsTransform")
AND
DB_RC_DF_Bear(_Bear)
THEN
Transform(_Bear,"Animals_Bear_A_Voidwoken_a0f68491-6af5-4190-9c45-c4559d29c08f",1);
SetFaction(_Bear,"Evil NPC");


IF
GameStarted("RC_Main",_)
AND
DB_Ghosts(CHARACTERGUID_S_RC_DF_Bear_DeadSourcerer_Ghost_000_692af522-be6b-4746-8e39-fed0089a9767,_)
AND
NOT DB_RC_DF_Bear_DeadSourcererCollar(CHARACTERGUID_S_RC_DF_Bear_DeadSourcerer_000_9e591955-b590-42ff-adc8-80df06c0b1af)
THEN
DB_RC_DF_Bear_DeadSourcererCollar(CHARACTERGUID_S_RC_DF_Bear_DeadSourcerer_000_9e591955-b590-42ff-adc8-80df06c0b1af);
PROC_LoopEffect("RS3_FX_GP_Status_HoECollar_01",CHARACTERGUID_S_RC_DF_Bear_DeadSourcerer_000_9e591955-b590-42ff-adc8-80df06c0b1af,"RC_DW_WC_SourceCollar","RC_Main","Dummy_NeckFX");


PROC
Proc_RC_DF_Bear_SetupCrimes()
AND
DB_RC_DF_Bear(_Bear)
THEN
ProcCrimeSetCustomPrimarySensibleAction(_Bear,"SummonAttackAnimal","RC_DF_Bear_CustomCrimeReaction");
ProcCrimeSetCustomSecondarySensibleAction(_Bear,"SummonAttackAnimal","RC_DF_Bear_CustomCrimeReaction");
DB_CrimeReaction_CustomWarning(_Bear,"SummonAttackAnimal","RC_DF_AD_Bear_Warning");
ProcCrimeSetCustomPrimarySensibleAction(_Bear,"AttackAnimal","RC_DF_Bear_CustomCrimeReaction");
ProcCrimeSetCustomSecondarySensibleAction(_Bear,"AttackAnimal","RC_DF_Bear_CustomCrimeReaction");
DB_CrimeReaction_CustomWarning(_Bear,"AttackAnimal","RC_DF_AD_Bear_Warning");
DB_CrimeWarningIsAD("RC_DF_AD_Bear_Warning");
DB_DontCreateMurder(_Bear);

//END_REGION


//REGION Voice bark / AD

IF
CharacterCharacterEvent((CHARACTERGUID)_Bear,_Player,"RC_DF_Bear_WarnClosePlayer")
AND
DB_RC_DF_Bear(_Bear)
AND
Random(10000,_Rand)
AND
IntegerSum(_Rand,15000,_Timer)
THEN
CharacterLookAt(_Bear,_Player);
Proc_StartDialog(1,"RC_DF_AD_Bear_Warning",_Bear);
ProcObjectTimer(_Bear,"RC_DF_Bear_RestartSpotting",_Timer);

PROC
ProcObjectTimerFinished(_Bear,"RC_DF_Bear_RestartSpotting")
AND
GlobalGetFlag("RC_DF_BearsTransformed",0)
THEN
ObjectSetFlag(_Bear,"RC_DF_Bear_RestartSpotting");


PROC
ProcOneShotTriggerEntered(_Player,TRIGGERGUID_S_RC_DF_Bears_SpotBox_d237ad85-f176-4ea3-aea5-27eba6284bbe)
THEN
Proc_StartDialog(1,"RC_DF_AD_Bear_Noticed",_Player);

//END_REGION


//REGION Bear transformation triggers

PROC
ProcBlockUseOfItem((CHARACTERGUID)_Player,(ITEMGUID)_Beehive)
AND
DB_RC_DF_Beehive(_Beehive)
AND
Qry_RC_DF_BearCanSee(_Player)
THEN
DB_CustomUseItemResponse(_Player,_Beehive,0);
Proc_RC_DF_Bears_GiveHiveDialog(_Player);


PROC
Proc_RC_DF_Bears_GiveHiveDialog((CHARACTERGUID)_Player)
AND
Qry_RC_DF_GetClosestBear(_Player)
AND
DB_RC_DF_Bear_ClosestBear(_Bear,_Dist)
THEN
NOT DB_RC_DF_Bear_ClosestBear(_Bear,_Dist);
Proc_StartDialog(0,"RC_BF_Bear_TouchedHive",_Bear,_Player);

PROC
Proc_RC_DF_Bears_GiveHiveDialog((CHARACTERGUID)_Player)
THEN
Proc_RC_DF_Bear_SetHostile();


IF
CharacterReceivedDamage(_Bear, _, _)
AND
DB_RC_DF_Bear(_Bear)
THEN
Proc_RC_DF_Bear_SetHostile();


QRY
Qry_RC_DF_BearCanSee((CHARACTERGUID)_Player)
AND
DB_RC_DF_Bear(_Bear)
AND
CharacterCanSee(_Bear,_Player,1)
THEN
DB_NOOP(1);


QRY
Qry_RC_DF_GetClosestBear((CHARACTERGUID)_Player)
AND
DB_RC_DF_Bear(_Bear)
AND
GetDistanceTo(_Bear,_Player,_Dist)
THEN
DB_RC_DF_Bear_ClosestBear(_Bear,_Dist);

QRY
Qry_RC_DF_GetClosestBear((CHARACTERGUID)_Player)
AND
DB_RC_DF_Bear_ClosestBear(_Bear,_Dist)
AND
DB_RC_DF_Bear_ClosestBear(_OtherBear,_OtherDist)
AND
_Bear != _OtherBear
AND
_Dist <= _OtherDist
THEN
NOT DB_RC_DF_Bear_ClosestBear(_OtherBear,_OtherDist);

QRY
Qry_RC_DF_GetClosestBear((CHARACTERGUID)_Player)
AND
DB_RC_DF_Bear_ClosestBear(_Bear,_Dist)
AND
DB_RC_DF_Bear_ClosestBear(_OtherBear,_OtherDist)
AND
_Bear != _OtherBear
AND
_Dist > _OtherDist
THEN
NOT DB_RC_DF_Bear_ClosestBear(_Bear,_Dist);

//END_REGION


//REGION Bear set hostile & transforms

PROC
Proc_RC_DF_Bear_SetHostile()
AND
GlobalGetFlag("RC_DF_BearsTransformed",0)
AND
DB_RC_DF_Bear(_Bear)
THEN
SetFaction(_Bear,"Evil NPC");


IF
StoryEvent(_,"RC_DF_Bear_TransformIntoVW")
THEN
Proc_RC_DF_Bear_StartTransform();

PROC
Proc_RC_DF_Bear_StartTransform()
AND
GlobalGetFlag("RC_DF_BearsTransformed",0)
THEN
Proc_RC_DF_Bear_Transform();


PROC
Proc_RC_DF_Bear_Transform()
AND
DB_RC_DF_Bear(_Bear)
AND
GetPosition(_Bear,_X,_Y,_Z)
THEN
PROC_RemoveDialogFromCharacter(_Bear);
SetStoryEvent(_Bear,"RC_DF_Bear_ChangeStatAndAddSkills");
PlayEffect(_Bear,"RS3_FX_Char_Animals_Bear_A_VoidTransform_Overlays_01");
PlayEffectAtPosition("RS3_FX_Char_Animals_Bear_A_VoidTransform_01",_X,_Y,_Z);


PROC
Proc_RC_DF_Bear_Transform()
AND
GlobalGetFlag("RC_DF_BearsTransformed",0)
THEN
GlobalSetFlag("RC_DF_BearsTransformed");
TimerLaunch("RC_DF_Bears_Transforms",1100);
TimerLaunch("RC_DF_Bears_TransformsAD",200);


IF
TimerFinished("RC_DF_Bears_TransformsAD")
AND
DB_RC_DF_Bear(_Bear)
THEN
Proc_StartDialog(1,"RC_DF_AD_Bear_Transform",_Bear);


IF
TimerFinished("RC_DF_Bears_Transforms")
AND
DB_RC_DF_Bear(_Bear)
THEN
NOT DB_RC_DF_Bear(_Bear);
DB_RC_DF_VoidBear(_Bear);
Transform(_Bear,"QUEST_RC_BF_TheBears_Bear_A_Voidwoken_87bd467b-5eb3-4d79-aac5-fcc35785fa1a",1);
TriggerSetAtmosphere(TRIGGERGUID_AtmosphereTrigger_RC_DriftwoodFields_72187165-71fe-4977-8b72-e48db9b8a00f,"4b41cfe5-35bc-4473-b50c-59301e38ef9f");
SetFaction(_Bear,"Evil NPC");
CharacterAddSourcePoints(_Bear,3);


IF
TimerFinished("RC_DF_Bears_Transforms")
THEN
Proc_RC_DF_Bear_CastSkill();


PROC
Proc_RC_DF_Bear_CastSkill()
AND
DB_RC_DF_VoidBear(_Bear)
AND
NOT DB_Dead(_Bear)
AND
GetClosestAlivePlayer(_Bear,_Player,_Dist)
AND
_Dist < 10
AND
QueryOnlyOnce("RC_DF_Bear_CastSkill_EnemyEarthquake")
THEN
JumpToTurn(_Bear);
CharacterUseSkill(_Bear,"Quake_EnemyEarthquake",_Player,1);


//--- Clear ownership:
PROC
Proc_RC_DF_Bear_Transform()
AND
DB_RC_DF_Beehive(_Beehive)
THEN
ItemClearOwner(_Beehive);
NOT DB_RC_DF_Beehive(_Beehive);

//END_REGION


//REGION Bear dies

IF
CharacterDied(_Bear)
AND
DB_RC_DF_Bear(_Bear)
THEN
NOT DB_RC_DF_Bear(_Bear);


IF
CharacterDied(CHARACTERGUID_S_RC_DF_Bear_000_549586fc-3b50-402b-bf00-4389890f66e2)
AND
DB_RC_DF_Beehive(_Beehive)
AND
CharacterIsDead(CHARACTERGUID_S_RC_DF_Bear_001_759e95f0-4f86-48bc-bf24-025f1cdf0b90,0)
AND
IsTagged(CHARACTERGUID_S_RC_DF_Bear_001_759e95f0-4f86-48bc-bf24-025f1cdf0b90,"VOIDWOKEN",0)
THEN
ItemSetOwner(_Beehive,CHARACTERGUID_S_RC_DF_Bear_001_759e95f0-4f86-48bc-bf24-025f1cdf0b90);


IF
CharacterDied(CHARACTERGUID_S_RC_DF_Bear_000_549586fc-3b50-402b-bf00-4389890f66e2)
AND
CharacterIsDead(CHARACTERGUID_S_RC_DF_Bear_001_759e95f0-4f86-48bc-bf24-025f1cdf0b90,1)
THEN
Proc_RC_DF_Bear_PrepCloseQuest();

IF
CharacterDied(CHARACTERGUID_S_RC_DF_Bear_001_759e95f0-4f86-48bc-bf24-025f1cdf0b90)
AND
CharacterIsDead(CHARACTERGUID_S_RC_DF_Bear_000_549586fc-3b50-402b-bf00-4389890f66e2,1)
THEN
Proc_RC_DF_Bear_PrepCloseQuest();


PROC
Proc_RC_DF_Bear_PrepCloseQuest()
THEN
TriggerSetAtmosphere(TRIGGERGUID_AtmosphereTrigger_RC_DriftwoodFields_72187165-71fe-4977-8b72-e48db9b8a00f,"d6fc4e50-31c2-45cf-8ec5-b5d655b216af"); 
DB_RC_DF_BearDead(CHARACTERGUID_S_RC_DF_Bear_000_549586fc-3b50-402b-bf00-4389890f66e2);
DB_RC_DF_BearDead(CHARACTERGUID_S_RC_DF_Bear_001_759e95f0-4f86-48bc-bf24-025f1cdf0b90);


//END_REGION

PROC
ProcRegionEnded("RC_Main")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
