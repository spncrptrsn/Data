Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_CrimeReaction_DoNotInterrogate(RC_FTJ_HighPriest_2a09f30c-0a3b-495f-8386-5390a6c4c08d);
CharacterSetCanTrade(CHARACTERGUID_S_FTJ_HighPriest_2a09f30c-0a3b-495f-8386-5390a6c4c08d, 0);


KBSECTION
//REGION Courtroomins
//http://fileserver-dmz/mediawiki/index.php/The_Courtroom
IF 
StoryEvent(_Player,"StartCourtroomDialog")
AND
CharacterIsDead(CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,0)
AND
GlobalGetFlag("FTJ_CourtroomTransformPrisoner",0)
AND
QueryOnlyOnce("RC_FTJ_CourtRoomEventOneShot")
THEN
ProcForceStopDialog(CHARACTERGUID_S_FTJ_HighPriest_2a09f30c-0a3b-495f-8386-5390a6c4c08d);
Proc_StartDialog(0,"FTJ_CourtRoomEvent",RC_FTJ_HighPriest_2a09f30c-0a3b-495f-8386-5390a6c4c08d,RC_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,RC_FTJ_CourtRoomGuard_001_c51d581d-9245-431f-a1eb-88adc8149827,_Player);

IF 
StoryEvent(_Player,"StartCourtroomDialog")
AND
QueryOnlyOnce("RC_FTJ_CourtRoomEventOneShot")
THEN
ProcForceStopDialog(CHARACTERGUID_S_FTJ_HighPriest_2a09f30c-0a3b-495f-8386-5390a6c4c08d);
CharacterSetRelationFactionToFaction("Hero","RC_FTJ_ChapelMagister",0);
CharacterSetRelationFactionToFaction("RC_FTJ_ChapelMagister","Hero",0);

IF
AttackedByObject((CHARACTERGUID)_Magister,(CHARACTERGUID)_Player,_,_,_)
AND
DB_IsPlayer(_Player)
AND
DB_FTJ_ChapelMagisters(_Magister)
AND
NOT DB_CourtroomMagistersGoHostile(_Magister)
AND
QueryOnlyOnce("FTJ_PurgeCourtroomPrisoner")
THEN
Proc_FTJ_CourtRoomMagisters_HostileBeforeSceneEnds();

PROC
Proc_FTJ_CourtRoomMagisters_HostileBeforeSceneEnds()
AND
DB_FTJ_ChapelMagisters(_Magister)
THEN
DB_CourtroomMagistersGoHostile(_Magister);
ProcForceStopDialog(_Magister);

PROC
Proc_FTJ_CourtRoomMagisters_HostileBeforeSceneEnds()
THEN
CharacterSetRelationFactionToFaction("Hero","RC_FTJ_ChapelMagister",0);
CharacterSetRelationFactionToFaction("RC_FTJ_ChapelMagister","Hero",0);
GlobalSetFlag("FTJ_CourtroomTransformPrisoner");

IF
DialogEnded("FTJ_CourtRoomEvent",_)
THEN
CharacterSetRelationFactionToFaction("Hero","RC_FTJ_ChapelMagister",0);
CharacterSetRelationFactionToFaction("RC_FTJ_ChapelMagister","Hero",0);
Proc_SceneOver("FTJ_CourtRoomEvent");


//Run away if the magisters are in combat and Prisoner is neutral
IF
GlobalFlagSet("FTJ_CourtroomTransformPrisoner")
AND
GetPosition(CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,_X,_Y,_Z)
THEN
CharacterLookAt(CHARACTERGUID_S_FTJ_HighPriest_2a09f30c-0a3b-495f-8386-5390a6c4c08d,CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487);
CharacterAddSkill(CHARACTERGUID_S_FTJ_HighPriest_2a09f30c-0a3b-495f-8386-5390a6c4c08d,"Target_Quest_PurgeCharacter");
CharacterUseSkill(CHARACTERGUID_S_FTJ_HighPriest_2a09f30c-0a3b-495f-8386-5390a6c4c08d,"Target_Quest_PurgeCharacter",CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,0);
PlayEffectAtPosition("RS3_FX_Skills_Source_Resurrect_Target_Cast",_X,_Y,_Z);
TimerLaunch("FTJ_TransformPrisoner",4250);
CharacterSetAnimationOverride(CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,"cower");

IF
TextEventSet("tCR")
THEN
GlobalSetFlag("FTJ_CourtroomTransformPrisoner");

IF
TimerFinished("FTJ_TransformPrisoner")
AND
GetPosition(CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,_X,_Y,_Z)
THEN
CharacterSetAnimationOverride(CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,"");
Transform(CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,"QUEST_FTJ_Courtroom_SilentMonk_b0556648-ec3b-42e5-894f-838953f77dbd",0);
ApplyStatus(CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,"KNOCKED_DOWN",1.5,1);
SetCanFight(CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,1);
CharacterAddSkill(CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,"Jump_EnemyTacticalRetreat");
SetFaction(CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,"GLO_SilentMonks");
CharacterSetImmortal(CHARACTERGUID_S_FTJ_HighPriest_2a09f30c-0a3b-495f-8386-5390a6c4c08d,0);


IF
TimerFinished("FTJ_TransformPrisoner")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Purging");
CharacterAddAttitudeTowardsPlayer(CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,_Player,-100);
//SetRelationIndivFactionToPlayers(CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,0);

IF
ObjectTurnStarted(CHARACTERGUID_S_FTJ_HighPriest_2a09f30c-0a3b-495f-8386-5390a6c4c08d)
AND
GlobalGetFlag("FTJ_CourtroomTransformPrisoner",0)
AND
CharacterIsDead(CHARACTERGUID_S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,0)
THEN
GlobalSetFlag("FTJ_CourtroomTransformPrisoner");

//END_REGION

IF
DB_FTJ_ChapelMagisters((CHARACTERGUID)_Magister)
THEN
DB_SceneManager(_Magister,"FTJ_CourtRoomEvent");
DB_CrimeReaction_DoNotInterrogate(_Magister);
ProcCharacterEnableCrime(_Magister,"Assault");
ProcCharacterEnableCrime(_Magister,"Murder");

PROC 
Proc_SceneInterrupted(_NPC,_Player,"FTJ_CourtRoomEvent","Attacked")
THEN
GlobalSetFlag("FTJ_CourtroomTransformPrisoner");
ProcForceStopDialog(_NPC);

PROC 
Proc_SceneInterrupted(_NPC,_Player,"FTJ_CourtRoomEvent","PlayerTeleportInDialog")
THEN
GlobalSetFlag("FTJ_CourtroomTransformPrisoner");
ProcForceStopDialog(_NPC);


IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_FTJ_XP_CourtRoomQuarters_87e27e73-69ce-4a41-ad6f-6c04decc34e6)
AND
QRY_SpeakerIsAvailable(_Player)
AND
QueryOnlyOnce("FTJ_CourtRoomQuartersVB")
THEN
StartVoiceBark("FTJ_VB_CourtRoomQuarters",_Player);

//REGION Add Limbs on Death

IF
CharacterDying(S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487)
THEN
ItemTemplateAddTo("LOOT_Bodyparts_C_a3c85ced-1b60-4e91-930b-2d76109791b5",S_FTJ_CourtRoomPrisoner_44d58ae4-66ab-4dfb-9f1d-0dcd59a48487,1);

IF
CharacterDying(S_FTJ_HighPriest_2a09f30c-0a3b-495f-8386-5390a6c4c08d)
THEN
ItemTemplateAddTo("LOOT_Bodyparts_G_982bdb9f-0843-4e5f-a471-e1b0e9e0c7a5",S_FTJ_HighPriest_2a09f30c-0a3b-495f-8386-5390a6c4c08d,1);
//END_REGION

PROC
ProcRegionEnded("FJ_FortJoy_Main")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "FortJoy"
