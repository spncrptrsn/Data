Version 1
SubGoalCombiner SGC_AND
INITSECTION
//Loading Maps will remove players from DB_GLO_HoERespawnPoint
//Add to DB_GLO_MapStartsInHoE("Level_Name") to prevent this
KBSECTION
//REGION Respawn Mechanic

IF
DB_GLO_HoERespawnPoint(_Player,_)
THEN
ApplyStatus(_Player,"SPIRIT_VISION",-1.0,1);
CharacterEnableWaypointUsage(_Player,0);

IF
CharacterStatusRemoved(_Player,"SPIRIT_VISION",_)
AND
DB_GLO_HoERespawnPoint(_Player,_)
THEN
ApplyStatus(_Player,"SPIRIT_VISION",-1.0,1);

IF
DB_GLO_HoERespawnPoint(_Player,_NewSpawn)
AND
DB_GLO_HoERespawnPoint(_Player,_OldSpawn)
AND
_NewSpawn != _OldSpawn
THEN
NOT DB_GLO_HoERespawnPoint(_Player,_OldSpawn);


IF
CharacterDying(_Player)
AND
DB_GLO_HoERespawnPoint(_Player,_Trigger)
THEN
CreateSurface(_Trigger,"SurfaceNone",6.0,2.0);
CharacterDetachFromGroup(_Player);
FadeToBlack(_Player,1.5,0,"Placeholder");
ProcObjectTimer(_Player,"GLO_HoERespawn_FadeIn",4500);

//Do one-speaker dialog
IF
CharacterDying(_Player)
AND
DB_GLO_HoERespawnPoint(_Player,_Trigger)
THEN
ProcObjectTimer(_Player,"GLO_HoERespawn_DoDialog",7500);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"GLO_HoERespawn_FadeIn")
AND
DB_GLO_HoERespawnPoint(_Player,_Trigger)
THEN
TeleportTo(_Player,_Trigger);
CharacterResurrect(_Player);


PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"GLO_HoERespawn_DoDialog")
THEN
FadeToBlack(_Player,2.5,1,"Placeholder");
Proc_StartDialog(0,"LV_HoE_PlayerDied",_Player);

IF
TextEventSet("hoedeath")
AND
CharacterGetHostCharacter((CHARACTERGUID)_Player)
THEN
DB_GLO_HoERespawnPoint(_Player,TRIGGERGUID_S_LV_HoE_DeathFromAbove_StartPoint_9055a2d2-3e27-48a4-8ddb-217c3d1c8f17);


//Use this PROC whenever player leaves HoE for clean up

PROC
PROC_GLO_LeaveHallOfEchoes((CHARACTERGUID)_Player)
AND
DB_GLO_HoERespawnPoint(_Player,_Trigger)
THEN
NOT DB_GLO_HoERespawnPoint(_Player,_Trigger);

PROC
PROC_GLO_LeaveHallOfEchoes((CHARACTERGUID)_Player)
THEN
RemoveStatus(_Player,"SPIRIT_VISION");
CharacterEnableWaypointUsage(_Player,1);

//END_REGION

//REGION Respawn fix for applying the HoE state on the Same frame
IF
CharacterResurrected((CHARACTERGUID)_Player)
AND
DB_GLO_HoERespawnPoint(_Player,_Trigger)
THEN
NOT DB_GLO_HoERespawnPoint(_Player,_Trigger);
DB_GLO_HoERespawnPoint(_Player,_Trigger);


//END_REGION

IF
RegionStarted(_Level)
AND
NOT DB_GLO_MapStartsInHoE(_Level)
AND
StringConcatenate("RemoveHoEStateFromPlayers_",_Level,_OnceString)
AND
QueryOnlyOnce(_OnceString)
AND
DB_GLO_HoERespawnPoint(_Player,_)
THEN
PROC_GLO_LeaveHallOfEchoes(_Player);


IF
TextEventSet("HoE_FxTest")
AND
CharacterGetHostCharacter((CHARACTERGUID)_Player)
THEN
PlayEffect(_Player,"RS3_FX_GP_ScriptedEvent_HOE_Summoning_01");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start"
