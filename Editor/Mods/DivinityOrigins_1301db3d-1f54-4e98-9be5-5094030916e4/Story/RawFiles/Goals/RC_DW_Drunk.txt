Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(S_RC_DW_Drunk_ad6b6989-1fda-4202-9c46-3bd2f61a2ba5,"RC_DW_Drunk");
ProcDeclareCounter("RC_DW_Drunk_DrinkCount");


DB_QuestDef_AddEvent("RC_DW_SpidersKiss","RC_DW_Drunk_TellAboutSpider");
DB_QuestDef_UpdateEvent("RC_DW_SpidersKiss","DrunkElfRefSpider","RC_DW_Drunk_TellAboutSpider");
KBSECTION
//REGION Drinking With Safa
IF
ObjectFlagSet("RC_DW_Drunk_PlayerDrink1",(CHARACTERGUID)_Char,_ID)
THEN
proc_RC_DW_DrunkMakePlayerDrink(_Char,15.0);

IF
ObjectFlagSet("RC_DW_Drunk_PlayerDrink2",(CHARACTERGUID)_Char,_ID)
THEN
proc_RC_DW_DrunkMakePlayerDrink(_Char,30.0);

IF
ObjectFlagSet("RC_DW_Drunk_PlayerDrink3",(CHARACTERGUID)_Char,_ID)
THEN
proc_RC_DW_DrunkMakePlayerDrink(_Char,60.0);

PROC
proc_RC_DW_DrunkMakePlayerDrink((CHARACTERGUID)_Player,(REAL)_DrunkDuration)
AND
IsTagged(_Player,"UNDEAD",0)
THEN
RemoveStatus(_Player,"DRUNK");
ApplyStatus(_Player,"DRUNK",_DrunkDuration,-1);

PROC
proc_RC_DW_DrunkMakePlayerDrink((CHARACTERGUID)_Player,(REAL)_DrunkDuration)
THEN
ProcIncreaseCounter("RC_DW_Drunk_DrinkCount");
//END_REGION


//REGION Safa Drunk too Much

IF
DB_GlobalCounter("RC_DW_Drunk_DrinkCount",_Count)
AND
_Count >= 3
THEN
GlobalSetFlag("RC_DW_Drunk_HadTooManyDrinks");


IF
DialogEnded("RC_DW_Drunk",_ID)
AND
GlobalGetFlag("RC_DW_Drunk_HadTooManyDrinks",1)
AND
DB_DialogNPCs(_ID,S_RC_DW_Drunk_ad6b6989-1fda-4202-9c46-3bd2f61a2ba5,1)
THEN
RemoveStatus(S_RC_DW_Drunk_ad6b6989-1fda-4202-9c46-3bd2f61a2ba5,"DRUNK");
RemoveStatus(S_RC_DW_Drunk_ad6b6989-1fda-4202-9c46-3bd2f61a2ba5,"SITTING");
ApplyStatus(S_RC_DW_Drunk_ad6b6989-1fda-4202-9c46-3bd2f61a2ba5,"KNOCKED_DOWN",-1.0,-1);
ProcRemoveAllDialogEntriesForSpeaker(S_RC_DW_Drunk_ad6b6989-1fda-4202-9c46-3bd2f61a2ba5);
SetHasDialog(S_RC_DW_Drunk_ad6b6989-1fda-4202-9c46-3bd2f61a2ba5,1);

IF
CharacterStatusRemoved(S_RC_DW_Drunk_ad6b6989-1fda-4202-9c46-3bd2f61a2ba5,"KNOCKED_DOWN",_)
AND
GlobalGetFlag("RC_DW_Drunk_HadTooManyDrinks",1)
THEN
DB_Dialogs(S_RC_DW_Drunk_ad6b6989-1fda-4202-9c46-3bd2f61a2ba5,"RC_DW_Drunk");
GlobalClearFlag("RC_DW_Drunk_HadTooManyDrinks");

PROC
PROC_GLOBAL_DialogStartRequested(S_RC_DW_Drunk_ad6b6989-1fda-4202-9c46-3bd2f61a2ba5,_Player)
AND
GlobalGetFlag("RC_DW_Drunk_HadTooManyDrinks",1)
THEN
Proc_StartDialog(1,"RC_DW_AD_Drunk_DrankTooMuch",S_RC_DW_Drunk_ad6b6989-1fda-4202-9c46-3bd2f61a2ba5);
//END_REGION

//REGION Safa Is Dead 
IF
CharacterDied(S_RC_DW_Drunk_ad6b6989-1fda-4202-9c46-3bd2f61a2ba5)
THEN
GlobalSetFlag("RC_DW_Drunk_IsDead"); // Used in the innkeeper dialog To reference the elf
//END_REGION



IF
DialogEnded("RC_DW_Drunk",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,1)
AND
ObjectGetFlag(_Player,"RC_DW_Drunk_PlayerDrink1",1)
THEN
ObjectClearFlag(_Player,"RC_DW_Drunk_PlayerDrink1",_Inst);
ObjectClearFlag(_Player,"RC_DW_Drunk_PlayerDrink2",_Inst);
ObjectClearFlag(_Player,"RC_DW_Drunk_PlayerDrink3",_Inst);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
