Version 1
SubGoalCombiner SGC_AND
INITSECTION
//WIKI: https://sites.google.com/a/larian.com/projecttest/home/wiki-articles/quest-design-documentation/fort-joy---swamps---rivers-puddles

DB_FTJ_SW_RiverSurface(S_FTJ_SW_TriggerRiverSurface1_5a87ebf5-f787-4362-abb1-274868dd29ab, S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73);
DB_FTJ_SW_RiverSurface(TRIGGERGUID_S_FTJ_SW_TriggerRiverSurface2_5405fbf4-431a-4eba-af4a-d4c763fdde94, ITEMGUID_S_FTJ_SW_EmitterRiverSurface2_99edaf17-aa34-49f1-a14a-34240faea743);
DB_FTJ_SW_RiverSurface(TRIGGERGUID_S_FTJ_SW_TriggerRiverSurface3_d660aea6-0fb5-4d35-a24f-59ec31baad60, ITEMGUID_S_FTJ_SW_EmitterRiverSurface3_a7e0f165-5e53-41b2-bcc2-a4494105018e);
DB_FTJ_SW_RiverSurface(TRIGGERGUID_S_FTJ_SW_TriggerRiverSurface4_10a6a724-fd29-42ca-afb1-cdabcc945f73, ITEMGUID_S_FTJ_SW_EmitterRiverSurface4_6d45a715-09bd-4992-b0dd-8e5d23843a39);

DB_FTJ_SW_RiverSurfaceEncounter((ITEMGUID)ITEMGUID_S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73, (CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_000_d61a5845-383b-4759-9fe3-99f519dec4dc);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73, CHARACTERGUID_S_FTJ_VoidlingAmbush_001_eedb56aa-aad1-4de2-8097-3fd7241be1ec);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73, CHARACTERGUID_S_FTJ_VoidlingAmbush_002_a8318c72-e603-4a08-b01d-09232110bccc);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73, CHARACTERGUID_S_FTJ_VoidlingAmbush_003_53680e8b-a4ee-4b00-9419-3860e91e76e6);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73, CHARACTERGUID_S_FTJ_VoidlingAmbush_004_3fe3a69c-97b6-42d5-b1db-bc646a66ab15);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73, CHARACTERGUID_S_FTJ_VoidlingAmbush_005_03ed2bcc-3b3b-4e9c-bfd1-54c7f6a1bcaa);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73, CHARACTERGUID_S_FTJ_VoidlingAmbush_006_0cf5424e-2183-4c52-980e-de156c31f5e4);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73, CHARACTERGUID_S_FTJ_VoidlingAmbush_007_1aa2f181-c36b-4e9e-ae5e-9652fe038824);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73, CHARACTERGUID_S_FTJ_VoidlingAmbush_008_8c83992f-328d-405d-bebd-0f5461d027ad);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73, CHARACTERGUID_S_FTJ_VoidlingAmbush_009_811f904d-4822-463c-b77e-d658a0fb3380);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73, CHARACTERGUID_S_FTJ_VoidlingAmbush_010_7dfba778-1b94-4cf7-8b26-663dfcb760d3);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73, CHARACTERGUID_S_FTJ_VoidlingAmbush_011_a01a4838-c65a-452e-bde5-dc7b8e3dca27);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface1_fb67d25e-0e34-4c5e-9ac5-ca3aa1cd5f73, CHARACTERGUID_S_FTJ_VoidlingAmbush_012_360a68c3-e5f1-4834-aa9e-7dc7497d9301);

DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface2_99edaf17-aa34-49f1-a14a-34240faea743, CHARACTERGUID_S_FTJ_SwampBuildup_A_Undead_Assassin_a54a04a3-8507-4a37-a8b6-068fd0ec8146);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface2_99edaf17-aa34-49f1-a14a-34240faea743, CHARACTERGUID_S_FTJ_SwampBuildup_A_Undead_Melee_01_8b70b76c-24f8-4b3c-aae8-3c78c93ab2bb);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface2_99edaf17-aa34-49f1-a14a-34240faea743, CHARACTERGUID_S_FTJ_SwampBuildup_A_Undead_Pyro_01_7dee6a3d-ef4f-4281-a311-a65d483e13d1);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface2_99edaf17-aa34-49f1-a14a-34240faea743, CHARACTERGUID_S_FTJ_SwampBuildup_B_Undead_Melee_01_e45ec44b-4033-4994-b6a4-f236dea40561);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface2_99edaf17-aa34-49f1-a14a-34240faea743, CHARACTERGUID_S_FTJ_SwampBuildup_B_Undead_Ranger_01_1195a59b-ba51-4662-afa7-7602b224cfc8);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface2_99edaf17-aa34-49f1-a14a-34240faea743, CHARACTERGUID_S_FTJ_SwampBuildup_B_Undead_Ranger_02_5468e7d7-8f83-4245-94fc-7303c11612b5);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface2_99edaf17-aa34-49f1-a14a-34240faea743, CHARACTERGUID_S_FTJ_SwampBuildup_B_Undead_Sword_9ee2fb19-5483-42a5-9037-c8147e9695fd);

DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface3_a7e0f165-5e53-41b2-bcc2-a4494105018e, CHARACTERGUID_S_FTJ_SW_ShelterBackSalamander1_26d2a05f-bd32-408c-adab-c01767271bbf);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface3_a7e0f165-5e53-41b2-bcc2-a4494105018e, CHARACTERGUID_S_FTJ_SW_ShelterBackSalamander2_e3812c55-7530-4d74-b79b-e8f3c91558a4);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface3_a7e0f165-5e53-41b2-bcc2-a4494105018e, CHARACTERGUID_S_FTJ_SW_ShelterBackSalamander3_62ac9493-260e-40bf-a615-5cdf475208d9);

DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface4_6d45a715-09bd-4992-b0dd-8e5d23843a39, CHARACTERGUID_S_FTJ_SW_VWBoss_VoidWoken_112f8c17-ea77-4658-ac72-239154772fb8);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface4_6d45a715-09bd-4992-b0dd-8e5d23843a39, CHARACTERGUID_S_FTJ_SW_VWBoss_Mage_01_5cf41c21-bfed-499e-a6fe-6eda7c24b118);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface4_6d45a715-09bd-4992-b0dd-8e5d23843a39, CHARACTERGUID_S_FTJ_SW_VWBoss_Mage_02_2f619e60-5cfc-4323-a094-e285ea922903);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface4_6d45a715-09bd-4992-b0dd-8e5d23843a39, CHARACTERGUID_S_FTJ_SW_VWBoss_Melee_01_961c827b-43d1-43c8-8553-6d1d4c8e8aed);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface4_6d45a715-09bd-4992-b0dd-8e5d23843a39, CHARACTERGUID_S_FTJ_SW_VWBoss_Melee_02_8644ff57-7eb3-4ed7-a496-00e977227b53);
DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface4_6d45a715-09bd-4992-b0dd-8e5d23843a39, CHARACTERGUID_S_FTJ_SW_VWBoss_Ranger_01_e8ad5533-b8f0-4c55-a261-4192f5cf1e48);

DB_FTJ_SW_RiverSurfaceFlag("FTJ_SW_RiverSurface4", "FTJ_SW_RiverSurface4");
DB_FTJ_SW_RiverSurfaceFlag("FTJ_SW_RiverSurface3", "FTJ_SW_RiverSurface4");
DB_FTJ_SW_RiverSurfaceFlag("FTJ_SW_RiverSurface2", "FTJ_SW_RiverSurface3");
DB_FTJ_SW_RiverSurfaceFlag("FTJ_SW_RiverSurface1", "FTJ_SW_RiverSurface2");


DB_FTJ_SW_RiverSurfaceDialog("LOHSE", "FTJ_SW_AD_RiverSurfaceLohse");
DB_FTJ_SW_RiverSurfaceDialog("UNDEAD", "FTJ_SW_AD_RiverSurfaceUndead");
DB_FTJ_SW_RiverSurfaceDialog("MASKED_UNDEAD", "FTJ_SW_AD_RiverSurfaceUndead");
DB_FTJ_SW_RiverSurfaceDialog("SHAPESHIFT_LOHSE", "FTJ_SW_AD_RiverSurfaceLohse");
DB_FTJ_SW_RiverSurfaceDialog("REALLY_HUMAN", "FTJ_SW_AD_RiverSurfaceRhalic");
DB_FTJ_SW_RiverSurfaceDialog("REALLY_LIZARD", "FTJ_SW_AD_RiverSurfaceZorlStissa");
DB_FTJ_SW_RiverSurfaceDialog("REALLY_DWARF", "FTJ_SW_AD_RiverSurfaceDuna");
DB_FTJ_SW_RiverSurfaceDialog("REALLY_ELF", "FTJ_SW_AD_RiverSurfaceTirCendelius");
KBSECTION
IF
CharacterDied(_Creature)
AND
DB_FTJ_SW_RiverSurfaceEncounter(_Helper, _Creature)
THEN
Proc_FTJ_SW_RiverSurfaceCreatureDied(_Helper, _Creature);

PROC
Proc_FTJ_SW_RiverSurfaceCreatureDied((ITEMGUID)_Helper, (CHARACTERGUID)_Creature)
THEN
NOT DB_FTJ_SW_RiverSurfaceEncounter(_Helper, _Creature);

PROC
Proc_FTJ_SW_RiverSurfaceCreatureDied((ITEMGUID)_Helper, _)
AND
NOT DB_FTJ_SW_RiverSurfaceEncounter(_Helper, _)
AND
GetVarInteger(_Helper, "Active", 1)
THEN
SetVarInteger(_Helper, "Active", 2);

PROC
Proc_FTJ_SW_RiverSurfaceCreatureDied(ITEMGUID_S_FTJ_SW_EmitterRiverSurface4_6d45a715-09bd-4992-b0dd-8e5d23843a39, _)
AND
NOT DB_FTJ_SW_RiverSurfaceEncounter(ITEMGUID_S_FTJ_SW_EmitterRiverSurface4_6d45a715-09bd-4992-b0dd-8e5d23843a39, _)
THEN
GlobalSetFlag("FTJ_SW_VWBossEncounterDead");

//REGION Selecting optimal receiver of AD
IF
StoryEvent(_Helper, "FTJ_SW_PuddleAD")
AND
DB_IsPlayer(_Player)
AND
CharacterIsDeadOrFeign(_Player, 0)
AND
QRY_FTJ_SW_IsBetterPuddleCandidate(_Player, _Helper)
THEN
SetVarObject(_Helper, "TargetPC", _Player);

QRY
QRY_FTJ_SW_IsBetterPuddleCandidate((CHARACTERGUID)_Player, (ITEMGUID)_Helper)
AND
GetVarObject(_Helper, "TargetPC", _CurrentTarget)
AND
_CurrentTarget == NULL_00000000-0000-0000-0000-000000000000
THEN
DB_NOOP();

QRY
QRY_FTJ_SW_IsBetterPuddleCandidate((CHARACTERGUID)_Player, (ITEMGUID)_Helper)
AND
GetVarObject(_Helper, "TargetPC", _CurrentTarget)
AND
_CurrentTarget != NULL_00000000-0000-0000-0000-000000000000
AND
QRY_FTJ_SW_IsBetterPuddleCandidate_TooFarCheck((CHARACTERGUID)_Player, (CHARACTERGUID)_CurrentTarget, (ITEMGUID)_Helper) //Current Target is too far - not in combat or beyond max distance.
THEN
DB_NOOP();

QRY
QRY_FTJ_SW_IsBetterPuddleCandidate((CHARACTERGUID)_Player, (ITEMGUID)_Helper)
AND
GetVarObject(_Helper, "TargetPC", _CurrentTarget)
AND
_CurrentTarget != NULL_00000000-0000-0000-0000-000000000000
AND
NOT QRY_FTJ_SW_IsBetterPuddleCandidate_TooFarCheck((CHARACTERGUID)_Player, (CHARACTERGUID)_CurrentTarget, (ITEMGUID)_Helper) //Current target is not too far
AND
QRY_FTJ_SW_IsBetterPuddleCandidate_AvatarCheck((CHARACTERGUID)_Player, (CHARACTERGUID)_CurrentTarget, (ITEMGUID)_Helper) //But new target is an Avatar, and old one isn't.
THEN
DB_NOOP();

QRY
QRY_FTJ_SW_IsBetterPuddleCandidate((CHARACTERGUID)_Player, (ITEMGUID)_Helper)
AND
GetVarObject(_Helper, "TargetPC", _CurrentTarget)
AND
_CurrentTarget != NULL_00000000-0000-0000-0000-000000000000
AND
NOT QRY_FTJ_SW_IsBetterPuddleCandidate_TooFarCheck((CHARACTERGUID)_Player, (CHARACTERGUID)_CurrentTarget, (ITEMGUID)_Helper) //Current Target is not too far.
AND
NOT QRY_FTJ_SW_IsBetterPuddleCandidate_AvatarCheck((CHARACTERGUID)_Player, (CHARACTERGUID)_CurrentTarget, (ITEMGUID)_Helper) //New target has Avatar precedence
AND
NOT QRY_FTJ_SW_IsBetterPuddleCandidate_AvatarCheck((CHARACTERGUID)_CurrentTarget, (CHARACTERGUID)_Player, (ITEMGUID)_Helper) //Old target has Avatar precedence
AND
QRY_FTJ_SW_IsBetterPuddleCandidate_CloserCheck((CHARACTERGUID)_Player, (CHARACTERGUID)_CurrentTarget, (ITEMGUID)_Helper)
THEN
DB_NOOP();

QRY
QRY_FTJ_SW_IsBetterPuddleCandidate_TooFarCheck((CHARACTERGUID)_Player, (CHARACTERGUID)_CurrentTarget, (ITEMGUID)_Helper)
AND
DB_CombatObjects(_Helper, _ID)
AND
DB_CombatObjects(_Player, _ID)
AND
NOT DB_CombatObjects(_CurrentTarget, _ID)
THEN
DB_NOOP();

QRY
QRY_FTJ_SW_IsBetterPuddleCandidate_TooFarCheck((CHARACTERGUID)_Player, (CHARACTERGUID)_CurrentTarget, (ITEMGUID)_Helper)
AND
NOT DB_CombatObjects(_Helper, _)
AND
GetDistanceTo(_CurrentTarget, _Helper, _DistOld)
AND
_DistOld >= 10
AND
QRY_FTJ_SW_IsBetterPuddleCandidate_CloserCheck((CHARACTERGUID)_Player, (CHARACTERGUID)_CurrentTarget, (ITEMGUID)_Helper)
THEN
DB_NOOP();

QRY
QRY_FTJ_SW_IsBetterPuddleCandidate_AvatarCheck((CHARACTERGUID)_Player, (CHARACTERGUID)_CurrentTarget, (ITEMGUID)_Helper)
AND
IsTagged(_CurrentTarget, "AVATAR", 0)
AND
IsTagged(_Player, "AVATAR", 1)
THEN
DB_NOOP();

QRY
QRY_FTJ_SW_IsBetterPuddleCandidate_CloserCheck((CHARACTERGUID)_Player, (CHARACTERGUID)_CurrentTarget, (ITEMGUID)_Helper)
AND
GetDistanceTo(_Player, _Helper, _DistNew)
AND
GetDistanceTo(_CurrentTarget, _Helper, _DistOld)
AND
_DistOld > _DistNew
THEN
DB_NOOP();

//END_REGION

//REGION Finding out the number of this particular utterance.
IF
StoryEvent(_Helper, "FTJ_SW_PuddleAD")
THEN
NOT DB_OnlyOnce("FTJ_SW_PuddleAD");

IF
StoryEvent(_Helper, "FTJ_SW_PuddleAD")
AND
GetVarObject(_Helper, "TargetPC", _Player)
AND
DB_FTJ_SW_RiverSurfaceFlag(_OldFlag, _Newflag)
AND
PartyGetFlag((CHARACTERGUID)_Player, _OldFlag, 1)
AND
QueryOnlyOnce("FTJ_SW_PuddleAD")
THEN
PartySetFlag(_Player, _NewFlag, 1);
ObjectSetFlag(_Helper, _NewFlag, 0);

IF
StoryEvent(_Helper, "FTJ_SW_PuddleAD")
AND
GetVarObject(_Helper, "TargetPC", _Player)
AND
PartyGetFlag((CHARACTERGUID)_Player, "FTJ_SW_RiverSurface1", 0)
AND
QueryOnlyOnce("FTJ_SW_PuddleAD")
THEN
UserSetFlag(_Player, "FTJ_SW_RiverSurface1", 1);
ObjectSetFlag(_Helper, "FTJ_SW_RiverSurface1", 0);
//END_REGION

//REGION Selecting the correct dialogue
IF
StoryEvent(_Helper, "FTJ_SW_PuddleAD")
AND
GetVarObject(_Helper, "TargetPC", _Player)
AND
DB_FTJ_SW_RiverSurfaceDialog(_Tag, _Dialog)
AND
IsTagged((CHARACTERGUID)_Player, _Tag, 1)
AND
NOT DB_AlreadySpoke(_Helper)
THEN
Proc_StartDialog(1, _Dialog, _Helper);
DB_FTJ_SW_WaitingPuddleReaction(_Player);
DB_AlreadySpoke(_Helper);
//END_REGION

IF
AutomatedDialogEnded(_Dialog, _)
AND
DB_FTJ_SW_RiverSurfaceDialog(_, _Dialog)
AND
DB_FTJ_SW_WaitingPuddleReaction(_Player)
AND
CharacterIsInCombat((CHARACTERGUID)_Player, 0)
THEN
Proc_StartDialog(1, "FTJ_SW_AD_RiverSurfaceReaction", _Player);
NOT DB_FTJ_SW_WaitingPuddleReaction(_Player);

IF
ObjectTurnStarted(_Player)
AND
CharacterIsDeadOrFeign((CHARACTERGUID)_Player, 0)
AND
DB_FTJ_SW_WaitingPuddleReaction(_Player)
THEN
NOT DB_FTJ_SW_WaitingPuddleReaction(_Player);
Proc_StartDialog(1, "FTJ_SW_AD_RiverSurfaceReaction", _Player);

IF
ObjectLeftCombat(_Player, _)
AND
CharacterIsDeadOrFeign((CHARACTERGUID)_Player, 0)
AND
DB_FTJ_SW_WaitingPuddleReaction(_Player)
THEN
NOT DB_FTJ_SW_WaitingPuddleReaction(_Player);
Proc_StartDialog(1, "FTJ_SW_AD_RiverSurfaceReaction", _Player);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "FortJoy"
