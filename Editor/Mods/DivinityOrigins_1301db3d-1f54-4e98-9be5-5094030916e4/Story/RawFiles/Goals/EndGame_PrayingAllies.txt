Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_AppearingAllies((CHARACTERGUID)CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e, (TRIGGERGUID)TRIGGERGUID_S_EG_MaladyDestination_fedf586e-0f0b-48ae-b2d4-c686ee713a4a,"MaladyIsSpeaker");
DB_AppearingAllies(CHARACTERGUID_S_FTJ_SW_Warrior_SeekerCaptain_1329f018-23e4-4717-9bc8-074b28d04c54, TRIGGERGUID_S_EG_GarethDestination_dd172357-8f5e-4692-b5c9-d13689ead257,"GarethIsSpeaker");
DB_AppearingAllies(CHARACTERGUID_S_GLO_Tarquin_a3d0cdcc-d0b0-464d-9ad7-43b38baed620, TRIGGERGUID_S_EG_TarquinDestination_4f78e018-7e2c-4876-bd06-23f84c4af9ee,"TarquinIsSpeaker");
DB_AppearingAllies(CHARACTERGUID_S_GLO_Almira_49b466a9-e67f-4e3c-8f66-3636ba7dd54f,  TRIGGERGUID_S_EG_AlmiraDestination_0f9b61e5-ae8f-40ac-ac48-f6d516a74608,"AlmiraIsSpeaker");

ProcTriggerRegisterForPlayers(TRIGGERGUID_S_EG_Allies_6af19c86-ef2d-4dfa-b5a4-f3ddd3f9e685);

DB_EG_GlobalDialogs("EG_Shipmates");

ShutdownCrimeSystem();
Proc_PyramidGlobalBlockerOn();
ProcCancelAllRelationshipDialogsForAllCompanions();
PROC_EG_Init();
GlobalSetFlag("GLO_DisableRelationshipDialogsPermanently");
GlobalSetFlag("GLO_DisableReflectionsPermanently");

PROC_EG_CheckSilentMonkGareth();
KBSECTION
//REGION Debugevents
IF
TextEventSet("StartEG")
THEN
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_EG_Allies_6af19c86-ef2d-4dfa-b5a4-f3ddd3f9e685);
//END_REGION

//REGION Init
/*
PROC
PROC_EG_Init()
AND
GlobalGetFlag("RC_ARX_Sewers_Q_IsDead",0)
THEN
GlobalSetFlag("ARX_InitiateGenocide");
*/
PROC
PROC_EG_Init()
AND
GlobalGetFlag("ARX_InitiateGenocide_ToSea",0)
AND
GlobalGetFlag("ARX_InitiateGenocide_Destroy",0)
THEN
GlobalSetFlag("ARX_InitiateGenocide");


PROC
PROC_EG_Init()
AND
DB_IsPlayer(_Player)
THEN
PROC_BlockWaypointUsage(_Player);
Proc_ReflectionDialog_CancelForPlayer(_Player);
DB_EG_PlayerInEndGame(_Player);

PROC
PROC_EG_Init()
AND
DB_OriginInPartyDialog(_Companion,_Dialog)
THEN
NOT DB_OriginInPartyDialog(_Companion,_Dialog);
SetHasDialog(_Companion,0);

// We need in-party dialogs (even if only empty ones) because otherwise
// we cannot add characters back to the party
QRY
QRY_GLO_PartyMembers_GetInPartyDialog((CHARACTERGUID)_Origin)
AND
NOT DB_GLO_PartyMembers_InPartyDialog(_Origin,_)
THEN
DB_GLO_PartyMembers_InPartyDialog(_Origin,"");


PROC
PROC_EG_Init()
AND
DB_IsPlayer(_Player)
AND
NOT DB_Avatars(_Player)
THEN
ProcRemoveAllDialogEntriesForSpeaker(_Player);
DB_AD_Dialog(_Player, "GLO_AD_CompanionCantTalk");

IF
DB_IsPlayer(_Player)
AND
NOT IsTagged(_Player, "AVATAR", 1)
THEN
ProcRemoveAllDialogEntriesForSpeaker(_Player);
DB_AD_Dialog(_Player, "GLO_AD_CompanionCantTalk");
//END_REGION

//REGION Teleporting the Allies
IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_EG_Allies_6af19c86-ef2d-4dfa-b5a4-f3ddd3f9e685)
AND
DB_AppearingAllies(_Ally, _Trigger,_Event)
AND
NOT DB_Dead(_Ally)
AND
QRY_EG_ImportantNPCIsAllied(_Ally)
THEN
PROC_EG_EntryBoomOnce();
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_EG_Allies_6af19c86-ef2d-4dfa-b5a4-f3ddd3f9e685);
ProcRemoveAllDialogEntriesForSpeaker(_Ally);
SetHasDialog(_Ally,0);
TeleportTo(_Ally, TRIGGERGUID_S_ARX_EndGame_Enter_bd166e2a-7623-490e-94df-78079e7cbacc,"EG_AllyTeleported",0);
SetVarFixedString(_Ally,"currentState","");
CharacterSetFightMode(_Ally, 0, 0);
Foop(_Ally, "RS3_FX_GP_ScriptedEvent_ARX_EndingTeleport_01");
DB_EG_AppearingAllyInEG(_Ally,_Event);
SetVarInteger(_Ally,"bool_CanCower",0);
SetVarInteger(_Ally,"bool_CanFleeWhileCowering",0);

PROC
PROC_EG_EntryBoomOnce()
AND
QueryOnlyOnce("EG_BOOMBOOMBOOM")
THEN
// Not in the init section, crashes the game
// On on GameStarted, crashes the editor
CharacterSetCustomName(CHARACTERGUID_S_GLO_Braccus_358ba541-0710-43be-8263-87afe1a8d9b5, "GLO_Vredeman");
PlayEffect(TRIGGERGUID_S_ARX_EndGame_Enter_bd166e2a-7623-490e-94df-78079e7cbacc,"RS3_FX_GP_ScriptedEvent_ARX_Ending_Boom_01");
PROC_EG_EntryKnockDownPlayers();

PROC
PROC_EG_EntryKnockDownPlayers()
AND
DB_IsPlayer(_Player)
AND
NOT DB_Dead(_Player)
THEN
DB_EG_PlayerKnockedDown(_Player);
ApplyStatus(_Player,"KNOCKED_DOWN",3.0,1);

IF
StoryEvent((CHARACTERGUID)_Ally,"EG_AllyTeleported")
AND
DB_AppearingAllies(_Ally, _Trigger,_)
THEN
ProcCharacterMoveTo(_Ally,_Trigger,0,"EG_AllyArrived");

IF
StoryEvent((CHARACTERGUID)_Ally,"EG_AllyArrived")
AND
DB_AppearingAllies(_Ally, _Trigger,_)
THEN
CharacterLookFromTrigger(_Ally,_Trigger,0);

QRY
QRY_EG_ImportantNPCIsAllied((CHARACTERGUID)CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e)
THEN
DB_NOOP(0);

QRY
QRY_EG_ImportantNPCIsAllied(CHARACTERGUID_S_GLO_Jahan_4d222cfd-49c8-41d3-8cb7-2a2b56232633)
THEN
DB_NOOP(0);

QRY
QRY_EG_ImportantNPCIsAllied(CHARACTERGUID_S_FTJ_SW_Warrior_SeekerCaptain_1329f018-23e4-4717-9bc8-074b28d04c54)
AND
DB_GlobalFlag("CoS_GarethRevenge_Resolution_Peaceful")
THEN
DB_NOOP(0);

QRY
QRY_EG_ImportantNPCIsAllied(CHARACTERGUID_S_GLO_Tarquin_a3d0cdcc-d0b0-464d-9ad7-43b38baed620)
THEN
DB_NOOP(0);

QRY
QRY_EG_ImportantNPCIsAllied(CHARACTERGUID_S_GLO_Almira_49b466a9-e67f-4e3c-8f66-3636ba7dd54f)
AND
DB_GlobalFlag("GLO_Almira_ReceivedSwornbreaker")
THEN
DB_NOOP(0);
//END_REGION

//REGION Start the dialog
IF
CharacterStatusRemoved(_Player, "RESTED", _)
AND
DB_EG_PlayerKnockedDown(_Player)
THEN
ApplyStatus(_Player,"KNOCKED_DOWN",3.0,1);

IF
CharacterStatusRemoved(_Player,"KNOCKED_DOWN",_)
AND
DB_EG_PlayerKnockedDown(_Player)
THEN
NOT DB_EG_PlayerKnockedDown(_Player);
PROC_EG_StartDialogIfAllGottenUp();

PROC
PROC_EG_StartDialogIfAllGottenUp()
AND
NOT DB_EG_PlayerKnockedDown(_)
AND
DB_IsPlayer(_Player)
THEN
SetStoryEvent(_Player,"EG_AllyStartDialog");

IF
StoryEvent(_,"EG_AllyStartDialog")
THEN
TimerLaunch("EG_AllyTeleported_CodeNeedsAFrame",10);

IF
TimerFinished("EG_AllyTeleported_CodeNeedsAFrame")
AND
DB_EG_AppearingAllyInEG(_Ally,_Event)
AND
DB_Avatars(_Player)
AND
CharacterIsDead(_Player,0)
AND
QueryOnlyOnce("EG_StartAllyDialog")
THEN
ObjectSetFlag(_Ally,_Event);
Proc_StartDialog(0,"EG_Shipmates",_Ally,NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,_Player);

//Fallback ANY player (companion/henchman if all avatars are dead)
IF
TimerFinished("EG_AllyTeleported_CodeNeedsAFrame")
AND
DB_EG_AppearingAllyInEG(_Ally,_Event)
AND
DB_IsPlayer(_Player)
AND
CharacterIsDead(_Player,0)
AND
QueryOnlyOnce("EG_StartAllyDialog")
THEN
ObjectSetFlag(_Ally,_Event);
Proc_StartDialog(0,"EG_Shipmates",_Ally,NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,_Player);

IF
DialogStarted("EG_Shipmates",_ID)
THEN
TimerLaunch("EG_CodeWorkAroundForDialogStart_EG_Shipmates",100);

IF
TimerFinished("EG_CodeWorkAroundForDialogStart_EG_Shipmates")
AND
DB_DialogName("EG_Shipmates",_ID)
AND
DB_EG_AppearingAllyInEG(CHARACTERGUID_S_FTJ_SW_Warrior_SeekerCaptain_1329f018-23e4-4717-9bc8-074b28d04c54,_)
AND
NOT DB_DialogNPCs(_ID,CHARACTERGUID_S_FTJ_SW_Warrior_SeekerCaptain_1329f018-23e4-4717-9bc8-074b28d04c54,1)
THEN
DialogAddActorAt(_ID,CHARACTERGUID_S_FTJ_SW_Warrior_SeekerCaptain_1329f018-23e4-4717-9bc8-074b28d04c54,2);

IF
TimerFinished("EG_CodeWorkAroundForDialogStart_EG_Shipmates")
AND
DB_DialogName("EG_Shipmates",_ID)
AND
DB_EG_AppearingAllyInEG(CHARACTERGUID_S_GLO_Tarquin_a3d0cdcc-d0b0-464d-9ad7-43b38baed620,_)
AND
NOT DB_DialogNPCs(_ID,CHARACTERGUID_S_GLO_Tarquin_a3d0cdcc-d0b0-464d-9ad7-43b38baed620,1)
THEN
DialogAddActorAt(_ID,CHARACTERGUID_S_GLO_Tarquin_a3d0cdcc-d0b0-464d-9ad7-43b38baed620,3);

IF
TimerFinished("EG_CodeWorkAroundForDialogStart_EG_Shipmates")
AND
DB_DialogName("EG_Shipmates",_ID)
AND
DB_EG_AppearingAllyInEG(CHARACTERGUID_S_GLO_Almira_49b466a9-e67f-4e3c-8f66-3636ba7dd54f,_)
AND
NOT DB_DialogNPCs(_ID,CHARACTERGUID_S_GLO_Almira_49b466a9-e67f-4e3c-8f66-3636ba7dd54f,1)
THEN
DialogAddActorAt(_ID,CHARACTERGUID_S_GLO_Almira_49b466a9-e67f-4e3c-8f66-3636ba7dd54f,4);
//END_REGION

//REGION Praying after the dialog
IF
DialogEnded("EG_Shipmates",_)
AND
DB_EG_AppearingAllyInEG(_Ally,_)
AND
DB_AppearingAllies(_Ally, _Trigger,_)
THEN
DB_EG_AllyIsPraying(_Ally);
ProcRemoveAllDialogEntriesForSpeaker(_Ally);
CharacterPurgeQueue(_Ally);
CharacterSetAnimationOverride(_Ally,"skill_prepare_totem_01_loop");
PROC_LoopEffect("RS3_FX_GP_ScriptedEvent_ChanelledSource_01",_Ally,"EG_SourcePrayerEffect","ARX_Endgame","");
CharacterLookFromTrigger(_Ally,_Trigger,0);

IF
DialogEnded("EG_Shipmates",_)
THEN
TimerLaunch("EG_ReveredStatus",2000);
PROC_LoopEffect("RS3_FX_GP_ScriptedEvent_ChanelledSourceOrb_01",TRIGGERGUID_S_EG_SourceOrb_2e22ed4d-94d5-4955-8665-ae4327e026eb,"EG_SourcePrayerEffectBall","ARX_Endgame","");

IF
TimerFinished("EG_ReveredStatus")
AND
DB_IsPlayer(_Player)
THEN
CharacterAddSourcePoints(_Player,3);
ApplyStatus(_Player,"QUEST_REVERED",-1.0);
ObjectSetFlag(_Player, "EG_HasQuest_Revered", 0);
//END_REGION

//REGION Attacking an Ally
IF
ObjectEnteredCombat(_Ally,_)
AND
DB_EG_AppearingAllyInEG((CHARACTERGUID)_Ally,_)
AND
QueryOnlyOnce("EG_PrayingAlliesLeave")
THEN
PROC_EG_PoofAllies();

IF
AttackedByObject(_Ally,_,_Attacker,_,_)
AND
_Ally != _Attacker
AND
DB_EG_AppearingAllyInEG((CHARACTERGUID)_Ally,_)
AND
QueryOnlyOnce("EG_PrayingAlliesLeave")
THEN
PROC_EG_PoofAllies();

PROC
PROC_EG_PoofAllies()
AND
DB_EG_AppearingAllyInEG(_Ally,_)
THEN
PROC_StopLoopEffect(_Ally,"EG_SourcePrayerEffect");
Poof(_Ally,"RS3_FX_GP_ScriptedEvent_ARX_EndingTeleport_01");

PROC
PROC_EG_PoofAllies()
THEN
PROC_StopLoopEffect(TRIGGERGUID_S_EG_SourceOrb_2e22ed4d-94d5-4955-8665-ae4327e026eb,"EG_SourcePrayerEffectBall");

PROC
PROC_EG_PoofAllies()
AND
DB_IsPlayer(_Player)
THEN
RemoveStatus(_Player,"QUEST_REVERED");
//END_REGION


//REGION When you get resurrected, get your status back.
IF
CharacterResurrected(_Player)
AND
ObjectGetFlag(_Player, "EG_HasQuest_Revered", 1)
AND
NOT DB_EG_EpilogueSetupStarted(1)
THEN
ApplyStatus(_Player,"QUEST_REVERED",-1.0);
//END_REGION

//REGION When you get resurrected, get your status back.
IF
CharacterStatusApplied(_Player, "DRAIN", _)
AND
HasActiveStatus(_Player, "QUEST_REVERED", 1)
THEN
ProcObjectTimer(_Player, "EG_RestoreMissingSP", 750);

PROC
ProcObjectTimerFinished(_Player, "EG_RestoreMissingSP")
AND
HasActiveStatus(_Player, "QUEST_REVERED", 1)
THEN
CharacterAddSourcePoints((CHARACTERGUID)_Player, 3);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "EndGame"
