Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_KilledEvent(CHARACTERGUID_S_ARX_EG_Lucian_62d0ca66-b6db-4a10-a457-effe0b64aca0,"QuestUpdate_FTJ_Ifan_DarkFaction_KilledLucianCrypt");

//REGION Crypt Denied/Allowed
DB_CompanionCryptDenied((CHARACTERGUID)CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"QuestUpdate_FTJ_COM_Ifan_Crypt_Denied");
DB_CompanionCryptDenied((CHARACTERGUID)CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,"QuestUpdate_FTJ_COM_Beast_Crypt_Denied");
DB_CompanionCryptDenied((CHARACTERGUID)CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,"QuestUpdate_FTJ_COM_Lohse_Crypt_Denied");
DB_CompanionCryptDenied((CHARACTERGUID)CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f,"QuestUpdate_FTJ_COM_RedPrince_Crypt_Denied");
DB_CompanionCryptDenied((CHARACTERGUID)CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,"QuestUpdate_FTJ_COM_Sebille_Crypt_Denied");
DB_CompanionCryptDenied((CHARACTERGUID)CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb,"QuestUpdate_FTJ_COM_Fane_Crypt_Denied");

DB_CompanionCryptAllowed((CHARACTERGUID)CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"QuestUpdate_FTJ_COM_Ifan_Crypt_Allowed");
DB_CompanionCryptAllowed((CHARACTERGUID)CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,"QuestUpdate_FTJ_COM_Beast_Crypt_Allowed");
DB_CompanionCryptAllowed((CHARACTERGUID)CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,"QuestUpdate_FTJ_COM_Lohse_Crypt_Allowed");
DB_CompanionCryptAllowed((CHARACTERGUID)CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f,"QuestUpdate_FTJ_COM_RedPrince_Crypt_Allowed");
DB_CompanionCryptAllowed((CHARACTERGUID)CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,"QuestUpdate_FTJ_COM_Sebille_Crypt_Allowed");
DB_CompanionCryptAllowed((CHARACTERGUID)CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb,"QuestUpdate_FTJ_COM_Fane_Crypt_Allowed");
//END_REGION
//REGION EG

//DB_EG_GlobalFlagEndingUpdate(_GlobalFlag,_EGUpdate,_FTJ_GodwokenUpdate);
DB_EG_GlobalFlagEndingUpdate("EG_Ending1_NewDivine","QuestUpdate_EG_Ending1","QuestUpdate_FTJ_Godwoken_DivinityAccept_End1");
DB_EG_GlobalFlagEndingUpdate("EG_Ending2_SourceForAll","QuestUpdate_EG_Ending2","QuestUpdate_FTJ_Godwoken_DivinityAccept_End2");
DB_EG_GlobalFlagEndingUpdate("EG_Ending4_GodKing","QuestUpdate_EG_Ending4","QuestUpdate_FTJ_Godwoken_DivinityAccept_End4");
DB_EG_GlobalFlagEndingUpdate("EG_Ending_5_Demon","QuestUpdate_EG_Ending5","QuestUpdate_FTJ_Godwoken_DivinityAccept_End5");

DB_EG_GlobalFlagEndingThreeUpdate(0,"QuestUpdate_EG_Ending3_NoLucian","QuestUpdate_FTJ_Godwoken_DivinityAccept_End3_NoLucian");
DB_EG_GlobalFlagEndingThreeUpdate(1,"QuestUpdate_EG_Ending3","QuestUpdate_FTJ_Godwoken_DivinityAccept_End3");

//No Divinity
DB_EG_EndingNoDivinity((CHARACTERGUID)CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"");
DB_EG_EndingNoDivinity((CHARACTERGUID)CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,"");
DB_EG_EndingNoDivinity((CHARACTERGUID)CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,"");
DB_EG_EndingNoDivinity((CHARACTERGUID)CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f,"");
DB_EG_EndingNoDivinity((CHARACTERGUID)CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,"");
DB_EG_EndingNoDivinity((CHARACTERGUID)CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb,"");

DB_EG_AvatarUpdateEndingAppend("EG_Ending1_NewDivine_ObjFlag","_ChoseToAscend");
DB_EG_AvatarUpdateEndingAppend("EG_Ending2_SourceForAll_ObjFlag","_ChoseSource4All");
DB_EG_AvatarUpdateEndingAppend("EG_Ending3_Purge_ObjFlag","_ChoseLucian");
DB_EG_AvatarUpdateEndingAppend("EG_Ending4_GodKing_ObjFlag","_ChoseGodKing");
DB_EG_AvatarUpdateEndingAppend("EG_Ending_5_Demon_ObjFlag","_ChoseDemon");
DB_EG_AvatarUpdateEndingAppend("EG_SidedWithDoctor","_ChoseDemon");


DB_EG_AvatarQuest((CHARACTERGUID)CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"QuestUpdate_FTJ_Ifan_DarkFaction");
DB_EG_AvatarQuest((CHARACTERGUID)CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,"QuestUpdate_FTJ_OriginBeast");
DB_EG_AvatarQuest((CHARACTERGUID)CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,"QuestUpdate_FTJ_OriginSebille");
DB_EG_AvatarQuest((CHARACTERGUID)CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f,"QuestUpdate_FTJ_OriginRedPrince");
DB_EG_AvatarQuest((CHARACTERGUID)CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb,"QuestUpdate_FTJ_OriginFane");
DB_EG_AvatarQuest((CHARACTERGUID)CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,"QuestUpdate_FTJ_OriginLohse");


DB_EG_PurgeEndingCOREQuestUpdates(0, "QuestUpdate_CORE_Epilogue_Endgame3_User_NoLucian", "QuestUpdate_CORE_Epilogue_Endgame3_Companion_NoLucian");
DB_EG_PurgeEndingCOREQuestUpdates(1, "QuestUpdate_CORE_Epilogue_Endgame3_User", "QuestUpdate_CORE_Epilogue_Endgame3_Companion");




//END_REGION
KBSECTION
//REGION Crypt Denied/Allowed
//We only conclusively know that the Avatar chose to stand against his companions if there's an arena fight
//Before that, it is possible that the Avatar stood aside and let their companions decide by themselves,
//which would make the _Divinity flag applicable. 
//It might also be the case that several companions compete for Divinity, so we have to check that their avatar
//(who should be the only eprson with their quest) is also a competitor.
PROC
PROC_EG_LaunchFinalArena()
AND
DB_EG_DivinityCompetitor(_Companion)
AND
DB_CompanionCryptDenied(_Companion,_Update)
AND
DB_IsPlayer(_Player)
AND
DB_EG_DivinityCompetitor(_Player)
THEN
UserSetFlag(_Player,_Update);

IF
ObjectFlagSet("EG_Companion_Crypt_Allowed",(CHARACTERGUID)_Companion,_)
AND
DB_CompanionCryptAllowed(_Companion,_Update)
THEN
UserSetFlag(_Companion,_Update);
//END_REGION

//REGION EG - PARTY
PROC
ProcRegionStarted("ARX_EndGame")
AND
DB_IsPlayer(_Player)
THEN
PartySetFlag(_Player,"QuestUpdate_EG_EnterTomb");


//Object flags
IF
ObjectFlagSet("ARX_PDD_EndGame_SideLucian_No", _Player, _)
AND
// Not all players may be in the same party, and flag is set only on the player that made the decision
DB_IsPlayer(_AnyPlayer)
THEN
PartySetFlag(_AnyPlayer, "QuestUpdate_EG_RefusePurge");

IF
ObjectFlagSet("ARX_PDD_EndGame_SideLucian_Yes", _Player, _)
AND
DB_IsPlayer(_AnyPlayer)
THEN
PartySetFlag(_AnyPlayer, "QuestUpdate_EG_AcceptPurge");

IF
DialogStarted("EG_PrisonersDilemma",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,_)
THEN
PartySetFlag((CHARACTERGUID)_Player,"QuestUpdate_EG_DivineElection");


// Global flags
IF
GlobalFlagSet("EG_PromisedFight")
AND
DB_IsPlayer(_Player)
THEN
PartySetFlag((CHARACTERGUID)_Player,"QuestUpdate_EG_SwornAttack");

IF
GlobalFlagSet("EG_ConvincedBraccus")
AND
DB_IsPlayer(_Player)
THEN
PartySetFlag((CHARACTERGUID)_Player,"QuestUpdate_EG_BraccusPersuaded");


IF
GlobalFlagSet("EndFight_BraccusTransform")
AND
DB_DialogPlayers(_Inst,_Player,_)
AND
PartyGetFlag((CHARACTERGUID)_Player,"QuestUpdate_EG_RefusePurge",0)
THEN
PartySetFlag((CHARACTERGUID)_Player,"QuestUpdate_EG_BraccusBetrayal_Purge");

IF
GlobalFlagSet("EndFight_BraccusTransform")
AND
DB_DialogPlayers(_Inst,_Player,_)
AND
PartyGetFlag((CHARACTERGUID)_Player,"QuestUpdate_EG_RefusePurge",1)
THEN
PartySetFlag((CHARACTERGUID)_Player,"QuestUpdate_EG_BraccusBetrayal");


PROC
PROC_EG_GiveFinalJournalUpdates()
AND
DB_EG_GlobalFlagEndingUpdate(_GlobalFlag,_EGUpdate,_)
AND
GlobalGetFlag(_GlobalFlag,1)
AND
DB_IsPlayer(_Player)
THEN
PartySetFlag(_Player,_EGUpdate);

PROC
PROC_EG_GiveFinalJournalUpdates()
AND
GlobalGetFlag("EG_Ending3_Purge",1)
AND
DB_IsPlayer(_Player)
AND
PartyGetFlag(_Player,"QuestUpdate_EG_AcceptPurge",_SidedWithLucian)
AND
DB_EG_GlobalFlagEndingThreeUpdate(_SidedWithLucian,_EGUpdate, _)
AND
DB_IsPlayer(_Player)
THEN
PartySetFlag(_Player,_EGUpdate);


//END_REGION
//REGION FTJ_Godwoken - CHARACTER

IF
DialogStarted("EG_LucianDallisBraccus",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,_)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Godwoken_DallisCrypt");

PROC
PROC_EG_GiveFinalJournalUpdates()
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player,"AVATAR",1)
AND
ObjectGetFlag(_Player,"EG_TheChoice_MadeIt",0)
AND
ObjectGetFlag(_Player,"EG_Ending4_GodKing_ObjFlag",0)
AND
DB_EG_GlobalFlagEndingUpdate(_GlobalFlag,_,_)
AND
GlobalGetFlag(_GlobalFlag,1)
THEN
ObjectSetFlag(_Player,"FTJ_Godwoken_DivinityFail");


PROC
PROC_EG_GiveFinalJournalUpdates()
AND
DB_EG_Decider(_Player)
AND
IsTagged(_Player,"AVATAR",1)
AND
DB_EG_GlobalFlagEndingUpdate(_GlobalFlag,_,_FTJ_GodwokenUpdate)
AND
GlobalGetFlag(_GlobalFlag,1)
AND
NOT Qry_EG_Ending4_GodKing_AvatarFaneChose()
THEN
ObjectSetFlag(_Player,_FTJ_GodwokenUpdate);

PROC
PROC_EG_GiveFinalJournalUpdates()
AND
DB_EG_Decider(_Player)
AND
IsTagged(_Player,"AVATAR",1)
AND
GlobalGetFlag("EG_Ending3_Purge",1)
AND
PartyGetFlag(_Player,"QuestUpdate_EG_AcceptPurge",_SidedWithLucian)
AND
DB_EG_GlobalFlagEndingThreeUpdate(_SidedWithLucian,_,_FTJ_GodwokenUpdate)
THEN
ObjectSetFlag(_Player,_FTJ_GodwokenUpdate);


// It seems only Fane can explicitely chose the God King ending
QRY
Qry_EG_Ending4_GodKing_AvatarFaneChose()
AND
DB_GlobalFlag("EG_Ending4_GodKing")
AND
ObjectGetFlag(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb,"EG_Ending4_GodKing_ObjFlag",1)
AND
IsTagged(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb,"AVATAR",1)
THEN
DB_NOOP(1);


//END_REGION
//REGION CHARACTER quest origins - endings
QRY
QRY_EG_OriginFinalJournalUpdateAppend((CHARACTERGUID)_Player,(STRING)_ChoiceFlag,(STRING)_Append)
AND
DB_EG_OriginFinalJournalUpdateAppend(_Append)
THEN
NOT DB_EG_OriginFinalJournalUpdateAppend(_Append);

QRY
QRY_EG_OriginFinalJournalUpdateAppend(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb,"EG_Ending1_NewDivine_ObjFlag",(STRING)_Append)
AND
GlobalGetFlag("EG_Ending1A_EternalsReturn", 1)
THEN
DB_EG_OriginFinalJournalUpdateAppend("_ChoseToAscend_Fane");

QRY
QRY_EG_OriginFinalJournalUpdateAppend((CHARACTERGUID)_Player,(STRING)_ChoiceFlag,(STRING)_Append)
AND
NOT DB_EG_OriginFinalJournalUpdateAppend(_)
THEN
DB_EG_OriginFinalJournalUpdateAppend(_Append);


PROC
PROC_EG_GiveFinalJournalUpdates()
AND
NOT DB_GlobalFlag("EG_Ending4_GodKing")
AND
DB_EG_Decider(_Player)
AND
IsTagged(_Player,"AVATAR",1)
AND
DB_EG_AvatarQuest(_Player,_Quest)
AND
DB_EG_AvatarUpdateEndingAppend(_ChoiceFlag,_Append)
AND
ObjectGetFlag(_Player,_ChoiceFlag,1)
AND
QRY_EG_OriginFinalJournalUpdateAppend(_Player,_ChoiceFlag,_Append)
AND
DB_EG_OriginFinalJournalUpdateAppend(_NewAppend)
AND
StringConcatenate(_Quest,_NewAppend,_NewUpdate)
THEN
ObjectSetFlag(_Player,_NewUpdate);



// giving the ChoseGodKing update to all avatar if the God King won and they let happen
PROC
PROC_EG_GiveFinalJournalUpdates()
AND
DB_GlobalFlag("EG_Ending4_GodKing")
AND
DB_IsPlayer(_Player)
AND
_Player != CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb
AND
IsTagged(_Player,"AVATAR",1)
AND
ObjectGetFlag(_Player,"EG_PlayerChoseNoDivinity",1)
AND
ObjectGetFlag(_Player,"EG_Ending4_GodKing_ObjFlag",0) //give the correct update to Fane
AND
DB_EG_AvatarQuest(_Player,_Quest)
AND
DB_EG_AvatarUpdateEndingAppend("EG_Ending4_GodKing_ObjFlag",_Append)
AND
StringConcatenate(_Quest,_Append,_Update)
THEN
ObjectSetFlag(_Player,_Update);

PROC
PROC_EG_GiveFinalJournalUpdates()
AND
DB_IsPlayer(_Player)
AND
DB_EG_FirstFightKickedPromisePlayer(_Player)
AND
NOT DB_Dead(_Player)
AND
DB_GlobalFlag("EG_Ending4_GodKing")
AND
DB_EG_AvatarQuest(_Player,_Quest)
AND
StringConcatenate(_Quest,"_ChoseGodKing",_Update)
THEN
ObjectSetFlag(_Player,_Update);

PROC
PROC_EG_GiveFinalJournalUpdates()
AND
DB_IsPlayer(_Player)
AND
DB_EG_DemonDealer(_Player)
AND
DB_GlobalFlag("EG_Ending_5_Demon")
AND
DB_EG_AvatarQuest(_Player,_Quest)
AND
StringConcatenate(_Quest,"_ChoseDemon",_Update)
THEN
ObjectSetFlag(_Player,_Update);

PROC
PROC_EG_GiveFinalJournalUpdates()
AND
Qry_EG_Ending4_GodKing_AvatarFaneChose()
THEN
ObjectSetFlag(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb,"QuestUpdate_FTJ_OriginFane_ChoseGodKing");
ObjectSetFlag(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb,"QuestUpdate_FTJ_Godwoken_DivinityAccept_End4");

//END_REGION
//REGION Core story - CORE_Chapter7


PROC
PROC_EG_GiveFinalJournalUpdates()
AND
DB_GlobalFlag("EG_PromisedFight")
AND
DB_OnlyOnce("The Promised won. The Eighth takes over. The End.")
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"EG_PromisedFight_ObjFlag",1)
THEN
UserSetFlag((CHARACTERGUID)_Player,"Quest_Update_CORE_Chapter7_CRYPT_End_TakeSwornWon");

PROC
PROC_EG_GiveFinalJournalUpdates()
AND
DB_GlobalFlag("EG_PromisedFight")
AND
DB_EG_FirstFightKickedPromisePlayer(_Player)
THEN
UserSetFlag((CHARACTERGUID)_Player,"CORE_Chapter7_CRYPT_End_TakeSwornFail");

PROC
PROC_EG_GiveFinalJournalUpdates()
AND
DB_GlobalFlag("EG_KillThePromised")
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"EG_KillPromised_ObjFlag",1)
THEN
UserSetFlag((CHARACTERGUID)_Player,"Quest_Update_CORE_Chapter7_CRYPT_End_TakeSwornRefused");


//END_REGION
//REGION Core story - CORE_Epilogue

//ENDING 1
IF
ObjectFlagSet("EG_Ending1_NewDivine_ObjFlag",(CHARACTERGUID)_Player,_)
AND
GlobalGetFlag("EG_Ending1A_EternalsReturn",0)
THEN
UserSetFlag(_Player,"QuestUpdate_CORE_Epilogue_Endgame1_User");
Proc_EG_CORE_Epilogue_GiveOtherUserUpdate(_Player,"QuestUpdate_CORE_Epilogue_Endgame1_Companion");

IF
ObjectFlagSet("EG_Ending1_NewDivine_ObjFlag",(CHARACTERGUID)_Player,_)
AND
GlobalGetFlag("EG_Ending1A_EternalsReturn",1)
THEN
UserSetFlag(_Player,"QuestUpdate_CORE_Epilogue_Endgame1_Fane");
Proc_EG_CORE_Epilogue_GiveOtherUserUpdate(_Player,"QuestUpdate_CORE_Epilogue_Endgame1_Companion");


//ENDING 2
IF
ObjectFlagSet("EG_Ending2_SourceForAll_ObjFlag",(CHARACTERGUID)_Player,_)
THEN
UserSetFlag(_Player,"QuestUpdate_CORE_Epilogue_Endgame2_User");
Proc_EG_CORE_Epilogue_GiveOtherUserUpdate(_Player,"");


//ENDING 3
IF
ObjectFlagSet("EG_Ending3_Purge_ObjFlag",(CHARACTERGUID)_Player,_)
AND
PartyGetFlag(_Player, "QuestUpdate_EG_AcceptPurge", _SidedWithLucian)
AND
DB_EG_PurgeEndingCOREQuestUpdates(_SidedWithLucian, _UserUpdate, _CompanionUpdate)
THEN
UserSetFlag(_Player, _UserUpdate);
Proc_EG_CORE_Epilogue_GiveOtherUserUpdate(_Player, _CompanionUpdate);


PROC
Proc_EG_CORE_Epilogue_GiveOtherUserUpdate((CHARACTERGUID)_Player,(STRING)_Update)
AND
CharacterGetReservedUserID(_Player,_UserID)
AND
DB_IsPlayer(_OtherChar)
AND
_Player != _OtherChar
AND
CharacterGetReservedUserID(_OtherChar,_OtherUserID)
AND
_UserID != _OtherUserID
THEN
UserSetFlag(_OtherChar,_Update);


//ENDING 4
IF
GlobalFlagSet("EG_Ending4_GodKing")
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player,"PROMISED",1)
THEN
UserSetFlag(_Player,"QuestUpdate_CORE_Epilogue_Endgame4_Sworn");

IF
GlobalFlagSet("EG_Ending4_GodKing")
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player,"PROMISED",0)
AND
UserGetFlag(_Player,"QuestUpdate_CORE_Epilogue_Endgame4_Sworn",0)
THEN
UserSetFlag(_Player,"QuestUpdate_CORE_Epilogue_Endgame4_NotSworn");


//ENDING 5
IF
GlobalFlagSet("EG_Ending_5_Demon")
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"EG_SidedWithDoctor",1)
THEN
UserSetFlag(_Player,"QuestUpdate_CORE_Epilogue_Endgame5_User");

IF
GlobalFlagSet("EG_Ending_5_Demon")
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"EG_SidedWithDoctor",1)
AND
UserGetFlag(_Player,"QuestUpdate_CORE_Epilogue_Endgame5_User",0)
THEN
UserSetFlag(_Player,"QuestUpdate_CORE_Epilogue_Endgame5_Companion");


//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "EndGame"
