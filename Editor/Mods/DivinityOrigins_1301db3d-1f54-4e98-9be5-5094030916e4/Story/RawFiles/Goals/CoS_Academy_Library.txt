Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Ghosts(CHARACTERGUID_S_LV_HoE_Curator_96b950ec-bd65-42c9-9f63-aae406f0ee14,"CoS_Academy_Curator");
CharacterLinkGhost(CHARACTERGUID_S_CoS_Academy_Curator_Skeleton_c3711bbf-df57-4c75-bc75-095d16bda782,CHARACTERGUID_S_CoS_Academy_Curator_96b950ec-bd65-42c9-9f63-aae406f0ee14);

DB_CoS_Academy_LibrarySequence(ITEMGUID_S_CoS_Academy_LibraryLever_001_1de1fc71-1582-4d2a-8758-5f308d47cac6,0);
DB_CoS_Academy_LibrarySequence(ITEMGUID_S_CoS_Academy_LibraryLever_002_c2707b40-6709-4dfe-822e-5ad475bc6827,1);
DB_CoS_Academy_LibrarySequence(ITEMGUID_S_CoS_Academy_LibraryLever_003_1be97faf-9285-4cd7-a2e7-667a39225ebb,2);
DB_CoS_Academy_LibrarySequence(ITEMGUID_S_CoS_Academy_LibraryLever_004_61358eca-f8b9-4e49-ad6e-d90d51ed8c90,3);
DB_CoS_Academy_LibrarySequence(ITEMGUID_S_CoS_Academy_LibraryLever_005_4bee04aa-294f-45e0-8ac9-1df047952609,-1);
ProcDeclareCounter("CoS_Academy_LibrarySequence");

KBSECTION
IF
GameBookInterfaceClosed(ITEMGUID_S_CoS_Academy_Skillbook_SV_aa594b98-8148-4aee-80b2-b5aa12977d94,(CHARACTERGUID)_Player)
AND
CharacterHasSkill(_Player,"Target_SourceVampirism",0)
THEN
CharacterAddSkill(_Player,"Target_SourceVampirism");
PlayEffect(_Player,"RS3_FX_GP_ScriptedEvent_HOE_Summoning_01");


IF
GameBookInterfaceClosed(ITEMGUID_S_CoS_Academy_Skillbook_SpiritVision_b664b3fe-8766-41d0-8ccc-a99bb7cf558d,(CHARACTERGUID)_Player)
AND
CharacterHasSkill(_Player,"Shout_SpiritVision",0)
THEN
CharacterAddSkill(_Player,"Shout_SpiritVision");
PlayEffect(_Player,"RS3_FX_GP_ScriptedEvent_HOE_Summoning_01");


IF
ObjectFlagSet("QuestUpdate_CoS_MissingStudent_FriendInfo",(CHARACTERGUID)_Player,_)
THEN
ShowMapMarker(_Player,"CoS_ForbiddenLibrary",1);


//REGION Library Lever Sequence
//Check for Fail
IF
CharacterUsedItem(_Char,_Lever)
AND
DB_CoS_Academy_LibrarySequence(_Lever,_)
THEN
PlayAnimation(_Char,"use_activate");
CharacterItemSetEvent(_Char,_Lever,"CoS_Academy_LibrarySequence_Activate");

IF
CharacterItemEvent(_Char,_Lever,"CoS_Academy_LibrarySequence_Activate")
AND
DB_CoS_Academy_LibrarySequence(_Lever,_Int)
AND
DB_GlobalCounter("CoS_Academy_LibrarySequence",_OtherInt)
AND
_Int != _OtherInt
THEN
ProcRemoveCounter("CoS_Academy_LibrarySequence");
SetStoryEvent(_Char,"CoS_Academy_LibrarySequence_ApplyDamage");

IF
StoryEvent((CHARACTERGUID)_Char,"CoS_Academy_LibrarySequence_ApplyDamage")
THEN
PlayEffect(_Char,"RS3_FX_Char_Creatures_Shriker_LightningBolt_Impact_01");
ApplyDamage(_Char,200,"Air");
CharacterPurgeQueue(_Char);
ApplyStatus(_Char,"STUNNED",3.0,1);
ProcObjectTimer(_Char,"CoS_Academy_VB_FailedLibraryPuzzle_Timer",3100);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char,"CoS_Academy_VB_FailedLibraryPuzzle_Timer")
AND
QRY_SpeakerIsAvailable(_Char)
THEN
Proc_StartDialog(1,"CoS_Academy_VB_FailedLibraryPuzzle",_Char);

//check for succeed
IF
CharacterItemEvent(_Char,_Lever,"CoS_Academy_LibrarySequence_Activate")
AND
DB_CoS_Academy_LibrarySequence(_Lever,_Int)
AND
DB_GlobalCounter("CoS_Academy_LibrarySequence",_OtherInt)
AND
_Int == _OtherInt
THEN
ProcIncreaseCounter("CoS_Academy_LibrarySequence",1);

//Reset if the Counter was removed
IF
CharacterItemEvent(_,_Lever,"CoS_Academy_LibrarySequence_Activate")
AND
DB_CoS_Academy_LibrarySequence(_Lever,_)
AND
NOT DB_GlobalCounter("CoS_Academy_LibrarySequence",_)
THEN
ProcDeclareCounter("CoS_Academy_LibrarySequence");


//Open the Door
IF
DB_GlobalCounter("CoS_Academy_LibrarySequence",4)
THEN
ItemUnLock(ITEMGUID_S_CoS_Academy_DoorToForbiddenLibrary_94e2ebf7-b7f4-4439-bcca-6827531f9c13);
ItemOpen(ITEMGUID_S_CoS_Academy_DoorToForbiddenLibrary_94e2ebf7-b7f4-4439-bcca-6827531f9c13);

IF
ItemOpened(ITEMGUID_S_CoS_Academy_DoorToForbiddenLibrary_94e2ebf7-b7f4-4439-bcca-6827531f9c13)
THEN
GlobalSetFlag("CoS_ForbiddenLibrary_Open");
Proc_CoS_Academy_ConductorPuzzle_LightsOn("CoS_Academy_DoorToForbiddenLibrary");


//Clear the DB_s
IF
ItemOpened(ITEMGUID_S_CoS_Academy_DoorToForbiddenLibrary_94e2ebf7-b7f4-4439-bcca-6827531f9c13)
AND
DB_CoS_Academy_LibrarySequence(_Lever,_Int)
THEN
NOT DB_CoS_Academy_LibrarySequence(_Lever,_Int);
ProcRemoveCounter("CoS_Academy_LibrarySequence");

IF
GlobalFlagSet("CoS_ForbiddenLibrary_Open")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_CoS_MissingStudent_OpenDoor");


IF
CharacterUsedItem(_Char,ITEMGUID_S_CoS_Academy_LibraryLever_003_1be97faf-9285-4cd7-a2e7-667a39225ebb)
THEN
ItemRotateY(ITEMGUID_S_CoS_Academy_LibraryLever_003_1be97faf-9285-4cd7-a2e7-667a39225ebb,360.0,100.0);
PlaySound(ITEMGUID_S_CoS_Academy_LibraryLever_003_1be97faf-9285-4cd7-a2e7-667a39225ebb,"Items_Puzzles_MovingObjects_STONE_Start");
TimerLaunch("CoS_Academy_LibraryLever_003_Reset",3500);
ItemSetCanInteract(ITEMGUID_S_CoS_Academy_LibraryLever_003_1be97faf-9285-4cd7-a2e7-667a39225ebb,0);

IF
TimerFinished("CoS_Academy_LibraryLever_003_Reset")
THEN
PlaySound(ITEMGUID_S_CoS_Academy_LibraryLever_003_1be97faf-9285-4cd7-a2e7-667a39225ebb,"Items_Puzzles_MovingObjects_STONE_Stop");
ItemSetCanInteract(ITEMGUID_S_CoS_Academy_LibraryLever_003_1be97faf-9285-4cd7-a2e7-667a39225ebb,1);

//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "CoS_Academy"
