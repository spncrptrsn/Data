Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_9183620e-c7d1-4762-b7c6-512045da9325,"DLC_SquirrelKnight");
DB_Ghosts(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_9183620e-c7d1-4762-b7c6-512045da9325,CHARACTERGUID_S_GLO_DLC_SquirrelWizard_Ghost_7044e6f2-cc71-4a18-bd5f-c7fb3d4fdbc7,"DLC_SquirrelKnight");
DB_IgnoreAssault(CHARACTERGUID_S_GLO_SquirrelKnight_9183620e-c7d1-4762-b7c6-512045da9325);
SetOnStage(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_9183620e-c7d1-4762-b7c6-512045da9325,0);
ObjectSetFlag(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_Ghost_7044e6f2-cc71-4a18-bd5f-c7fb3d4fdbc7,"DLC_SquirrelKnight_IsGhost");

DB_SquirrelWizard_ReflectionDialogs("FTJ_SW_HoEReturnToShrine","DLC_SquirrelKnight_PlayerMetGod","CharacterFlag");
DB_SquirrelWizard_ReflectionDialogs("LV_HoE_Main","DLC_SquirrelKnight_EnteredLV","Region");
DB_SquirrelWizard_ReflectionDialogs("RC_Main","DLC_SquirrelKnight_EnteredRC","Region");
DB_SquirrelWizard_ReflectionDialogs("RC_DW_Meistr_Dream_Visit1","DLC_SquirrelKnight_PlayerMetGod_RC","CharacterFlag");
DB_SquirrelWizard_ReflectionDialogs("CoS_Main","DLC_SquirrelKnight_EnteredCoS","Region");
DB_SquirrelWizard_ReflectionDialogs("QuestUpdate_CoS_ArenaOfTheOne_End","DLC_SquirrelKnight_DNotS","CharacterFlag");
DB_SquirrelWizard_ReflectionDialogs("Arx_Main","DLC_SquirrelKnight_EnteredArx","Region");
DB_SquirrelWizard_ReflectionDialogs("ARX_EnteredArx","DLC_SquirrelKnight_EnteredArx_City","CharacterFlag");
DB_SquirrelWizard_ReflectionDialogs("Arx_Endgame","DLC_SquirrelKnight_EnteredEndgame","Region");


//DB_SquirrelWizard_RecipeUnlock(_Flag/Trigger,_Flag/TriggerType,_UnlockFlag,_LearnFlag,_Recipe)
DB_SquirrelWizard_RecipeUnlock("FTJ_SW_HoEReturnToShrine","CharacterFlag","DLC_SquirrelWizard_RecipeUnlock_001","DLC_SquirrelWizard_RecipeLearn_001","SKILLBOOK_Air_Windwalker");
DB_SquirrelWizard_RecipeUnlock("RC_Main","Region","DLC_SquirrelWizard_RecipeUnlock_002","DLC_SquirrelWizard_RecipeLearn_002","SKILLBOOK_Fire_Fireblood");
DB_SquirrelWizard_RecipeUnlock("CoS_Main","Region","DLC_SquirrelWizard_RecipeUnlock_003","DLC_SquirrelWizard_RecipeLearn_003","SKILLBOOK_Water_VampiricHungerAura_A");
DB_SquirrelWizard_RecipeUnlock("CoS_Main","Region","DLC_SquirrelWizard_RecipeUnlock_003","DLC_SquirrelWizard_RecipeLearn_003","SKILLBOOK_Water_VampiricHungerAura_B");
DB_SquirrelWizard_RecipeUnlock("CoS_Main","Region","DLC_SquirrelWizard_RecipeUnlock_003","DLC_SquirrelWizard_RecipeLearn_003","SKILLBOOK_Water_VampiricHungerAura_C");
DB_SquirrelWizard_RecipeUnlock("ARX_EnteredArx","CharacterFlag","DLC_SquirrelWizard_RecipeUnlock_004","DLC_SquirrelWizard_RecipeLearn_004","SKILLBOOK_Water_BloodStorm_A");
DB_SquirrelWizard_RecipeUnlock("ARX_EnteredArx","CharacterFlag","DLC_SquirrelWizard_RecipeUnlock_004","DLC_SquirrelWizard_RecipeLearn_004","SKILLBOOK_Water_BloodStorm_B");
DB_SquirrelWizard_RecipeUnlock("ARX_EnteredArx","CharacterFlag","DLC_SquirrelWizard_RecipeUnlock_004","DLC_SquirrelWizard_RecipeLearn_004","SKILLBOOK_Water_BloodStorm_C");



//Exclusive flags, do not transfer to ghost
DB_DLC_SquirrelWizard_Flags_DoNotTransfer("NULL");

DB_DLC_SquirrelKnight_RecruitPositions("FJ_FortJoy_Main",TRIGGERGUID_S_DLC_SquirrelKnight_RecruitPosition_Shelter_924c4ad1-9924-4435-b05b-3ff9ff7013e1);
DB_DLC_SquirrelKnight_RecruitPositions("LV_HoE_Main",TRIGGERGUID_S_DLC_SquirrelKnight_RecruitPosition_LV_b9b5f5bd-f888-4116-8235-ee0efc711bfe);
DB_DLC_SquirrelKnight_RecruitPositions("RC_Main",TRIGGERGUID_S_DLC_SquirrelKnight_RecruitPosition_LV_b9b5f5bd-f888-4116-8235-ee0efc711bfe);
DB_DLC_SquirrelKnight_RecruitPositions("CoS_Main",TRIGGERGUID_S_DLC_SquirrelKnight_RecruitPosition_LV_b9b5f5bd-f888-4116-8235-ee0efc711bfe);
DB_DLC_SquirrelKnight_RecruitPositions("CoS_Main_Ending",TRIGGERGUID_S_DLC_SquirrelKnight_RecruitPosition_LV_b9b5f5bd-f888-4116-8235-ee0efc711bfe);
DB_DLC_SquirrelKnight_RecruitPositions("Arx_Main",TRIGGERGUID_S_DLC_SquirrelKnight_RecruitPosition_LV_b9b5f5bd-f888-4116-8235-ee0efc711bfe);
DB_DLC_SquirrelKnight_RecruitPositions("Arx_Endgame",TRIGGERGUID_S_DLC_SquirrelKnight_RecruitPosition_EG_RecruitPosition_14d819f0-29a9-45ff-bf6f-24124293c1cd);

DB_Followers(CHARACTERGUID_S_GLO_DLC_SquirrelKnight_9183620e-c7d1-4762-b7c6-512045da9325,"DLC_SquirrelKnight_OwnerFlag");
DB_Followers(CHARACTERGUID_S_GLO_DLC_SquirrelKnight_Ghost_7044e6f2-cc71-4a18-bd5f-c7fb3d4fdbc7,"DLC_SquirrelKnight_Ghost_OwnerFlag");


//ADs
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_BlackCat","FTJ_VB_BlackCat","DialogEnd");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_EnterImpTemple","CoS_Temples_Imp_VB_EnteredPocketRealm","DialogEnd");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_EnteredShelter","DLC_SquirrelKnight_FTJ_EnteredShelter","CharacterFlag");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_ReturnedToLvFromHoE","LV_HoE_PlayerReturnedToLvFromHoE","CharacterFlag");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_AotO","CoS_AotO_VB_AwakenInRubble","DialogEnd");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_LVSkeletonCat","LV_SkeletonCat","DialogEnd");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_FtjEnteredHoE","FTJ_SW_VB_EnteredHoE","DialogEnd");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_BeforeEndGameBattle","EG_ShipMates","DialogEnd");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_ArxLoremaster","ARX_Loremaster","DialogEnd");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_ArxSquirrels","ARX_Outskirts_Squirrel_000","PetPalDialogEnd");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_ArxSquirrels","ARX_Outskirts_Squirrel_001","PetPalDialogEnd");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_ArxSquirrels","ARX_Outskirts_Squirrel_002","PetPalDialogEnd");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_PeeperJoined","RC_DW_VC_IsOwnerOfChick","CharacterFlag");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_SawConsulateLizardRespawn","DLC_SquirrelKnight_SawConsulateLizardRespawn","CharacterFlag");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_DeathFogReleasedToArx","QuestUpdate_RC_ARX_TheImperialDwarves_Deathfog_ReleasedToArx","CharacterFlag");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_SavedArx","QuestUpdate_RC_ARX_TheImperialDwarves_Deathfog_ReleasedToSea","CharacterFlag");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_SavedArx","QuestUpdate_RC_ARX_TheImperialDwarves_Deathfog_DestroyedMachine","CharacterFlag");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_LeftFortJoy",TRIGGERGUID_S_FTJ_DallisEventAutoEnd_Poly_c66c0ceb-84d0-42f2-af77-854d178a58d5);
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_EnteredMargsBridge",TRIGGERGUID_S_RC_DF_Troll_Bridge_FieldsWest_ControlBox_dec3e360-4f07-4c8b-936b-47ee164c6d27);
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_EnterEternalTemple","RC_OIL_VB_TombComment","DialogEnd");
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_EnterSawMill",TRIGGERGUID_S_RC_MIL_REGION_SawMill_2fb470b0-2430-4a16-a1f6-1439a679759e);
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_ExploreBloodIsland",TRIGGERGUID_S_DLC_SquirrelKnight_BIVoiceBark_Trigger_be8d0f8a-e317-439b-8f56-db85e8880393);
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_CombatWithFtjVwBoss",CHARACTERGUID_S_FTJ_SW_VWBoss_VoidWoken_112f8c17-ea77-4658-ac72-239154772fb8);
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_EnterAcademyLibrary",TRIGGERGUID_S_CoS_SUB_Academy_Library_bb3fbf51-7449-4dd9-b230-d5692c517e6e);
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_ExploreElfTemple",TRIGGERGUID_S_DLC_SquirrelKnight_ElfTempleVoiceBark_Trigger_8128358a-8eec-4a8c-85f9-a6aaeb65e9bd);

DB_SharedObjectFlags("DLC_SquirrelKnight_Dismissed");
KBSECTION
//REGION Misc functions
QRY
QRY_CharacterHasDLC((CHARACTERGUID)_Player,(STRING)_DLC,(INTEGER)_Bool)
AND
CharacterHasDLC(_Player,_DLC,_Bool)
THEN
DB_Noop(1);

PROC
Proc_DialogFlagSetup("DLC_SquirrelKnight",(GUIDSTRING)_,(GUIDSTRING)_Player)
THEN
ObjectClearFlag(_Player,"DLC_SquirrelKnight_HasDLC");

PROC
Proc_DialogFlagSetup("DLC_SquirrelKnight",(GUIDSTRING)_,(GUIDSTRING)_Player)
AND
QRY_CharacterHasDLC(_Player,"DLC Squirrel",1)
THEN
ObjectSetFlag(_Player,"DLC_SquirrelKnight_HasDLC");

PROC
Proc_CheckTransferSquirrelTo((CHARACTERGUID)_NewPlayer)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,(CHARACTERGUID)_Player)
AND
_Player != _NewPlayer
THEN
Proc_TransferSquirrelTo(_Squirrel,_NewPlayer);

PROC
Proc_TransferSquirrelTo((CHARACTERGUID)_Squirrel,(CHARACTERGUID)_NewPlayer)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"DLC_SquirrelKnight_Dismissed",1)
THEN
ObjectShareFlag(_NewPlayer,"DLC_SquirrelKnight_Dismissed");

PROC
Proc_TransferSquirrelTo((CHARACTERGUID)_Squirrel,(CHARACTERGUID)_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,(CHARACTERGUID)_Player)
AND
DB_Followers(_Squirrel,_Flag)
THEN
ObjectClearFlag(_player,_Flag);
ClearTag(_Player,"SQUIRRELKNIGHT_PLAYER");
NOT DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player);
ObjectClearFlag(_Player,"DLC_SquirrelKnight_Dismissed");

PROC
Proc_TransferSquirrelTo((CHARACTERGUID)_Squirrel,(CHARACTERGUID)_Candidate)
AND
DB_Followers(_Squirrel,_)
THEN
SetTag(_Candidate,"SQUIRRELKNIGHT_PLAYER");
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Candidate);
Proc_LetSquirrelFollowNewOwner(_Squirrel, _Candidate);
Proc_SquirrelFoop(_Squirrel);

PROC
Proc_LetSquirrelFollowNewOwner((CHARACTERGUID)_Squirrel,(CHARACTERGUID)_Candidate)
AND
NOT DB_DLC_SquirrelWizard_FollowPaused("Dismissed")	//dismiss also sets this
AND
DB_Followers(_Squirrel,_Flag)
THEN
ObjectSetFlag(_Candidate,_Flag);
Proc_SquirrelTeleportToOwner(_Squirrel,_Candidate);

PROC
Proc_SquirrelTeleportToOwner((CHARACTERGUID)_Squirrel,(CHARACTERGUID)_Candidate)
AND
GetDistanceTo(_Squirrel,_Candidate,_Dist)
AND
_Dist > 4.0
THEN
ProcForceStopDialog(_Squirrel);
TeleportTo(_Squirrel,_Candidate,"",0);

PROC
Proc_SquirrelFoop((CHARACTERGUID)_Squirrel)
AND
ObjectIsOnStage(_Squirrel,0)
THEN
Foop(_Squirrel);

//END_REGION
// DLC Squirrel assign/reassign logic:
/**************************************
If host has DLC, host gets Squirrel
If host does not have DLC
If client joins with DLC, that client gets the squirrel (unless someone else already has it)
If client with DLC leaves the game, the squirrel leaves the game as well if noone else has the DLC. Otherwise it gets re-assigned to another player with the DLC
If client with DLC rejoins, squirrel gets assigned to him again if nobody else took over the squirrel
If host loads a savegame, we check if there's anyone with the DLC in the game
*************************************/
//REGION spawn conditions
IF
RegionStarted("FJ_FortJoy_Main")
AND
GlobalGetFlag("GLO_DLC_SquirrelWizard_Activated",0)
THEN
Proc_DLC_TrySpawn_SquirrelWizard();

IF
SavegameLoaded(_,_,_,_)
AND
DB_CurrentLevel("FJ_FortJoy_Main")
AND
GlobalGetFlag("GLO_DLC_SquirrelWizard_Activated",0)
THEN
Proc_DLC_TrySpawn_SquirrelWizard();

IF
UserConnected(_,_,_)
AND
DB_CurrentLevel("FJ_FortJoy_Main")
AND
GlobalGetFlag("GLO_DLC_SquirrelWizard_Activated",0)
THEN
Proc_DLC_TrySpawn_SquirrelWizard();

IF
DLCUpdated("DLC Squirrel",_,1)
AND
DB_CurrentLevel("FJ_FortJoy_Main")
AND
GlobalGetFlag("GLO_DLC_SquirrelWizard_Activated",0)
THEN
Proc_DLC_TrySpawn_SquirrelWizard();


IF
TextEventSet("DLC_SquirrelKnight")
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player,"AVATAR",1)
AND
NOT DB_DLC_SquirrelWizard_PlayerBond(_,_)
THEN
Proc_DLC_SquirrelWizardSpawn(_Player);

IF
TextEventSet("DLC_SquirrelKnight")
AND
DB_DLC_SquirrelWizard_PlayerBond((CHARACTERGUID)_Squirrel,(CHARACTERGUID)_Player)
AND
GetRegion(_Player,_Region)
AND
_Region != "FJ_FortJoy_Main"
THEN
TeleportTo(_Squirrel,_Player,"",0);

IF
TextEventSet("DLC_SquirrelKnight")
AND
DB_DLC_SquirrelWizard_PlayerBond(_,(CHARACTERGUID)_Player)
AND
GetRegion(_Player,_Region)
AND
DB_SquirrelWizard_ReflectionDialogs(_Region,_Dialog,"Region")
THEN
Proc_SquirrelWizard_QueueReflectionDialog(_Dialog);

//END_REGION

//REGION respawn conditions
IF
UserConnected(_,_,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_)
AND
ObjectIsOnStage(_Squirrel,0)
THEN
Proc_DLC_TryRespawn_SquirrelWizard();

IF
SavegameLoaded(_,_,_,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_)
AND
ObjectIsOnStage(_Squirrel,0)
THEN
Proc_DLC_TryRespawn_SquirrelWizard();
//END_REGION

//REGION reassign conditions
IF
DB_DLC_SquirrelWizard_PlayerBond(_,_Origin)
AND
NOT DB_IsPlayer(_Origin)
THEN
Proc_DLC_TryReassign_SquirrelWizard();
//END_REGION

//REGION despawn conditions
IF
SavegameLoaded(_,_,_,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_,_)
THEN
Proc_DLC_TryDeSpawn_SquirrelWizard();

//any user that leaves, since the server could've stolen the squirrel from the client that owns it
IF
UserDisconnected(_,_,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_,_)
THEN
Proc_DLC_TryDeSpawn_SquirrelWizard();
//END_REGION

//REGION spawning
//Check if host character has DLC first
PROC
Proc_DLC_TrySpawn_SquirrelWizard()
AND
CharacterGetHostCharacter(_Player)
AND
QRY_CharacterHasDLC(_Player,"DLC Squirrel",1)
AND
NOT DB_DLC_SquirrelWizard_PlayerBond(_,_)
THEN
Proc_DLC_SquirrelWizardSpawn(_Player);

//Check if any active players have DLC as fallback
PROC
Proc_DLC_TrySpawn_SquirrelWizard()
AND
GlobalGetFlag("GLO_DLC_SquirrelWizard_Activated",0)
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
QRY_CharacterHasDLC(_Player,"DLC Squirrel",1)
AND
NOT DB_DLC_SquirrelWizard_PlayerBond(_,_)
THEN
Proc_DLC_SquirrelWizardSpawn(_Player);

PROC
Proc_DLC_SquirrelWizardSpawn((CHARACTERGUID)_Player)
THEN
ObjectSetFlag(_Player,"DLC_SquirrelKnight_Dismissed");
SetVarInteger(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_9183620e-c7d1-4762-b7c6-512045da9325, "PauseFollowWhileHidden", 1);
TeleportTo(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_9183620e-c7d1-4762-b7c6-512045da9325,TRIGGERGUID_S_FTJ_DLC_SquirrelWizard_SpawnPoint_2fd623f7-34f5-470d-bce3-3aa60ce50c3b,"",0);
CharacterLookFromTrigger(CHARACTERGUID_S_GLO_DLC_SquirrelKnight_9183620e-c7d1-4762-b7c6-512045da9325,TRIGGERGUID_S_FTJ_DLC_SquirrelWizard_SpawnPoint_2fd623f7-34f5-470d-bce3-3aa60ce50c3b,1);
SetTag(_Player,"SQUIRRELKNIGHT_PLAYER");
GlobalSetFlag("GLO_DLC_SquirrelWizard_Activated");
DB_DLC_SquirrelWizard_PlayerBond(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_9183620e-c7d1-4762-b7c6-512045da9325,_Player);
SetOnStage(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_9183620e-c7d1-4762-b7c6-512045da9325,1);
//END_REGION

//REGION Despawning
PROC
Proc_DLC_TryDeSpawn_SquirrelWizard()
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
QRY_CharacterHasDLC(_Player,"DLC Squirrel",1)
THEN
DB_DLC_SquirrelPotentialCandidate(_Player);

PROC
Proc_DLC_TryDeSpawn_SquirrelWizard()
AND
NOT DB_DLC_SquirrelPotentialCandidate(_)
AND
CharacterGetHostCharacter(_Player)
AND
QRY_CharacterHasDLC(_Player,"DLC Squirrel",1)
THEN
DB_DLC_SquirrelPotentialCandidate(_Player);

PROC
Proc_DLC_TryDeSpawn_SquirrelWizard()
AND
DB_IsPlayer(_Player)
AND
NOT DB_DLC_SquirrelPotentialCandidate(_)
AND
QRY_CharacterHasDLC(_Player,"DLC Squirrel",1)
THEN
DB_DLC_SquirrelPotentialCandidate(_Player);

PROC
Proc_DLC_TryDeSpawn_SquirrelWizard()
AND
NOT DB_DLC_SquirrelPotentialCandidate(_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
DB_Followers(_Squirrel,_Flag)
THEN
ObjectClearFlag(_Player,_Flag);
ProcForceStopDialog(_Squirrel);
Poof(_Squirrel);

PROC
Proc_DLC_TryDeSpawn_SquirrelWizard()
AND
DB_DLC_SquirrelPotentialCandidate(_Candidate)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_CurrentPlayer)
AND
_CurrentPlayer != _Candidate
THEN
Proc_TransferSquirrelTo(_Squirrel,_Candidate);

PROC
Proc_DLC_TryDeSpawn_SquirrelWizard()
AND
DB_DLC_SquirrelPotentialCandidate(_Candidate)
THEN
NOT DB_DLC_SquirrelPotentialCandidate(_Candidate);
//END_REGION

//REGION Respawning
PROC
Proc_DLC_TryRespawn_SquirrelWizard()
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
QRY_CharacterHasDLC(_Player,"DLC Squirrel",1)
THEN
DB_DLC_SquirrelPotentialCandidate(_Player);

PROC
Proc_DLC_TryRespawn_SquirrelWizard()
AND
NOT DB_DLC_SquirrelPotentialCandidate(_)
AND
CharacterGetHostCharacter(_Player)
AND
QRY_CharacterHasDLC(_Player,"DLC Squirrel",1)
THEN
DB_DLC_SquirrelPotentialCandidate(_Player);

PROC
Proc_DLC_TryRespawn_SquirrelWizard()
AND
DB_IsPlayer(_Player)
AND
NOT DB_DLC_SquirrelPotentialCandidate(_)
AND
QRY_CharacterHasDLC(_Player,"DLC Squirrel",1)
THEN
DB_DLC_SquirrelPotentialCandidate(_Player);

PROC
Proc_DLC_TryRespawn_SquirrelWizard()
AND
DB_DLC_SquirrelPotentialCandidate(_Candidate)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Candidate)
THEN
Foop(_Squirrel);
Proc_SetFollowerFlag(_Squirrel,_Candidate);

PROC
Proc_SetFollowerFlag((CHARACTERGUID)_Squirrel,(CHARACTERGUID)_Candidate)
AND
DB_Followers(_Squirrel,_Flag)
AND
ObjectGetFlag(_Candidate,"DLC_SquirrelKnight_Dismissed",0)
THEN
ObjectSetFlag(_Candidate,_Flag);

PROC
Proc_DLC_TryRespawn_SquirrelWizard()
AND
DB_DLC_SquirrelPotentialCandidate(_Candidate)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_CurPlayer)
AND
_CurPlayer!=_Candidate
THEN
Proc_TransferSquirrelTo(_Squirrel,_Candidate);

PROC
Proc_DLC_TryRespawn_SquirrelWizard()
AND
DB_DLC_SquirrelPotentialCandidate(_Candidate)
THEN
NOT DB_DLC_SquirrelPotentialCandidate(_Candidate);

//END_REGION

//REGION Reassigning

//first look for another character of that user (if he has the DLC)
PROC
Proc_DLC_TryReassign_SquirrelWizard()
AND
DB_DLC_SquirrelWizard_PlayerBond(_,_Origin)
AND
QRY_CharacterHasDLC(_Origin,"DLC Squirrel",1)
AND
CharacterGetReservedUserID(_Origin,_User)
AND
DB_IsPlayer(_Player)
AND
CharacterGetReservedUserID(_Player,_User)
THEN
DB_DLC_SquirrelPotentialCandidate(_Player);

//check the host next
PROC
Proc_DLC_TryReassign_SquirrelWizard()
AND
NOT DB_DLC_SquirrelPotentialCandidate(_)
AND
CharacterGetHostCharacter(_Player)
AND
DB_IsPlayer(_Player)	//you never know...
AND
QRY_CharacterHasDLC(_Player,"DLC Squirrel",1)
THEN
DB_DLC_SquirrelPotentialCandidate(_Player);

//anyone else
PROC
Proc_DLC_TryReassign_SquirrelWizard()
AND
DB_IsPlayer(_Player)
AND
NOT DB_DLC_SquirrelPotentialCandidate(_)
AND
QRY_CharacterHasDLC(_Player,"DLC Squirrel",1)
THEN
DB_DLC_SquirrelPotentialCandidate(_Player);

PROC
Proc_DLC_TryReassign_SquirrelWizard()
AND
DB_DLC_SquirrelPotentialCandidate(_Candidate)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_CurPlayer)
AND
_CurPlayer!=_Candidate
THEN
Proc_TransferSquirrelTo(_Squirrel,_Candidate);

//this should normally not happen
PROC
Proc_DLC_TryReassign_SquirrelWizard()
AND
NOT DB_DLC_SquirrelPotentialCandidate(_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_)
THEN
ProcForceStopDialog(_Squirrel);
Poof(_Squirrel);			

PROC
Proc_DLC_TryReassign_SquirrelWizard()
AND
DB_DLC_SquirrelPotentialCandidate(_Player)
THEN
NOT DB_DLC_SquirrelPotentialCandidate(_Player);

//END_REGION

IF
DialogEnded("DLC_SquirrelKnight",_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
DB_DLC_SquirrelWizard_PlayerBond(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_9183620e-c7d1-4762-b7c6-512045da9325,_Player)
THEN
ObjectSetFlag(_Player,"DLC_SquirrelKnight_OwnerFlag");

IF
StoryEvent(_Player,"DLC_SquirrelKnight_StartDialogOnSight")
THEN
Proc_StartDialog(0,"DLC_SquirrelKnight",CHARACTERGUID_S_GLO_DLC_SquirrelKnight_9183620e-c7d1-4762-b7c6-512045da9325,_Player);
ObjectClearFlag(_Player,"DLC_SquirrelKnight_Dismissed");
ObjectSetFlag(CHARACTERGUID_S_GLO_SquirrelKnight_9183620e-c7d1-4762-b7c6-512045da9325,"DLC_SquirrelKnight_HasMetOwner");

//REGION Ghost Logic
IF
CharacterDied(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_9183620e-c7d1-4762-b7c6-512045da9325)
AND
DB_DLC_SquirrelWizard_PlayerBond(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_9183620e-c7d1-4762-b7c6-512045da9325,_Player)
THEN
NOT DB_DLC_SquirrelWizard_PlayerBond(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_9183620e-c7d1-4762-b7c6-512045da9325,_Player);
DB_DLC_SquirrelWizard_PlayerBond(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_Ghost_7044e6f2-cc71-4a18-bd5f-c7fb3d4fdbc7,_Player);
ObjectSetFlag(_Player,"DLC_SquirrelKnight_Ghost_OwnerFlag");
TeleportTo(CHARACTERGUID_S_GLO_DLC_SquirrelKnight_Ghost_7044e6f2-cc71-4a18-bd5f-c7fb3d4fdbc7,CHARACTERGUID_S_GLO_DLC_SquirrelKnight_9183620e-c7d1-4762-b7c6-512045da9325);
DB_Dialogs(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_Ghost_7044e6f2-cc71-4a18-bd5f-c7fb3d4fdbc7,"DLC_SquirrelKnight");

//Keep flag state consistent between
IF
ObjectFlagSet(_Flag,CHARACTERGUID_S_GLO_DLC_SquirrelWizard_9183620e-c7d1-4762-b7c6-512045da9325,_)
AND
CharacterIsDead(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_9183620e-c7d1-4762-b7c6-512045da9325,0)
AND
NOT DB_DLC_SquirrelWizard_Flags_DoNotTransfer(_Flag)
THEN
ObjectSetFlag(CHARACTERGUID_S_GLO_DLC_SquirrelWizard_Ghost_7044e6f2-cc71-4a18-bd5f-c7fb3d4fdbc7,_Flag);


//END_REGION

//REGION Level changes

IF
CharacterEnteredRegion(_Player,_)
AND
ObjectGetFlag(_Player,"DLC_SquirrelKnight_Dismissed",0)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
THEN
TeleportTo(_Squirrel,_Player,"",0);

IF
CharacterEnteredRegion(_Player,_Region)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
ObjectGetFlag(_Player,"DLC_SquirrelKnight_Dismissed",1)
AND
DB_DLC_SquirrelKnight_RecruitPositions(_Region,_RecruitLocation)
THEN
TeleportTo(_Squirrel,_RecruitLocation,"",0);

IF
CharacterEnteredRegion(_Player,_Region)
AND
_Region != "FJ_FortJoy_Main"
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
ObjectGetFlag(_Squirrel,"DLC_SquirrelKnight_HasMetOwner",0)
THEN
SetStoryEvent(CHARACTERGUID_S_GLO_SquirrelKnight_9183620e-c7d1-4762-b7c6-512045da9325,"StartEventOnSight_RestartSpotting");

//END_REGION

//REGION  Manual Sets

//Start Epilogue
PROC
PROC_EG_Epilogue_Start()
THEN
GlobalSetFlag("DLC_SquirrelKnight_EpilogueStarted");

//LV_HoE return to LV
IF
ObjectFlagSet("QuestUpdate_LV_HoE_Main_ReturnToLV",(CHARACTERGUID)_Player,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_,_Player)
THEN
ProcObjectTimer(_Player,"LV_HoE_PlayerReturnedToLvFromHoE_Timer",12000);

PROC
ProcObjectTimerFinished(_Player,"LV_HoE_PlayerReturnedToLvFromHoE_Timer")
THEN
ObjectSetFlag(_Player,"LV_HoE_PlayerReturnedToLvFromHoE");


//Enter Shelter
IF
CharacterEnteredTrigger((CHARACTERGUID)_Player,TRIGGERGUID_S_FTJ_SW_SUB_Shelter_51c3953f-c756-472d-a7ca-dc2a6232b386)
AND
DB_SquirrelWizard_AutomatedDialogs("DLC_SquirrelKnight_AD_EnteredShelter","DLC_SquirrelKnight_FTJ_EnteredShelter","CharacterFlag")
AND
DB_DLC_SquirrelWizard_PlayerBond(_,_Player)
THEN
ProcObjectTimer(_Player,"DLC_SquirrelKnight_FTJ_EnteredShelter_Timer",5000);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"DLC_SquirrelKnight_FTJ_EnteredShelter_Timer")
AND
DB_InRegion(_Player,TRIGGERGUID_S_FTJ_SW_SUB_Shelter_51c3953f-c756-472d-a7ca-dc2a6232b386)
THEN
ObjectSetFlag(_Player,"DLC_SquirrelKnight_FTJ_EnteredShelter");


//Explore Blood Island Trigger
IF
RegionStarted("RC_Main")
AND
DB_DLC_SquirrelWizard_PlayerBond(_,_)
THEN
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_DLC_SquirrelKnight_BIVoiceBark_Trigger_be8d0f8a-e317-439b-8f56-db85e8880393);

//Explore Elf Temple
IF
RegionStarted("CoS_Main")
AND
DB_DLC_SquirrelWizard_PlayerBond(_,_)
THEN
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_DLC_SquirrelKnight_ElfTempleVoiceBark_Trigger_8128358a-8eec-4a8c-85f9-a6aaeb65e9bd);

IF
CharacterResurrected(_Lizard)
AND
DB_ARX_LizardConsulate_InfernalLizards(_Lizard,_)
THEN
ProcObjectTimer(_Lizard,"DLC_SquirrelKnight_SawConsulateLizardRespawn_Timer",150);
DB_DLC_SquirrelKnight_SawConsulateLizardRespawn(_Lizard);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Lizard,"DLC_SquirrelKnight_SawConsulateLizardRespawn_Timer")
AND
DB_DLC_SquirrelKnight_SawConsulateLizardRespawn(_Lizard)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
ObjectIsCharacter((CHARACTERGUID)_Squirrel,1)
AND
CharacterCanSee(_Squirrel,_Lizard,1)
THEN
ObjectSetFlag(_Player,"DLC_SquirrelKnight_SawConsulateLizardRespawn");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Lizard,"DLC_SquirrelKnight_SawConsulateLizardRespawn_Timer")
AND
DB_DLC_SquirrelKnight_SawConsulateLizardRespawn(_Lizard)
AND
DB_DLC_SquirrelWizard_PlayerBond(_,_Player)
AND
CharacterCanSee(_Player,_Lizard,1)
THEN
ObjectSetFlag(_Player,"DLC_SquirrelKnight_SawConsulateLizardRespawn");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Lizard,"DLC_SquirrelKnight_SawConsulateLizardRespawn_Timer")
AND
DB_DLC_SquirrelKnight_SawConsulateLizardRespawn(_Lizard)
THEN
NOT DB_DLC_SquirrelKnight_SawConsulateLizardRespawn(_Lizard);

//END_REGION

//REGION Block Follower

//REGION Surprise Date

IF
GlobalFlagSet("RC_DW_SurpriseDate_Ongoing")
AND
DB_DLC_SquirrelWizard_PlayerBond(_,(CHARACTERGUID)_Player)
THEN
Proc_DLC_SquirrelWizard_PauseFollow(_Player,"Dismissed");

IF
GlobalFlagCleared("RC_DW_SurpriseDate_Ongoing")
AND
DB_DLC_SquirrelWizard_PlayerBond(_,(CHARACTERGUID)_Player)
THEN
Proc_DLC_SquirrelWizard_ResumeFollow(_Player);


//END_REGION

//REGION DNOTS

IF
StoryEvent(_Player,"CoS_HoE_LadyVengeance_DNotSRoom")
AND
DB_DLC_SquirrelWizard_PlayerBond(_,(CHARACTERGUID)_Player)
THEN
Proc_DLC_SquirrelWizard_PauseFollow(_Player,"Dismissed");

IF
ObjectFlagCleared("CoS_DNotS_Ongoing",_Player,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_,(CHARACTERGUID)_Player)
THEN
Proc_DLC_SquirrelWizard_ResumeFollow(_Player);

//END_REGION

//REGION Assaulted
//Dismissal overrides the assault follow block
IF
DB_DLC_SquirrelWizard_FollowPaused("Dismissed")
AND
DB_DLC_SquirrelWizard_FollowPaused("Assaulted")
THEN
NOT DB_DLC_SquirrelWizard_FollowPaused("Assaulted");

IF
ObjectFlagSet("DLC_SquirrelKnight_ReactToAssault",(CHARACTERGUID)_Squirrel,_)
AND
NOT DB_DLC_SquirrelWizard_FollowPaused("Dismissed")
AND
NOT DB_DLC_SquirrelWizard_FollowPaused("Assaulted")
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
DB_Followers(_Squirrel,_FollowerFlag)
AND
ObjectGetFlag(_Player,_FollowerFlag,1)
THEN
Proc_DLC_SquirrelWizard_PauseFollow((CHARACTERGUID)_Player,"Assaulted");

IF
ObjectFlagCleared("DLC_SquirrelKnight_ReactToAssault",(CHARACTERGUID)_Squirrel,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,(CHARACTERGUID)_Player)
AND
NOT DB_DLC_SquirrelWizard_FollowPaused("Dismissed")
AND
DB_DLC_SquirrelWizard_FollowPaused("Assaulted")
THEN
Proc_DLC_SquirrelWizard_ResumeFollow(_Player);

//END_REGION

PROC
Proc_DLC_SquirrelWizard_PauseFollow((CHARACTERGUID)_,(STRING)_Cause)
THEN
DB_DLC_SquirrelWizard_FollowPaused(_Cause);

PROC
Proc_DLC_SquirrelWizard_PauseFollow((CHARACTERGUID)_Player,(STRING)_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
DB_Followers(_Squirrel,_FollowerFlag)
THEN
ObjectClearFlag(_Player,_FollowerFlag);

PROC
Proc_DLC_SquirrelWizard_ResumeFollow((CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"DLC_SquirrelKnight_Dismissed",0)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
DB_Followers(_Squirrel,_FollowerFlag)
THEN
ObjectSetFlag(_Player,_FollowerFlag);

PROC
Proc_DLC_SquirrelWizard_ResumeFollow(_)
THEN
NOT DB_DLC_SquirrelWizard_FollowPaused("Dismissed");
//END_REGION

//REGION Squirrel Reflection Dialogs

//Triggered from Global Flag
IF
GlobalFlagSet(_Flag)
AND
DB_DLC_SquirrelWizard_PlayerBond(_,_)
AND
DB_SquirrelWizard_ReflectionDialogs(_Flag,_Dialog,"GlobalFlag")
THEN
Proc_SquirrelWizard_QueueReflectionDialog(_Dialog);

//Triggered from Region Started
IF
RegionStarted(_Region)
AND
DB_DLC_SquirrelWizard_PlayerBond(_,_)
AND
DB_SquirrelWizard_ReflectionDialogs(_Region,_Dialog,"Region")
THEN
Proc_SquirrelWizard_QueueReflectionDialog(_Dialog);

//Triggered from Character flag on Player
IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Player,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_,_Player)
AND
DB_SquirrelWizard_ReflectionDialogs(_Flag,_Dialog,"CharacterFlag")
THEN
Proc_SquirrelWizard_QueueReflectionDialog(_Dialog);

//Triggered from Character flag on Squirrel
IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Squirrel,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_)
AND
DB_SquirrelWizard_ReflectionDialogs(_Flag,_Dialog,"CharacterFlag")
THEN
Proc_SquirrelWizard_QueueReflectionDialog(_Dialog);

//If RD triggered, and another RD is queued, remove the currently queued 
PROC
Proc_SquirrelWizard_QueueReflectionDialog(_Dialog)
AND
DB_SquirrelWizard_QueuedReflectionDialog(_OtherDialog)
AND
_Dialog != _OtherDialog
THEN
NOT DB_SquirrelWizard_QueuedReflectionDialog(_OtherDialog);

//Discovery AD
PROC
Proc_SquirrelWizard_QueueReflectionDialog((STRING)_Dialog)
AND
DB_SquirrelWizard_ReflectionDialogs(_Flag,_Dialog,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
ObjectGetFlag(_Player,"DLC_SquirrelKnight_Dismissed",0)
AND
IsTagged(_Player,"PETPAL",1)
THEN
Proc_StartDialog(1,"DLC_SquirrelKnight_AD_Discovery",_Squirrel);

PROC
Proc_SquirrelWizard_QueueReflectionDialog((STRING)_Dialog)
AND
DB_SquirrelWizard_ReflectionDialogs(_Flag,_Dialog,_EventType)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
ObjectGetFlag(_Player,"DLC_SquirrelKnight_Dismissed",0)
THEN
NOT DB_SquirrelWizard_ReflectionDialogs(_Flag,_Dialog,_EventType);
ApplyStatus(_Squirrel,"REFLECTION_SQUIRREL",-1.0,1);
DB_SquirrelWizard_QueuedReflectionDialog(_Dialog);



PROC
PROC_GLOBAL_DialogStartRequested((CHARACTERGUID)_Squirrel,(CHARACTERGUID)_Player)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
DB_SquirrelWizard_QueuedReflectionDialog(_Dialog)
THEN
Proc_StartDialog(0,_Dialog,_Squirrel,_Player);

//REGION Clean up RDs
IF
CharacterDied(_Squirrel)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_)
AND
DB_SquirrelWizard_QueuedReflectionDialog(_Dialog)
THEN
Proc_SquirrelWizard_ClearReflectionDialog((STRING)_Dialog);

IF
DialogStarted(_Dialog,_)
AND
DB_SquirrelWizard_QueuedReflectionDialog(_Dialog)
THEN
Proc_SquirrelWizard_ClearReflectionDialog((STRING)_Dialog);

PROC
Proc_SquirrelWizard_ClearReflectionDialog((STRING)_Dialog)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_)
THEN
NOT DB_SquirrelWizard_QueuedReflectionDialog(_Dialog);
RemoveStatus(_Squirrel,"REFLECTION_SQUIRREL");
//END_REGION

//END_REGION

//REGION Recipe Unlocks / Learning
//Object Flag set on player
IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Player,_)
AND
DB_SquirrelWizard_RecipeUnlock(_Flag,"CharacterFlag",_UnlockFlag,_,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
THEN
ObjectSetFlag(_Squirrel,_UnlockFlag);

//Triggered from Region Started
IF
RegionStarted(_Region)
AND
DB_SquirrelWizard_RecipeUnlock(_Region,"Region",_UnlockFlag,_,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
THEN
ObjectSetFlag(_Squirrel,_UnlockFlag);

IF
ObjectFlagSet(_LearnFlag,(CHARACTERGUID)_Player,_)
AND
DB_SquirrelWizard_RecipeUnlock(_,_,_,_LearnFlag,_Recipe)
THEN
CharacterUnlockRecipe(_Player,_Recipe,1);

//END_REGION

//REGION Squirrel ADs


//Triggered from Global Flag
IF
GlobalFlagSet(_Flag)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_)
AND
ObjectIsCharacter((CHARACTERGUID)_Squirrel,1)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Flag,"GlobalFlag")
THEN
Proc_DLC_SquirrelKnight_StartAD(_Squirrel,_AD);


IF
RegionStarted(_Region)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_)
AND
ObjectIsCharacter((CHARACTERGUID)_Squirrel,1)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Region,"Region")
THEN
Proc_DLC_SquirrelKnight_StartAD(_Squirrel,_AD);

//Triggered from Character flag on Player
IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Char,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
ObjectIsCharacter((CHARACTERGUID)_Squirrel,1)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Flag,"CharacterFlag")
THEN
Proc_DLC_SquirrelKnight_StartAD(_Squirrel,_AD);

//Triggered from Character flag on Squirrel
IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Squirrel,_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Flag,"CharacterFlag")
THEN
Proc_DLC_SquirrelKnight_StartAD(_Squirrel,_AD);


//Triggered from Dialog End
IF
DialogEnded(_Dialog,_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
ObjectIsCharacter((CHARACTERGUID)_Squirrel,1)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Dialog,"DialogEnd")
THEN
Proc_DLC_SquirrelKnight_StartAD(_Squirrel,_AD);

//Triggered from Petpal Dialog End
IF
DialogEnded(_Dialog,_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
IsTagged(_Player,"PETPAL",1)
AND
ObjectIsCharacter((CHARACTERGUID)_Squirrel,1)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Dialog,"PetPalDialogEnd")
THEN
Proc_DLC_SquirrelKnight_StartAD(_Squirrel,_AD);

//Triggered from AD End
IF
AutomatedDialogEnded(_Dialog,_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
ObjectIsCharacter((CHARACTERGUID)_Squirrel,1)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Dialog,"DialogEnd")
THEN
Proc_DLC_SquirrelKnight_StartAD(_Squirrel,_AD);

//Triggered from VB End
IF
VoiceBarkEnded(_Dialog,_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
ObjectIsCharacter((CHARACTERGUID)_Squirrel,1)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Dialog,"DialogEnd")
THEN
Proc_DLC_SquirrelKnight_StartAD(_Squirrel,_AD);

IF
CharacterEnteredTrigger(_Player,_Trigger)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
ObjectIsCharacter((CHARACTERGUID)_Squirrel,1)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Trigger)
AND
ObjectIsCharacter((TRIGGERGUID)_Trigger,0)
AND
ObjectIsItem(_Trigger,0)
THEN
Proc_DLC_SquirrelKnight_StartAD(_Squirrel,_AD);

//Enter combat with
IF
DB_CombatCharacters(_Player, _ID)
AND
DB_CombatCharacters(_Character, _ID)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
ObjectIsCharacter((CHARACTERGUID)_Squirrel,1)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Character)
AND
ObjectIsCharacter((CHARACTERGUID)_Character,1)
THEN
Proc_DLC_SquirrelKnight_StartAD(_Squirrel,_AD);

//REGION Main procedures

PROC
Proc_DLC_SquirrelKnight_StartAD((CHARACTERGUID)_Squirrel,(STRING)_AD)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
IsTagged(_Player,"PETPAL",1)
AND
ObjectGetFlag(_Player,"DLC_SquirrelKnight_Dismissed",0)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Event,_EventType)
AND
QRY_SpeakerIsAvailable(_Squirrel)
THEN
ProcObjectTimer(_Squirrel,"DLC_SquirrelKnight_StartDialogDelay",1000);
DB_DLC_SquirrelKnight_StartDialogDelay(_AD);
Proc_DLC_SquirrelKnight_ClearAD(_AD);

PROC
Proc_DLC_SquirrelKnight_StartAD((CHARACTERGUID)_Squirrel,(STRING)_AD)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
IsTagged(_Player,"PETPAL",1)
AND
ObjectGetFlag(_Player,"DLC_SquirrelKnight_Dismissed",0)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Object)
AND
QRY_SpeakerIsAvailable(_Squirrel)
THEN
ProcObjectTimer(_Squirrel,"DLC_SquirrelKnight_StartDialogDelay",1000);
DB_DLC_SquirrelKnight_StartDialogDelay(_AD);
Proc_DLC_SquirrelKnight_ClearAD(_AD);

//AD delay to prevent failure from Squirrel Teleport
PROC
ProcObjectTimerFinished(_Squirrel,"DLC_SquirrelKnight_StartDialogDelay")
AND
DB_DLC_SquirrelKnight_StartDialogDelay(_AD)
AND
QRY_SpeakerIsAvailable(_Squirrel)
THEN
Proc_StartDialog(1,_AD,_Squirrel);
NOT DB_DLC_SquirrelKnight_StartDialogDelay(_AD);

PROC
Proc_DLC_SquirrelKnight_ClearAD((STRING)_AD)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Object)
THEN
NOT DB_SquirrelWizard_AutomatedDialogs(_AD,_Object);

PROC
Proc_DLC_SquirrelKnight_ClearAD((STRING)_AD)
AND
DB_SquirrelWizard_AutomatedDialogs(_AD,_Event,_EventType)
THEN
NOT DB_SquirrelWizard_AutomatedDialogs(_AD,_Event,_EventType);
//END_REGION

//END_REGION

//REGION Dismissal/Re-recruit
//Dismiss
IF
DialogEnded("DLC_SquirrelKnight",_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"DLC_SquirrelKnight_Dismissed",1)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
ObjectIsCharacter((CHARACTERGUID)_Squirrel,1)
THEN
Proc_DLC_SquirrelKnightDismiss(_Squirrel,_Player);

PROC
Proc_DLC_SquirrelKnightDismiss((CHARACTERGUID)_Squirrel,(CHARACTERGUID)_Player)
THEN
GlobalSetFlag("DLC_SquirrelKnight_IsDismissed");
Proc_DLC_SquirrelWizard_PauseFollow(_Player,"Dismissed");
SetHasDialog(_Squirrel,0);
Proc_DLC_SquirrelWizard_ReturnToRecruitPos(_Squirrel,_Player);

PROC
Proc_DLC_SquirrelWizard_ReturnToRecruitPos((CHARACTERGUID)_Squirrel,(CHARACTERGUID)_Player)
AND
DB_DLC_SquirrelKnight_RecruitPositions_Current(_,_RecruitLocation)
AND
GetDistanceTo(_Squirrel,_RecruitLocation,_Dist)
AND
_Dist > 18.0
THEN
CharacterDisappearOutOfSightToObject(_Squirrel,_Player,1,"DLC_SquirrelKnight_AppearAtRecruitPoint",0);

PROC
Proc_DLC_SquirrelWizard_ReturnToRecruitPos((CHARACTERGUID)_Squirrel,(CHARACTERGUID)_Player)
AND
DB_DLC_SquirrelKnight_RecruitPositions_Current(_,_RecruitLocation)
AND
GetDistanceTo(_Squirrel,_RecruitLocation,_Dist)
AND
_Dist <= 18.0
THEN
CharacterMoveTo(_Squirrel,_RecruitLocation,0,"DLC_SquirrelKnight_AppearAtRecruitPoint",0);


IF
StoryEvent((CHARACTERGUID)_Squirrel,"DLC_SquirrelKnight_AppearAtRecruitPoint")
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_)
AND
DB_DLC_SquirrelKnight_RecruitPositions_Current(_,_RecruitLocation)
THEN
TeleportTo(_Squirrel,_RecruitLocation,"",0);
SetHasDialog(_Squirrel,1);
SetOnStage(_Squirrel,1);


IF
RegionStarted(_Region)
AND
DB_DLC_SquirrelKnight_RecruitPositions(_Region,_)
AND
DB_DLC_SquirrelKnight_RecruitPositions_Current(_OtherRegion,_RecruitLocation)
AND
_Region != _OtherRegion
THEN
NOT DB_DLC_SquirrelKnight_RecruitPositions_Current(_OtherRegion,_RecruitLocation);

IF
RegionStarted(_Region)
AND
NOT DB_DLC_SquirrelKnight_RecruitPositions_Current(_Region,_)
AND
DB_DLC_SquirrelKnight_RecruitPositions(_Region,_RecruitLocation)
THEN
DB_DLC_SquirrelKnight_RecruitPositions_Current(_Region,_RecruitLocation);


//Re-recruit
IF
ObjectFlagCleared("DLC_SquirrelKnight_Dismissed",(CHARACTERGUID)_Player,_Inst)
THEN
Proc_CheckRehireOfSquirrel(_Player,_Inst);

PROC
Proc_CheckRehireOfSquirrel((CHARACTERGUID)_Player,(INTEGER)_Inst)
AND
DB_DialogPlayers(_Inst,_Player,_)
AND
DB_DialogName("DLC_SquirrelKnight",_Inst)
THEN
GlobalClearFlag("DLC_SquirrelKnight_IsDismissed");
Proc_CheckTransferSquirrelTo(_Player);
ObjectClearFlag(_Player,"DLC_SquirrelKnight_Dismissed");
Proc_DLC_SquirrelWizard_ResumeFollow(_Player);

IF
ObjectFlagSet("DLC_SquirrelKnight_Hire",(CHARACTERGUID)_Player,_Inst)
THEN
ObjectClearFlag(_Player,"DLC_SquirrelKnight_Hire");
Proc_CheckRehireOfSquirrel(_Player,_Inst);

//REGION Update recruit for Epilogue

//REGION Squirrel Leaves before first combat with Dallis/Lucian, update recruit Pos
IF
DialogEnded("DLC_SquirrelKnight_EnteredEndgame",_Id)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Squirrel,"DLC_SquirrelKnight_Endgame_RunOff",1)
THEN
NOT DB_DLC_SquirrelKnight_RecruitPositions_Current("Arx_Endgame",TRIGGERGUID_S_DLC_SquirrelKnight_RecruitPosition_EG_RecruitPosition_14d819f0-29a9-45ff-bf6f-24124293c1cd);
DB_DLC_SquirrelKnight_RecruitPositions_Current("Arx_Endgame",TRIGGERGUID_S_DLC_SquirrelKnight_RecruitPosition_Epilogue_RecruitPosition_0dd28cee-91a9-4c83-a706-ef7a30b76587);
ObjectSetFlag(_Player,"DLC_SquirrelKnight_Dismissed");
Proc_DLC_SquirrelKnightDismiss(_Squirrel,_Player);

PROC
PROC_EG_Epilogue_Start()
AND
DB_DLC_SquirrelWizard_PlayerBond((CHARACTERGUID)_Squirrel,_)
THEN
PROC_RemoveDialogFromCharacter(_Squirrel);
DB_Dialogs(_Squirrel,"DLC_SquirrelKnight_Epilogue");

PROC
PROC_EG_StartFirstFightDialog(_)
AND
DB_DLC_SquirrelWizard_PlayerBond((CHARACTERGUID)_Squirrel,(CHARACTERGUID)_Player)
THEN
ObjectSetFlag(_Squirrel,"DLC_SquirrelKnight_Endgame_RunOff");
ProcForceStopDialog(_Squirrel);
ObjectSetFlag(_Player,"DLC_SquirrelKnight_Dismissed");
NOT DB_DLC_SquirrelKnight_RecruitPositions_Current("Arx_Endgame",TRIGGERGUID_S_DLC_SquirrelKnight_RecruitPosition_EG_RecruitPosition_14d819f0-29a9-45ff-bf6f-24124293c1cd);
DB_DLC_SquirrelKnight_RecruitPositions_Current("Arx_Endgame",TRIGGERGUID_S_DLC_SquirrelKnight_RecruitPosition_Epilogue_RecruitPosition_0dd28cee-91a9-4c83-a706-ef7a30b76587);
Proc_DLC_SquirrelKnightDismiss(_Squirrel,_Player);

//END_REGION

IF
GlobalFlagSet("DLC_SquirrelKnight_EpilogueStarted")
AND
DB_DLC_SquirrelKnight_RecruitPositions_Current(_Region,_RecruitLocation)
THEN
NOT DB_DLC_SquirrelKnight_RecruitPositions_Current(_Region,_RecruitLocation);
DB_DLC_SquirrelKnight_RecruitPositions_Current("Arx_Endgame",TRIGGERGUID_S_DLC_SquirrelKnight_RecruitPosition_Epilogue_RecruitPosition_0dd28cee-91a9-4c83-a706-ef7a30b76587);

IF
GlobalFlagSet("DLC_SquirrelKnight_EpilogueStarted")
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
AND
ObjectGetFlag(_Player,"DLC_SquirrelKnight_Dismissed",1)
THEN
TeleportTo(_Squirrel,TRIGGERGUID_S_DLC_SquirrelKnight_RecruitPosition_Epilogue_RecruitPosition_0dd28cee-91a9-4c83-a706-ef7a30b76587);

//END_REGION

//END_REGION

//REGION Respec mirror
IF
DB_Illusionist(_Player,_Mirror)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
THEN
Proc_DLC_SquirrelWizard_PauseFollow(_Player,"Dismissed");

PROC
Proc_HomesteadTeleportAfterMirror((CHARACTERGUID)_Player,(ITEMGUID)_,(TRIGGERGUID)_)
AND
DB_DLC_SquirrelWizard_PlayerBond(_Squirrel,_Player)
THEN
Proc_DLC_SquirrelWizard_ResumeFollow(_Player);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOS2ModWrapper"
