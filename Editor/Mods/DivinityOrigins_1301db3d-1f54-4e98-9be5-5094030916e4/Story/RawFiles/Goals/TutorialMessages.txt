Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_StartTutMessages(1);

DB_TutorialInfo("TUT_ClearingSurfaces","TUT_CAT_Exploring","TUT_ClearingSurfaces_Title",0,0,-1,1,16,0);
DB_TutorialInfo("TUT_SecretPassage","TUT_CAT_Exploring","TUT_SecretPassage_Title",0,0,-1,1,16,0);
DB_TutorialInfo("TUT_SleepingBag","TUT_CAT_Exploring","TUT_SleepingBag_Title",0,0,-1,1,0,0);
DB_TutorialInfo("TUT_COM","TUT_CAT_Social","TUT_COM_Title",0,0,-1,1,16,0);
DB_TutorialInfo("TUT_Comp_Talking_To_Comp","TUT_CAT_Social","TUT_Comp_Talking_To_Comp_Title",0,0,-1,1,16,0);
DB_TutorialInfo("TUT_AOM_Switch","TUT_CAT_Social","TUT_AOM_Switch_Title",0,0,-1,1,16,0);
DB_TutorialInfo("TUT_Relationship_Examine","TUT_CAT_Social","TUT_Relationship_Examine_Title",0,0,-1,1,16,0);
DB_TutorialInfo("TUT_CurseSkill","TUT_CAT_Combat","TUT_CurseSkill_Title",0,0,-1,1,16,0);
DB_TutorialInfo("TUT_BlessSkill","TUT_CAT_Combat","TUT_BlessSkill_Title",0,0,-1,1,16,0);
DB_TutorialInfo("TUT_GetWeaponsBack","TUT_CAT_Exploring","TUT_GetWeaponsBack_Title",0,0,-1,1,19,0);
DB_TutorialInfo("TUT_VeiledUndead","TUT_CAT_Social","TUT_VeiledUndead_Title",0,0,-1,1,31,0);
DB_TutorialInfo("TUT_VeiledUndeadAttacked","TUT_CAT_Social","TUT_VeiledUndeadAttacked_Title",0,0,-1,1,16,0);
DB_TutorialInfo("TUT_FirstSpiritVision","TUT_CAT_Exploring","TUT_FirstSpiritVision_Title",0,0,-1,1,16,0);
DB_TutorialInfo("TUT_LoneWolf","TUT_CAT_Social","TUT_LoneWolf_Title",0,0,-1,1,16,0);
DB_TutorialInfo("TUT_StorageChest","TUT_CAT_Exploring","TUT_StorageChest_Title",0,0,-1,1,16,0);
DB_TutorialInfo("TUT_EdibleLimbs","TUT_CAT_Exploring","TUT_EdibleLimbs_Title",0,1,-1,1,16,0);
DB_TutorialInfo_Msgbox("TUT_LearnedSpiritVision","TUT_CAT_Exploring","TUT_LearnedSpiritVision_Title",0,0,-1,1,16,0);
DB_TutorialInfo_MsgBox("TUT_UseSpiritVision","TUT_CAT_Exploring","TUT_UseSpiritVision_Title",0,1,-1,1,16,0);
DB_TutorialInfo("TUT_CompanionSwitch","TUT_CAT_General","TUT_CompanionSwitch_Title",0,3,0,1,3,0);

DB_Tutorial_EnemyCount(0);
KBSECTION
//REGION Combat Popup
IF				
ObjectTurnStarted(_Player)
AND
(CHARACTERGUID)_Player.DB_IsPlayer()
THEN
Proc_CharCountHelper(_Player,"CombatTurnCounter");

IF //Counts enemies in combat
ObjectEnteredCombat((CHARACTERGUID)_Char,_ID)
AND
NOT DB_Tutorial_TargetCycling()
AND
DB_IsPlayer(_Player)
AND
CharacterIsEnemy(_Player,_Char,1)
AND
DB_Tutorial_EnemyCount(_OldCount)
AND
IntegerSum(_OldCount,1,_Count)
THEN
NOT DB_Tutorial_EnemyCount(_OldCount);
DB_Tutorial_EnemyCount(_Count);

IF
CombatEnded(_ID)
AND
NOT DB_Tutorial_TargetCycling()
AND
DB_Tutorial_EnemyCount(_Count)
THEN
NOT DB_Tutorial_EnemyCount(_Count);
DB_Tutorial_EnemyCount(0);

IF
DB_CharCountHelper(_Player,"CombatTurnCounter",_Turn)
AND
_Turn>0
THEN
CharacterSendGlobalCombatCounter(_Player,_Turn);

IF
DB_CharCountHelper(_Player,"CombatTurnCounter",_Turn)
AND
_Turn >= 1
THEN
PROC_CheckPlayTut(_Player,"TUT_Combat");

IF
TutorialBoxClosed(_Player,"TUT_Combat")
THEN
PROC_CheckPlayTut(_Player,"TUT_Combat_TurnOrder");

IF
DB_CharCountHelper(_Player,"CombatTurnCounter",_Turn)
AND
_Turn >= 3
AND
DB_Tutorial_EnemyCount(_Count)
AND
_Count >= 3
THEN
DB_Tutorial_TargetCycling();
PROC_CheckPlayTut(_Player,"TUT_Combat_TargetCycle");

IF
DB_CharCountHelper(_Player,"CombatTurnCounter",_Turn)
AND
_Turn >= 4
AND
NOT DB_TUT_TacticalView(_Player)
THEN
DB_TUT_TacticalView(_Player);

IF
DB_CharCountHelper(_Player,"CombatTurnCounter",_Turn)
AND
_Turn >= 5
AND
CombatGetIDForCharacter(_Player,_CombatID)
AND
CombatGetNumberOfInvolvedPartyMembers(_CombatID,_NumMembers)
AND
_NumMembers > 1
THEN
PROC_CheckPlayTut(_Player,"TUT_Combat_Guard");

IF
DB_TUT_TacticalView(_Player)
THEN
ProcCheckForTacticalViewTutorial(_Player);

IF
ObjectTurnStarted((CHARACTERGUID)_Player)
AND
DB_TUT_TacticalView(_Player)
AND
NOT DB_TUT_DidTacticalView(_Player)
THEN
ProcCheckForTacticalViewTutorial(_Player);

PROC
ProcCheckForTacticalViewTutorial(_)
AND
DB_TacticalViewEnemyCount(_Count)
THEN
NOT DB_TacticalViewEnemyCount(_Count);

PROC
ProcCheckForTacticalViewTutorial(_)
THEN
DB_TacticalViewEnemyCount(0);

PROC
ProcCheckForTacticalViewTutorial((CHARACTERGUID)_Player)
AND
DB_CombatCharacters(_Player, _ID)
AND
DB_CombatCharacters(_Enemy, _ID)
AND
NOT DB_Dead(_Enemy)
AND
CharacterIsEnemy(_Player,_Enemy,1)
AND
DB_TacticalViewEnemyCount(_Count)
AND
IntegerSum(_Count,1,_NewCount)
THEN
NOT DB_TacticalViewEnemyCount(_Count);
DB_TacticalViewEnemyCount(_NewCount);

PROC
ProcCheckForTacticalViewTutorial((CHARACTERGUID)_Player)
AND
DB_TacticalViewEnemyCount(_Count)
AND
_Count>=5
THEN
DB_TUT_DidTacticalView(_Player);
PROC_CheckPlayTut(_Player,"TUT_Combat_TacticalView");


//END_REGION 

//REGION Area Triggers
IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_FTJ_TUT_SecretPassages_6c725690-fb06-43e6-a27b-c64d08fbd78c)
THEN
PROC_CheckPlayTut(_Player,"TUT_SecretPassage");

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_FTJ_TUT_SaveLoad_df13b935-3930-43a5-8739-2ba60a4060be)
THEN
PROC_CheckPlayTut(_Player,"TUT_SaveLoad");
//END_REGION

//REGION Sleeping Bag
IF
ItemAddedToCharacter(_Item,_Player)
AND
_Player.DB_IsPlayer()
AND
GetTemplate(_Item, _Template)
AND
DB_RestTemplates(_Template,_,_,_)
THEN
PROC_CheckPlayTut(_Player, "TUT_SleepingBag");
//END_REGION

//REGION Curse & Bless spells
IF
SkillAdded(_Player, "Target_Curse", _)
THEN
PROC_CheckPlayTut(_Player, "TUT_CurseSkill");

IF
SkillAdded(_Player, "Target_Bless", _)
AND
IsTagged(_Player,"AVATAR",1)
THEN
PROC_CheckPlayTut(_Player, "TUT_BlessSkill");
//END_REGION

//REGION Veiled undead
// Only trigger the tutorial message after the character was marked veiled at least once;
// that's how all undead start, and this is to avoid triggering it while they are getting equipped
IF
ObjectWasTagged((CHARACTERGUID)_Player,"VEILED_UNDEAD")
AND
DB_IsPlayer(_Player)
THEN
DB_TutorialMessages_VeiledUndeadTagged(_Player);

QRY
QRY_TutorialMessages_ShowVeiledUndeadTut((CHARACTERGUID)_Player)
AND
_Player != CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb
THEN
DB_NOOP(1);

QRY
QRY_TutorialMessages_ShowVeiledUndeadTut((CHARACTERGUID)CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb)
AND
NOT DB_OriginsFane_BlockVeiledUndeadTut(1)
THEN
DB_NOOP(1);

IF
ObjectLostTag((CHARACTERGUID)_Player,"VEILED_UNDEAD")
AND
DB_TutorialMessages_VeiledUndeadTagged(_Player)
AND
// Fane gets blocked temporarily in the tutorial level to prevent the tutorial message
// and his voice bark after the Windego event appearing at the same time
QRY_TutorialMessages_ShowVeiledUndeadTut(_Player)
THEN
PROC_CheckPlayTut(_Player, "TUT_VeiledUndead");

//Attacked because unveiled undead
IF
DialogStarted("GEB_Attack_Undead", _ID)
AND
DB_DialogPlayers(_ID, _Player, _)
THEN
PROC_CheckPlayTut((CHARACTERGUID)_Player, "TUT_VeiledUndeadAttacked");
//END_REGION

//REGION Spirit/source vision

//Spirit Vision in Act 1
IF
AutomatedDialogEnded("FTJ_SW_VB_EnteredHoE",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
THEN
PROC_CheckPlayTut(_Player,"TUT_FirstSpiritVision");

IF
VoiceBarkFailed("FTJ_SW_VB_EnteredHoE")
AND
DB_IsPlayer(_Player)
AND
HasActiveStatus(_Player,"SPIRIT_VISION",1)
THEN
PROC_CheckPlayTut(_Player,"TUT_FirstSpiritVision");

//Spirit vision skill in Act 2

IF
ObjectFlagSet("RC_DW_Meistr_GiveSpiritVision",(CHARACTERGUID)_Player,_)
THEN
PROC_CheckPlayTut(_Player,"TUT_LearnedSpiritVision");
DB_SpiritVisionSkillTut(1);

IF
CharacterLearnedSkill(_Player,"Shout_SpiritVision")
AND
NOT DB_SpiritVisionSkillTut(1)
THEN
DB_SpiritVisionSkillTut(1);
PROC_CheckPlayTut(_Player,"TUT_LearnedSpiritVision");

IF
CharacterCharacterEvent(_Spirit,_Player,"GLO_SpiritVisionTutorialCheck")
AND
CharacterCanSeeGhost(_Player,_Spirit,0)
AND
NOT DB_SpiritVisionUseTut(1)
THEN
DB_SpiritVisionUseTut(1);
GlobalSetFlag("GLO_SpiritVisionTutorialShown");
PROC_CheckPlayTut(_Player,"TUT_UseSpiritVision");

//END_REGION

//REGION Lone Wolf
IF
CharacterUnlockedTalent(_Player, "LoneWolf")
AND
DB_IsPlayer(_Player)
THEN
PROC_CheckPlayTut(_Player,"TUT_LoneWolf");

//END_REGION

//REGION Edable Limbs
IF
ItemAddedToCharacter(_Item,_Player)
AND
NOT DB_EdibleLimbTut(1)
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Item,"BODYPART",1)
THEN
DB_EdibleLimbTut(1);
PROC_CheckPlayTut(_Player, "TUT_EdibleLimbs");

//END_REGION

//REGION Companion switch
PROC
PROC_GLO_PartyMembers_Add((CHARACTERGUID)_Origin,(CHARACTERGUID)_Player)
AND
DB_CurrentLevel(_)
THEN
PROC_CheckPlayTut(_Player, "TUT_CompanionSwitch");
//END_REGION

//REGION Savegame patching
IF
SavegameLoaded(_Major,_Minor,_Rev,_Build)
AND
QRY_VersionIsOlderThan(_Major,_Minor,_Rev,_Build,3, 5, 1, 0)
THEN
DB_TutorialInfo("TUT_VeiledUndeadAttacked","TUT_CAT_Social","TUT_VeiledUndeadAttacked_Title",0,0,-1,1,16,0);
NOT DB_TutorialInfo("TUT_CompanionSwitch","TUT_CAT_General","TUT_CompanionSwitch_Title",0,0,0,1,3,0);
DB_TutorialInfo("TUT_CompanionSwitch","TUT_CAT_General","TUT_CompanionSwitch_Title",0,3,0,1,3,0);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start"
