Version 1
SubGoalCombiner SGC_AND
INITSECTION
//https://sites.google.com/a/larian.com/projecttest/home/wiki-articles/quest-design-documentation/wrecker-hills---dwarven-camp
//https://www.lucidchart.com/documents/edit/4dde4b19-05ba-4e6b-8080-a356aa47c973
// See also RC_WH_DwarvenCamp_Gone

DB_RC_WH_DwarvenCamp_Dwarves((CHARACTERGUID)S_RC_WH_DwarvenCamp_Warron_3be7c843-8d73-4d0e-be57-637c97a8b3bd);
DB_RC_WH_DwarvenCamp_Dwarves(S_RC_WH_DwarvenCamp_Valine_d3e3ee57-00f3-4ca6-9018-58f4e4e0c436);

CharacterSetCanTrade(CHARACTERGUID_S_RC_WH_DwarvenCamp_Warron_3be7c843-8d73-4d0e-be57-637c97a8b3bd,0);

DB_RC_WH_DwarvenCamp_DialogUnluckyFlags("RC_WH_DwarvenCamp_DialogFail");
DB_RC_WH_DwarvenCamp_DialogUnluckyFlags("RC_WH_DwarvenCamp_DwarvesNotConvinced");

DB_RC_WH_DwarvenCamp_MoveToLoharStools((CHARACTERGUID)CHARACTERGUID_S_RC_WH_DwarvenCamp_Warron_3be7c843-8d73-4d0e-be57-637c97a8b3bd,(ITEMGUID)ITEMGUID_S_RC_DW_Warron_Stool_e830756c-71ed-46b5-9595-fbeeba10f8db);
DB_RC_WH_DwarvenCamp_MoveToLoharStools(CHARACTERGUID_S_RC_WH_DwarvenCamp_Valine_d3e3ee57-00f3-4ca6-9018-58f4e4e0c436,ITEMGUID_S_RC_DW_Valine_Stool_fecdd630-5db6-4176-85de-4ff960fc4d44);

ProcDeclareCounter("RC_WH_DwarvenCamp_MovedToLohar");

DB_RC_WH_DwarvenCamp_SeeYouDialogs((CHARACTERGUID)CHARACTERGUID_S_RC_WH_DwarvenCamp_Warron_3be7c843-8d73-4d0e-be57-637c97a8b3bd,"RC_WH_DwarvenCamp_WarronSeeYouAtLohar");
DB_RC_WH_DwarvenCamp_SeeYouDialogs(CHARACTERGUID_S_RC_WH_DwarvenCamp_Valine_d3e3ee57-00f3-4ca6-9018-58f4e4e0c436,"RC_WH_DwarvenCamp_ValineSeeYouAtLohar");

DB_RC_WH_DwarvenCamp_SeeYouADDialogs((CHARACTERGUID)CHARACTERGUID_S_RC_WH_DwarvenCamp_Warron_3be7c843-8d73-4d0e-be57-637c97a8b3bd,"RC_WH_AD_DwarvenCamp_WarronSeeYouAtLohar");
DB_RC_WH_DwarvenCamp_SeeYouADDialogs(CHARACTERGUID_S_RC_WH_DwarvenCamp_Valine_d3e3ee57-00f3-4ca6-9018-58f4e4e0c436,"RC_WH_AD_DwarvenCamp_ValineSeeYouAtLohar");

DB_RC_WH_DwarvenCamp_UndertavernDialogs((CHARACTERGUID)CHARACTERGUID_S_RC_WH_DwarvenCamp_Warron_3be7c843-8d73-4d0e-be57-637c97a8b3bd,"RC_DW_Undertavern_Warron");
DB_RC_WH_DwarvenCamp_UndertavernDialogs(CHARACTERGUID_S_RC_WH_DwarvenCamp_Valine_d3e3ee57-00f3-4ca6-9018-58f4e4e0c436,"RC_DW_Undertavern_Valine");

SetOnStage(ITEMGUID_S_RC_WH_DwarvenCamp_DwarvesSneakPath_640d856d-09e1-412d-945d-37a4f3957a90,0);
SetOnStage(CHARACTERGUID_S_RC_WH_DwarvenCamp_DeadSneaker_76867382-7d9f-4b5a-bb8d-0117f3705acb,0);

DB_RC_WH_DwarvenCamp_PerceptionValue(15);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_RC_WH_DwarvenCamp_PerceptionVBTrigger_41c100e7-c1fa-47cf-8b08-d4644c8d905c);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_RC_WH_DwarvenCamp_PerceptionADTrigger_8253cb7e-ab87-4a40-bb4f-e77c81091994);

DB_Ghosts(CHARACTERGUID_S_RC_WH_DwarvenCamp_DeadDwarf_1_290de482-005c-4def-a3ee-9454a49f6fb9,CHARACTERGUID_S_RC_WH_DwarvenCamp_DeadDwarf_1_Ghost_08514449-a1d8-4759-a928-d083cd28a458,"RC_WH_DwarvenCamp_DeadDwarfGhost");
KBSECTION
IF
DB_RC_WH_DwarvenCamp_Dwarves(_Dwarf)
THEN
SetVisible(_Dwarf,0);
ProcSetInvulnerable(_Dwarf,1);
ProcCharacterDisableAllCrimes(_Dwarf);


//REGION Account for alive dwarves
IF
CharacterDied(_Dwarf)
AND
DB_RC_WH_DwarvenCamp_Dwarves(_Dwarf)
THEN
NOT DB_RC_WH_DwarvenCamp_Dwarves(_Dwarf);
Proc_RC_WH_DwarvenCamp_CheckDead();

PROC
Proc_RC_WH_DwarvenCamp_CheckDead()
AND
SysCount("DB_RC_WH_DwarvenCamp_Dwarves",1,0)
THEN
GoalCompleted;
//END_REGION


IF
CharacterEnteredTrigger(_Character,TRIGGERGUID_S_RC_WH_DwarvenCamp_PerceptionVBTrigger_41c100e7-c1fa-47cf-8b08-d4644c8d905c)
AND
ObjectGetFlag(CHARACTERGUID_S_RC_WH_DwarvenCamp_Warron_3be7c843-8d73-4d0e-be57-637c97a8b3bd,"RC_WH_DwarvenCamp_Left",0)
AND
DB_RC_WH_DwarvenCamp_PerceptionValue(_Wits)
AND
QRY_CheckPlayerStat(_Character,"Wits",_Wits)
THEN
StartVoiceBark("RC_WH_VB_DwarvenCamp_NoticeDen",_Character);
ProcUnregisterForUser(TRIGGERGUID_S_RC_WH_DwarvenCamp_PerceptionVBTrigger_41c100e7-c1fa-47cf-8b08-d4644c8d905c,_Character);

IF
CharacterEnteredTrigger(_Character,TRIGGERGUID_S_RC_WH_DwarvenCamp_PerceptionADTrigger_8253cb7e-ab87-4a40-bb4f-e77c81091994)
AND
ObjectGetFlag(CHARACTERGUID_S_RC_WH_DwarvenCamp_Warron_3be7c843-8d73-4d0e-be57-637c97a8b3bd,"RC_WH_DwarvenCamp_Left",0)
AND
DB_RC_WH_DwarvenCamp_PerceptionValue(_Wits)
AND
QRY_CheckPlayerStat(_Character,"Wits",_Wits)
THEN
DB_RC_WH_DwarvenCamp_NoticedDen((CHARACTERGUID)_Character);
ObjectSetFlag(ITEMGUID_S_RC_WH_DwarvenCamp_Den_5f34496f-9d7f-4cd3-85d3-cba62061b41a,"Revealed");

IF
CharacterLeftTrigger(_Character,TRIGGERGUID_S_RC_WH_DwarvenCamp_PerceptionADTrigger_8253cb7e-ab87-4a40-bb4f-e77c81091994)
THEN
NOT DB_RC_WH_DwarvenCamp_NoticedDen((CHARACTERGUID)_Character);
Proc_RC_WH_DwarvenCamp_CheckNoticed();

PROC
Proc_RC_WH_DwarvenCamp_CheckNoticed()
AND
QRY_IsEmptyDB("DB_RC_WH_DwarvenCamp_NoticedDen",1)
THEN
ObjectClearFlag(ITEMGUID_S_RC_WH_DwarvenCamp_Den_5f34496f-9d7f-4cd3-85d3-cba62061b41a,"Revealed",0);

IF
DialogStarted("RC_WH_DwarvenCamp_HidingDwarves",_)
THEN
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_RC_WH_DwarvenCamp_PerceptionADTrigger_8253cb7e-ab87-4a40-bb4f-e77c81091994);
ObjectClearFlag(ITEMGUID_S_RC_WH_DwarvenCamp_Den_5f34496f-9d7f-4cd3-85d3-cba62061b41a,"Revealed",0);

IF
DialogStarted("RC_WH_AD_DwarvenCamp_Rude",_)
THEN
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_RC_WH_DwarvenCamp_PerceptionADTrigger_8253cb7e-ab87-4a40-bb4f-e77c81091994);
ObjectClearFlag(ITEMGUID_S_RC_WH_DwarvenCamp_Den_5f34496f-9d7f-4cd3-85d3-cba62061b41a,"Revealed",0);



//REGION Dialogs with hiding dwarves
// When dwarves already agreed to help someone (even not us), we get a one-liner
// RC_WH_DwarvenCamp_RudeCommentsSaid && RC_WH_DwarvenCamp_AgreedToHelp => Replicas dialog
IF
CharacterUsedItem(_Character,ITEMGUID_S_RC_WH_DwarvenCamp_Den_5f34496f-9d7f-4cd3-85d3-cba62061b41a)
AND
QRY_RC_WH_DwarvenCamp_Rude_Agreed_Check(1,1)
THEN
Proc_RC_WH_DwarvenCamp_ReplicasDialog(_Character);

// We haven't spoken or didn't convince them to join yet (and noone did), we get a normal dialog
// RC_WH_DwarvenCamp_RudeCommentsSaid && !RC_WH_DwarvenCamp_AgreedToHelp && !Unlucky flags => Default dialog
IF
CharacterUsedItem(_Character,ITEMGUID_S_RC_WH_DwarvenCamp_Den_5f34496f-9d7f-4cd3-85d3-cba62061b41a)
AND
QRY_RC_WH_DwarvenCamp_Rude_Agreed_Check(1,0)
AND
NOT QRY_RC_WH_DwarvenCamp_DialogUnlucky(_Character)
THEN
Proc_RC_WH_DwarvenCamp_DefaultDialog(_Character);

// We've failed to convince Gthem to join, we get a one-liner
// RC_WH_DwarvenCamp_RudeCommentsSaid && !RC_WH_DwarvenCamp_AgreedToHelp && Any unlucky flag => Replicas dialog
IF
CharacterUsedItem(_Character,ITEMGUID_S_RC_WH_DwarvenCamp_Den_5f34496f-9d7f-4cd3-85d3-cba62061b41a)
AND
QRY_RC_WH_DwarvenCamp_Rude_Agreed_Check(1,0)
AND
QRY_RC_WH_DwarvenCamp_DialogUnlucky(_Character)
THEN
Proc_RC_WH_DwarvenCamp_ReplicasDialog(_Character);

// First couple of times we attempt to speak, we get rude comments
// !RC_WH_DwarvenCamp_RudeCommentsSaid => Rude Comments
IF
CharacterUsedItem(_Character,ITEMGUID_S_RC_WH_DwarvenCamp_Den_5f34496f-9d7f-4cd3-85d3-cba62061b41a)
AND
ObjectGetFlag(ITEMGUID_S_RC_WH_DwarvenCamp_HidingSpeaker_Warron_d9feb166-e3b7-4064-a405-82ea19d52e4d,"RC_WH_DwarvenCamp_RudeCommentsSaid",0)
THEN
Proc_StartDialog(1,"RC_WH_AD_DwarvenCamp_Rude",ITEMGUID_S_RC_WH_DwarvenCamp_HidingSpeaker_Warron_d9feb166-e3b7-4064-a405-82ea19d52e4d);

IF
ObjectFlagSet("RC_WH_DwarvenCamp_DwarvesConvinced",_Character,_)
THEN
DB_RC_WH_DwarvenCamp_HelpFightCharacters((CHARACTERGUID)_Character);


QRY
QRY_RC_WH_DwarvenCamp_Rude_Agreed_Check((INTEGER)_Rude,(INTEGER)_Agreed)
AND
ObjectGetFlag(ITEMGUID_S_RC_WH_DwarvenCamp_HidingSpeaker_Warron_d9feb166-e3b7-4064-a405-82ea19d52e4d,"RC_WH_DwarvenCamp_RudeCommentsSaid",_Rude)
AND
GlobalGetFlag("RC_WH_DwarvenCamp_AgreedToHelp",_Agreed)
THEN
DB_NOOP(1);

QRY
QRY_RC_WH_DwarvenCamp_DialogUnlucky((CHARACTERGUID)_Character)
AND
DB_RC_WH_DwarvenCamp_DialogUnluckyFlags(_DlgUnluckyFlag)
AND
ObjectGetDialogFlag(_Character,_DlgUnluckyFlag,1)
THEN
DB_NOOP(1);

PROC
Proc_RC_WH_DwarvenCamp_ReplicasDialog((CHARACTERGUID)_Character)
THEN
Proc_StartDialog(1,"RC_WH_AD_DwarvenCamp_Replicas",ITEMGUID_S_RC_WH_DwarvenCamp_HidingSpeaker_Warron_d9feb166-e3b7-4064-a405-82ea19d52e4d,_Character);

PROC
Proc_RC_WH_DwarvenCamp_DefaultDialog((CHARACTERGUID)_Character)
THEN
Proc_StartDialog(0,"RC_WH_DwarvenCamp_HidingDwarves",ITEMGUID_S_RC_WH_DwarvenCamp_HidingSpeaker_Warron_d9feb166-e3b7-4064-a405-82ea19d52e4d,CHARACTERGUID_S_RC_WH_DwarvenCamp_Warron_3be7c843-8d73-4d0e-be57-637c97a8b3bd,_Character);
//END_REGION


//REGION Weird case when player brought combat to the Den, while dwarves are hiding there
IF
ObjectEnteredCombat(_Dwarf,_CombatID)
AND
DB_RC_WH_DwarvenCamp_Dwarves((CHARACTERGUID)_Dwarf)
AND
ObjectGetFlag(_Dwarf,"RC_WH_DwarvenCamp_Left",0)
THEN
TeleportTo(_Dwarf,TRIGGERGUID_S_RC_WH_DwarvenCamp_PerceptionVBTrigger_41c100e7-c1fa-47cf-8b08-d4644c8d905c);
Proc_RC_WH_DwarvenCamp_LeaveCamp(_Dwarf);
Foop(_Dwarf); // for the effect
//END_REGION

//REGION Join combat if they agreed
// Combat near Wrecker Cave entrance starts => Warron and Valine join the character who talked to them.
IF
ObjectEnteredCombat((CHARACTERGUID)_RCDwarf,_CombatID)
AND
DB_RC_WH_RuinsCliff_Dwarves(_RCDwarf)
AND
DB_RC_WH_DwarvenCamp_HelpFightCharacters(_Char)
AND
CharacterIsInCombat(_Char,1)
AND
CombatGetIDForCharacter(_Char,_CombatID)
AND
QueryOnlyOnce("RC_WH_DwarvenCamp_JoinFight")
AND
DB_RC_WH_DwarvenCamp_Dwarves(_Dwarf)
THEN
Proc_RC_WH_DwarvenCamp_LeaveCamp(_Dwarf);
TeleportTo(_Dwarf,TRIGGERGUID_S_RC_WH_WarronValine_CombatAppearTrigger_048f5d1c-dc9c-4887-a6b9-5f1e54a55924);
PlayEffect(_Dwarf,"RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01");
SetOnStage(ITEMGUID_S_RC_WH_DwarvenCamp_DwarvesSneakPath_640d856d-09e1-412d-945d-37a4f3957a90,1);
SetOnStage(CHARACTERGUID_S_RC_WH_DwarvenCamp_DeadSneaker_76867382-7d9f-4b5a-bb8d-0117f3705acb,1);
CharacterSetRelationIndivFactionToIndivFaction((CHARACTERGUID)_Dwarf,(CHARACTERGUID)_Char,100);
CharacterSetRelationIndivFactionToIndivFaction((CHARACTERGUID)_Char,(CHARACTERGUID)_Dwarf,100);
EnterCombat(_Dwarf,_Char);
//END_REGION


//REGION Move to Lohar

//REGION On combat finished
// Warron/Valine left combat, all dwarves near Wrecker cave entrance are dead => Warron and Valine travel to Lohar in Undertavern.
IF
GlobalFlagSet("RC_WH_WreckerCaveEntranceDwarvesDead")
AND
DB_RC_WH_DwarvenCamp_Dwarves(_Dwarf)
AND
Query_CharacterIsAliveAndNotInCombat(_Dwarf)
AND
ObjectGetFlag(_Dwarf,"RC_WH_DwarvenCamp_Left",1)
THEN
Proc_RC_WH_DwarvenCamp_MoveToLoharFromCombat(_Dwarf);

IF
GlobalFlagSet("RC_WH_WreckerCaveEntranceDwarvesDead")
AND
DB_RC_WH_DwarvenCamp_Dwarves(_Dwarf)
AND
Query_CharacterIsAliveAndNotInCombat(_Dwarf)
AND
NOT DB_DialogNPCs(_,CHARACTERGUID_S_RC_WH_DwarvenCamp_Warron_3be7c843-8d73-4d0e-be57-637c97a8b3bd,_)
AND
ObjectGetFlag(_Dwarf,"RC_WH_DwarvenCamp_Left",0)
THEN
Proc_RC_WH_DwarvenCamp_MoveToLoharFromDen(_Dwarf);

PROC
Proc_ObjectLeftCombat((CHARACTERGUID)_Dwarf,_)
AND
DB_RC_WH_DwarvenCamp_Dwarves(_Dwarf)
AND
ObjectGetFlag(_Dwarf,"RC_WH_DwarvenCamp_Left",1)
AND
GlobalGetFlag("RC_WH_WreckerCaveEntranceDwarvesDead",1)
THEN
Proc_RC_WH_DwarvenCamp_MoveToLoharFromCombat(_Dwarf);

// In case dwarves didn't join combat, but someone is talking to them
IF
DialogEnded("RC_WH_DwarvenCamp_HidingDwarves",_)
AND
GlobalGetFlag("RC_WH_WreckerCaveEntranceDwarvesDead",1)
AND
DB_RC_WH_DwarvenCamp_Dwarves(_Dwarf)
THEN
Proc_RC_WH_DwarvenCamp_MoveToLoharFromDen(_Dwarf);
//END_REGION

//REGION On Mordus death
// Mordus is dead => Warron and Valine travel to Lohar in Undertavern.
IF
GlobalFlagSet("RC_DW_WC_BossElderIsDead")
AND
DB_RC_WH_DwarvenCamp_Dwarves(_Dwarf)
AND
Query_CharacterIsAliveAndNotInCombat(_Dwarf)
AND
NOT DB_DialogNPCs(_,_Dwarf,_)
THEN
Proc_RC_WH_DwarvenCamp_MoveToLoharFromDen(_Dwarf);

// In case someone talks to them
IF
DialogEnded("RC_WH_DwarvenCamp_HidingDwarves",_)
AND
GlobalGetFlag("RC_DW_WC_BossElderIsDead",1)
AND
DB_RC_WH_DwarvenCamp_Dwarves(_Dwarf)
THEN
Proc_RC_WH_DwarvenCamp_MoveToLoharFromDen(_Dwarf);
//END_REGION


PROC
Proc_RC_WH_DwarvenCamp_MoveToLoharFromCombat((CHARACTERGUID)_Dwarf)
THEN
Proc_RC_WH_DwarvenCamp_FindCharToFarewell();

PROC
Proc_RC_WH_DwarvenCamp_MoveToLoharFromCombat((CHARACTERGUID)_Dwarf)
AND
DB_RC_WH_DwarvenCamp_CharToFarewell(_Dwarf,_Char)
AND
DB_RC_WH_DwarvenCamp_SeeYouDialogs(_Dwarf,_Dialog)
THEN
Proc_StartDialog(0,_Dialog,_Dwarf,_Char);

PROC
Proc_RC_WH_DwarvenCamp_MoveToLoharFromCombat((CHARACTERGUID)_Dwarf)
AND
NOT DB_RC_WH_DwarvenCamp_CharToFarewell(_Dwarf,_)
AND
DB_RC_WH_DwarvenCamp_SeeYouADDialogs(_Dwarf,_Dialog)
THEN
Proc_StartDialog(1,_Dialog,_Dwarf);
CharacterDisappearOutOfSight(_Dwarf,0,0,"RC_WH_WarronValine_TeleportToUndertavern",1);

IF
DialogEnded(_Dialog,_)
AND
DB_RC_WH_DwarvenCamp_SeeYouDialogs(_,_Dialog)
AND
DB_RC_WH_DwarvenCamp_Dwarves(_Dwarf)
THEN
CharacterDisappearOutOfSight(_Dwarf,0,0,"RC_WH_WarronValine_TeleportToUndertavern",1);

PROC
Proc_RC_WH_DwarvenCamp_FindCharToFarewell()
AND
QRY_IsEmptyDB("DB_RC_WH_DwarvenCamp_CharToFarewell", 2)
AND
DB_RC_WH_DwarvenCamp_Dwarves((CHARACTERGUID)_Dwarf)
AND
DB_RC_WH_DwarvenCamp_HelpFightCharacters((CHARACTERGUID)_Char)
AND
QRY_SpeakerIsAvailableAndInDialogRange(_Char,_Dwarf)
THEN
DB_RC_WH_DwarvenCamp_CharToFarewell(_Dwarf,_Char);


PROC
Proc_RC_WH_DwarvenCamp_MoveToLoharFromDen((CHARACTERGUID)_Dwarf)
THEN
Proc_RC_WH_DwarvenCamp_LeaveCamp(_Dwarf);
SetStoryEvent(_Dwarf,"RC_WH_WarronValine_TeleportToUndertavern");

IF
StoryEvent((CHARACTERGUID)_Dwarf,"RC_WH_WarronValine_TeleportToUndertavern")
AND
DB_RC_WH_DwarvenCamp_MoveToLoharStools(_Dwarf,_Stool)
THEN
SetFaction(_Dwarf,"RC_DW_Undertavern");
TeleportTo(_Dwarf,TRIGGERGUID_S_RC_DW_Tavern_Hideout_Ent_3c484eae-b27c-42a9-9407-09dcdf52ae4b);
SetOnStage(_Dwarf,1); // if we used DisappearOutOfSight
SetVisible(_Dwarf,1);
ProcSetInvulnerable(_Dwarf,0);
CharacterSetCanTrade(_Dwarf,1);
ProcCharacterMoveTo(_Dwarf,_Stool,0,"RC_WH_WarronValine_MoveToLohar");

IF
StoryEvent((CHARACTERGUID)_Dwarf,"RC_WH_WarronValine_MoveToLohar")
AND
DB_RC_WH_DwarvenCamp_MoveToLoharStools(_Dwarf,_Stool)
AND
DB_RC_WH_DwarvenCamp_UndertavernDialogs(_Dwarf,_Dialog)
THEN
DB_Dialogs(_Dwarf,_Dialog);
CharacterUseItem(_Dwarf,_Stool,"");
ProcIncreaseCounter("RC_WH_DwarvenCamp_MovedToLohar");

IF
StoryEvent((CHARACTERGUID)_Dwarf,"RC_WH_WarronValine_MoveToLohar")
AND
SysCount("DB_RC_WH_DwarvenCamp_Dwarves",1,_DwarvesAlive)
AND
DB_GlobalCounter("RC_WH_DwarvenCamp_MovedToLohar",_DwarvesAlive)
THEN
GoalCompleted;
//END_REGION


PROC
Proc_RC_WH_DwarvenCamp_LeaveCamp((CHARACTERGUID)_Dwarf)
THEN
ObjectSetFlag(_Dwarf,"RC_WH_DwarvenCamp_Left");
SetVisible(_Dwarf,1);
ProcSetInvulnerable(_Dwarf,0);
ProcCharacterEnableAllCrimes(_Dwarf);
EXITSECTION
SysClear("DB_RC_WH_DwarvenCamp_DialogUnluckyFlags",1);
SysClear("DB_RC_WH_DwarvenCamp_Dwarves",1);
SysClear("DB_RC_WH_DwarvenCamp_SeeYouDialogs",2);
SysClear("DB_RC_WH_DwarvenCamp_SeeYouADDialogs",2);
SysClear("DB_RC_WH_DwarvenCamp_MoveToLoharStools",2);
SysClear("DB_RC_WH_DwarvenCamp_UndertavernDialogs",2);
SysClear("DB_RC_WH_DwarvenCamp_HelpFightCharacters",1);
SysClear("DB_RC_WH_DwarvenCamp_CharToFarewell",2);
SysClear("DB_RC_WH_DwarvenCamp_NoticedDen",1);
ProcRemoveCounter("RC_WH_DwarvenCamp_MovedToLohar");
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_RC_WH_DwarvenCamp_PerceptionVBTrigger_41c100e7-c1fa-47cf-8b08-d4644c8d905c);
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_RC_WH_DwarvenCamp_PerceptionADTrigger_8253cb7e-ab87-4a40-bb4f-e77c81091994);
ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
