Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Subregion(TRIGGERGUID_S_LV_HoE_SUB_GodRoom_001_e937bd81-971f-4ae7-9ac9-829c8b61858f,"LV_HoE_SUB_HoE",0);
DB_Subregion(TRIGGERGUID_S_LV_HoE_SUB_TreeArea_ebc5ba90-1adf-40c8-aa14-a2a980b99865,"LV_HoE_SUB_TreeArea",0);
DB_Subregion(TRIGGERGUID_S_LV_HoE_SUB_LadyVengeanceInHoE_4964e348-3891-4a7b-ba5a-06c965c6ca38,"LV_HoE_SUB_LadyVengeanceInHoE",0);

ProcDeclareCounter("LV_HoE_InvolvedPlayerCount");
ProcDeclareCounter("LV_HoE_PlayersInCavern");

DB_LV_HoE_DeadGods((CHARACTERGUID)CHARACTERGUID_S_LV_HoE_TirCendelius_6c131637-366e-43f1-a005-7b45786e2aa6);
DB_LV_HoE_DeadGods(CHARACTERGUID_S_LV_HoE_Duna_aedde842-f1bc-4ec5-9c4f-897c4ed5058c);
DB_LV_HoE_DeadGods(CHARACTERGUID_S_LV_HoE_Rhalic_8bb114d4-47de-413d-80a7-52b3f5e08b28);
DB_LV_HoE_DeadGods(CHARACTERGUID_S_LV_HoE_Lohse_bfc36e1e-9e18-4dea-811c-3522fb87ce93);
DB_LV_HoE_DeadGods(CHARACTERGUID_S_LV_HoE_Amadia_2bdb57d7-9a6a-44d0-a839-94ccb04a3106);
DB_LV_HoE_DeadGods(CHARACTERGUID_S_LV_HoE_ZorlStissa_929bcf83-ac27-4007-95a3-d34edaf8bcbe);
DB_LV_HoE_DeadGods(CHARACTERGUID_S_LV_HoE_OrcGod_ed633ff7-9e03-4fbb-af50-47196c89cc13);



DB_Dialogs(CHARACTERGUID_S_LV_HoE_Amadia_2bdb57d7-9a6a-44d0-a839-94ccb04a3106,"LV_HoE_God_Amadia");
DB_Dialogs(CHARACTERGUID_S_LV_HoE_Duna_aedde842-f1bc-4ec5-9c4f-897c4ed5058c,"LV_HoE_God_Duna");
DB_Dialogs(CHARACTERGUID_S_LV_HoE_Rhalic_8bb114d4-47de-413d-80a7-52b3f5e08b28,"LV_HoE_God_Rhalic");
DB_Dialogs(CHARACTERGUID_S_LV_HoE_TirCendelius_6c131637-366e-43f1-a005-7b45786e2aa6,"LV_HoE_God_TirCendelius");
DB_Dialogs(CHARACTERGUID_S_LV_HoE_ZorlStissa_929bcf83-ac27-4007-95a3-d34edaf8bcbe,"LV_HoE_God_ZorlStissa");
DB_Dialogs(CHARACTERGUID_S_LV_HoE_OrcGod_ed633ff7-9e03-4fbb-af50-47196c89cc13,"LV_HoE_God_Vrogir");
DB_Dialogs(CHARACTERGUID_S_LV_HoE_ImpGod_bfc36e1e-9e18-4dea-811c-3522fb87ce93,"LV_HoE_God_Xantezza");


DB_LV_HoE_GodPlatformHelpers(ITEMGUID_S_LV_HoE_GodPlatformHelper_001_2e85d53c-afb5-4ea1-8432-2b92997e2e35);
DB_LV_HoE_GodPlatformHelpers(ITEMGUID_S_LV_HoE_GodPlatformHelper_002_7e41d042-67ac-4767-bac8-328b7c139b96);
DB_LV_HoE_GodPlatformHelpers(ITEMGUID_S_LV_HoE_GodPlatformHelper_003_d6b35b33-24a5-4036-b6b8-7b7004314789);
DB_LV_HoE_GodPlatformHelpers(ITEMGUID_S_LV_HoE_GodPlatformHelper_004_a9bfcbd9-48f9-4ef0-bb5a-ab4e826426c1);
DB_LV_HoE_GodPlatformHelpers(ITEMGUID_S_LV_HoE_GodPlatformHelper_005_530d70c6-3745-40d7-8572-630ae8f069e4);
DB_LV_HoE_GodPlatformHelpers(ITEMGUID_S_LV_HoE_GodPlatformHelper_006_345e9c66-adb2-4af3-8d96-5a7032a3543b);
DB_LV_HoE_GodPlatformHelpers(ITEMGUID_S_LV_HoE_GodPlatformHelper_007_7c253eb4-cee5-41c8-b47e-44e712d54af6);



//LV_HoE_God_UndeadGod
DB_Dialogs(ITEMGUID_S_LV_HoE_ShipHeadInHoE_15132297-e9b6-40ea-ad96-ab6552d3b481,"LV_HoE_ShipHeadInHoE");

ProcTriggerRegisterForPlayers(TRIGGERGUID_S_LV_HoE_PathToTreeVB_Box_001_753d2745-b70c-41d0-a9db-f109bfd40b50);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_LV_HoE_PathToTreeVB_Box_002_275b5ad7-1539-4eb9-8694-450cfa4e5773);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_LV_HoE_LowerDeckEmptyVB_Box_987d660e-9747-4e93-aa2a-b5309da173a9);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_LV_HoE_MidDeckEmptyVB_Box_ca879256-c7b5-41e4-ac38-eefbb5961904);

Proc_PyramidBlockerTriggerAdd(TRIGGERGUID_S_LV_HoE_SUB_GodRoom_001_e937bd81-971f-4ae7-9ac9-829c8b61858f);

DB_LV_Companion_Ghosts((CHARACTERGUID)S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f,(CHARACTERGUID)LV_RedPrince_Ghost_14a63143-b1a5-4d7d-8ed8-360f159e8d4d,"LV_RedPrince_Ghost");
DB_LV_Companion_Ghosts(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,CHARACTERGUID_LV_Ifan_Ghost_8623e194-02bb-4fc9-855c-6f0da8a1c87a,"LV_Ifan_Ghost");
DB_LV_Companion_Ghosts(CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,CHARACTERGUID_LV_Sebille_Ghost_fe763c4d-0687-40d8-ac0e-97a2e9b1d9b8,"LV_Sebille_Ghost");
DB_LV_Companion_Ghosts(CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,CHARACTERGUID_LV_Lohse_Ghost_422c4340-f8aa-44f6-9a37-1868659bc2b7,"LV_Lohse_Ghost");
DB_LV_Companion_Ghosts(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,CHARACTERGUID_LV_Beast_Ghost_0b6c190c-305f-4fc5-922c-99f0bb087d3a,"LV_Beast_Ghost");
DB_LV_Companion_Ghosts(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb,CHARACTERGUID_LV_Fane_Ghost_43619c0e-dea6-4d90-b1a7-127e7542a662,"LV_Fane_Ghost");

KBSECTION
//REGION HoE Setup
IF
MovieFinished("CS_LV-Battle_to_HOE")
AND
GlobalGetFlag("FTJ_LV_StartHoELevel",1)
AND
QueryOnlyOnce("FTJ_LV_StartHoELevel_SetupAfterMovie")
THEN
TimerLaunch("FTJ_LV_SendPlayersToHoE",500);
Proc_PyramidBlockerTriggerAdd(TRIGGERGUID_S_LV_SUB_Assault_53da8e16-a89d-4b63-be70-fdf42d0a3ce2);

IF
MovieFinished("CS_LV-Battle_to_HOE")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
FadeToBlack(_Player,2.0,1,"Placeholder");

IF
DB_LV_HoE_DeadGods((CHARACTERGUID)_God)
THEN
CharacterSetForceSynch(_God,1);
CharacterUseItem(_God,ITEMGUID_S_LV_HoE_SpiritTree_02bda965-05ae-4be8-8400-2c58a68490a0,"LV_HoE_Gods_SitFinished");
ApplyStatus(_God,"CURSED",-1.0,1);
ProcCharacterDisableAllCrimes(_God);

IF
StoryEvent((CHARACTERGUID)_God,"LV_HoE_Gods_SitFinished")
THEN
CharacterSetAnimationOverride(_God,"Hung_04_Loop");
CharacterSetForceSynch(_God,0);
//END_REGION



IF
GlobalFlagSet("FTJ_LV_StartHoELevel")
THEN
ActivatePersistentLevelTemplate(ce8c7fa1-9f7e-4d22-8902-e0535f0f8f85);
ItemSetCanInteract(ITEMGUID_S_LV_HoE_SpiritTree_02bda965-05ae-4be8-8400-2c58a68490a0,0);


IF
GlobalFlagSet("FTJ_LV_StartHoELevel")
AND
DB_LV_HoE_DeadGods(_God)
THEN
CharacterSetForceSynch(_God,1);
SetVarInteger(_God,"bool_CanCower",0);
SetVarInteger(_God,"bool_CanFleeWhileCowering",0);
SetVarInteger(_God,"FleeFromDangerousSurface",0);

IF
GlobalFlagSet("FTJ_LV_StartHoELevel")
AND
DB_IsPlayer(_Player)
THEN
ProcForceStopDialog(_Player);
CharacterRemoveSummons(_Player,0);
ObjectSetFlag(_Player,"QuestAdd_LV_HoE_Main");
ObjectSetFlag(_Player,"QuestUpdate_LV_HoE_Main_Start");
CharacterResurrect(_Player);
Proc_CharacterFullRestore(_Player);
Proc_PyramidBlockerTriggerAdd(TRIGGERGUID_S_LV_SUB_LowerDeck_fa227b55-06d3-4204-8b3b-abe7d2d51aed);
Proc_PyramidBlockerTriggerAdd(TRIGGERGUID_S_LV_HoE_SUB_LadyVengeance_Cabin_467af4b3-5383-447c-a53b-5c4c5e87ae9b);
Proc_PyramidBlockerTriggerAdd(TRIGGERGUID_S_LV_SUB_DallisSecretRoom_608e1902-0ed8-4aa6-99b9-fec2d1e518cd);
Proc_PyramidBlockerTriggerAdd(TRIGGERGUID_S_LV_SUB_Assault_53da8e16-a89d-4b63-be70-fdf42d0a3ce2);

IF
GlobalFlagSet("FTJ_LV_StartHoELevel")
THEN
PROC_RemoveDialogFromCharacter(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e);
DB_Dialogs(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,"LV_HoE_River");
CreateSurface(TRIGGERGUID_S_LV_SUB_LowerDeck_fa227b55-06d3-4204-8b3b-abe7d2d51aed,"SurfaceNone",150.0,1.0);
CreateSurface(TRIGGERGUID_S_LV_HoE_SUB_LadyVengeance_Cabin_467af4b3-5383-447c-a53b-5c4c5e87ae9b,"SurfaceNone",150.0,1.0);
CreateSurface(TRIGGERGUID_S_LV_MagisterPrisonerWander_Poly_8b97a309-6922-4948-9669-0ef62b319083,"SurfaceNone",150.0,1.0);


//Kill Unrecruited Companions & Spawn their ghosts.
IF
DB_LV_Companion_Ghosts(_,_Ghost,_)
THEN
SetOnStage(_Ghost,0);

IF
GlobalFlagSet("FTJ_LV_StartHoELevel")
AND
DB_LV_Companion_Ghosts(_Companion,_Ghost,_GhostDialog)
AND
NOT DB_IsPlayer((CHARACTERGUID)_Companion)
AND
GetPosition(_Ghost,_X,_Y,_Z)
THEN
// Kill the companions on the Dallis boat, so we don't see them or their blood lying around
TeleportTo(_Companion,TRIGGERGUID_S_LV_DallisStart_Point_72a8b763-8201-44f5-b3c5-af773e267bf7);
// Needs to be on stage for force synching to work after reload
SetOnStage(_Companion,1);
DB_Ghosts(_Companion,_Ghost,_GhostDialog);
// Needed to make ghost active regardless of distance between companion body and ghost
CharacterSetForceSynch(_Companion,1);
CharacterDie(_Companion,0,"DoT");
NOT DB_LV_Companion_Ghosts(_Companion,_Ghost,_GhostDialog);
DB_LV_HoE_DiedOnLV(_Companion);
DB_LV_CompanionGhosts(_Ghost,_Companion);
DB_LV_CompanionGhostOriginalPosition(_Ghost,_X,_Y,_Z);
Proc_OriginLeft(_Companion,"_DiedInLV");

// Now bring the ghost back to our LV
IF
DB_Dead(_Companion)
AND
DB_LV_CompanionGhosts(_Ghost,_Companion)
AND
DB_LV_CompanionGhostOriginalPosition(_Ghost,_X,_Y,_Z)
THEN
NOT DB_LV_CompanionGhostOriginalPosition(_Ghost,_X,_Y,_Z);
TeleportToPosition(_Ghost,_X,_Y,_Z,"",0);


PROC
ProcRegionEnded("LV_HoE_Main")
AND
DB_LV_CompanionGhosts(_Ghost,_Companion)
THEN
CharacterSetForceSynch(_Companion,0);

PROC
ProcRegionEnded("LV_HoE_Main")
AND
DB_LV_CompanionGhosts(_Ghost,_Companion)
AND
// Ifan can have disappeared after talking to him, and purging wand can have been used
NOT DB_GoneGhosts(_Ghost)
THEN
Proc_GhostHacks_DisableGlobalNPCGhost(_Ghost);

//REGION Teleport Players to Tree Area
//Avatar
IF
GlobalFlagSet("FTJ_LV_StartHoELevel")
THEN
PROC_GLO_PartyMembers_TempRemoveAll(1);


IF
GlobalFlagSet("FTJ_LV_StartHoELevel")
AND
DB_Avatars(_Player)
THEN
CharacterSetReadyCheckBlocked(_Player,1);
CharacterResurrect(_Player);
Proc_CharacterFullRestore(_Player);
DB_GLO_HoERespawnPoint(_Player,TRIGGERGUID_S_LV_HoE_GodTreeStart_Point_76175c61-128a-45fd-b486-9a6ffbea1110);
PlayEffect(_Player,"RS3_FX_GP_Combat_CameraShake_A");
PlayEffect(_Player,"RS3_FX_GP_ScriptedEvent_HOE_Summoning_01");
TeleportTo(_Player,TRIGGERGUID_S_LV_HoE_GodTreeStart_Point_76175c61-128a-45fd-b486-9a6ffbea1110,"",0);


IF
GlobalFlagSet("FTJ_LV_StartHoELevel")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
DB_Avatars(_Player)
THEN
ProcIncreaseCounter("LV_HoE_InvolvedPlayerCount",1);
ProcStartMovie("CS_LV-Battle_to_HOE");


//Fall back for non AVATAR owning users
IF
GlobalFlagSet("FTJ_LV_StartHoELevel")
THEN
Proc_LV_HoE_SendNonAvatarUsersToHoE();

PROC
Proc_LV_HoE_SendNonAvatarUsersToHoE()
AND
DB_IsPlayer(_Player)
AND
NOT DB_Avatars(_Player)
THEN
TeleportTo(_Player,TRIGGERGUID_S_LV_HoE_Cave_Point_001_a8945040-2fd9-4c19-bffe-7972fb84eb83);
DB_GLO_HoERespawnPoint(_Player,TRIGGERGUID_S_LV_HoE_Cave_Point_001_a8945040-2fd9-4c19-bffe-7972fb84eb83);
CharacterResurrect(_Player);
Proc_CharacterFullRestore(_Player);

//Check to see if all players are able to leave
IF
DB_GlobalCounter("LV_HoE_PlayersInCavern",_Count)
AND
DB_GlobalCounter("LV_HoE_InvolvedPlayerCount",_TotalCount)
AND
_Count != 0
AND
_Count >= _TotalCount
THEN
GlobalSetFlag("LV_HoE_CanLeaveHoE");

//Tree Area VB
IF
CharacterEnteredTrigger((CHARACTERGUID)_Player,TRIGGERGUID_S_LV_HoE_SUB_TreeArea_ebc5ba90-1adf-40c8-aa14-a2a980b99865)
AND
QueryOnlyOnce("LV_HoE_VB_SpiritTree_Once")
THEN
StartVoiceBark("LV_HoE_VB_SpiritTree",_Player);
ObjectSetFlag(_Player,"QuestUpdate_LV_HoE_Main_God_Hanging");

//REGION Cursed Gods

//Cursed journal update
IF
DialogEnded(_Dialog,_Id)
AND
DB_LV_HoE_DeadGods(_God)
AND
DB_Dialogs(_God,_Dialog)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_God,"LV_HoE_GodBlessed",0)
THEN
ObjectSetFlag(_Player,"QuestUpdate_LV_HoE_Main_God_Cursed");

IF
CharacterUsedSkillOnTarget((CHARACTERGUID)_Player,(CHARACTERGUID)_God,"Target_Bless",_,_)
AND
DB_LV_HoE_DeadGods(_God)
THEN
RemoveStatus(_God,"CURSED");
ObjectSetFlag(_God,"LV_HoE_GodBlessed");
PROC_LoopEffect("RS3_FX_GP_Status_Blessed_01",_God,"LV_HoE_GodBlessed_LoopFx","LV_HoE_Main","");
Proc_RC_FTJ_Godwoken_SetUpdate(_Player,"QuestUpdate_FTJ_Godwoken_GodHanged");
DB_LV_HoE_GodBlessed_StartDialog(_Player,_God);
ProcObjectTimer(_God,"LV_HoE_GodBlessed_StartDialog_Timer",1300);
ProcObjectTimer(_Player,"LV_HoE_GodBlessed_StartDialog_Timer",2000);

PROC
Proc_RC_FTJ_Godwoken_SetUpdate((CHARACTERGUID)_Player,(STRING)_Update)
AND
_Player != CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295
THEN
ObjectSetFlag(_Player,_Update);

PROC
Proc_RC_FTJ_Godwoken_SetUpdate((CHARACTERGUID)CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,(STRING)_String)
AND
StringConcatenate(_String,"_Lohse",_Update)
THEN
ObjectSetFlag(CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,_Update);


PROC
ProcObjectTimerFinished(_God,"LV_HoE_GodBlessed_StartDialog_Timer")
THEN
PROC_StopLoopEffect(_God,"LV_HoE_GodBlessed_LoopFx");


//Start dialog with player who Blessed them
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"LV_HoE_GodBlessed_StartDialog_Timer")
AND
DB_LV_HoE_GodBlessed_StartDialog(_Player,(CHARACTERGUID)_God)
AND
IsTagged(_Player,"LOHSE",0)
AND
QRY_IsAvailableTo(_Player,_God)
AND
DB_Dialogs(_God,_Dialog)
THEN
Proc_StartDialog(0,_Dialog,_God,_Player);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"LV_HoE_GodBlessed_StartDialog_Timer")
AND
DB_LV_HoE_GodBlessed_StartDialog(_Player,(CHARACTERGUID)_God)
THEN
NOT DB_LV_HoE_GodBlessed_StartDialog(_Player,_God);

//Lohse
IF
CharacterUsedSkillOnTarget((CHARACTERGUID)_Player,(CHARACTERGUID)_God,"Target_Bless",_,_)
AND
IsTagged(_Player,"LOHSE",1)
AND
DB_LV_HoE_DeadGods(_God)
AND
GetPosition(TRIGGERGUID_S_LV_HoE_Lohse_Point_97ccd9c3-dc9b-4d4e-bc58-ea7ee66721ed,_X,_Y,_Z)
AND
CharacterCreateAtPosition(_X,_Y,_Z,"QUEST_HoE_Lohse_e4305b97-4ec2-43c2-b2a9-994b1df35636",1,_Lohse)
THEN
RemoveStatus(_God,"CURSED");
ProcSetInvulnerable(_Lohse,1);
CharacterSetImmortal(_Lohse,1);
ProcCharacterDisableAllCrimes(_Lohse);
PlayEffect(_Lohse,"RS3_FX_GP_ScriptedEvent_HOE_Summoning_01");
CharacterTransformAppearanceTo(_Lohse,_Player,1,1);
ProcObjectTimer(_Player,"LV_HoE_God_Lohse_Dialog_Timer",2250);
TimerLaunch("LV_HoE_Lohse_SetMaterial",100);
DB_LV_HoE_LohseGod(_Lohse);

//hack to reset lohse material
IF
TimerFinished("LV_HoE_Lohse_SetMaterial")
AND
DB_LV_HoE_LohseGod(_Lohse)
THEN
SetStoryEvent(_Lohse,"SetMaterialEvent");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"LV_HoE_God_Lohse_Dialog_Timer")
AND
DB_LV_HoE_LohseGod(_Lohse)
THEN
ProcForceStopDialog(_Player);
DB_Dialogs(_Lohse,"LV_HoE_God_Lohse");
Proc_StartDialog(0,"LV_HoE_God_Lohse",_Lohse,_Player);

IF
DialogEnded("LV_HoE_God_Lohse",_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"LV_HoE_SendPlayerToLadyVengeance",1)
AND
DB_LV_HoE_LohseGod(_Lohse)
AND
GetPosition(_Lohse,_X,_Y,_Z)
THEN
Proc_LV_HoE_SendPlayerToLadyVengeance(_Player);
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_HOE_Summoning_01",_X,_Y,_Z);
SetOnStage(_Lohse,0);
PROC_RemoveDialogFromCharacter(_Lohse);



//General
IF
DialogEnded(_Dialog,_Id)
AND
DB_LV_HoE_DeadGods(_God)
AND
DB_Dialogs(_God,_Dialog)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"LV_HoE_SendPlayerToLadyVengeance",1)
THEN
Proc_LV_HoE_SendPlayerToLadyVengeance(_Player);

//Reunite Users with their cmpanions and send them to the LV
PROC
Proc_LV_HoE_SendPlayerToLadyVengeance((CHARACTERGUID)_Player)
THEN
PlayEffect(_Player,"RS3_FX_GP_Combat_CameraShake_A");
CharacterFreeze(_Player);
CharacterSetImmortal(_Player,1);
ProcSetInvulnerable(_Player,1);
ProcObjectTimer(_Player,"LV_HoE_SendPlayerToLadyVengeance_Timer",2550);
PlayAnimation(_Player,"stillblind");
PlayEffect(_Player,"RS3_FX_GP_ScriptedEvent_HOE_Summoning_01");
FadeToBlack(_Player,2.5,0,"Placeholder");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"LV_HoE_SendPlayerToLadyVengeance_Timer")
THEN
CharacterSetReadyCheckBlocked(_Player,0);
ObjectSetFlag(_Player,"QuestUpdate_LV_HoE_Main_ReturnToLV");
ProcIncreaseCounter("LV_HoE_PlayersInCavern",1);
CharacterDetachFromGroup(_Player);
TeleportTo(_Player,TRIGGERGUID_S_LV_HoE_Cave_Point_001_a8945040-2fd9-4c19-bffe-7972fb84eb83,"",0);
PROC_GLO_PartyMembers_TempRestore(_Player);
CharacterUnfreeze(_Player);
CharacterSetImmortal(_Player,0);
ProcSetInvulnerable(_Player,0);
FadeToBlack(_Player,2.5,1,"Placeholder");
PlayEffect(_Player,"RS3_FX_GP_ScriptedEvent_HOE_Summoning_01");
Proc_FixatePartyMembers(_Player);
GlobalSetFlag("GLO_Companions_Fixated");
UserSetFlag(_Player,"QuestUpdate_CORE_Chapter3_LV_Hoe");

PROC
Proc_FixatePartyMembers((CHARACTERGUID)_Avatar)
AND
DB_CompanionAvatarBond(_Companion,_Avatar)
AND
DB_Companion_FixatedFlag(_Companion,_Flag)
THEN
ObjectSetFlag(_Avatar,_Flag);

PROC
Proc_LV_HoE_SendPlayerToLadyVengeance((CHARACTERGUID)_Player)
AND
DB_IsPlayer(_Player)
THEN
DB_GLO_HoERespawnPoint(_Player,TRIGGERGUID_S_LV_HoE_Cave_Point_001_a8945040-2fd9-4c19-bffe-7972fb84eb83);

PROC
Proc_LV_HoE_SendPlayerToLadyVengeance((CHARACTERGUID)_Player)
AND
QueryOnlyOnce("LV_HoE_VB_ReturnedToLadyVengeance_Once")
THEN
ProcObjectTimer(_Player,"LV_HoE_VB_ReturnedToLadyVengeance_Timer",6500);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"LV_HoE_VB_ReturnedToLadyVengeance_Timer")
THEN
StartVoiceBark("LV_HoE_VB_ReturnedToLadyVengeance",_Player);

IF
CharacterEnteredTrigger((CHARACTERGUID)_Player,TRIGGERGUID_S_LV_HoE_SUB_LadyVengeance_Cabin_467af4b3-5383-447c-a53b-5c4c5e87ae9b)
AND
GlobalGetFlag("FTJ_LV_StartHoELevel",1)
THEN
ObjectSetFlag(_Player,"QuestUpdate_LV_HoE_Main_EmptyLV");

//Do RD after all returned
IF
GlobalFlagSet("LV_HoE_CanLeaveHoE")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
IsTagged(_Player,"AVATAR",1)
THEN
ProcDefineReflectionDialog("LV_HoE_RD_HuntingTheGods",_Player);

IF
GlobalFlagSet("LV_HoE_CanLeaveHoE")
AND
DB_LV_HoE_DeadGods(_God)
THEN
CharacterSetForceSynch(_God,0);

IF
CharacterEnteredTrigger((CHARACTERGUID)_Player,TRIGGERGUID_S_LV_HoE_LowerDeckEmptyVB_Box_987d660e-9747-4e93-aa2a-b5309da173a9)
AND
GlobalGetFlag("FTJ_LV_StartHoELevel",1)
AND
QueryOnlyOnce("LV_HoE_LowerDeckEmptyVB_Box_Once")
THEN
StartVoiceBark("LV_HoE_VB_LowerDeckEmpty",_Player);
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_LV_HoE_LowerDeckEmptyVB_Box_987d660e-9747-4e93-aa2a-b5309da173a9);


IF
CharacterEnteredTrigger((CHARACTERGUID)_Player,TRIGGERGUID_S_LV_HoE_MidDeckEmptyVB_Box_ca879256-c7b5-41e4-ac38-eefbb5961904)
AND
GlobalGetFlag("FTJ_LV_StartHoELevel",1)
AND
QueryOnlyOnce("LV_HoE_MidDeckEmptyVB_Box_Once")
THEN
StartVoiceBark("LV_HoE_VB_MidDeckEmpty",_Player);
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_LV_HoE_MidDeckEmptyVB_Box_ca879256-c7b5-41e4-ac38-eefbb5961904);

//END_REGION


//END_REGION


//Delay on the summoning fx
IF
TimerFinished("FTJ_LV_SendPlayersToHoE")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
PlayEffect(_Player,"RS3_FX_GP_ScriptedEvent_HOE_Summoning_01");

//Autosave
IF
TimerFinished("FTJ_LV_SendPlayersToHoE")
THEN
TimerLaunch("FTJ_LV_SendPlayersToHoE_Autosave",1000);

IF
TimerFinished("FTJ_LV_SendPlayersToHoE_Autosave")
THEN
AutoSave();


IF
TimerFinished("FTJ_LV_SendPlayersToHoE")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
IsTagged(_Player,"AVATAR",1)
THEN
ProcObjectTimer(_Player,"LV_HoE_DoStartDialog",1500);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"LV_HoE_DoStartDialog")
THEN
Proc_StartDialog(0,"LV_HoE_PlayerEnteredHoE",_Player);

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_LV_HoE_PathToTreeVB_Box_001_753d2745-b70c-41d0-a9db-f109bfd40b50)
AND
QueryOnlyOnce("LV_HoE_DoStartVB_Once")
THEN
StartVoiceBark("LV_HoE_VB_EnteredHoE",_Player);
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_LV_HoE_PathToTreeVB_Box_001_753d2745-b70c-41d0-a9db-f109bfd40b50);


IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_LV_HoE_PathToTreeVB_Box_002_275b5ad7-1539-4eb9-8694-450cfa4e5773)
AND
NOT DB_OnlyOnce("LV_HoE_VB_SpiritTree_Once")
AND
QueryOnlyOnce("LV_HoE_VB_PathToTree_002_once")
THEN
StartVoiceBark("LV_HoE_VB_PathToTree_002",_Player);
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_LV_HoE_PathToTreeVB_Box_002_275b5ad7-1539-4eb9-8694-450cfa4e5773);

//REGION River Dialog at Act End

IF
StoryEvent((CHARACTERGUID)_Player,"FTJ_LV_RiverStartDialog")
AND
GlobalGetFlag("FTJ_LV_StartHoELevel",1)
THEN
Proc_StartDialog(0,"LV_HoE_River",CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,_Player);


IF
DialogEnded("LV_HoE_River",_Inst)
AND
GlobalGetFlag("LV_HoE_StartAct2",1)
AND
DB_DialogPlayers(_Inst,_Player,_)
THEN
GlobalClearFlag("LV_HoE_StartAct2");
ReadyCheckStart(_Player,"LV_HoE_StartAct2");

IF
ReadyCheckPassed("LV_HoE_StartAct2")
THEN
Proc_StartAct2();


IF
ReadyCheckFailed("LV_HoE_StartAct2")
THEN
GlobalClearFlag("LV_HoE_RiverCastLeaveSpell");
CharacterSetAnimationOverride(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,"");

// Cleanup for origin companions that might have been dismissed while on the HoE LV
PROC
Proc_StartAct2()
AND
DB_OriginRecruitmentLocation_Region(_,_Char,_,_)
AND
NOT DB_IsPlayer(_Char)
THEN
Proc_StartAct2_CleanUp(_Char);

// Cleanup for players
PROC
Proc_StartAct2()
AND
DB_IsPlayer(_Player)
THEN
Proc_StartAct2_CleanUp(_Player);

PROC
Proc_StartAct2_CleanUp((CHARACTERGUID)_Char)
THEN
ObjectSetFlag(_Char,"QuestUpdate_LV_HoE_Main_Meistr");
ObjectSetFlag(_Char,"QuestUpdate_LV_HoE_Main_EndAct");
PROC_GLO_LeaveHallOfEchoes(_Char);
CharacterSetRelationIndivFactionToFaction(_Char,"RC_Magister",50);
CharacterSetRelationFactionToIndivFaction("RC_Magister",_Char,50);

PROC
Proc_StartAct2_CleanUp((CHARACTERGUID)_Char)
AND
DB_IsPlayer(_Char)
THEN
Proc_ShakeCameraForTime(_Char,2200);

PROC
Proc_StartAct2()
THEN
TimerLaunch("LV_HoE_StartAct2FadeOut_Timer",2200);

PROC
Proc_StartAct2()
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
CharacterFreeze(_Player);
FadeToBlack(_Player,2.0,0,"Placeholder");


//Turn off during HoE, turn back on once Act 2 starts
IF
GlobalFlagSet("FTJ_LV_StartHoELevel")
THEN
Proc_PyramidGlobalBlockerOn();

PROC
Proc_StartAct2()
THEN
Proc_PyramidGlobalBlockerOff();


IF
TimerFinished("LV_HoE_StartAct2FadeOut_Timer")
THEN
CharacterSetAnimationOverride(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,"");

IF
TimerFinished("LV_HoE_StartAct2FadeOut_Timer")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
CharacterUnfreeze(_Player);
ProcForceStopDialog(_Player);

IF
TimerFinished("LV_HoE_StartAct2FadeOut_Timer")
THEN
DB_PlayRC_MainMovie(1);
CharacterTeleportPartiesToTriggerWithMovie(TRIGGERGUID_StartPoint_000_e30fe0c4-9b40-4040-9670-e8edd53a34ce,"","CS_BackToRC");

//for debug
IF
RegionEnded("LV_HoE_Main")
AND
DB_GLO_HoERespawnPoint((CHARACTERGUID)_Player,_)
THEN
PROC_GLO_LeaveHallOfEchoes(_Player);

//END_REGION



//REGION Remove non-recruited Players
IF
GlobalFlagSet("FTJ_LV_BeginLVAssault")
AND
DB_OriginRecruitmentLocation_Region(_,_Companion,_,_)
AND
NOT DB_IsPlayer(_Companion)
THEN
SetOnStage(_Companion,0);

PROC
Proc_StartAct2()
AND
DB_OriginRecruitmentLocation_Region(_,_Companion,_,_)
THEN
SetOnStage(_Companion,1);

//END_REGION

//Fallback if players are somehow still in HoE mode
PROC
ProcRegionEnded("LV_HoE_Main")
AND
DB_GLO_HoERespawnPoint(_Player,_HoETrigger)
THEN
PROC_GLO_LeaveHallOfEchoes(_Player);


PROC
ProcRegionEnded("LV_HoE_Main")
THEN
GoalCompleted;

//REGION Debug
IF 
TextEventSet("LV_HoE_SendPlayerToLadyVengeance")
AND
DB_IsPlayer(_Player)
THEN
Proc_LV_HoE_SendPlayerToLadyVengeance((CHARACTERGUID)_Player);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LadyVengeance"
