Version 1
SubGoalCombiner SGC_AND
INITSECTION
//https://sites.google.com/a/larian.com/larian-wikisite/home/quest-design-documentation/voidwoken-attack

//ApplyStatus(S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b,"CRIPPLED",-1.0,1);

DB_OIL_VoidwokenAttack_ExplosionPoint((TRIGGERGUID)TRIGGERGUID_S_RC_OIL_ExplosionPoint1_af149f7b-7f72-4ec2-b36b-9186d96adcee,100);
DB_OIL_VoidwokenAttack_ExplosionPoint(TRIGGERGUID_S_RC_OIL_ExplosionPoint2_942bcf8b-09c3-4c34-8f9e-3b3c4d328d3e,1000);
DB_OIL_VoidwokenAttack_ExplosionPoint(TRIGGERGUID_S_RC_OIL_ExplosionPoint3_cf9c425b-eb8a-41a6-a563-fb79221688c5,1500);
DB_OIL_VoidwokenAttack_ExplosionPoint(TRIGGERGUID_S_RC_OIL_ExplosionPoint4_21b287c4-5182-4dd3-9a32-1851f88391f8,2000);
DB_OIL_VoidwokenAttack_ExplosionPoint(TRIGGERGUID_S_RC_OIL_ExplosionPoint5_96f31969-66fb-4c63-8575-9d23d73cb4ec, 100);
DB_OIL_VoidwokenAttack_ExplosionPoint(TRIGGERGUID_S_RC_OIL_ExplosionPoint6_c28237a0-c91c-49d7-adf9-bf7c72881771, 700);
DB_OIL_VoidwokenAttack_ExplosionPoint(TRIGGERGUID_S_RC_OIL_ExplosionPoint7_667fc074-2e05-43cb-bd0a-cf8f7cccd47e, 1200);
DB_OIL_VoidwokenAttack_ExplosionPoint(TRIGGERGUID_S_RC_OIL_ExplosionPoint8_fcdbf3e9-bf60-467c-a157-9c4e16c9c572, 1800);
DB_OIL_VoidwokenAttack_ExplosionPoint(TRIGGERGUID_S_RC_OIL_ExplosionPoint9_f3c8548b-78b4-47bf-a27e-5f45bea35b87, 1900);
DB_OIL_VoidwokenAttack_ExplosionPoint(TRIGGERGUID_S_RC_OIL_ExplosionPoint10_ab04e107-14c3-4d7e-ac3d-35ceba56f3c0, 1300);

//Voidwoken have the AD RC_OIL_AD_VoidwokenAttack_VoidwokenTaunt annd should be added as speakers to it if this DB changes.
DB_OIL_VoidwokenAttack_Voidwoken((CHARACTERGUID)CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W1_Small_01_699862de-3a17-40b5-8b62-d6bbef9d4089, 1);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W1_Small_02_0127579c-4174-405b-a403-bdf9538aa024, 1);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W1_Small_03_32ffd555-6170-49f3-9fb9-03aa029c9369, 1);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W1_Small_04_7276c7a1-989e-42b9-98dd-1c307028b0d6, 1);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W1_Small_05_ab1f6cf8-c59b-4147-9f36-2aaf75041e9e, 1);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W1_Small_06_eafe0006-98c0-4e0e-a4aa-153120c56d86, 1);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W2_Small_01_326b9a26-bfb0-41aa-b845-abcb747cd148, 2);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W2_Small_02_dc215b13-f604-4fd4-b7f7-65b0c6ead331, 2);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W2_Small_03_e3080aef-4223-4e83-b70c-967af23a7814, 2);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W2_Small_04_073bcb4b-d891-4531-9fa2-12de0de44d41, 2);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W2_Small_05_0fc9d760-6848-46a3-b779-74eb4baf1771, 2);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W2_Small_06_53bc85af-19ed-41fc-8fb6-cee3f19f9537, 2);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W2_Big_014492ae99-8a12-4cf6-ae53-49175b4e03e8, 2);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W2_Big_0216da4ed3-d78e-48fa-bf55-0e07aa54b75c, 2);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W3_Fire_01_373ee6a6-0ed7-4029-8235-7442398a1b73, 3);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W3_Fire_02_f719095f-f4cc-41d4-8246-d18bd4e3d8f0, 3);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W3_Fire_03_87e02e0f-ab15-42da-9ef0-a6b39b8965b0, 3);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W3_Fire_04_4ced02df-5aff-4dfb-bd8d-72b5e797d27b, 3);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W3_Fire_05_9dcbb70d-b193-4baf-bc27-637af86f55f6, 3);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W3_Fire_06_a5623263-a308-4cac-8af0-17e011947621, 3);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W3_FireBig_01_50d7ff90-8423-448c-a33b-4159eaccb693, 3);
DB_OIL_VoidwokenAttack_Voidwoken(CHARACTERGUID_S_RC_OIL_VoidwokenAttack_VW_W3_FireBig_02_22a1608a-3d5c-4eb6-b5c6-3d08d11a5522, 3);

DB_RC_OIL_VoidwokenDeadCounter(0);
DB_RC_OIL_VoidwokenAttackCurrentWave(1);
DB_RC_OIL_VoidwokenAttackAliveCount(0);

//Magisters have the AD RC_OIL_AD_VoidwokenAttack_MagisterRecogniseSourcerer annd should be added as speakers to it if this DB changes.
DB_OIL_VoidwokenAttack_Magisters((CHARACTERGUID)CHARACTERGUID_S_RC_OIL_InnerField_Magister_4_acb7ad5c-fe9b-4926-8130-19d37bf19ab2);
DB_OIL_VoidwokenAttack_Magisters(CHARACTERGUID_S_RC_OIL_InnerField_Magister_6_5694bda3-9413-4e02-b6ec-81f028748995);

DB_Dialogs((CHARACTERGUID)CHARACTERGUID_S_RC_OIL_InnerField_Magister_4_acb7ad5c-fe9b-4926-8130-19d37bf19ab2, "RC_OIL_InnerField_Magister_4");
DB_Dialogs(CHARACTERGUID_S_RC_OIL_InnerField_Magister_6_5694bda3-9413-4e02-b6ec-81f028748995, "RC_OIL_InnerField_Magister_6");
//DB_OIL_VoidwokenAttack_Magisters(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, "RC_OIL_InnerField_Magister_5");
ProcCharacterDisableAllCrimes(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b);

SetOnStage(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, 0);
//Triggers
/*DB_OIL_VoidwokenAttack_MagistersDialogs(CHARACTERGUID_S_RC_OIL_InnerField_Magister_3_e15fdf74-6381-4bc8-8d6f-6c4d371eb6ae,"RC_OIL_AD_Magister_03_CreepsAppeared");
DB_OIL_VoidwokenAttack_MagistersDialogs(CHARACTERGUID_S_RC_OIL_InnerField_Magister_4_acb7ad5c-fe9b-4926-8130-19d37bf19ab2,"RC_OIL_AD_Magister_04_Hault");
DB_OIL_VoidwokenAttack_MagistersDialogs(CHARACTERGUID_S_RC_OIL_InnerField_Magister_6_5694bda3-9413-4e02-b6ec-81f028748995,"RC_OIL_AD_Magister_AlarmSource2");*/

DB_QuestDef_AddEvent("RC_OIL_ThirdHouse", "RC_OIL_ThirdHouse_KnowSourcerer");
DB_QuestDef_UpdateEvent("RC_OIL_ThirdHouse", "KnowSourcerer", "RC_OIL_ThirdHouse_KnowSourcerer");
DB_QuestDef_UpdateEvent("RC_OIL_ThirdHouse", "RescuedSourcerer", "RC_OIL_InnerField_HelpedSourcererEscape");
DB_QuestDef_State("RC_OIL_ThirdHouse", "SourcererDead");
DB_QuestDef_State("RC_OIL_ThirdHouse", "FamilyDead");
DB_QuestDef_UpdateEvent("RC_OIL_ThirdHouse", "FamilyMemberDead", "RC_OIL_ThirdHouse_FoundDeadFamilyMember");
DB_QuestDef_UpdateEvent("RC_OIL_ThirdHouse", "Intervened", "RC_OIL_ThirdHouse_Rescued");
DB_QuestDef_State("RC_OIL_ThirdHouse", "FoundHouse", 1);
DB_QuestDef_State("RC_OIL_ThirdHouse","Reward", -1);
DB_QuestDef_State("RC_OIL_ThirdHouse","NoReward", -1);
DB_QuestDef_State("RC_OIL_ThirdHouse","SentHannag", 1);
DB_QuestDef_State("RC_OIL_ThirdHouse","FamilyLeftSourcererDead", -1);
DB_QuestDef_State("RC_OIL_ThirdHouse","FoundSourcerer", 1);
DB_QuestDef_State("RC_OIL_ThirdHouse","GwydianEscapedOther");
DB_QuestDef_State("RC_OIL_ThirdHouse","CloseLeftRCApprenticeOut", -1);
DB_QuestDef_State("RC_OIL_ThirdHouse","CloseLeftRCFamilyOut", -1);

DB_OneShotPlayerTrigger(TRIGGERGUID_S_RC_OIL_VoidwokenAttack_Setup_0aa8f162-e5f8-473d-8955-09ee1bc244d1);
DB_OneShotPlayerTrigger(TRIGGERGUID_S_RC_OIL_VoidwokenAttack_Setup_2_c9277b74-ee9c-4db6-87b6-f6663726dc51);
DB_HasStoryEvent(ITEMGUID_S_RC_OIL_JonathansRing_a8ed9b13-b5b7-42c9-a404-a4e89a5a4961, "RC_OIL_HasJonathansRing");
DB_GiveItemToEventWithClear(ITEMGUID_S_RC_OIL_JonathansRing_a8ed9b13-b5b7-42c9-a404-a4e89a5a4961, "RC_OIL_GiveJonathansRinger");

DB_Ghosts(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b,CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_Ghost_7fe152ad-0fb4-4b11-a039-d834cf96f383,"RC_OIL_VoidwokenAttack_Magister5_Ghost");

TriggerRegisterForCharacter(TRIGGERGUID_S_RC_OIL_InnerField_SourcererDialogStart_1266a4cf-f736-49a0-a76c-e56b8a99a0d1,CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b);
TriggerRegisterForCharacter(TRIGGERGUID_S_RC_OIL_InnerField_SourcererDialogStart_1266a4cf-f736-49a0-a76c-e56b8a99a0d1,CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);

SetOnStage(CHARACTERGUID_S_GLO_GarethFriend_Ghost_9166409f-fda0-459e-9c30-39ad01f829f8,0);
KBSECTION
//REGION Setup

PROC
ProcOneShotTriggerEntered(_, TRIGGERGUID_S_RC_OIL_VoidwokenAttack_Setup_0aa8f162-e5f8-473d-8955-09ee1bc244d1) //Fallback in case the previous scene isn't over.
THEN
PROC_RC_DF_EmergencyJonathanSceneStop();

PROC
ProcOneShotTriggerEntered(_, TRIGGERGUID_S_RC_OIL_VoidwokenAttack_Setup_2_c9277b74-ee9c-4db6-87b6-f6663726dc51) //Fallback in case e.g. Gareth's moving off-stage has been interrupted.
THEN
PROC_RC_DF_GarethAndJonathanSetupNextScene();

PROC
PROC_RC_OIL_VoidwokenAttackSetup()
AND
CharacterIsDead(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, 0)
THEN
PROC_RC_OIL_VoidwokenAttackSetup(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);

PROC
PROC_RC_OIL_VoidwokenAttackSetup()
AND
CharacterIsDead(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, 1)
THEN
PROC_RC_OIL_VoidwokenAttackSetup(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b);

PROC
PROC_RC_OIL_VoidwokenAttackSetup(_)
AND
NOT DB_OnlyOnce("RC_OIL_VoidwokenAttackSetup")
THEN
NOT DB_OneShotPlayerTrigger(TRIGGERGUID_S_RC_OIL_VoidwokenAttack_Setup_0aa8f162-e5f8-473d-8955-09ee1bc244d1);
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_RC_OIL_VoidwokenAttack_Setup_0aa8f162-e5f8-473d-8955-09ee1bc244d1);

PROC
PROC_RC_OIL_VoidwokenAttackSetup((CHARACTERGUID)_Character)
THEN
DB_OIL_VoidwokenAttack_Magisters(_Character);
DB_DoNotFace(_Character);

PROC
PROC_RC_OIL_VoidwokenAttackSetup((CHARACTERGUID)_Character)
AND
NOT DB_OnlyOnce("RC_OIL_VoidwokenAttackSetup")
AND
DB_OIL_VoidwokenAttack_Magisters(_Magister)
THEN
SetVarObject(_Magister, "LeaderMagister", _Character);

PROC
PROC_RC_OIL_VoidwokenAttackSetup((CHARACTERGUID)_Character)
AND
NOT DB_OnlyOnce("RC_OIL_VoidwokenAttackSetup")
THEN
TeleportTo(_Character, TRIGGERGUID_S_RC_OIL_InnerField_WhiteMagisterStartPoint_bdbb5ddf-33b9-4fcf-b939-3e41ff328ff6);
SetVarFixedString(_Character, "currentState", "State_RC_OIL_HangSourcerer");
SetOnStage(_Character, 1);
DB_KilledEvent(_Character, "RC_OIL_KilledGarethCaptor");

PROC
PROC_RC_OIL_VoidwokenAttackSetup(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b)
THEN
GlobalSetFlag("RC_OIL_VoidwokenAttack_LeaderLeah");
DB_Dialogs(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "RC_OIL_VoidwokenAttack_SourcererTorture");


IF
TextEventSet("grtfrnd")
THEN
PROC_RC_DF_GarethAndJonathanSetupNextScene();


PROC
PROC_RC_OIL_VoidwokenAttackSetup(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203)
THEN
DB_Ghosts(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203,CHARACTERGUID_S_GLO_GarethFriend_Ghost_9166409f-fda0-459e-9c30-39ad01f829f8,"RC_OIL_VoidwokenAttack_GarethFriend_Ghost");
ObjectSetFlag(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, "GLO_StartSpot", 0);
SetFaction(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, "RC_OIL_CampMagisters");
SetOnStage(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, 1);
CharacterSetAnimationOverride(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, "");
RemoveStatus(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, "UNCONSCIOUS");
SetCombatGroupID(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, "8d2a0c92-0694-4b9e-904e-3139010a083c");
SetVarObject(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "WhiteMagister", CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);
DB_Dialogs(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "RC_OIL_VoidwokenAttack_GarethFriend");
CharacterSetHitpointsPercentage(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, 100.0);
ProcCharacterEnableAllCrimes(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);
NOT DB_DontCreateMurder(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);
NOT DB_IgnoreAssault(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);
CharacterLevelUpTo(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, 14);
SetCanFight(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, 1);

PROC
PROC_RC_OIL_VoidwokenAttackSetup(_)
THEN
DB_OnlyOnce("RC_OIL_VoidwokenAttackSetup");
//END_REGION

//REGION Execution Scene
IF
StoryEvent(_Player, "RC_OIL_VWAttack_WhiteMagisterSpot")
AND
GlobalGetFlag("RC_OIL_ExplosionHappened", 0)
AND
ObjectIsInTrigger(_Player, S_RC_OIL_InnerField_SourcererDialogStart_1266a4cf-f736-49a0-a76c-e56b8a99a0d1, 0)
THEN
ObjectClearFlag(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, "RC_OIL_StartSpot", 0);
ObjectSetFlag(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, "RC_OIL_StartSpot", 0);

IF
StoryEvent(_Player, "RC_OIL_VWAttack_WhiteMagisterSpot")
AND
HasActiveStatus(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "LYING", 1)
AND
ObjectIsInTrigger(_Player, S_RC_OIL_InnerField_SourcererDialogStart_1266a4cf-f736-49a0-a76c-e56b8a99a0d1, 1)
AND
GlobalGetFlag("RC_OIL_VoidwokenSpawned", 0)
AND
QueryOnlyOnce("RC_OIL_SourcererTorture")
THEN
Proc_StartDialog(0, "RC_OIL_VoidwokenAttack_SourcererTorture", CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, _Player);

IF
StoryEvent(_Player, "GLO_GarethFriendSpot")
AND
DB_OnlyOnce("RC_OIL_VoidwokenAttackSetup")
AND
GlobalGetFlag("RC_OIL_ExplosionHappened", 0)
AND
ObjectIsInTrigger(_Player, S_RC_OIL_InnerField_SourcererDialogStart_1266a4cf-f736-49a0-a76c-e56b8a99a0d1, 0)
THEN
ObjectSetFlag(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, "GLO_StartSpot", 0);

IF
StoryEvent(_Player, "GLO_GarethFriendSpot")
AND
HasActiveStatus(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "LYING", 1)
AND
ObjectIsInTrigger(_Player, S_RC_OIL_InnerField_SourcererDialogStart_1266a4cf-f736-49a0-a76c-e56b8a99a0d1, 1)
AND
GlobalGetFlag("RC_OIL_VoidwokenSpawned", 0)
AND
QueryOnlyOnce("RC_OIL_SourcererTorture")
THEN
Proc_StartDialog(0, "RC_OIL_VoidwokenAttack_GarethFriend", CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, _Player);

IF
DialogStarted("RC_OIL_VoidwokenAttack_SourcererTorture", _)
THEN
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b);
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b);
SetHasDialog(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, 0);
SetHasDialog(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, 0);

IF
DialogStarted("RC_OIL_VoidwokenAttack_GarethFriend", _)
THEN
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b);
SetHasDialog(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, 0);
SetHasDialog(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, 0);

IF
CharacterLeftTrigger(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b,TRIGGERGUID_S_RC_OIL_InnerField_SourcererDialogStart_1266a4cf-f736-49a0-a76c-e56b8a99a0d1)
AND
NOT DB_GlobalFlag("RC_OIL_Innerfield_SourcererRescued")
AND
NOT DB_Dead(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b)
AND
ObjectGetFlag(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "RC_OIL_InnerField_KillSourcerer", 0)
THEN
Proc_RC_OIL_VoidwokenAttackTeleportFallback();

IF
CharacterLeftTrigger(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203,TRIGGERGUID_S_RC_OIL_InnerField_SourcererDialogStart_1266a4cf-f736-49a0-a76c-e56b8a99a0d1)
AND
NOT DB_GlobalFlag("RC_OIL_Innerfield_SourcererRescued")
AND
NOT DB_Dead(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b)
AND
ObjectGetFlag(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "RC_OIL_InnerField_KillSourcerer", 0)
THEN
Proc_RC_OIL_VoidwokenAttackTeleportFallback();

PROC
Proc_RC_OIL_VoidwokenAttackTeleportFallback()
THEN
TimerLaunch("RC_OIL_VoidwokenAttackTeleportFallback", 1500);

IF
TimerFinished("RC_OIL_VoidwokenAttackTeleportFallback")
AND
NOT DB_OnlyOnce("RC_OIL_ExplosionScene")
THEN
ProcForceStopDialog(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b);
RemoveStatus(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "LYING");
Proc_RC_OIL_VoidwokenAttack_SetMagistersHostile();
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b);
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b);
SetHasDialog(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, 0);
SetHasDialog(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b,0);
SetHasDialog(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b,0);

IF
DialogEnded("RC_OIL_VoidwokenAttack_SourcererTorture", _)
THEN
TriggerUnregisterForCharacter(TRIGGERGUID_S_RC_OIL_InnerField_SourcererDialogStart_1266a4cf-f736-49a0-a76c-e56b8a99a0d1,CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b);

IF
DialogEnded("RC_OIL_VoidwokenAttack_SourcererTorture", _)
AND
ObjectGetFlag(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "RC_OIL_InnerField_KillSourcerer", 1)
THEN
SetVarFixedString(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, "currentState", "State_MurderSourcerer");

IF
DialogEnded("RC_OIL_VoidwokenAttack_SourcererTorture", _)
AND
ObjectGetFlag(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "RC_OIL_InnerField_FreeSourcerer", 1)
THEN
RemoveStatus(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "LYING");
Proc_RC_OIL_VoidwokenAttack_SetMagistersHostile();

IF
DialogEnded("RC_OIL_VoidwokenAttack_GarethFriend", _)
THEN
TriggerUnregisterForCharacter(TRIGGERGUID_S_RC_OIL_InnerField_SourcererDialogStart_1266a4cf-f736-49a0-a76c-e56b8a99a0d1,CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);

IF
DialogEnded("RC_OIL_VoidwokenAttack_GarethFriend", _)
AND
ObjectGetFlag(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "RC_OIL_InnerField_FreeSourcerer", 1)
THEN
RemoveStatus(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "LYING");
Proc_RC_OIL_VoidwokenAttack_SetMagistersHostile();

IF
DialogEnded("RC_OIL_VoidwokenAttack_GarethFriend", _)
AND
ObjectGetFlag(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "RC_OIL_InnerField_KillSourcerer", 1)
THEN
SetVarFixedString(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, "currentState", "State_MurderSourcerer");

IF
CharacterStatusRemoved(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "LYING",_)
THEN
ItemSetCanInteract(S_RC_OIL_InnerField_Gallows_7b103f4a-9b63-440a-a279-fec5a0737f0f, 0);
SetHasDialog(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b,0);
//END_REGION

//REGION White Magister reacts to the freeing of the Sourcerer
IF
ObjectTurnStarted(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b)
AND
GlobalGetFlag("RC_OIL_Innerfield_SourcererRescued", 1)
AND
QueryOnlyOnce("RC_OIL_AD_WhiteMagister_VoidwokenAttack")
THEN
Proc_StartDialog(1, "RC_OIL_AD_InnerField_SourcererEscape", CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b);

IF
ObjectTurnStarted(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203)
AND
GlobalGetFlag("RC_OIL_Innerfield_SourcererRescued", 1)
AND
QueryOnlyOnce("RC_OIL_AD_WhiteMagister_VoidwokenAttack")
THEN
Proc_StartDialog(1, "RC_OIL_AD_InnerField_SourcererEscape_GarethFriend", CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);
//END_REGION

//REGION Sourcerer reacts to the White Magister dying
IF
CharacterDied(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203)
AND
CharacterCanSee(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, 1)
THEN
CharacterLookAt(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);
Proc_StartDialog(1, "RC_OIL_AD_WhiteMagisterDied", CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b);

IF
CharacterDied(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203)
AND
CharacterCanSee(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, 1)
THEN
CharacterLookAt(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b);
Proc_StartDialog(1, "RC_OIL_AD_WhiteMagisterDied", CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b);
//END_REGION

//REGION Voidwoken attack
IF
DB_OIL_VoidwokenAttack_Voidwoken(_Voidwoken, _)
THEN
SetOnStage(_Voidwoken,0);

IF
ObjectEnteredCombat(_Voidwoken, _)
AND
DB_OIL_VoidwokenAttack_Voidwoken((CHARACTERGUID)_Voidwoken, _)
AND
DB_IsPlayer(_Player)
AND
ObjectIsInTrigger(_Player, S_RC_OIL_CombatArea_3c1f28c7-ac47-466f-906a-d1256505ebf7, 1)
AND
CharacterIsInCombat(_Player, 0)
THEN
EnterCombat(_Player, _Voidwoken);

IF
CharacterUsedSkill(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "Projectile_EnemyChainLightning", _, _)
AND
GlobalGetFlag("RC_OIL_ExplosionHappened", 0)
AND
DB_IsPlayer(_Player)
AND
ObjectIsInTrigger(_Player, S_RC_OIL_CombatArea_3c1f28c7-ac47-466f-906a-d1256505ebf7, 1)
THEN
Proc_RC_OIL_VoidwokenAttack_ShakeCamera();
Proc_RC_OIL_DoVoidwokenAttack();

IF
CharacterDied(_WhiteMagister)
AND
NOT DB_GlobalFlag("RC_OIL_ExplosionHappened")
AND
NOT DB_GlobalFlag("RC_OIL_InnerField_Sourcerer_GotHome")
AND
GetVarObject(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "WhiteMagister", _WhiteMagister)
THEN
RemoveStatus(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "LYING");

PROC
Proc_RC_OIL_VoidwokenAttack_ShakeCamera()
AND
DB_IsPlayer(_Player)
AND
ObjectIsInTrigger(_Player, S_RC_OIL_CombatArea_3c1f28c7-ac47-466f-906a-d1256505ebf7, 1)
THEN
Proc_ShakeCameraForTime(_Player,7000);

PROC
Proc_RC_OIL_DoVoidwokenAttack()
AND
DB_OIL_VoidwokenAttack_ExplosionPoint(_Point,_Time)
THEN
ProcObjectTimer(_Point,"Timer_Oilfields_Explosion",_Time);

PROC
Proc_RC_OIL_DoVoidwokenAttack()
AND
QueryOnlyOnce("RC_OIL_ExplosionScene")
THEN
GlobalSetFlag("RC_OIL_ExplosionHappened");
TimerLaunch("Timer_Oilfields_SpawnOilPits",2200);
TriggerSetAtmosphere(TRIGGERGUID_S_RC_OIL_InnerField_AtmosphereTrigger_7488ffa2-2580-4d0e-b7d3-5b05299cf0c4,"8b0f9d80-d583-45c9-ba2b-62577d3a25f6");

PROC
Proc_RC_OIL_DoVoidwokenAttack()
AND
DB_OIL_VoidwokenAttack_Magisters(_Magister)
THEN
PROC_RemoveDialogFromCharacter(_Magister);

PROC
ProcObjectTimerFinished(_Point,"Timer_Oilfields_Explosion")
AND
GetPosition(_Point,_X,_Y,_Z)
AND
_Point != TRIGGERGUID_S_RC_OIL_ExplosionPoint2_942bcf8b-09c3-4c34-8f9e-3b3c4d328d3e
THEN
PlayScaledEffectAtPosition("RS3_FX_GP_Impacts_Grenade_OilFlask_01",1.0,_X,_Y,_Z);
CreateSurface(_Point, "SurfaceOil", 4.7, -1.0);
ProcObjectTimer(_Point, "Timer_Oilfields_Puddle", 50);

PROC
ProcObjectTimerFinished(_Point, "Timer_Oilfields_Puddle")
THEN
CreatePuddle(_Point, "SurfaceOil", 80, 110, 8, 12, 0.1);

PROC
ProcObjectTimerFinished(TRIGGERGUID_S_RC_OIL_ExplosionPoint2_942bcf8b-09c3-4c34-8f9e-3b3c4d328d3e,"Timer_Oilfields_Explosion")
AND
GetPosition(TRIGGERGUID_S_RC_OIL_ExplosionPoint2_942bcf8b-09c3-4c34-8f9e-3b3c4d328d3e,_X,_Y,_Z)
THEN
PlayScaledEffectAtPosition("RS3_FX_GP_Impacts_Grenade_OilFlask_01",1.0,_X,_Y,_Z);
CreatePuddle(TRIGGERGUID_S_RC_OIL_ExplosionPoint2_942bcf8b-09c3-4c34-8f9e-3b3c4d328d3e, "SurfaceOil", 70, 100, 7, 10, 0.2);

IF
TimerFinished("Timer_Oilfields_SpawnOilPits")
THEN
GlobalSetFlag("RC_OIL_VoidwokenSpawned");

IF
TimerFinished("Timer_Oilfields_SpawnOilPits")
THEN
Proc_RC_OIL_SpawnNextWave(1);

PROC
Proc_RC_OIL_SpawnNextWave(_Num)
AND
_Num != 1
AND
DB_OIL_VoidwokenAttack_ExplosionPoint(_Point,_)
AND
Random(500, _Random)
AND
IntegerSum(500, _Random, _Time)
THEN
ProcObjectTimer(_Point,"Timer_Oilfields_Explosion",_Time);



//REGION Calculating how many are dead & Spawning new waves.
IF
CharacterDied(_VW)
AND
DB_OIL_VoidwokenAttack_Voidwoken(_VW,_)
AND
DB_RC_OIL_VoidwokenDeadCounter(_Count)
AND
IntegerSum(_Count,1,_NewCount)
THEN
NOT DB_RC_OIL_VoidwokenDeadCounter(_Count);
DB_RC_OIL_VoidwokenDeadCounter(_NewCount);

IF
DB_RC_OIL_VoidwokenDeadCounter(3)
THEN
Proc_RC_OIL_SpawnNextWave(2);

IF
DB_RC_OIL_VoidwokenDeadCounter(9)
THEN
Proc_RC_OIL_SpawnNextWave(3);

PROC
Proc_RC_OIL_SpawnNextWave((INTEGER)_Wave)
AND
DB_OIL_VoidwokenAttack_Voidwoken(_Voidwoken,_Wave)
AND
GetPosition(_Voidwoken, _X, _Y, _Z)
THEN
PlayScaledEffectAtPosition("RS3_FX_GP_Impacts_Grenade_OilFlask_01",0.5,_X,_Y,_Z);
CharacterAppear(_Voidwoken, 1, "RC_OIL_SurfaceEvents_CreepSpawned");

IF
DB_RC_OIL_VoidwokenDeadCounter(22)
THEN
TriggerResetAtmosphere(TRIGGERGUID_S_RC_OIL_InnerField_AtmosphereTrigger_7488ffa2-2580-4d0e-b7d3-5b05299cf0c4);
//END_REGION

/*IF
TimerFinished("Timer_Oilfields_SpawnOilPits")
AND
GetPosition(ITEMGUID_S_RC_OIL_InnerField_Gates_036d2b71-663a-4353-b611-4aee276a4035,_X,_Y,_Z)
THEN
PlayEffectAtPosition("RS3_FX_Skills_Fire_Impact_01",_X,_Y,_Z);
ApplyDamage(ITEMGUID_S_RC_OIL_InnerField_Gates_036d2b71-663a-4353-b611-4aee276a4035,2355,"Fire");*/

IF
ObjectEnteredCombat(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b,_)
AND
GlobalGetFlag("RC_OIL_VoidwokenSpawned",1)
AND
GlobalGetFlag("RC_OIL_Innerfield_SourcererRescued", 0)
AND
QueryOnlyOnce("RC_OIL_AD_WhiteMagister_VoidwokenAttack")
THEN
Proc_StartDialog(1,"RC_OIL_AD_WhiteMagister_VoidwokenAttack",CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b);

IF
ObjectEnteredCombat(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203,_)
AND
GlobalGetFlag("RC_OIL_VoidwokenSpawned",1)
AND
GlobalGetFlag("RC_OIL_Innerfield_SourcererRescued", 0)
AND
QueryOnlyOnce("RC_OIL_AD_WhiteMagister_VoidwokenAttack")
THEN
Proc_StartDialog(1,"RC_OIL_AD_GarethFriend_VoidwokenAttack", CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);

IF
ObjectTurnStarted(_Voidwoken)
AND
NOT DB_OnlyOnce("RC_OIL_AD_VoidwokenAttack_VoidwokenTaunt") //Added here so event stops being called after one time and later DB checks aren't executed.
AND
DB_OIL_VoidwokenAttack_Voidwoken((CHARACTERGUID)_Voidwoken, _)
AND
DB_CombatCharacters(_Voidwoken, _ID)
AND
DB_CombatCharacters(_Player, _ID)
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player, "AVATAR", 1) //Prioritise Avatars
AND
NOT DB_OnlyOnce("RC_OIL_AD_VoidwokenAttack_VoidwokenTauntPerPlayer") //Added here so only one player is selected.
AND
QRY_StartDialog(1, "RC_OIL_AD_VoidwokenAttack_VoidwokenTaunt", _Voidwoken, _Player)
THEN
DB_OnlyOnce("RC_OIL_AD_VoidwokenAttack_VoidwokenTaunt");
DB_OnlyOnce("RC_OIL_AD_VoidwokenAttack_VoidwokenTauntPerPlayer");

IF
ObjectTurnStarted(_Voidwoken)
AND
NOT DB_OnlyOnce("RC_OIL_AD_VoidwokenAttack_VoidwokenTaunt") //Added here so event stops being called after one time and later DB checks aren't executed.
AND
DB_OIL_VoidwokenAttack_Voidwoken((CHARACTERGUID)_Voidwoken, _)
AND
DB_CombatCharacters(_Voidwoken, _ID)
AND
DB_CombatCharacters(_Player, _ID)
AND
DB_IsPlayer(_Player)
AND
NOT DB_OnlyOnce("RC_OIL_AD_VoidwokenAttack_VoidwokenTauntPerPlayer") //Added here so only one player is selected.
AND
QRY_StartDialog(1, "RC_OIL_AD_VoidwokenAttack_VoidwokenTaunt", _Voidwoken, _Player)
THEN
DB_OnlyOnce("RC_OIL_AD_VoidwokenAttack_VoidwokenTaunt");
DB_OnlyOnce("RC_OIL_AD_VoidwokenAttack_VoidwokenTauntPerPlayer");

IF
ObjectTurnStarted(_Magister)
AND
DB_OIL_VoidwokenAttack_Magisters((CHARACTERGUID)_Magister)
AND
DB_CombatCharacters(_Magister, _ID)
AND
DB_CombatCharacters(_Player, _ID)
AND
DB_IsPlayer(_Player)
AND
DB_OnlyOnce("RC_OIL_AD_VoidwokenAttack_VoidwokenTaunt")
AND
NOT DB_OnlyOnce("RC_OIL_VoidwokenAttack_SetMagistersHostile")
AND
NOT DB_OnlyOnce("RC_OIL_AD_VoidwokenAttack_MagisterRecogniseSourcerer")
AND
QRY_StartDialog(1, "RC_OIL_AD_VoidwokenAttack_MagisterRecogniseSourcerer", _Magister)
THEN
DB_OnlyOnce("RC_OIL_AD_VoidwokenAttack_MagisterRecogniseSourcerer");
Proc_RC_OIL_VoidwokenAttack_SetMagistersHostile();

IF
GlobalFlagSet("RC_OIL_InnerField_SourcererRescued")
THEN
CharacterSetRelationFactionToFaction("RC_OIL_BoundSourcerer", "DW_Magister", 50);
CharacterSetRelationFactionToFaction("DW_Magister", "RC_OIL_BoundSourcerer", 50);
CharacterSetRelationFactionToFaction("RC_OIL_BoundSourcerer", "RC_OIL_CampMagisters", 50);
CharacterSetRelationFactionToFaction("RC_OIL_CampMagisters", "RC_OIL_BoundSourcerer", 50);

IF
GlobalFlagSet("RC_OIL_InnerField_SourcererRescued")
THEN
TimerLaunch("RC_OIL_InnerField_SourcererRescuedReaction", 1500);

IF
TimerFinished("RC_OIL_InnerField_SourcererRescuedReaction")
AND
DB_GlobalFlag("RC_OIL_VoidwokenAttack_LeaderLeah")
AND
DB_IsPlayer(_Player)
AND
CharacterCanSee(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, _Player, 1)
THEN
Proc_RC_OIL_VoidwokenAttack_SetMagistersHostile();


IF
TimerFinished("RC_OIL_InnerField_SourcererRescuedReaction")
AND
NOT DB_GlobalFlag("RC_OIL_VoidwokenAttack_LeaderLeah")
AND
DB_IsPlayer(_Player)
AND
CharacterCanSee(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, _Player, 1)
THEN
Proc_RC_OIL_VoidwokenAttack_SetMagistersHostile();
//END_REGION

//REGION The people the Sourcerer considers as having helped her.
IF
DB_CombatCharacters(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, _ID)
AND
DB_CombatCharacters(_Player, _ID)
AND
DB_IsPlayer(_Player)
AND
DB_CombatCharacters(_Magister, _ID)
AND
DB_OIL_VoidwokenAttack_Magisters(_Magister)
THEN
UserSetFlag(_Player, "RC_OIL_InnerField_HelpedSourcererEscape", 0);

IF
CharacterDestroyedItem(_Player, S_RC_OIL_InnerField_Gallows_7b103f4a-9b63-440a-a279-fec5a0737f0f)
AND
DB_IsPlayer(_Player)
THEN
UserSetFlag(_Player, "RC_OIL_InnerField_HelpedSourcererEscape", 0);

IF
CharacterUsedSkillOnTarget(_Player, CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, _, "Teleportation", _)
AND
GlobalGetFlag("RC_OIL_Innerfield_SourcererRescued", 0)
THEN
UserSetFlag(_Player, "RC_OIL_InnerField_HelpedSourcererEscape", 0);

IF
CharacterTeleported(_Magister, _Player, _OldX, _OldY, _OldZ, _NewX, _NewY, _NewZ, _) // If Gwydian sees someone teleport his captor away, he'll be grateful.
AND
NOT DB_GlobalFlag("RC_OIL_Innerfield_SourcererRescued")
AND
GetVarObject(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "WhiteMagister", _Magister)
AND
PositionIsInTrigger(_OldX, _OldY, _OldZ, TRIGGERGUID_S_RC_OIL_InnerField_SourcererDialogStart_1266a4cf-f736-49a0-a76c-e56b8a99a0d1, 1)
AND
PositionIsInTrigger(_NewX, _NewY, _NewZ, TRIGGERGUID_S_RC_OIL_InnerField_SourcererDialogStart_1266a4cf-f736-49a0-a76c-e56b8a99a0d1, 0)
AND
DB_IsPlayer(_OtherPlayer)
AND
CharacterGetReservedUserID(_Player, _ID)
AND
CharacterGetReservedUserID(_OtherPlayer, _ID)
AND
CharacterCanSee(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, _OtherPlayer, 1)
THEN
UserSetFlag(_Player, "RC_OIL_InnerField_HelpedSourcererEscape", 0);

IF
DB_CombatCharacters(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, _ID)
AND
DB_CombatCharacters(_Player, _ID)
AND
DB_IsPlayer(_Player)
AND
DB_CombatCharacters(_Magister, _ID)
AND
DB_RC_OIL_ThirdHouseMagister(_Magister)
THEN
UserSetFlag(_Player, "RC_OIL_InnerField_HelpedSourcererEscape", 0);
//END_REGION

//REGION After Voidwoken Attack
IF
StoryEvent(_Player, "RC_OIL_VWAttack_WhiteMagisterSpot")
AND
GlobalGetFlag("RC_OIL_ExplosionHappened", 1)
AND
QueryOnlyOnce("RC_OIL_AfterVoidwokenAttack")
THEN
Proc_StartDialog(0,"RC_OIL_AfterVoidwokenAttack",CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b,_Player);

IF
DialogEnded("RC_OIL_AfterVoidwokenAttack",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,_Player)
THEN
Proc_RC_OIL_VoidwokenAttack_SetMagistersHostile();

PROC
Proc_RC_OIL_VoidwokenAttack_SetMagistersHostile()
AND
NOT DB_OnlyOnce("RC_OIL_VoidwokenAttack_SetMagistersHostile")
AND
DB_OIL_VoidwokenAttack_Magisters(_Magister)
THEN
SetFaction(_Magister,"RC_OIL_CampMagistersHostile");

PROC
Proc_RC_OIL_VoidwokenAttack_SetMagistersHostile()
THEN
DB_OnlyOnce("RC_OIL_VoidwokenAttack_SetMagistersHostile");

IF
StoryEvent(_Player, "RC_OIL_InnerField_SourcererThank")
AND
GlobalGetFlag("RC_OIL_Innerfield_SourcererRescued", 1)
AND
GlobalGetFlag("RC_OIL_InnerField_Sourcerer_GotHome", 0)
THEN
Proc_StartDialog(0, "RC_OIL_InnerField_SourcererThank", CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b,  _Player);

//END_REGION

//REGION Finding the Sourcerer dead.
IF
CharacterDying(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b)
AND
DB_CombatCharacters(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, _ID)
AND
DB_IsPlayer(_Player)
AND
DB_CombatCharacters(_Player, _ID)
THEN
UserSetFlag(_Player, "QuestUpdate_RC_OIL_ThirdHouse_SourcererDead", 0);

IF
CharacterDying(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b)
AND
DB_IsPlayer(_Player)
AND
CharacterCanSee(_Player, CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, 1)
THEN
UserSetFlag(_Player, "QuestUpdate_RC_OIL_ThirdHouse_SourcererDead", 0);

IF
CharacterSawCharacter(_Player, CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b)
AND
CharacterIsDead(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, 1)
THEN
UserSetFlag(_Player, "QuestUpdate_RC_OIL_ThirdHouse_SourcererDead", 0);
//END_REGION

//REGION Journal logic
IF
ObjectFlagSet("RC_OIL_ThirdHouse_FoundDeadFamilyMember", _Player, _)
AND
GlobalGetFlag("RC_OIL_ThirdHouseExecutionEnded", 1)
THEN
UserSetFlag((CHARACTERGUID)_Player, "QuestUpdate_RC_OIL_ThirdHouse_FamilyDead", 0);

IF
GlobalFlagSet("RC_OIL_ThirdHouseExecutionEnded")
AND
DB_IsPlayer(_Player)
AND
PartyGetFlag((CHARACTERGUID)_Player, "RC_OIL_ThirdHouse_FoundDeadFamilyMember", 1)
THEN
UserSetFlag(_Player, "QuestUpdate_RC_OIL_ThirdHouse_FamilyDead", 0);

IF
ObjectFlagSet("QuestUpdate_RC_OIL_ThirdHouse_FamilyDead", _Player, _)
AND
PartyGetFlag((CHARACTERGUID)_Player, "QuestUpdate_RC_OIL_ThirdHouse_SourcererDead", 1)
THEN
ObjectSetFlag(_Player,"QuestClose_RC_OIL_ThirdHouse");

IF
ObjectFlagSet("QuestUpdate_RC_OIL_ThirdHouse_SourcererDead", _Player, _)
AND
PartyGetFlag((CHARACTERGUID)_Player, "QuestUpdate_RC_OIL_ThirdHouse_FamilyDead", 1)
THEN
ObjectSetFlag(_Player,"QuestClose_RC_OIL_ThirdHouse");

IF
ObjectFlagShared("QuestUpdate_RC_OIL_ThirdHouse_FamilyDead", _Player, 1)
AND
PartyGetFlag((CHARACTERGUID)_Player, "QuestUpdate_RC_OIL_ThirdHouse_SourcererDead", 1)
THEN
ObjectSetFlag(_Player,"QuestClose_RC_OIL_ThirdHouse");

IF
ObjectFlagShared("QuestUpdate_RC_OIL_ThirdHouse_SourcererDead", _Player, 1)
AND
PartyGetFlag((CHARACTERGUID)_Player, "QuestUpdate_RC_OIL_ThirdHouse_FamilyDead", 1)
THEN
ObjectSetFlag(_Player,"QuestClose_RC_OIL_ThirdHouse");

//END_REGION

//REGION Behaviour after the Sourcerer is executed
IF
CharacterDied(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b)
THEN
SetVarObject(CHARACTERGUID_S_RC_OIL_InnerField_Magister_6_5694bda3-9413-4e02-b6ec-81f028748995, "SplineGUID", SPLINEGUID_S_RC_OIL_Spline_InnerField_Patrol_1_After_bc833347-90f0-43b2-849a-4a64d344791e);
SetVarInteger(CHARACTERGUID_S_RC_OIL_InnerField_Magister_6_5694bda3-9413-4e02-b6ec-81f028748995, "currentSpline", -1);
SetStoryEvent(CHARACTERGUID_S_RC_OIL_InnerField_Magister_6_5694bda3-9413-4e02-b6ec-81f028748995, "InitiateSplinePatrol");
SetStoryEvent(CHARACTERGUID_S_RC_OIL_InnerField_Magister_6_5694bda3-9413-4e02-b6ec-81f028748995, "InterruptPatrol");
SetVarFixedString(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, "currentState", "State_AfterExecution");
SetVarFixedString(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, "currentState", "State_AfterExecution");
ProcCharacterMoveTo(CHARACTERGUID_S_RC_OIL_InnerField_Magister_4_acb7ad5c-fe9b-4926-8130-19d37bf19ab2, TRIGGERGUID_S_RC_OIL_VoidwokenAttack_Magister_4AfterExecution_5af74d8a-e5de-4a87-81ea-f15a2ccbd10b, 0, "");
GlobalSetFlag("RC_OIL_InnerField_Sourcerer_Dead");
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);
DB_Dialogs(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, "RC_OIL_InnerField_GarethFriend_AfterExecution");
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b);
DB_Dialogs(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, "RC_OIL_InnerField_Magister_5_AfterExecution");

IF
CharacterStatusRemoved(CHARACTERGUID_S_RC_OIL_InnerField_Sourcerer_632e47f2-22c3-4342-b3f7-152dd3534f3b, "LYING", _)
THEN
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b);
SetStoryEvent(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, "RC_OIL_StopSpot");
SetStoryEvent(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, "RC_OIL_StopSpot");

IF
StoryEvent(_, "RC_OIL_GiveUpExecution")
THEN
SetVarObject(CHARACTERGUID_S_RC_OIL_InnerField_Magister_6_5694bda3-9413-4e02-b6ec-81f028748995, "SplineGUID", SPLINEGUID_S_RC_OIL_Spline_InnerField_Patrol_1_After_bc833347-90f0-43b2-849a-4a64d344791e);
SetVarInteger(CHARACTERGUID_S_RC_OIL_InnerField_Magister_6_5694bda3-9413-4e02-b6ec-81f028748995, "currentSpline", -1);
SetStoryEvent(CHARACTERGUID_S_RC_OIL_InnerField_Magister_6_5694bda3-9413-4e02-b6ec-81f028748995, "InitiateSplinePatrol");
SetStoryEvent(CHARACTERGUID_S_RC_OIL_InnerField_Magister_6_5694bda3-9413-4e02-b6ec-81f028748995, "InterruptPatrol");
SetVarFixedString(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, "currentState", "State_AfterExecution");
SetVarFixedString(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, "currentState", "State_AfterExecution");
ProcCharacterMoveTo(CHARACTERGUID_S_RC_OIL_InnerField_Magister_4_acb7ad5c-fe9b-4926-8130-19d37bf19ab2, TRIGGERGUID_S_RC_OIL_VoidwokenAttack_Magister_4AfterExecution_5af74d8a-e5de-4a87-81ea-f15a2ccbd10b, 0, "");
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203);
DB_Dialogs(CHARACTERGUID_S_GLO_GarethFriend_9372d7fb-dc95-4acf-b455-8a36d610e203, "RC_OIL_InnerField_GarethFriend_AfterExecution");
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b);
DB_Dialogs(CHARACTERGUID_S_RC_OIL_InnerField_Magister_5_a8ca9245-b132-4e9e-b73a-81b41a6a297b, "RC_OIL_InnerField_Magister_5_AfterExecution");
//END_REGION

IF
ObjectFlagSet("RC_OIL_InnerField_DrawWeapon", _Character, _)
THEN
ObjectClearFlag(_Character, "RC_OIL_DrawWeapon", 0);
CharacterSetFightMode((CHARACTERGUID)_Character, 1, 0);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
