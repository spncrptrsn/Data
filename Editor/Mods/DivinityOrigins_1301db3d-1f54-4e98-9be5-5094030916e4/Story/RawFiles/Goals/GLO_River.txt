Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_NoLowAttitudeDialog(CHARACTERGUID_S_GLO_Dallis_69b951dc-55a4-44b8-a2d5-5efedbd7d572);
DB_NoLowAttitudeDialog(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e);
KBSECTION
//REGION If a player Attacks River
IF
AttackedByObject(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,(CHARACTERGUID)_player,_,_,(STRING)_DamageSource)
AND
NOT QryIgnoreDamageSource(_DamageSource)
AND
DB_IsPlayer(_player)
AND
ObjectGetFlag(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,"RC_DU_VL_PlayerAttackedRiver",0)
THEN
Proc_RC_DU_VL_AttackedRiver((CHARACTERGUID)_player,CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e);
ObjectSetFlag((CHARACTERGUID)_player,"RC_DU_VL_AttackedRiverAtLeastOnce");


//Variant for Fort Joy
PROC
Proc_RC_DU_VL_AttackedRiver((CHARACTERGUID)_player,(CHARACTERGUID)_river)
AND
GetRegion(_river,_Region)
AND
_Region == "FJ_FortJoy_Main"
THEN
CharacterLookAt(_river,_player);
DialogRequestStop(_river);
ObjectSetFlag(_river,"RC_DU_VL_PlayerAttackedRiver"); //She goes to the HoE temporarly
Proc_StartDialog(1,"RC_DU_VL_AD_River",_river);



//Variant for LV_HoE_Main
PROC
Proc_RC_DU_VL_AttackedRiver((CHARACTERGUID)_player,(CHARACTERGUID)_river)
AND
GlobalGetFlag("LV_HoE_LvAssaultActive",0)
AND
GetRegion(_river,_Region)
AND
_Region == "LV_HoE_Main"
THEN
CharacterLookAt(_river,_player);
DialogRequestStop(_river);
ObjectSetFlag(_river,"RC_DU_VL_PlayerAttackedRiver"); //She goes to the HoE temporarly
Proc_StartDialog(1,"RC_DU_VL_AD_River",_river);



//If already talked to River
PROC
Proc_RC_DU_VL_AttackedRiver((CHARACTERGUID)_player,(CHARACTERGUID)_river)
AND
ObjectGetFlag(_river,"RC_DU_VL_TalkedToRiver",1)
AND
GetRegion(_river,_Region)
AND
_Region == "RC_Main"
THEN
CharacterLookAt(_river,_player);
DialogRequestStop(_river);
ObjectSetFlag(_river,"RC_DU_VL_PlayerAttackedRiver"); //She goes to the HoE temporarly
Proc_StartDialog(1,"RC_DU_VL_AD_River",_river);

//If not talked to river yet. Initiate normal dialogue
PROC
Proc_RC_DU_VL_AttackedRiver((CHARACTERGUID)_player,(CHARACTERGUID)_river)
AND
ObjectGetFlag(_river,"RC_DU_VL_TalkedToRiver",0)
AND
GetRegion(_river,_Region)
AND
_Region == "RC_Main"
THEN
ObjectSetFlag(_river,"RC_DU_VL_PlayerAttackedRiver"); //She gets anoyed that you attacked her
Proc_RC_DU_VL_StartRiverDialogue((CHARACTERGUID)_player);


//Variant for DNotS
PROC
Proc_RC_DU_VL_AttackedRiver((CHARACTERGUID)_player,(CHARACTERGUID)_river)
AND
GetRegion(_river,_Region)
AND
_Region == "CoS_Main_Ending"
THEN
CharacterLookAt(_river,_player);
DialogRequestStop(_river);
ObjectSetFlag(_river,"RC_DU_VL_PlayerAttackedRiver"); //She goes to the HoE temporarly
Proc_StartDialog(1,"RC_DU_VL_AD_River",_river);


PROC
Proc_RC_DU_VL_AttackedRiver((CHARACTERGUID)_player,(CHARACTERGUID)_river)
AND
GetRegion(_river,_Region)
AND
_Region == "ARX_Main"
THEN
CharacterLookAt(_river,_player);
DialogRequestStop(_river);
ObjectSetFlag(_river,"RC_DU_VL_PlayerAttackedRiver"); //She goes to the HoE temporarly
Proc_StartDialog(1,"RC_DU_VL_AD_River",_river);
//END_REGION


//REGION Make river temporary disapear from stage on attack

IF
AutomatedDialogStarted("RC_DU_VL_AD_River",_)
AND
NOT DB_BlockRiverAttackReaction(1)
THEN
DB_GLO_RiverTeleportingAway(1);
SetCanJoinCombat(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,0);
SetCanFight(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,0);
PROC_LoopEffect("RS3_FX_GP_ScriptedEvent_HOE_Channel_Spell_StepA_01",CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,"GLO_RiverDisappearEffect","__ANY__","");
SetHasDialog(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,0);

IF
AutomatedDialogEnded("RC_DU_VL_AD_River",_)
AND
NOT DB_BlockRiverAttackReaction(1)
AND
ObjectGetFlag(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,"RC_DU_VL_PlayerAttackedRiver",1)
AND
GetPosition(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,_X,_Y,_Z)
THEN
NOT DB_GLO_RiverTeleportingAway(1);
DB_GLO_RiverTeleportedAway(1);
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_HOE_Summoning_01",_X,_Y,_Z);
SetOnStage(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,0);
PROC_GLO_River_TeleportingAwayCleanUp();
TimerLaunch("RC_DU_VL_RiverReapear",60000);

//Have her reapear
IF
TimerFinished("RC_DU_VL_RiverReapear")
THEN
NOT DB_GLO_RiverTeleportedAway(1);
CreateSurface(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,"SurfaceNone",3.0,1.0);
SetOnStage(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,1);
PlayEffect(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,"RS3_FX_GP_ScriptedEvent_HOE_Summoning_01");
PROC_GLO_River_TeleportedAwayCleanUp();

PROC
PROC_GLO_River_TeleportingAwayCleanUp()
THEN
ObjectClearFlag(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,"RC_DU_VL_PlayerAttackedRiver",0);
CharacterSetHitpointsPercentage(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,100.0);
PROC_StopLoopEffect(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,"GLO_RiverDisappearEffect");

PROC
PROC_GLO_River_TeleportedAwayCleanUp()
THEN
SetCanJoinCombat(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,1);
SetCanFight(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,1);
SetHasDialog(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,1);

PROC
PROC_GLO_River_AbortTeleportedAway()
AND
DB_GLO_RiverTeleportingAway(1)
THEN
NOT DB_GLO_RiverTeleportingAway(1);
PROC_GLO_River_TeleportingAwayCleanUp();

PROC
PROC_GLO_River_AbortTeleportedAway()
AND
DB_GLO_RiverTeleportedAway(1)
THEN
NOT DB_GLO_RiverTeleportedAway(1);
TimerCancel("RC_DU_VL_RiverReapear");
PROC_GLO_River_TeleportedAwayCleanUp();

PROC
ProcRegionEnded(_)
THEN
PROC_GLO_River_AbortTeleportedAway();
//END_REGION

IF
GlobalFlagSet("RC_RiverCoughSource")
THEN
PlayAnimation(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,"barf");
TimerLaunch("RC_RiverCoughSource",1000);

IF
TimerFinished("RC_RiverCoughSource")
THEN
CreateSurface(TRIGGERGUID_LV_RiverCoughSource_Point_c210fa21-57d1-4872-8991-11244a2daf86,"SurfaceSource",0.5,0.0);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start"
