Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_TUT_LowerDeckADs("TUT_AD_BeastShipConstruction", "TUT_AD_BeastShipConstruction_Block");
DB_TUT_LowerDeckADs("TUT_AD_LohsePleaseTellStory", "TUT_AD_LohsePleaseTellStory_Block");
DB_TUT_LowerDeckADs("TUT_AD_RedPrinceGruel", "TUT_AD_RedPrinceGruel_Block");
DB_TUT_LowerDeckADs("TUT_AD_IfanPrayingMagister", "TUT_AD_IfanPrayingMagister_Block");
DB_TUT_LowerDeckADs("TUT_AD_LowerDeck_InvestigatingMagister_DwarvenCarpenter", "TUT_AD_LowerDeck_InvestigatingMagister_Block");
DB_TUT_LowerDeckADs("TUT_AD_LowerDeck_InvestigatingMagister_Gruella", "TUT_AD_LowerDeck_InvestigatingMagister_Block");
DB_TUT_LowerDeckADs("TUT_AD_LowerDeck_InvestigatingMagister_LohseBoy2", "TUT_AD_LowerDeck_InvestigatingMagister_Block");

GlobalSetFlag("TUT_BlockLowerDeckADs");
KBSECTION
//REGION Block one dialogue while the other is active
IF
AutomatedDialogStarted(_Dialog, _ID)
AND
DB_TUT_LowerDeckADs(_Dialog, _)
THEN
DB_TUT_LowerDeckActiveAD(_Dialog);
GlobalSetFlag("TUT_BlockLowerDeckADs");

IF
AutomatedDialogEnded(_Dialog, _ID)
AND
DB_TUT_LowerDeckActiveAD(_Dialog)
THEN
NOT DB_TUT_LowerDeckActiveAD(_Dialog);
PROC_TUT_CheckADsOver();

PROC
PROC_TUT_CheckADsOver()
AND
NOT DB_TUT_LowerDeckActiveAD(_)
AND
Random(3000, _Random)
AND
IntegerSum(1000, _Random, _Timer)
THEN
TimerLaunch("TUT_ADBlockerTimer", _Timer);

IF
TimerFinished("TUT_ADBlockerTimer")
THEN
GlobalClearFlag("TUT_BlockLowerDeckADs");
//END_REGION

//REGION Block each AD from repeating
//Clear any blocking flags but the AD's
IF
AutomatedDialogStarted(_Dialog, _)
AND
DB_TUT_LowerDeckADs(_Dialog, _)
AND
DB_TUT_LowerDeckADs(_AnyDialog, _Flag)
THEN
GlobalClearFlag(_Flag);


IF
AutomatedDialogStarted(_Dialog, _)
AND
DB_TUT_LowerDeckADs(_Dialog, _Flag)
THEN
GlobalSetFlag(_Flag);
//END_REGION


//REGION When the door opens, the kids do an AD
IF
ItemOpened(ITEMGUID_S_TUT_LowerDeck_DoorToStairs_008_2a1b856c-757c-4d5d-9b1b-1114d5aad384)
AND
NOT DB_GlobalFlag("TUT_LowerDeckdoorOpen")
THEN
GlobalClearFlag("TUT_BlockLowerDeckADs");

IF
ItemOpened(ITEMGUID_S_TUT_LowerDeck_DoorToStairs_008_2a1b856c-757c-4d5d-9b1b-1114d5aad384)
AND
NOT DB_GlobalFlag("TUT_LowerDeckdoorOpen")
AND
IsTagged(CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295, "AVATAR", 0)
THEN
Proc_StartDialog(1, "TUT_AD_LohsePleaseTellStory", CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295, CHARACTERGUID_S_TUT_LowerDeck_LohseSongGirl1_a681c125-8493-4046-ab1f-6c2201336a22);

IF
ItemOpened(ITEMGUID_S_TUT_LowerDeck_DoorToStairs_008_2a1b856c-757c-4d5d-9b1b-1114d5aad384)
THEN
GlobalSetFlag("TUT_LowerDeckdoorOpen");

IF
ItemDestroyed(ITEMGUID_S_TUT_LowerDeck_DoorToStairs_008_2a1b856c-757c-4d5d-9b1b-1114d5aad384)
AND
NOT DB_GlobalFlag("TUT_LowerDeckdoorOpen")
THEN
GlobalClearFlag("TUT_BlockLowerDeckADs");

IF
ItemDestroyed(ITEMGUID_S_TUT_LowerDeck_DoorToStairs_008_2a1b856c-757c-4d5d-9b1b-1114d5aad384)
AND
NOT DB_GlobalFlag("TUT_LowerDeckdoorOpen")
AND
IsTagged(CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295, "AVATAR", 0)
THEN
Proc_StartDialog(1, "TUT_AD_LohsePleaseTellStory", CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295, CHARACTERGUID_S_TUT_LowerDeck_LohseSongGirl1_a681c125-8493-4046-ab1f-6c2201336a22);

IF
ItemDestroyed(ITEMGUID_S_TUT_LowerDeck_DoorToStairs_008_2a1b856c-757c-4d5d-9b1b-1114d5aad384)
THEN
GlobalSetFlag("TUT_LowerDeckdoorOpen");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Tutorial_PrisonShip"
