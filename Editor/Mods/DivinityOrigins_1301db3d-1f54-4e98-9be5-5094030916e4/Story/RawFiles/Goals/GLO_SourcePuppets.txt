Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION Source Puppets On Off Animations

IF
ObjectFlagSet("ARX_CreepyShop_SourcePuppet_IsOn",(CHARACTERGUID)_Puppet,_ID)
AND
HasActiveStatus(_Puppet,"SITTING",0)
AND
NOT DB_Dead(_Puppet)
THEN
PlayAnimation(_Puppet,"Custom_Activate_01");

IF
ObjectFlagSet("ARX_CreepyShop_SourcePuppet_IsOn",(CHARACTERGUID)_Puppet,_ID)
AND
HasActiveStatus(_Puppet,"SITTING",1)
AND
NOT DB_Dead(_Puppet)
THEN
PlayAnimation(_Puppet,"Custom_Sit_Activate_01");

IF
ObjectFlagSet("ARX_CreepyShop_SourcePuppet_IsOn",(CHARACTERGUID)_Puppet,_ID)
AND
DB_DoNotFace(_Puppet)
AND
NOT DB_Dead(_Puppet)
THEN
NOT DB_DoNotFace(_Puppet);

IF
ObjectFlagCleared("ARX_CreepyShop_SourcePuppet_IsOn",(CHARACTERGUID)_Puppet,_ID)
AND
HasActiveStatus(_Puppet,"SITTING",0)
AND
NOT DB_Dead(_Puppet)
THEN
PlayAnimation(_Puppet,"Custom_Deactivate_01");
DB_DoNotFace(_Puppet);

IF
ObjectFlagCleared("ARX_CreepyShop_SourcePuppet_IsOn",(CHARACTERGUID)_Puppet,_ID)
AND
HasActiveStatus(_Puppet,"SITTING",1)
AND
NOT DB_Dead(_Puppet)
THEN
PlayAnimation(_Puppet,"Custom_Sit_Deactivate_01");
DB_DoNotFace(_Puppet);
//END_REGION

//REGION Source puppet Death

IF
CharacterDying(_Puppet)
AND
IsTagged(_Puppet,"SOURCE_PUPPET",1)
AND
ObjectGetFlag(_Puppet,"ARX_CreepyShop_SourcePuppet_IsOn",1)
AND
GetPosition(_Puppet,_X,_Y,_Z)
THEN
ItemTemplateAddTo("POTION_SourceOrb_c47ac2cc-16e9-46a9-a49a-55b7412f9dd5",_Puppet,1);
CreateExplosionAtPosition(_X,_Y,_Z,"Projectile_EnemyLightningBolt",20);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ARX"
