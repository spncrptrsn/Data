Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_TUT_LoseWeapons_WeaponSlots("Weapon");
DB_TUT_LoseWeapons_WeaponSlots("Shield");

DB_TUT_LoseWeapons_RegainAfterTutorial((CHARACTERGUID)CHARACTERGUID_S_GLO_Windego_d783285f-d3be-4cba-8333-db8976cef182);
KBSECTION
//REGION Collect characters that have to lose their weapons
PROC
Proc_TUT_PlayerWeaponRecipesRecorded()
THEN
PROC_TUT_InitLoseWeapons();

PROC
PROC_TUT_InitLoseWeapons()
AND
DB_GLO_SourceCollars_PrisonersWithCollars(_Prisoner)
THEN
DB_TUT_LoseWeapons_LostWeapons(_Prisoner);

PROC
PROC_TUT_InitLoseWeapons()
AND
DB_IsPlayer(_Player)
THEN
DB_TUT_LoseWeapons_LostWeapons(_Player);

// Origins have to get their weapons back in Fort Joy
PROC
PROC_TUT_InitLoseWeapons()
AND
DB_OriginRecruitmentLocation_Region("TUT_Tutorial_A",_Origin,_StartTrigger,_StartState)
AND
NOT DB_IsPlayer(_Origin)
THEN
DB_TUT_LoseWeapons_RegainAfterTutorial(_Origin);
DB_TUT_LoseWeapons_LostWeapons(_Origin);

// Remove rain skill from Lohse for the duration of the tutorial
// (Red Prince tends to roast wet allies to remove their wet status)
PROC
PROC_TUT_InitLoseWeapons()
AND
DB_OriginRecruitmentLocation_Region("TUT_Tutorial_A",_Origin,_StartTrigger,_StartState)
AND
NOT DB_IsPlayer(_Origin)
AND
CharacterHasSkill(_Origin,"Rain_Water",1)
THEN
DB_TUT_LoseSkill(_Origin,"Rain_Water");

IF
CharacterMadePlayer(_Origin)
AND
DB_TUT_LoseSkill(_Origin,_Skill)
THEN
// recruit via debug book in tutorial
CharacterAddSkill(_Origin,_Skill);
NOT DB_TUT_LoseSkill(_Origin,_Skill);
//END_REGION

//REGION Take away weapons
IF
DB_TUT_LoseWeapons_LostWeapons(_Char)
AND
DB_TUT_LoseWeapons_WeaponSlots(_Slot)
AND
CharacterGetEquippedItem(_Char,_Slot,(ITEMGUID)_Item)
THEN
CharacterUnequipItem(_Char,_Item);
PROC_TUT_LoseWeapons_LostWeapon(_Char,_Slot,_Item);

PROC
PROC_TUT_LoseWeapons_LostWeapon((CHARACTERGUID)_Char,(STRING)_Slot,(ITEMGUID)_Item)
THEN
DB_TUT_LostWeapon(_Char,_Slot,_Item);

// Player weapons to evidence chest
PROC
PROC_TUT_LoseWeapons_LostWeapon((CHARACTERGUID)_Char,(STRING)_Slot,(ITEMGUID)_Item)
AND
DB_IsPlayer(_Char)
THEN
ItemToInventory(_Item,ITEMGUID_S_TUT_MiddleDeck_ConfiscationChest_aabf4b72-c5f7-4637-9606-fc612be92ff7);

// Weapons not returned to their owner after the tutorial get destroyed
PROC
PROC_TUT_LoseWeapons_LostWeapon((CHARACTERGUID)_Char,(STRING)_Slot,(ITEMGUID)_Item)
AND
NOT DB_IsPlayer(_Char)
AND
NOT DB_TUT_LoseWeapons_RegainAfterTutorial(_Char)
THEN
ItemDestroy(_Item);

// Weapons for NPCs that have to be returned after the tutorial are set off-stage
PROC
PROC_TUT_LoseWeapons_LostWeapon((CHARACTERGUID)_Char,(STRING)_Slot,(ITEMGUID)_Item)
AND
DB_TUT_LoseWeapons_RegainAfterTutorial(_Char)
THEN
ItemDrop(_Item);
SetOnStage(_Item,0);
//END_REGION

//REGION
IF
DB_TUT_LoseSkill(_Origin,_Skill)
THEN
CharacterRemoveSkill(_Origin,_Skill);
//END_REGION

//REGION Give shivs to prisoners (and take them away when ending the level)
PROC
PROC_TUT_LoseWeapons_LostWeapon((CHARACTERGUID)_Char,(STRING)_Slot,(ITEMGUID)_Item)
AND
NOT DB_TUT_LoseWeapons_GotShiv(_Char)
AND
NOT DB_IsPlayer(_Char)
AND
GetPosition(_Char,_X,_Y,_Z)
AND
CreateItemTemplateAtPosition("WPN_Common_Dagger_1H_A f1e0f3fc-c94b-48b6-a843-7d8072f87e9e",_X,_Y,_Z,_NewWeapon)
THEN
DB_TUT_LoseWeapons_GotShiv(_Char);
ItemToInventory(_NewWeapon,_Char);
CharacterEquipItem(_Char,_NewWeapon);

PROC
ProcRegionEnded("TUT_Tutorial_A")
AND
DB_TUT_LoseWeapons_GotShiv(_Char)
AND
CharacterGetEquippedItem(_Char,"Weapon",(ITEMGUID)_Shiv)
THEN
NOT DB_TUT_LoseWeapons_GotShiv(_Char);
CharacterUnequipItem(_Char,_Shiv);
ItemDestroy(_Shiv);
//END_REGION

//REGION Return NPC skills
PROC
ProcRegionEnded("TUT_Tutorial_A")
AND
DB_TUT_LoseSkill(_Origin,_Skill)
THEN
NOT DB_TUT_LoseSkill(_Origin,_Skill);
CharacterAddSkill(_Origin,_Skill);
//END_REGION

//REGION Return NPC weapons
PROC
ProcRegionEnded("TUT_Tutorial_A")
AND
DB_TUT_LoseWeapons_RegainAfterTutorial(_Char)
AND
DB_TUT_LostWeapon(_Char,_Slot,_Weapon)
THEN
NOT DB_TUT_LoseWeapons_RegainAfterTutorial(_Char);
SetOnStage(_Weapon,1);
ItemToInventory(_Weapon,_Char);
CharacterEquipItem(_Char,_Weapon);

PROC
ProcRegionEnded("TUT_Tutorial_A")
AND
DB_TUT_LoseWeapons_LostWeapons(_Char)
THEN
NOT DB_TUT_LoseWeapons_LostWeapons(_Char);

PROC
ProcRegionEnded("TUT_Tutorial_A")
AND
DB_TUT_LostWeapon(_Char,_Slot,_Item)
THEN
NOT DB_TUT_LostWeapon(_Char,_Slot,_Item);
 
PROC
ProcRegionEnded("TUT_Tutorial_A")
THEN
GoalCompleted;
//END_REGION

//REGION Set flag when the players open the chest and it is not empty.
IF
CharacterUsedItem(_Player, ITEMGUID_S_TUT_MiddleDeck_ConfiscationChest_aabf4b72-c5f7-4637-9606-fc612be92ff7)
THEN
SysClear("DB_TUT_SearchingConfiscationChest", 1);
DB_TUT_SearchingConfiscationChest(_Player);
InventoryLaunchIterator(ITEMGUID_S_TUT_MiddleDeck_ConfiscationChest_aabf4b72-c5f7-4637-9606-fc612be92ff7, "DB_TUT_CheckAnythingInConsfiscationChest", "");

IF
StoryEvent(_, "DB_TUT_CheckAnythingInConsfiscationChest")
AND
DB_TUT_SearchingConfiscationChest(_Player)
THEN
PartySetFlag(_Player, "QuestUpdate_TUT_ShipMurder_Weapons_Found");

//END_REGION

//REGION Set flag if the players make themselves some weapons
IF
ItemTemplateCombinedWithItemTemplate(_, _, _, _, _, _Player, _Item)
AND
DB_IsPlayer(_Player)
AND
DB_TUT_LoseWeapons_LostWeapons(_Player)
AND
GetStatString(_Item, _Stats)
AND
DB_Tutorial_WeaponRecipe(_Stats, _, _)
THEN
PartySetFlag(_Player, "QuestUpdate_TUT_ShipMurder_Weapons_Made");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Tutorial_PrisonShip"
