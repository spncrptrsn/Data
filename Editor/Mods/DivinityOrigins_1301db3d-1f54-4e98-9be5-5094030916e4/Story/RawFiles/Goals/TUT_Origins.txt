Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_TUT_OriginQuestStarts(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"QuestAdd_FTJ_Ifan_DarkFaction","QuestUpdate_FTJ_Ifan_DarkFaction_Start_BeachWakeup");
DB_TUT_OriginQuestStarts(CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,"QuestAdd_FTJ_OriginLohse","QuestUpdate_FTJ_OriginLohse_Start_BeachWakeup");
DB_TUT_OriginQuestStarts(CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,"QuestAdd_FTJ_OriginSebille","QuestUpdate_FTJ_OriginSebille_Start_Dreamer");
DB_TUT_OriginQuestStarts(CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f,"QuestAdd_FTJ_OriginRedPrince","QuestUpdate_FTJ_OriginRedPrince_Start_BeachWakeup");
DB_TUT_OriginQuestStarts(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb,"QuestAdd_FTJ_OriginFane","QuestUpdate_FTJ_OriginFane_Start_BoatWakeup");
DB_TUT_OriginQuestStarts(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,"QuestAdd_FTJ_OriginBeast","QuestUpdate_FTJ_OriginBeast_Start_BoatWakeup");

DB_DeadEvent(CHARACTERGUID_S_TUT_LowerDeck_PrayingMagister_096479c9-a702-4161-a5ba-fb2b3312bf76,"TUT_LowerDeck_PrayingMagisterDead");
KBSECTION
//REGION Origin-specific items
// Avatar Sebille has her needle
IF
GameStarted("TUT_Tutorial_A",_)
AND
QueryOnlyOnce("FTJ_SebilleOriginStoryInit_TUT")
AND
IsTagged(CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,"AVATAR",1)
THEN
SetOnStage(ITEMGUID_S_FTJ_SebilleNeedle_818d574a-5cad-4401-a574-4bff60ab7026,1);
ItemToInventory(ITEMGUID_S_FTJ_SebilleNeedle_818d574a-5cad-4401-a574-4bff60ab7026,CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,-1,0);

// Ifan has his contract regardless of whether he's companion or avatar
IF
GameStarted("TUT_Tutorial_A",_)
AND
QueryOnlyOnce("FTJ_IfanStoryInit_TUT")
THEN
ItemToInventory(ITEMGUID_S_FTJ_IfanNoteFromDarkFaction_51899f0f-6ea2-4522-893b-69ccd87b97fb,CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,-1,0);
//END_REGION

//REGION Origin quests start on the tutorial boat
PROC
Proc_TUT_GiveQuests((CHARACTERGUID)_Player)
AND
DB_TUT_OriginQuestStarts(_Player,_AddQuest,_FirstUpdate)
THEN
ObjectSetFlag(_Player,_AddQuest);
ObjectSetFlag(_Player,_FirstUpdate);
//END_REGION

//REGION Ifan 3-way dialog node
PROC
PROC_TUT_InitTutorial()
AND
NOT DB_IsPlayer(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f)
THEN
// Ifan has a custom dialog
SetHasDialog(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,1);

PROC
PROC_GLOBAL_DialogStartRequested(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,(GUIDSTRING)_Initiator)
AND
NOT DB_IsPlayer(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f)
AND
QRY_SpeakerIsAvailable(CHARACTERGUID_S_TUT_LowerDeck_PrayingMagister_096479c9-a702-4161-a5ba-fb2b3312bf76)
THEN
Proc_StartDialog(0,"TUT_LowerDeck_OriginIfan",CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,CHARACTERGUID_S_TUT_LowerDeck_PrayingMagister_096479c9-a702-4161-a5ba-fb2b3312bf76,_Initiator);

PROC
PROC_GLOBAL_DialogStartRequested(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,(GUIDSTRING)_Initiator)
AND
NOT DB_IsPlayer(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f)
AND
NOT QRY_SpeakerIsAvailable(CHARACTERGUID_S_TUT_LowerDeck_PrayingMagister_096479c9-a702-4161-a5ba-fb2b3312bf76)
THEN
Proc_StartDialog(0,"TUT_LowerDeck_OriginIfan_DeadMagister",CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,_Initiator);
//END_REGION

IF
CharacterDied(_Origin)
AND
DB_OriginRecruitmentLocation_Region("TUT_Tutorial_A",_Origin,_,_)
AND
NOT DB_IsPlayer(_Origin)
THEN
ObjectSetFlag(_Origin,"TUT_KilledOnPrisonShip");

//REGION Block table use after starting animation
PROC
ProcBlockUseOfItem(_Player,_Item)
AND
DB_TUT_LowerDeck_StartingTriggers(_,_Item)
AND
NOT DB_TUT_StartPlayerBed(_Player,_Item)
THEN
DB_CustomUseItemResponse(_Player,_Item,0);
Proc_StartDialog(1,"TUT_AD_LowerDeck_UseCollaringTable",_Player);
//END_REGION

IF
TextEventSet("TUT_unfreeze")
AND
DB_IsPlayer(_Player)
THEN
CharacterUnfreeze(_Player);

PROC
ProcRegionEnded("TUT_Tutorial_A")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Tutorial_PrisonShip"
