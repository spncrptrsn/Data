Version 1
SubGoalCombiner SGC_AND
INITSECTION
//https://sites.google.com/a/larian.com/projecttest/home/wiki-articles/quest-design-documentation/bottled-wish

//Dialogues
DB_Dialogs(CHARACTERGUID_S_RC_WH_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,"RC_WH_BottledWish");
DB_Dialogs(ITEMGUID_S_RC_WH_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,"RC_WH_BottledWish_UseBottle");

//Djinn Team for easy poofing
DB_RC_WH_DjinnTeam((CHARACTERGUID)CHARACTERGUID_S_RC_WH_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);

//Triggers
DB_OneShotPlayerTrigger(RC_WH_BottledWish_BottleNoticeTrigger_d074f64e-bb5c-46e6-aae3-d8abda3bb93c);
TriggerRegisterForItems(RC_WH_BottledWish_BottleNoticeTrigger_d074f64e-bb5c-46e6-aae3-d8abda3bb93c);
DB_HasStoryEvent(ITEMGUID_S_RC_WH_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,"RC_WH_BottledWish_HasBottle");

//Offstage
SetOnStage(CHARACTERGUID_S_RC_WH_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,0);

SetOnStage(ITEMGUID_RC_WH_BottledWish_DjinnTales_7edffe49-981a-45a8-8755-4b75a619e7b2, 0);
SetOnStage(ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697, 0);
SetOnStage(ITEMGUID_RC_WH_BottledWish_DjinnsScimitar_1e8171bf-540d-4c72-b530-2f7dc50ac021, 0);

ApplyStatus(CHARACTERGUID_S_RC_WH_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,"ETHEREAL_SOLES",-1.0);

DB_NoLowAttitudeDialog(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);

// Register for bribing
DB_CrimeTriggers_GeneralBribeDialog(1,"RC_WH_DijinnItem_Interrogation");
KBSECTION
//REGION Debug Commands

// full to empty
IF
TextEventSet("f-to-e")
THEN
Transform(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,"Quest_DjinnBottleEmpty_5589bdbc-36dc-4aa2-83f7-ee43e16afd4a",0);

// empty to full
IF
TextEventSet("e-to-f")
THEN
Transform(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,"Quest_Djinn_Bottle_dcb3a695-79d4-4a02-bca4-8503e49ddfa1",0);

// Summon angry Djinn
IF
TextEventSet("dtoc")
THEN
Foop(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);
SetFaction(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,"RC_WH_Djinn");

// Give bottle
IF
TextEventSet("djb")
AND
CharacterGetHostCharacter(_Character)
THEN
ItemToInventory(ITEMGUID_S_RC_WH_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,_Character);
DebugText(_Character,"Added Bottle");

//END_REGION


//REGION Quest starters
// When player moves near bottle, bark
PROC
ProcOneShotTriggerEntered(_Player,RC_WH_BottledWish_BottleNoticeTrigger_d074f64e-bb5c-46e6-aae3-d8abda3bb93c)
AND
ObjectIsInTrigger(ITEMGUID_S_RC_WH_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,RC_WH_BottledWish_BottleNoticeTrigger_d074f64e-bb5c-46e6-aae3-d8abda3bb93c,1)
THEN
StartVoiceBark("RC_WH_VB_BottledWish_PcNoticeBottle",_Player);
DB_RC_WH_DjinnBottle((ITEMGUID)ITEMGUID_S_RC_WH_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c);

// When player uses bottle, add quest
IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_FoundBottle");

// When player gets bottle, add quest
IF
ItemAddedToCharacter(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,_Player)
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_FoundBottle");
//END_REGION


//REGION Bottle Actions
IF
ItemAddedToCharacter(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,_Char)
AND
DB_IsPlayer(_Char)
THEN
ObjectSetFlag(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,"RC_WH_BottledWish_HasBottle");
ObjectClearFlag(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,"RC_WH_BottledWish_LostBottle");

IF
ItemRemovedFromCharacter(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,_Char)
AND
DB_IsPlayer(_Char)
THEN
ObjectClearFlag(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,"RC_WH_BottledWish_HasBottle");
ObjectSetFlag(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,"RC_WH_BottledWish_LostBottle");
Proc_RC_WH_BottledWish_BottleLostDuringDialog();

PROC
Proc_RC_WH_BottledWish_BottleLostDuringDialog()
AND
DB_DialogNPCs(_,ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,_)
THEN
DialogRequestStop(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c);


// If the player opens bottle, summon talkable Djinn
IF
DialogEnded("RC_WH_BottledWish_UseBottle",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
GlobalGetFlag("RC_WH_BottledWish_BottleOpened",1)
THEN
Proc_RC_WH_BottledWish_TryPickup(_Player);
TeleportTo(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0, _Player);
Foop(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);
Proc_RC_WH_BottledWish_TalkableDjinnAppears(_Player);

// If Djinn is put back in the dialog, poof him and make bottle interactable again
IF
DialogEnded("RC_WH_BottledWish",_ID)
AND
ObjectGetFlag(CHARACTERGUID_S_RC_WH_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0, "RC_WH_BottledWish_PutBack",1)
THEN
Poof(CHARACTERGUID_S_RC_WH_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);
GlobalClearFlag("RC_WH_BottledWish_BottleOpened");

// If the player broke the bottle in dialogue, summon enraged Djinn
IF
DialogEnded("RC_WH_BottledWish_UseBottle",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
GlobalGetFlag("RC_WH_BottledWish_BottleBroken",1)
THEN
TeleportTo(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0, _Player);
CharacterFlushQueue(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);
Foop(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);
ItemRemove(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c);
Proc_RC_WH_BottledWish_EnragedDjinnAppears(_Player);

// If the player broke the bottle in dialogue, summon enraged Djinn
IF
CharacterDestroyedItem(_Player,ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c)
AND
GetTemplate(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,"Quest_Djinn_Bottle_dcb3a695-79d4-4a02-bca4-8503e49ddfa1")
AND
ObjectGetFlag(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,"RC_WH_BottledWish_DjinnEnraged",0)
AND
GetPosition(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,_X,_Y,_Z)
THEN
CharacterAppearAtPosition(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,_X,_Y,_Z,0,"");
PlayEffect(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,"RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01");
Proc_RC_WH_BottledWish_EnragedDjinnAppears(_Player);


// When bottle should be used from dialogue, pick up animation will play, but only if the bottle isn't in inventory already
PROC
Proc_RC_WH_BottledWish_TryPickup((CHARACTERGUID)_Player)
AND
ItemGetOwner(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,_Char)
AND
NOT QryItemInMagicPockets(_Char,ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c)
THEN
CharacterPickupItem(_Player,ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,"");

IF
ItemAddedToContainer(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,_Container)
THEN
SetHasDialog(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,0);

IF
ItemRemovedFromContainer(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,_Container)
THEN
SetHasDialog(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,1);

IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c)
AND
ItemGetOwner(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,_Char)
AND
QryTakeItemFromMagicPockets(_Char,ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,_Player)
THEN
DB_NOOP(1);


//Level up the Djinn to summoning player's level
IF
CharacterWentOnStage(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,1)
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
QRY_IsInRange(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,_Player,35.0)
AND
CharacterGetLevel(_Player,_LVL)
THEN
CharacterLevelUpTo(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,_LVL);

// If bottle opened and Djinn not enraged: Djinn talks
PROC 
Proc_RC_WH_BottledWish_TalkableDjinnAppears((CHARACTERGUID)_Player)
AND
ObjectGetFlag(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,"RC_WH_BottledWish_DjinnEnraged",0)
THEN
Proc_StartDialog(0,"RC_WH_BottledWish",CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,_Player);

// If bottle opened, but Djinn was enraged before: act as if bottle broken
PROC 
Proc_RC_WH_BottledWish_TalkableDjinnAppears((CHARACTERGUID)_Player)
AND
ObjectGetFlag(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,"RC_WH_BottledWish_DjinnEnraged",1)
THEN
Proc_RC_WH_BottledWish_EnragedDjinnAppears(_Player);

// Bottle broken: Djinn attacks
PROC 
Proc_RC_WH_BottledWish_EnragedDjinnAppears((CHARACTERGUID)_Player)
THEN
SetFaction(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,"RC_WH_Djinn");
Proc_StartDialog(1,"RC_WH_AD_BottledWish_DjinnFree",CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);
SetHasDialog(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,0);
//CharacterAddSkill(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,"Tornado_EnemyAir");
//CharacterUseSkill(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,"Tornado_EnemyAir",CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);

// Player begins talking with Djinn, but then decides to attack in the dialogue: Djinn attacks
IF
ObjectFlagSet("RC_WH_BottledWish_PlayerAttacksDjinn",CHARACTERGUID_S_RC_WH_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,_ID)
AND
DB_DialogPlayers(_ID,_Player,_)
THEN
Proc_RC_WH_BottledWish_EnragedDjinnAppears((CHARACTERGUID)_Player);


// Bottle broken: quest update for players
PROC 
Proc_RC_WH_BottledWish_EnragedDjinnAppears((CHARACTERGUID)_Player)
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_FightDjinn");

//END_REGION


//REGION Ending functions
//Djinn says "I am free", plays ascending animation, poofs. Quest is closed
PROC
Proc_RC_WH_BottledWish_WishGranted()
THEN
ProcCharacterDisableAllCrimes(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);
SetHasDialog(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,0);
SetCanFight(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,0);
SetScriptFrame(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,"DjinnWishGranted");

IF
ObjectFlagSet("Djinn_CanVanishAfterWish",CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,_)
THEN
Poof(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);
ProcCloseForAll_DjinnFreedom();

PROC
ProcCloseForAll_DjinnFreedom()
AND
DB_RC_WH_BottledWish_QuestFinisher(_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_DjinnFreedom");
NOT DB_RC_WH_BottledWish_QuestFinisher(_Player);

//END_REGION

//REGION Wishes Endings
//---POWER---
IF
DialogEnded("RC_WH_BottledWish",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,_Player)
AND
ObjectGetFlag(_Player, "RC_WH_BottledWish_EndPowerGood",1)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_EndPowerGood");
SetOnStage(ITEMGUID_RC_WH_BottledWish_DjinnsScimitar_1e8171bf-540d-4c72-b530-2f7dc50ac021, 1);
ItemToInventory(ITEMGUID_RC_WH_BottledWish_DjinnsScimitar_1e8171bf-540d-4c72-b530-2f7dc50ac021,_Player);
//TODO Scimitar is boring
Proc_RC_WH_BottledWish_WishGranted();
DB_RC_WH_BottledWish_QuestFinisher(_Player);

IF
DialogEnded("RC_WH_BottledWish",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,_Player)
AND
ObjectGetFlag(_Player, "RC_WH_BottledWish_EndPowerBad",1)
THEN
PlayEffect(_Player, "RS3_FX_Skills_Air_Lightning_Target_Vertical_01");
ProcObjectTimer(_Player,"WH_RC_BottledWish_LightningStrike",800);
Proc_RC_WH_BottledWish_WishGranted();
DB_RC_WH_BottledWish_QuestFinisher(_Player);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"WH_RC_BottledWish_LightningStrike")
THEN
ApplyDamage(_Player,150,"Air");
ApplyStatus(_Player, "KNOCKED_DOWN", 1.0);
ApplyStatus(_Player, "STUNNED", 6.0);
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_EndPowerBad");



//---KNOWLEDGE---
IF
DialogEnded("RC_WH_BottledWish",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "RC_WH_BottledWish_EndKnowledgeGood",1)
THEN
CharacterGiveReward(_Player,"ST_SourceSkillBook_Forced");
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_EndKnowledgeGood");
Proc_RC_WH_BottledWish_WishGranted();
DB_RC_WH_BottledWish_QuestFinisher(_Player);

IF
DialogEnded("RC_WH_BottledWish",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,_Player)
AND
ObjectGetFlag(_Player, "RC_WH_BottledWish_EndKnowledgeBad",1)
THEN
SetOnStage(ITEMGUID_RC_WH_BottledWish_DjinnTales_7edffe49-981a-45a8-8755-4b75a619e7b2, 1);
ItemToInventory(ITEMGUID_RC_WH_BottledWish_DjinnTales_7edffe49-981a-45a8-8755-4b75a619e7b2,_Player);
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_EndKnowledgeBad");
Proc_RC_WH_BottledWish_WishGranted();
DB_RC_WH_BottledWish_QuestFinisher(_Player);


//---PURGE---
IF
DialogEnded("RC_WH_BottledWish",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,_Player)
AND
ObjectGetFlag(_Player, "RC_WH_BottledWish_EndPurgeGood",1)
THEN
ItemTemplateAddTo("Scroll_Skill_Source_HailStorm_9314d32c-0714-4478-817a-358a498c98dc",_Player,1);
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_EndPurgeGood");
Proc_RC_WH_BottledWish_WishGranted();
DB_RC_WH_BottledWish_QuestFinisher(_Player);

IF
DialogEnded("RC_WH_BottledWish",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "RC_WH_BottledWish_EndPurgeBad",1)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_EndPurgeBad");
ProcObjectTimer(_Player,"RC_WH_BottledWish_PartyBlind",100);
Proc_RC_WH_BottledWish_WishGranted();
DB_RC_WH_BottledWish_QuestFinisher(_Player);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"RC_WH_BottledWish_PartyBlind")
THEN
PartySetFlag(_Player,"RC_WH_BottledWish_PartyBlind");

IF
ObjectFlagSet("RC_WH_BottledWish_PartyBlind",(CHARACTERGUID)_Player,_ID)
AND
QRY_IsInRange(CHARACTERGUID_S_RC_WH_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0,_Player,35.0)
THEN
PlayEffect(_Player, "RS3_FX_Skills_Voodoo_Decaying_Impact_01");
ApplyStatus(_Player, "BLIND", -1.0,1);
ObjectClearFlag(_Player,"RC_WH_BottledWish_PartyBlind",0);


//---RICHES---
IF
DialogEnded("RC_WH_BottledWish",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "RC_WH_BottledWish_EndRichesGood",1)
THEN
SetOnStage(ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697, 1);
ItemToInventory(ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697,_Player);
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_EndRichesGood");
Proc_RC_WH_BottledWish_WishGranted();
DB_RC_WH_BottledWish_QuestFinisher(_Player);

IF
DialogEnded("RC_WH_BottledWish",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "RC_WH_BottledWish_ContRichesBad",1)
THEN
DebugText(_Player,"Continue Riches Bad");
SetOnStage(ITEMGUID_RC_WH_BottledWyeahish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697, 1);
ItemToInventory(ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697,_Player);
ItemSetOriginalOwner(ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697, CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_ContRichesBad");
Proc_RC_WH_BottledWish_WishGranted();
DB_RC_WH_BottledWish_QuestFinisher(_Player);

//END_REGION

//REGION Crime
IF
RegionEnded("RC_Main")
AND
DB_IsPlayer(_Player)
AND
DB_RestartDjinnCrime(_Player,_)
THEN
ProcStopDjinnCrime(_Player);

IF
CharacterDied(_Player)
AND
DB_RestartDjinnCrime(_Player,_)
THEN
ProcStopDjinnCrime(_Player);

IF
RequestTrade(_Player,_Trader)
AND
DB_RestartDjinnCrime(_Player,_)
THEN
DB_RC_WH_Djinn_ItemTransaction(1);

IF
TradeEnds(_Player,_Trader)
AND
ItemIsInCharacterInventory(ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697,_Trader,1)
AND
DB_RestartDjinnCrime(_Player,_)
THEN
ProcStopDjinnCrime(_Player);
CharacterRegisterCrime(_Player,"RC_WH_Djinn_Item_Sold",ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697,NULL_00000000-0000-0000-0000-000000000000,0);
NOT DB_RC_WH_Djinn_ItemTransaction(1);

IF
ItemAddedToCharacter(ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697,_Player)
AND
DB_IsPlayer(_Player)
AND
ItemGetOriginalOwner(ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697,CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0)
AND
CharacterGetLevel(_Player,_Lvl)
THEN
Proc_WH_DjinnItem_ClearOldOwner();
DB_RestartDjinnCrime(_Player,_Lvl);

PROC
Proc_WH_DjinnItem_ClearOldOwner()
AND
DB_RestartDjinnCrime(_Char,_Lvl)
THEN
ProcStopDjinnCrime(_Char);

IF
ItemRemovedFromCharacter(ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697,_Player)
AND
NOT DB_RC_WH_Djinn_ItemTransaction(1)
AND
DB_RestartDjinnCrime(_Player,_)
THEN
ProcStopDjinnCrime(_Player);

IF
DB_RestartDjinnCrime(_Player,_)
THEN
CharacterRegisterCrime(_Player,"RC_WH_Djinn_Item",ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697,NULL_00000000-0000-0000-0000-000000000000,0);

IF
CharacterLeveledUp(_Player)
AND
DB_RestartDjinnCrime(_Player,_StartLevel)
AND
CharacterGetLevel(_Player,_Lvl)
AND
IntegerSubtract(_Lvl,_StartLevel,_Dif)
AND
_Dif >= 2
THEN
ProcStopDjinnCrime(_Player);

IF
OnCrimeConfrontationDone(_Crime,_NPC,_Lead,_,_,_,_)
AND
CrimeGetType(_Crime,"RC_WH_Djinn_Item")
AND
DB_RestartDjinnCrime(_Player,_Level)
THEN
ProcCharacterDisableCrime(_NPC,"RC_WH_Djinn_Item");
ProcDelayDjinnCrime(_Player,_Lead);

IF
OnCrimeConfrontationDone(_Crime,_NPC,_Lead,_Player,_,_,_)
AND
CrimeGetType(_Crime,"RC_WH_Djinn_Item_Sold")
THEN
CharacterStopCrime(_Player,"RC_WH_Djinn_Item_Sold",ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697);

PROC
ProcDelayDjinnCrime((CHARACTERGUID)_Player,1)
THEN
CharacterStopCrime(_Player,"RC_WH_Djinn_Item",ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697);
ProcObjectTimer(_Player,"RestartRC_WH_Djinn_Item",600000);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"RestartRC_WH_Djinn_Item")
AND
DB_RestartDjinnCrime(_Player,_Level)
THEN
CharacterRegisterCrime(_Player,"RC_WH_Djinn_Item",ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697,NULL_00000000-0000-0000-0000-000000000000,0);

PROC
ProcStopDjinnCrime((CHARACTERGUID)_Player)
AND
DB_RestartDjinnCrime(_Player,_Level)
THEN
NOT DB_RestartDjinnCrime(_Player,_Level);
CharacterStopCrime(_Player,"RC_WH_Djinn_Item",ITEMGUID_RC_WH_BottledWish_DjinnsGift_9d02b617-4208-4ab5-8141-ca8a2981d697);

IF
ObjectFlagSet("Allow_Arrest",(CHARACTERGUID)_Player,_Inst)
AND
DB_RestartDjinnCrime(_Player,_)
AND
DB_DialogName("RC_WH_DijinnItem_Interrogation",_Inst)
THEN
ProcStopDjinnCrime(_Player);

IF
ObjectFlagSet("Resist_Arrest",(CHARACTERGUID)_Player,_Inst)
AND
DB_RestartDjinnCrime(_Player,_)
AND
DB_DialogName("RC_WH_DijinnItem_Interrogation",_Inst)
THEN
ProcStopDjinnCrime(_Player);

IF
DialogEnded("RC_WH_DijinnItem_Interrogation",_ID)
AND
DB_DialogPlayers(_ID,_Player,_)
AND
//sets in guard investigation dialogue
ObjectGetFlag(_Player,"RC_WH_BottledWish_EndRichesBad",1)
THEN
StartVoiceBark("RC_WH_VB_BottledWish_StolenNecklace",(CHARACTERGUID)_Player);

//END_REGION


//REGION Full/empty transformations
//transform full to open
IF
ItemAddedToCharacter(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,_Char)
AND
GlobalGetFlag("RC_WH_BottledWish_BottleOpened",1)
THEN
Transform(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,"Quest_DjinnBottleEmpty_5589bdbc-36dc-4aa2-83f7-ee43e16afd4a",0);

IF
GlobalFlagSet("RC_WH_BottledWish_BottleOpened")
THEN
Transform(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,"Quest_DjinnBottleEmpty_5589bdbc-36dc-4aa2-83f7-ee43e16afd4a",0);

PROC
ProcBlockUseOfItem(_Player,ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c)
AND
GlobalGetFlag("RC_WH_BottledWish_BottleOpened",1)
THEN
DB_CustomUseItemResponse(_Player,ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,0);

IF
GlobalFlagCleared("RC_WH_BottledWish_BottleOpened")
THEN
Transform(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,"Quest_Djinn_Bottle_dcb3a695-79d4-4a02-bca4-8503e49ddfa1",0);

//END_REGION


//REGION Transferring bottle to NPC
// If someone has bottle, and he's not a player character, the bottle will disappear to prevent abusing, and PROC remembering dialogue
IF
ItemAddedToCharacter(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,_NPC)
AND
GetTemplate(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,"Quest_Djinn_Bottle_dcb3a695-79d4-4a02-bca4-8503e49ddfa1")
AND
NOT DB_IsPlayer(_NPC)
AND
_NPC != CHARACTERGUID_S_RC_DW_Tavern_SurpriseDate_ThugLeader_56c58299-cc21-4920-8cb3-d351adda85a6
THEN
Transform(ITEMGUID_S_RC_WH_BottledWish_DjinnBottle_08a6859f-9e69-414c-89e5-bed50da3137c,"Quest_DjinnBottleEmpty_5589bdbc-36dc-4aa2-83f7-ee43e16afd4a",0);
Proc_RC_WH_DjinnBottleAddedToNpc(_NPC);

PROC
Proc_RC_WH_DjinnBottleAddedToNpc((CHARACTERGUID)_NPC)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_FoundBottle",1)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_TransferredBottleToNpc");

// If NPC got bottle and is in dialogue remember NPC. When NPC finishes his dialogue, he'll start summoning Djinn
PROC
Proc_RC_WH_DjinnBottleAddedToNpc((CHARACTERGUID)_NPC)
AND
IsSpeakerReserved(_NPC,1)
THEN
DB_RC_WH_BottledWish_NPC_DialogEnd(_NPC);

// If added and not in another dialogue then summon djinn
PROC
Proc_RC_WH_DjinnBottleAddedToNpc((CHARACTERGUID)_NPC)
AND
NOT DB_RC_WH_BottledWish_NPC_DialogEnd(_NPC)
THEN
ProcObjectTimer(_NPC,"WH_RC_BottledWish_NpcWaitForUseTimer",4000);

// When NPC finishes whatever dialogue they have, they summon Djinn
IF
DialogEnded(_,_ID)
AND
DialogGetInvolvedNPC(_ID,1,(CHARACTERGUID)_NPC)
AND
DB_RC_WH_BottledWish_NPC_DialogEnd(_NPC)
THEN
NOT DB_RC_WH_BottledWish_NPC_DialogEnd(_NPC);
ProcObjectTimer(_NPC,"WH_RC_BottledWish_NpcWaitForUseTimer",4000);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_NPC,"WH_RC_BottledWish_NpcWaitForUseTimer")
THEN
DialogRequestStop(_NPC);
SetHasDialog(_NPC,0);
PlayAnimation(_NPC,"use_craft");
Proc_StartDialog(1,"GEB_AD_Noticed_Summon",_NPC);
ProcObjectTimer(_NPC,"WH_RC_BottledWish_NpcSummonsDjinnTimer",8000);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_NPC,"WH_RC_BottledWish_NpcSummonsDjinnTimer")
THEN
TeleportTo(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0, _NPC);
SetHasDialog(_NPC,1);
Foop(CHARACTERGUID_S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);
CharacterLookAt(_NPC,CHARACTERGUID_S_RC_WH_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0);
PlayAnimation(_NPC,"cower");
Proc_RC_WH_BottledWish_EnragedDjinnAppears(_NPC);
CharacterSetRelationIndivFactionToFaction(_NPC,"RC_WH_Djinn",0);
CharacterSetRelationFactionToIndivFaction("RC_WH_Djinn",_NPC,0);

//END_REGION


//REGION Combat scripts
// When Djinn or elemental dies, poof him instead of leaving corpse
IF
CharacterDying(_Character)
AND
DB_RC_WH_DjinnTeam(_Character)
THEN
Poof(_Character);

// If Djinn dies, close quest
IF
CharacterDying(S_RC_WH_BottledWish_Djinn_838283f5-a45f-4892-a4ac-fae2f99d4de0)
THEN
ProcCloseForAll_DjinnDied();

PROC
ProcCloseForAll_DjinnDied()
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_WH_BottledWish_DjinnDied");

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
