Version 1
SubGoalCombiner SGC_AND
INITSECTION
//https://sites.google.com/a/larian.com/larian-wikisite/home/quest-design-documentation/smuggling-caches

DB_FishFactory_CellarCreep((CHARACTERGUID)CHARACTERGUID_S_RC_DW_FishFactory_Creep_01_Explosive_43b4c6a1-7d91-4bfe-87ad-0a4f903a420a,1000);
DB_FishFactory_CellarCreep(CHARACTERGUID_S_RC_DW_FishFactory_Creep_01_e1eaf78a-a444-46f3-bac7-3353771192f8,1200);
DB_FishFactory_CellarCreep(CHARACTERGUID_S_RC_DW_FishFactory_Creep_02_ef4f8f41-fcfe-4f77-be03-95846b82c8f4,1500);
DB_FishFactory_CellarCreep(CHARACTERGUID_S_RC_DW_FishFactory_Creep_02_Explosive_3772d2f7-fc28-4a48-8716-5c4e22989cae,1400);
DB_FishFactory_CellarCreep(CHARACTERGUID_S_RC_DW_FishFactory_Creep_03_8348b9b4-82fe-4214-9251-02a926f025de,1700);
DB_FishFactory_CellarCreep(CHARACTERGUID_S_RC_DW_FishFactory_Creep_03_Explosive_d645dacb-6626-407e-81a8-ffca5cd65891,1500);
DB_FishFactory_CellarCreep(CHARACTERGUID_S_RC_DW_FishFactory_Creep_04_Explosive_103d4aa2-e618-477e-8dda-3eba5c5f6264,1900);
DB_FishFactory_CellarCreep(CHARACTERGUID_S_RC_DW_FishFactory_Creep_05_Explosive_75280a26-a7ba-41ec-8f05-0366b7310b34,2500);
DB_FishFactory_CellarCreep(CHARACTERGUID_S_RC_DW_FishFactory_Creep_06_Explosive_0555501d-2132-44bd-8f50-2e03e0d4461c,2800);
DB_FishFactory_CellarCreep(CHARACTERGUID_S_RC_DW_FishFactory_Creep_07_Explosive_f082fa9c-5837-4a09-9261-55be773e6670,3200);
DB_FishFactory_CellarCreep(CHARACTERGUID_S_RC_DW_FishFactory_Creep_08_Explosive_c4f98683-7a38-40e2-9feb-7b630dce47b6,4200);
DB_FishFactory_CellarCreep(CHARACTERGUID_S_RC_DW_FishFactory_Creep_09_Explosive_e90c5400-6e58-402c-8dc6-1c03f6f4bcf8,4200);
DB_FishFactory_CellarCreep(CHARACTERGUID_S_RC_DW_FishFactory_Creep_10_Explosive_72f273ae-eef2-4294-93bb-e9f388be8cd1,4200);

DB_RC_DW_FishFactoryFakeBarrel((ITEMGUID)ITEMGUID_S_RC_DW_FishFactorySourceBarrelEmpty1_a7ff7aa7-f789-4703-a0f3-b8d5bd8343f0);
DB_RC_DW_FishFactoryFakeBarrel(ITEMGUID_S_RC_DW_FishFactorySourceBarrelEmpty2_ad52ec8e-f0ab-4e8e-8f68-36a9ad81f692);
DB_RC_DW_FishFactoryFakeBarrel(ITEMGUID_S_RC_DW_FishFactorySourceBarrelEmpty3_f3ff87da-8adb-45f8-95e9-abdb1994abab);

DB_OneShot_VoiceBarkTrigger(TRIGGERGUID_S_RC_DW_OneShot_CacheVB_ecef2368-56da-490e-a900-1e436fe52262,"RC_DW_VB_FishFactory_Cache");

DB_RC_DW_SmugglingCacheCreepFlag("IFAN","RC_DW_CellarCreep_Ifan");
DB_RC_DW_SmugglingCacheCreepFlag("RED_PRINCE","RC_DW_CellarCreep_RedPrince");
DB_RC_DW_SmugglingCacheCreepFlag("LOHSE","RC_DW_CellarCreep_Lohse");
DB_RC_DW_SmugglingCacheCreepFlag("BEAST","RC_DW_CellarCreep_Beast");
DB_RC_DW_SmugglingCacheCreepFlag("SEBILLE","RC_DW_CellarCreep_Sebille");
DB_RC_DW_SmugglingCacheCreepFlag("FANE","RC_DW_CellarCreep_Fane");
DB_RC_DW_SmugglingCacheCreepFlag("GENERIC","RC_DW_CellarCreep_Generic");

DB_RC_DW_HasCacheStoryEvent(ITEMGUID_S_RC_DW_SmugglingCache1_7ef3f7d0-ed34-48e8-abd8-fd07c8ee0d2b,"RC_DW_HasSmugglingCache1");
DB_RC_DW_HasCacheStoryEvent(ITEMGUID_S_RC_DW_SmugglingCache2_55cce70e-a043-4dea-80af-076655870e50,"RC_DW_HasSmugglingCache2");
DB_FishFactory_DeadCreep(0);

DB_RC_DW_FishFactoryOwnerTags("BARBARIAN");
DB_RC_DW_FishFactoryOwnerTags("VILLIAN");
DB_RC_DW_FishFactoryOwnerTags("OUTLAW");

ItemClearOwner(ITEMGUID_S_RC_DW_FishFactoryWayOut1_ab9cdb72-4719-4eca-82c5-644ea2c40ddb);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_RC_DW_FishFactoryOwner_Dwarf_2e3f44e2-83e6-4c99-a3e0-475ecbcaafe3);
TriggerRegisterForItems(TRIGGERGUID_S_RC_DW_FishFactorySmuggleBox_5426adf1-6a2d-445a-bea0-f24cd0d11c34);
DB_DeadEvent(CHARACTERGUID_S_RC_DW_FishFactory_Owner_74284ca3-3f7d-4a2a-8099-49aa3117ab0d,"RC_DW_Barnes_IsDead");

Proc_JDF_CreateCustomFlagsDependency("QuestUpdate_RC_DW_TaintedSmuggling_SmugglingCacheFoundBeforeJulian", "RC_DW_QuestFromJulian", "QuestUpdate_RC_DW_TaintedSmuggling_SmugglingCacheReportToMagister");
KBSECTION
//REGION FishFactory owner

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_RC_DW_FishFactoryOwner_Dwarf_2e3f44e2-83e6-4c99-a3e0-475ecbcaafe3)
AND
IsTagged(_Player,"DWARF",1)
AND
CharacterCanSee(CHARACTERGUID_S_RC_DW_FishFactory_Owner_74284ca3-3f7d-4a2a-8099-49aa3117ab0d,_Player,1)
AND
QueryOnlyOnce("RC_DW_FishFactoryOwner_Dwarf")
THEN
Proc_StartDialog(0,"RC_DW_FishFactoryOwner",CHARACTERGUID_S_RC_DW_FishFactory_Owner_74284ca3-3f7d-4a2a-8099-49aa3117ab0d,_Player);
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_RC_DW_FishFactoryOwner_Dwarf_2e3f44e2-83e6-4c99-a3e0-475ecbcaafe3);

IF
DialogStarted("RC_DW_FishFactoryOwner",_Inst)
AND
DialogGetInvolvedPlayer(_Inst,1,(CHARACTERGUID)_Player)
AND
DB_RC_DW_FishFactoryOwnerTags(_Tag)
AND
IsTagged(_Player,_Tag,1)
THEN
ObjectSetFlag(_Player,"RC_DW_FishFactoryThreatCond");

IF
DialogStarted("RC_DW_FishFactoryOwner",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,1)
AND
IsTagged(_Player,"DWARF",1)
THEN
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_RC_DW_FishFactoryOwner_Dwarf_2e3f44e2-83e6-4c99-a3e0-475ecbcaafe3);

IF
DialogEnded("RC_DW_FishFactoryOwner",_Inst)
AND
DialogGetInvolvedPlayer(_Inst,1,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"RC_DW_FishFactoryThreatCond",1)
THEN
ObjectClearFlag(_Player,"RC_DW_FishFactoryThreatCond",0);

IF
ObjectFlagSet("RC_DW_FishFactory_HatchKey",_Player,_)
AND
ItemIsInCharacterInventory(ITEMGUID_S_RC_DW_FishFactoryHatchKey_31fbd9ab-c4d0-4618-af12-741ada632f71,CHARACTERGUID_S_RC_DW_FishFactory_Owner_74284ca3-3f7d-4a2a-8099-49aa3117ab0d,1)
THEN
ItemToInventory(ITEMGUID_S_RC_DW_FishFactoryHatchKey_31fbd9ab-c4d0-4618-af12-741ada632f71,_Player);

IF
ObjectFlagSet("RC_DW_FishFactory_SafeKey",_Player,_)
AND
ItemIsInCharacterInventory(ITEMGUID_S_RC_DW_FishFactorySkeletonKey_ab3594bc-f64a-41a4-996b-28d80c04b7e9,CHARACTERGUID_S_RC_DW_FishFactory_Owner_74284ca3-3f7d-4a2a-8099-49aa3117ab0d,1)
THEN
ItemToInventory(ITEMGUID_S_RC_DW_FishFactorySkeletonKey_ab3594bc-f64a-41a4-996b-28d80c04b7e9,_Player);
ItemClearOwner(ITEMGUID_S_RC_DW_FishFactorySafe_513d05e4-c4d3-4237-849f-8467d05b3df8);

IF
ObjectFlagSet("RC_DW_FishFactory_SafeKey",_Player,_)
THEN
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_RC_DW_FishFactoryOwner_Dwarf_2e3f44e2-83e6-4c99-a3e0-475ecbcaafe3);

IF
ObjectFlagSet("RC_DW_FishFactoryGold",(CHARACTERGUID)_Player,_)
THEN
CharacterAddGold(_Player,100);

IF
ObjectFlagSet("RC_DW_SmugglingCache_HatchMarker",(CHARACTERGUID)_Player,_)
THEN
ProcShowMarker(_Player,"RC_DW_FishFactoryHatch");

IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_DW_FishFactoryHatch_50b734ed-4910-4455-b4fb-05213064bc78)
THEN
ProcHideMarker(_Player,"RC_DW_FishFactoryHatch");

IF
GameBookInterfaceClosed(ITEMGUID_S_RC_DW_FishFactoryOwner_Invoice_38f889c9-9c26-40e1-8352-b4e13d3396b7,_Player)
AND
UserGetFlag(_Player,"RC_ARX_BlackHouseNote",0)
THEN
UserSetFlag(_Player,"RC_ARX_BlackHouseNote");
ProcShowMarker(_Player,"RC_ARX_BlackHouse");
StartVoiceBark("RC_DW_VB_OwnerBlackHouse",_Player);
ObjectSetFlag(_Player,"QuestUpdate_RC_DW_TaintedSmuggling_SmugglingBuyerLearn");

IF
CharacterDying(CHARACTERGUID_S_RC_DW_FishFactory_Owner_74284ca3-3f7d-4a2a-8099-49aa3117ab0d)
THEN
ItemTemplateAddTo("QUEST_ARX_FishBarrelScroll_d2e74ff6-9e68-4e7c-bfb3-cab1ef9bd14d",CHARACTERGUID_S_RC_DW_FishFactory_Owner_74284ca3-3f7d-4a2a-8099-49aa3117ab0d,1);

//END_REGION

//REGION Cellar

IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_DW_FishFactorySourceBarrel_cb5db6db-4648-8eee-3451-8c1537124f1a)
AND
ItemIsLocked(ITEMGUID_S_RC_DW_FishFactorySourceBarrel_cb5db6db-4648-8eee-3451-8c1537124f1a,1)
THEN
Proc_StartDialog(0,"RC_DW_HiddenCache",ITEMGUID_S_RC_DW_FishFactorySourceBarrel_cb5db6db-4648-8eee-3451-8c1537124f1a,_Player);

IF
DialogEnded("RC_DW_HiddenCache",_ID)
AND
DB_GlobalFlag("RC_DW_UnlockContraband")
AND
DB_DialogPlayers(_ID, _Player, _)
THEN
ItemUnLock(ITEMGUID_S_RC_DW_FishFactorySourceBarrel_cb5db6db-4648-8eee-3451-8c1537124f1a);
CharacterUseItem((CHARACTERGUID)_Player, ITEMGUID_S_RC_DW_FishFactorySourceBarrel_cb5db6db-4648-8eee-3451-8c1537124f1a, "");

IF
DB_RC_DW_FishFactoryFakeBarrel(_Barrel)
THEN
DB_Dialogs(_Barrel,"RC_DW_HiddenCacheEmpty");

IF
DB_FishFactory_CellarCreep(_Creep,_)
THEN
SetOnStage(_Creep,0);

IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_DW_FishFactoryHatch_50b734ed-4910-4455-b4fb-05213064bc78)
AND
QryItemInMagicPockets(_Player,ITEMGUID_S_RC_DW_FishFactoryHatchKey_31fbd9ab-c4d0-4618-af12-741ada632f71)
AND
QueryOnlyOnce("RC_DW_FishFactory_OpenedCellar")
THEN
UserSetFlag(_Player,"RC_DW_FishFactory_OpenedCellar");

IF
ItemRemovedFromContainer(_Item,ITEMGUID_S_RC_DW_FishFactorySourceBarrel_cb5db6db-4648-8eee-3451-8c1537124f1a)
AND
DB_RC_DW_HasCacheStoryEvent(_Item,_)
THEN
Proc_RC_DW_SmugglingCacheAmbush();

PROC
Proc_RC_DW_SmugglingCacheAmbush()
AND
QueryOnlyOnce("RC_DW_FishCellarAmbush")
AND
DB_FishFactory_CellarCreep(_Creep,_Time)
THEN
ProcObjectTimer(_Creep,"RC_DW_HiddenCacheTaken",_Time);

IF
ItemDestroyed(ITEMGUID_S_RC_DW_FishFactorySourceBarrel_cb5db6db-4648-8eee-3451-8c1537124f1a)
THEN
Proc_RC_DW_SmugglingCacheAmbush();

IF
ItemLeftTrigger(ITEMGUID_S_RC_DW_FishFactorySourceBarrel_cb5db6db-4648-8eee-3451-8c1537124f1a,TRIGGERGUID_S_RC_DW_FishFactorySmuggleBox_5426adf1-6a2d-445a-bea0-f24cd0d11c34,_)
THEN
Proc_RC_DW_SmugglingCacheAmbush();
TriggerUnregisterForItems(ITEMGUID_S_RC_DW_FishFactorySourceBarrel_cb5db6db-4648-8eee-3451-8c1537124f1a);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Creep,"RC_DW_HiddenCacheTaken")
AND
GetPosition(_Creep,_X,_Y,_Z)
THEN
//PlayScaledEffectAtPosition("RS3_FX_Skills_Earth_PyroclasticRock_Impact_01",1.0,_X,_Y,_Z);
CharacterAppear(_Creep,1,"");

IF
ObjectFlagSet("RC_DW_FakeCacheUnsealed",(ITEMGUID)_Barrel,_)
THEN
ItemSetCanInteract(_Barrel,0);
ItemUnlockAndOpen(_Barrel);

IF
DialogEnded("RC_DW_HiddenCacheEmpty",_Inst)
AND
DialogGetInvolvedNPC(_Inst,1,(ITEMGUID)_Barrel)
AND
ObjectGetFlag(_Barrel,"RC_DW_FakeCacheUnsealed",1)
THEN
ObjectSetFlag(_Barrel,"RC_DW_FakeCacheCreateCloud");
NOT DB_RC_DW_FishFactoryFakeBarrel(_Barrel);

IF
ItemUnlocked(_Barrel,_Player,_)
AND
DB_RC_DW_FishFactoryFakeBarrel(_Barrel)
THEN
ObjectSetFlag(_Barrel,"RC_DW_FakeCacheUnsealed");
ObjectSetFlag(_Barrel,"RC_DW_FakeCacheCreateCloud");

IF
ItemDestroying(_Barrel)
AND
DB_RC_DW_FishFactoryFakeBarrel(_Barrel)
AND
ObjectGetFlag(_Barrel,"RC_DW_FakeCacheUnsealed",0)
AND
GetPosition(_Barrel,_X,_Y,_Z)
THEN
CreateSurfaceAtPosition(_X,_Y,_Z,"SurfacePoisonCloud",3.0,10.0);
PlayEffectAtPosition("RS3_FX_GP_Projectiles_SlimeBall_Impact_01",_X,_Y,_Z);

IF
ObjectTurnStarted((CHARACTERGUID)_Creep)
AND
DB_FishFactory_CellarCreep(_Creep,_)
AND
GetClosestAlivePlayer(_Creep,_Player,_)
AND
DB_RC_DW_SmugglingCacheCreepFlag(_Tag,_Flag)
AND
IsTagged(_Player,_Tag,1)
THEN
GlobalSetFlag(_Flag);
Proc_StartDialog(1,"RC_DW_AD_CellarCreep",_Creep);

IF
CharacterKilledBy(_Defender,_AttackerOwner,_Attacker)
AND
DB_FishFactory_CellarCreep(_Defender,_)
AND
DB_IsPlayer(_AttackerOwner)
THEN
PartySetFlag(_AttackerOwner,"RC_DW_KilledCacheCreep");

IF
CharacterDied(_Creep)
AND
DB_FishFactory_CellarCreep(_Creep,_)
THEN
Proc_CountHelper("RC_DW_SmugglingCache_CreepCount");

IF
DB_CountHelper("RC_DW_SmugglingCache_CreepCount",10)
THEN
GlobalSetFlag("RC_DW_SmugglingCacheCreepsDead");

IF
DB_RC_DW_HasCacheStoryEvent(_Item,_Flag)
THEN
DB_HasStoryEvent((ITEMGUID)_Item,_Flag);

IF
ObjectFlagSet(_Flag, _Player, _)
AND
DB_RC_DW_HasCacheStoryEvent(_,_Flag)
THEN
ObjectSetFlag((CHARACTERGUID)_Player, "RC_DW_HasSmugglingCache", 0);

IF
ObjectFlagCleared(_Flag,_Player, _)
AND
DB_RC_DW_HasCacheStoryEvent(_,_Flag)
AND
NOT QRY_RC_DW_HasCache((CHARACTERGUID)_Player)
THEN
ObjectClearFlag((CHARACTERGUID)_Player, "RC_DW_HasSmugglingCache", 0);

QRY
QRY_RC_DW_HasCache((CHARACTERGUID)_Player)
AND
DB_RC_DW_HasCacheStoryEvent(_,_Flag)
AND
ObjectGetFlag(_Player, _Flag, 1)
THEN
DB_NOOP(1);

IF
ObjectFlagSet("RC_DW_HasSmugglingCache",(CHARACTERGUID)_Player,_)
AND
DB_IsPlayer(_Player)
THEN
PROC_RC_DW_SmugglingCacheFound(_Player);

IF
ObjectFlagSet("RC_DW_SmugglingCacheFound",(CHARACTERGUID)_Player,_)
THEN
PROC_RC_DW_SmugglingCacheFound(_Player);

PROC
PROC_RC_DW_SmugglingCacheFound((CHARACTERGUID)_Player)
THEN
ObjectSetFlag(_Player,"RC_DW_FishFactory_FoundCache");
ObjectSetFlag(_Player,"QuestUpdate_RC_DW_TaintedSmuggling_SmugglingFound");

PROC
PROC_RC_DW_SmugglingCacheFound((CHARACTERGUID)_Player)
AND
PartyGetFlag(_Player, "RC_DW_QuestFromJulian", 1)
AND
PartyGetFlag(_Player, "QuestUpdate_RC_DW_TaintedSmuggling_SmugglingCacheFoundBeforeJulian", 0)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_DW_TaintedSmuggling_SmugglingCacheFoundAfterJulian");

PROC
PROC_RC_DW_SmugglingCacheFound((CHARACTERGUID)_Player)
AND
PartyGetFlag(_Player, "RC_DW_QuestFromJulian", 0)
AND
PartyGetFlag(_Player, "QuestUpdate_RC_DW_TaintedSmuggling_SmugglingCacheFoundAfterJulian", 0)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_DW_TaintedSmuggling_SmugglingCacheFoundBeforeJulian");

// The SmugglingFound update always gets set regardless of how you open the |Rotten Goods| subquest
// -> use it to check whether we need to add an update for the parent quest too
IF
ObjectFlagSet("QuestUpdate_RC_DW_TaintedSmuggling_SmugglingFound", (CHARACTERGUID)_Player, _)
AND
QuestAccepted(_Player, "RC_DW_DwarvenCriminals", 0)
THEN
ObjectSetFlag(_Player, "QuestUpdate_RC_DW_DwarvenCriminals_SmugglingFoundBeforeMagister_MainStartFallback");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
