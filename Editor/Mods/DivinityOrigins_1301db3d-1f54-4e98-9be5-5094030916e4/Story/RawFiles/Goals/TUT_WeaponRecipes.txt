Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Tutorial_WeaponRecipe("WPN_CraftedAxe_1H","LOOT_WoodenStick_A_LOOT_PieceOfRock_A","Fighter");
DB_Tutorial_WeaponRecipe("WPN_Common_Mace_1H_A","LOOT_WoodenBranch_A_PiecesOfRock","Fighter");
DB_Tutorial_WeaponRecipe("WPN_Mace_2H","LOOT_WoodenBranch_A_PiecesOfRock","Fighter");
DB_Tutorial_WeaponRecipe("WPN_CraftedStaff_Air","LOOT_WoodenBranch_A_HAR_Shell","Mage");
DB_Tutorial_WeaponRecipe("WPN_CraftedSword_1H","LOOT_WoodenStick_A_Dagger","Fighter");
DB_Tutorial_WeaponRecipe("WPN_CraftedSword_1H","LOOT_WoodenStick_A_Knife","Fighter");
DB_Tutorial_WeaponRecipe("WPN_CraftedMace_2H","LOOT_WoodenBranch_A_LOOT_Scraps_Cloth_A","Fighter");
DB_Tutorial_WeaponRecipe("WPN_CraftedSword_2H","LOOT_WoodenStick_A_Dagger","Fighter");
DB_Tutorial_WeaponRecipe("WPN_CraftedSword_2H","LOOT_WoodenStick_A_Knife","Fighter");
DB_Tutorial_WeaponRecipe("WPN_CraftedBow","LOOT_WoodenStick_A_LOOT_String_Bow_A","Ranger");
DB_Tutorial_WeaponRecipe("WPN_CraftedDagger","LOOT_MetalShard_A_LOOT_Scraps_Cloth_A","Rogue");
DB_Tutorial_WeaponRecipe("WPN_CraftedWand_Fire","LOOT_WoodenStick_A_LOOT_Skull_Bird_A","Mage");
DB_Tutorial_WeaponRecipe("WPN_CraftedStaff_Fire","LOOT_WoodenBranch_A_HAR_Shell","Mage");
DB_Tutorial_WeaponRecipe("WPN_CraftedCrossbow","LOOT_WoodenStick_A_LOOT_WoodenBranch_A","Ranger");
DB_Tutorial_WeaponRecipe("WPN_CraftedWand_Poison","LOOT_WoodenStick_A_LOOT_Skull_Human_A","Mage");
DB_Tutorial_WeaponRecipe("WPN_CraftedStaff_Poison","LOOT_WoodenBranch_A_HAR_Shell","Mage");
DB_Tutorial_WeaponRecipe("WPN_Pitchfork","LOOT_WoodenBranch_A_LOOT_MetalShard_A","Fighter");

DB_Tutorial_ExtraRecipes("Fighter","CON_Potion_Empty_A_CON_Nature_Mushroom_Amadouvier_A");
DB_Tutorial_ExtraRecipes("Fighter","LOOT_Rope_A_LOOT_Rope_A");
DB_Tutorial_ExtraRecipes("Fighter","ITEM_EmptyBottle_Oil");
DB_Tutorial_ExtraRecipes("Fighter","ITEM_BottleWithOil_GRN_Ingredient_Fuse_A");

DB_Tutorial_ExtraRecipes("Rogue","CON_Potion_Empty_A_CON_Nature_Mushroom_Chanterelle_A");
DB_Tutorial_ExtraRecipes("Rogue","GRN_Ingredient_Flask_Empty_A_Poison");
DB_Tutorial_ExtraRecipes("Rogue","Poison_Knife");
DB_Tutorial_ExtraRecipes("Rogue","Poison_Dagger");

DB_Tutorial_ExtraRecipes("Ranger","CON_Potion_Empty_A_CON_Nature_Mushroom_Boletus_A");
DB_Tutorial_ExtraRecipes("Ranger","LOOT_Essence_Fire_A_WPN_ArrowHead_A");
DB_Tutorial_ExtraRecipes("Ranger","ArrowShaft_WPN_ArrowHead_Fire_A");

DB_Tutorial_ExtraRecipes("Mage","CON_Potion_Empty_A_CON_Nature_Mushroom_Calocera_A");
DB_Tutorial_ExtraRecipes("Mage","BOOK_Paper_Sheet_A_LOOT_Essence_Water_A_FOOD_FishA");
DB_Tutorial_ExtraRecipes("Mage","LOOT_Needle_Thread_A_LOOT_Scraps_Cloth_A_TOOL_Pouch_Dust_Pixie_A");


DB_Tutorial_UndeadRecipe("CON_Potion_Empty_A_CON_Herb_Mushroom_C");
DB_Tutorial_UndeadRecipe("CON_Potion_Empty_A_CONT_Barrel_Ooze");
DB_Tutorial_UndeadRecipe("Poison_Food");
DB_Tutorial_UndeadRecipe("Poison_Drinks");

DB_Tutorial_ClawfingerRecipe("LOOT_ClawFinger_A_Food");
DB_Tutorial_ClawfingerRecipe("LOOT_ClawFinger_A_Drinks");
DB_Tutorial_ClawfingerRecipe("LOOT_ClawFinger_A_POTION_Minor_Healing_Potion");
KBSECTION
// Recipes for starting weapons
PROC
Proc_TUT_StartGame()
AND
DB_IsPlayer(_Player)
AND
CharacterGetEquippedWeapon(_Player,_Item)
THEN
PROC_TUT_WeaponRecipes_UnlockIfBasicWeapon((ITEMGUID)_Item,_Player,1);

//REGION Recipes for picking up weapons/items
IF
ItemAddedToCharacter(_Weapon,_Player)
AND
DB_IsPlayer(_Player)
THEN
PROC_TUT_WeaponRecipes_UnlockIfBasicWeapon(_Weapon,_Player,0);

IF
ItemAddedToCharacter(_Item,_Player)
AND
DB_IsPlayer(_Player)
AND
GetStatString(_Item,"LOOT_ClawFinger_A")
AND
DB_Tutorial_ClawfingerRecipe(_Recipe)
THEN
CharacterUnlockRecipe(_Player,_Recipe,1);
//END_REGION

//REGION Undead recipes
PROC
Proc_TUT_StartGame()
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player,"UNDEAD",1)
AND
DB_Tutorial_UndeadRecipe(_Recipe)
THEN
CharacterUnlockRecipe(_Player,_Recipe,0);

PROC
PROC_GLO_PartyMembers_RecruiteeAvatarBond((CHARACTERGUID)_Companion,(CHARACTERGUID)_Avatar)
AND
IsTagged(_Companion,"UNDEAD",1)
AND
DB_Tutorial_UndeadRecipe(_Recipe)
THEN
CharacterUnlockRecipe(_Companion,_Recipe,1);
//END_REGION

//REGION Actual unlocking of recipes
PROC
PROC_TUT_WeaponRecipes_UnlockIfBasicWeapon((ITEMGUID)_Item,(CHARACTERGUID)_Player,(INTEGER)_StartWeapon)
AND
DB_Negate(_StartWeapon,_PickedUpWeapon)
AND
GetStatString(_Item,_Stat)
AND
DB_Tutorial_WeaponRecipe(_Stat,_Recipe,_Arch)
THEN
CharacterUnlockRecipe(_Player,_Recipe,_PickedUpWeapon);
PROC_TUT_WeaponRecipes_UnlockExtraRecipesIfStartWeapon(_Player,_Stat,_StartWeapon);

PROC
PROC_TUT_WeaponRecipes_UnlockExtraRecipesIfStartWeapon((CHARACTERGUID)_Player,(STRING)_Stat,1)
AND
DB_Tutorial_WeaponRecipe(_Stat,_,_ArcheType)
AND
DB_Tutorial_ExtraRecipes(_ArcheType,_Recipe)
THEN
CharacterUnlockRecipe(_Player,_Recipe,0);
//END_REGION


// Now remove the initially equiped weapons
PROC
Proc_TUT_StartGame()
THEN
Proc_TUT_PlayerWeaponRecipesRecorded();
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Tutorial_PrisonShip"
