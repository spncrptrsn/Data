Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_ARX_TheEighth_Punishments("ARX_TheEighth_Punish_Deathfog","PERMANENTLY_CURSED");
DB_ARX_TheEighth_Punishments("ARX_TheEighth_Punish_LizardPrincess","MARKED");

DB_ARX_TheEighth((CHARACTERGUID)CHARACTERGUID_S_ARX_GodKing_InvisHelper_01_01f3e972-6a2d-4587-87c2-2c4c2e68b5e0);
DB_ARX_TheEighth(CHARACTERGUID_S_ARX_GodKing_InvisHelper_02_6d2bb33b-f9aa-432f-8765-f592672f967d);
DB_ARX_TheEighth(CHARACTERGUID_S_ARX_GodKing_InvisHelper_03_fc9f8d6f-02f3-4c20-b188-f8c3b09c1426);
DB_ARX_TheEighth(CHARACTERGUID_S_ARX_GodKing_InvisHelper_04_125bae7d-eaaf-4e0f-bbe3-a52aaf1f4a5a);

DB_ARX_KemmVault_GodKingHelper(ITEMGUID_S_ARX_KemmVault_GodKingHelper_01_195362e3-a5c7-418f-81ca-5048b01cc6f9);
DB_ARX_KemmVault_GodKingHelper(ITEMGUID_S_ARX_KemmVault_GodKingHelper_02_4c2a2218-7a26-4c19-a8da-10cd5505fb5d);
DB_ARX_KemmVault_GodKingHelper(ITEMGUID_S_ARX_KemmVault_GodKingHelper_03_9bf80598-456d-4dc0-9972-96ea309dcc4a);
DB_ARX_KemmVault_GodKingHelper(ITEMGUID_S_ARX_KemmVault_GodKingHelper_04_b4c90af7-8b48-4b86-bd5e-b3a56789cc4e);
KBSECTION
//REGION on init
IF
DB_ARX_TheEighth(_Char)
THEN
SetVisible(_Char,0);
ProcSetInvulnerable(_Char,1);
CharacterSetImmortal(_Char,1);
//END_REGION

//REGION status
IF
CharacterStatusRemoved(_Char,_Status,(CHARACTERGUID)_Causee)
AND
DB_ARX_TheEighth_Punishments(_Flag,_Status)
AND
ObjectGetFlag(_Char,_Flag,1)
AND
CharacterIsAlly(_Causee,_Char,1)
AND
NOT DB_DialogPlayers(_,_Char,_)
THEN
StartVoiceBark("ARX_VB_TheEighth_RemoveStatus",_Char);

IF
SavegameLoaded(_Major,_Minor,_Rev,_Build)
AND
QRY_VersionIsOlderThan(_Major,_Minor,_Rev,_Build, 3, 6, 0, 4)
AND
DB_JoinedParty(_Char)
AND
DB_ARX_TheEighth_Punishments(_Flag,_Status)
AND
ObjectGetFlag(_Char, _Flag, 1)
THEN
RemoveStatus(_Char, _Status);
ObjectClearFlag(_Char, _Flag, 0);
ObjectSetFlag(_Char, _Flag, 0); //Triggers the Game script that centralised permanent statuses of this nature..
//END_REGION

//REGION punishment dialog starters
IF
CharacterLeftTrigger(_Char,TRIGGERGUID_S_ARX_Sewers_QueensFloor_c2ac0f3b-cea2-4c74-a3bf-2b4e82a54b85)
AND
IsTagged(_Char,"PROMISED",1)
AND
ObjectGetFlag(_Char,"ARX_TheEighth_Sewers_TalkedToSpeaker",1)
AND
ObjectGetFlag(_Char,"ARX_TheEighth_Punish_Deathfog",0)
AND
GlobalGetFlag("ARX_InitiateGenocide",0)
AND
QRY_AnyRegionActive()
AND
DB_ARX_TheEighth(_Speaker)
AND
QRY_SpeakerIsAvailable(_Speaker)
THEN
TeleportTo(_Speaker,_Char,"",0);
Proc_StartDialog(0,"ARX_Sewers_TheEighth",_Speaker,_Char);


//END_REGION

//REGION sewers arx
PROC
ReactOnKillCounter("ARX_Sewers_QUnamsked")
AND
GlobalGetFlag("ARX_InitiateGenocide",0)
AND
DB_InRegion(_Char,TRIGGERGUID_S_ARX_Sewers_QueensFloor_c2ac0f3b-cea2-4c74-a3bf-2b4e82a54b85)
AND
IsTagged(_Char,"PROMISED",1)
THEN
Proc_ARX_TheEighth_InitiateDialog(_Char,"ARX_Sewers_TheEighth");

IF
DialogEnded("ARX_Sewers_TheEighth",_)
AND
DB_ARX_Sewers_QUnmasked_PromisedInteraction(_Avatar)
AND
DB_CompanionAvatarBond(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,_Avatar)
THEN
Proc_RelationshipDialog(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"ARX_Sewers_TheEighth_ARD_Ifan",_Avatar);

IF
ObjectFlagSet("ARX_Sewers_TheEighth_Pain",(CHARACTERGUID)_Char,_)
THEN
CharacterMakeStoryNpc(_Char,0);
PlayEffect(_Char,"RS3_FX_GP_ScriptedEvent_MakePromise_End_01");
CharacterSetHitpointsPercentage(_Char,10.0);
CharacterSetAnimationOverride(_Char,"stillcrippled");
CharacterMakeStoryNpc(_Char,1);

//END_REGION

//REGION kemm vault
IF
CharacterEnteredTrigger(_Char,TRIGGERGUID_S_ARX_KemmVault_SecondFloor_EighthInteraction_b1555297-83e9-4fb0-bb39-e3894bcd18d6)
AND
QueryOnlyOnce("ARX_KemmVault_SecondFloor_EighthInteraction")
THEN
Proc_ARX_KemmVault_TheEighth_CheckPromised(_Char);

PROC
Proc_ARX_KemmVault_TheEighth_CheckPromised((CHARACTERGUID)_)
AND
DB_InRegion(_Char,TRIGGERGUID_S_ARX_KemmVault_SecondFloor_c3c87da9-f9ae-4954-9402-6906b1c23d86)
AND
IsTagged(_Char,"PROMISED",1)
AND
IsTagged(_Char,"AVATAR",1)
THEN
Proc_ARX_TheEighth_InitiateDialog(_Char,"ARX_KemmVault_TheEighth");
DB_ARX_KemmVault_TheEighth_DoDialog(_Char);

PROC
Proc_ARX_KemmVault_TheEighth_CheckPromised((CHARACTERGUID)_)
AND
DB_InRegion(_Char,TRIGGERGUID_S_ARX_KemmVault_SecondFloor_c3c87da9-f9ae-4954-9402-6906b1c23d86)
AND
IsTagged(_Char,"PROMISED",1)
AND
NOT DB_ARX_KemmVault_TheEighth_DoDialog(_Char)
THEN
Proc_ARX_TheEighth_InitiateDialog(_Char,"ARX_KemmVault_TheEighth");
DB_ARX_KemmVault_TheEighth_DoDialog(_Char);

PROC
Proc_ARX_KemmVault_TheEighth_CheckPromised((CHARACTERGUID)_OtherChar)
AND
DB_InRegion(_Char,TRIGGERGUID_S_ARX_KemmVault_SecondFloor_c3c87da9-f9ae-4954-9402-6906b1c23d86)
AND
IsTagged(_Char,"PROMISED",1)
AND
DB_ARX_KemmVault_TheEighth_DoDialog(_Char)
AND
CharacterGetReservedUserID(_Char,_Id)
AND
NOT CharacterGetReservedUserID(_OtherChar,_Id)
THEN
Proc_ARX_TheEighth_InitiateDialog(_Char,"ARX_KemmVault_TheEighth");
DB_ARX_KemmVault_TheEighth_DoDialog(_Char);

PROC
Proc_ARX_KemmVault_TheEighth_CheckPromised((CHARACTERGUID)_Char)
AND
NOT DB_ARX_KemmVault_TheEighth_DoDialog(_)
THEN
Proc_ARX_TheEighth_InitiateDialog(_Char,"ARX_KemmVault_TheEighth");

//END_REGION


//REGION dialog start logic
PROC
Proc_ARX_TheEighth_InitiateDialog((CHARACTERGUID)_Player,"ARX_Sewers_TheEighth")
AND
DB_ARX_TheEighth(_Char)
AND
QRY_SpeakerIsAvailable(_Char)
AND
NOT DB_ARX_TheEighth_InDialog(_,_,_Player)
THEN
TeleportTo(_Char,_Player,"",0);
Proc_StartDialog(0,"ARX_Sewers_TheEighth",_Char,_Player);
DB_ARX_TheEighth_InDialog("ARX_Sewers_TheEighth",_Char,_Player);
CharacterSetAnimationOverride(_Player,"cower");

PROC
Proc_ARX_TheEighth_InitiateDialog((CHARACTERGUID)_Player,"ARX_KemmVault_TheEighth")
AND
DB_ARX_TheEighth(_Char)
AND
QRY_SpeakerIsAvailable(_Char)
AND
DB_ARX_KemmVault_GodKingHelper(_Helper)
AND
QRY_SpeakerIsAvailable(_Helper)
AND
NOT DB_ARX_TheEighth_InDialog(_,_,_Player)
THEN
TeleportTo(_Char,_Player,"",0);
Proc_StartDialog(0,"ARX_KemmVault_TheEighth",_Char,_Player,_Helper);
DB_ARX_TheEighth_InDialog("ARX_KemmVault_TheEighth",_Char,_Player);
CharacterSetAnimationOverride(_Player,"cower");

IF
DialogStarted(_Dialog,_Id)
AND
DB_ARX_TheEighth_InDialog(_Dialog,_,_Player)
AND
DB_DialogPlayers(_Id,_Player,_)
THEN
DB_ARX_TheEighth_InDialog_Id(_Player,_Id);

PROC
Proc_ARX_TheEighth_InitiateDialog((CHARACTERGUID)_Player,"ARX_Sewers_TheEighth")
AND
DB_ARX_TheEighth_InDialog(_,_,_Player)
THEN
DB_ARX_Sewers_QUnmasked_PromisedInteraction(_Player);

IF
DialogEnded(_,_Id)
AND
DB_ARX_TheEighth_InDialog_Id(_Player,_Id)
AND
DB_ARX_TheEighth_InDialog(_Dialog,_Char,_Player)
THEN
CharacterSetAnimationOverride(_Player,"");
NOT DB_ARX_TheEighth_InDialog(_Dialog,_Char,_Player);
NOT DB_ARX_TheEighth_InDialog_Id(_Player,_Id);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ARX"
