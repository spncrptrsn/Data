Version 1
SubGoalCombiner SGC_AND
INITSECTION
CharacterSetAnimationSetOverride(CHARACTERGUID_S_RC_DW_UnderTavern_Bartender_cb016e2c-2e96-4dd7-bf78-aa1fdefb5424,"StandOnStool");
ItemToInventory(S_RC_DW_UT_Effies_Notes_ca656244-9d55-47c2-b627-d95437134e19,CHARACTERGUID_S_RC_DW_UnderTavern_Bartender_cb016e2c-2e96-4dd7-bf78-aa1fdefb5424);

DB_RC_DW_Herbmix("RC_DW_UT_HerbmixSelected_Alertness","Quest_Herbmix_Alertness_ff4adb8c-c79c-4976-9a3a-cef0d901ccee","HERBMIX_ALERTNESS");
DB_RC_DW_Herbmix("RC_DW_UT_HerbmixSelected_Courage","Quest_Herbmix_Courage_2a160ee8-3abd-4e28-a533-2efcaefd0b53","HERBMIX_COURAGE");
DB_RC_DW_Herbmix("RC_DW_UT_HerbmixSelected_Ferocity","Quest_Herbmix_Ferocity_ff74d2bf-8a91-452f-9ea8-f6756bb0d700","HERBMIX_FEROCITY");
DB_RC_DW_Herbmix("RC_DW_UT_HerbmixSelected_Enlightenment","Quest_Herbmix_Enlightenment_ff07e9ef-d489-4eb8-af72-548b183f87bc","HERBMIX_ENLIGHTENMENT");

DB_RC_DW_Herbmix_Potent("Quest_Herbmix_Alertness_Potent_656ef42a-58ab-4705-8b71-c37a60d7205a","HERBMIX_ALERTNESS");
DB_RC_DW_Herbmix_Potent("Quest_Herbmix_Enlightenment_Potent_e87fdd7d-6e23-4095-8aa1-b0f8d447cead","HERBMIX_ENLIGHTENMENT");
DB_RC_DW_Herbmix_Potent("Quest_Herbmix_Ferocity_Potent_e241285b-c449-43f1-9048-898cdb9372ca","HERBMIX_FEROCITY");

DB_DialogMoneyTransfer(1,"RC_DW_UnderTavern_Bartender",150); //Price for a regular herbmix
KBSECTION
//REGION Create HerbMixes
IF
ObjectFlagSet(_Flagname,_Player,_ID)
AND
DB_RC_DW_Herbmix(_Flagname,_HerbMix,_)
AND
DB_RC_DW_Herbmix_Selected(_Player,_HerbMix2)
THEN
NOT DB_RC_DW_Herbmix_Selected(_Player,_HerbMix2);

IF
ObjectFlagSet(_Flagname,_Player,_ID)
AND
DB_RC_DW_Herbmix(_Flagname,_HerbMix,_)
THEN
ObjectClearFlag(_Player,_Flagname,_ID);
DB_RC_DW_Herbmix_Selected(_Player,_HerbMix);

IF
ObjectFlagSet("RC_DW_UnderTavern_RegularHerbMix_Received",_Player,_ID)
AND
DB_RC_DW_Herbmix_Selected(_Player,_HerbMix)
AND
DB_DialogMoneyTransfer(1,"RC_DW_UnderTavern_Bartender",_Price)
AND
IntegerSubtract(0,_Price,_RemoveGold)
THEN
NOT DB_RC_DW_Herbmix_Selected(_Player,_HerbMix);
ProcAddGoldToMagicPockets((CHARACTERGUID)_Player,_RemoveGold);
CharacterAddGold(CHARACTERGUID_S_RC_DW_UnderTavern_Bartender_cb016e2c-2e96-4dd7-bf78-aa1fdefb5424,_Price);
ItemTemplateAddTo(_HerbMix,_Player,1);

IF
ObjectFlagSet("RC_DW_UnderTavern_RegularHerbMix_Received",(CHARACTERGUID)_Player,_ID)
THEN
ObjectClearFlag(_Player,"RC_DW_UnderTavern_RegularHerbMix_Received",_ID);
PartySetFlag(_Player,"RC_DW_UnderTavern_RegularHerbMix_Received_ShowMarker");

IF
ObjectFlagSet("RC_DW_UnderTavern_RegularHerbMix_Received_ShowMarker",(CHARACTERGUID)_Player,_)
THEN
ProcShowMarker(_Player,"RC_DW_Undertavern_DreamerPipe");

//END_REGION

//REGION Using the Pipe
IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_DW_UnderTavern_DreamPipe_48858acc-7d5a-47ff-97b0-3c8312e08967)
THEN
ObjectClearFlag(_Player,"RC_DW_UT_HasDrudanae",0);
ObjectClearFlag(_Player,"RC_DW_UT_HasHerbmix",0);
ObjectClearFlag(_Player,"RC_DW_UT_EffieAtDesk",0);

IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_DW_UnderTavern_DreamPipe_48858acc-7d5a-47ff-97b0-3c8312e08967)
AND
CharacterIsDead(CHARACTERGUID_S_RC_DW_UnderTavern_Bartender_cb016e2c-2e96-4dd7-bf78-aa1fdefb5424,0)
AND
GetDistanceTo(CHARACTERGUID_S_RC_DW_UnderTavern_Bartender_cb016e2c-2e96-4dd7-bf78-aa1fdefb5424,ITEMGUID_S_RC_DW_UnderTavern_DreamPipe_48858acc-7d5a-47ff-97b0-3c8312e08967,_Dist)
AND
_Dist < 5.0
THEN
ObjectSetFlag(_Player,"RC_DW_UT_EffieAtDesk");

IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_DW_UnderTavern_DreamPipe_48858acc-7d5a-47ff-97b0-3c8312e08967)
AND
DB_DrudenaeTemplates(_DrudanaeTemplate)
AND
QryItemTemplateInMagicPockets(_Player,_DrudanaeTemplate)
THEN
ObjectSetFlag(_Player,"RC_DW_UT_HasDrudanae");

IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_DW_UnderTavern_DreamPipe_48858acc-7d5a-47ff-97b0-3c8312e08967)
AND
DB_RC_DW_Herbmix(_,_HerbmixTemplate,_)
AND
QryItemTemplateInMagicPockets(_Player,_HerbmixTemplate)
THEN
ObjectSetFlag(_Player,"RC_DW_UT_HasHerbmix");

IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_DW_UnderTavern_DreamPipe_48858acc-7d5a-47ff-97b0-3c8312e08967)
AND
DB_RC_DW_Herbmix_Potent(_HerbmixTemplate,_)
AND
QryItemTemplateInMagicPockets(_Player,_HerbmixTemplate)
THEN
ObjectSetFlag(_Player,"RC_DW_UT_HasHerbmix");

IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_DW_UnderTavern_DreamPipe_48858acc-7d5a-47ff-97b0-3c8312e08967)
THEN
Proc_StartDialog(0,"RC_DW_UnderTavern_DreamerPipe",_Player);

IF
ObjectFlagSet("RC_DW_UT_DreampipeIntroduction",_Player,_)
THEN
DB_RC_DW_DreamerHole_PlayerWantsDreamerPipeIntro(_Player);
ObjectClearFlag(_Player,"RC_DW_UT_DreampipeIntroduction",0);

IF
DialogEnded("RC_DW_UnderTavern_DreamerPipe",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,1)
AND
DB_RC_DW_DreamerHole_PlayerWantsDreamerPipeIntro(_Player)
AND
QRY_SpeakerIsAvailable(CHARACTERGUID_S_RC_DW_UnderTavern_Bartender_cb016e2c-2e96-4dd7-bf78-aa1fdefb5424)
AND
GetDistanceTo(_Player,CHARACTERGUID_S_RC_DW_UnderTavern_Bartender_cb016e2c-2e96-4dd7-bf78-aa1fdefb5424,_Dist)
AND
_Dist < 10.0
THEN
NOT DB_RC_DW_DreamerHole_PlayerWantsDreamerPipeIntro(_Player);
Proc_StartDialog(0,"RC_DW_UnderTavern_DreamerPipe_EffieIntro",CHARACTERGUID_S_RC_DW_UnderTavern_Bartender_cb016e2c-2e96-4dd7-bf78-aa1fdefb5424,_Player);
CharacterMoveTo(CHARACTERGUID_S_RC_DW_UnderTavern_Bartender_cb016e2c-2e96-4dd7-bf78-aa1fdefb5424,TRIGGERGUID_S_RC_DW_UT_Effie_Intro_f49ad071-2bb1-4643-9889-0be79b29e868,0,"RC_DW_UT_EffieAtIntroPoint",0);

IF
ObjectFlagSet("RC_DW_UT_OpenCraft",_Player,_)
THEN
DB_RC_DW_DreamerHole_OpenCraft(_Player);
ObjectClearFlag(_Player,"RC_DW_UT_OpenCraft",0);

IF
DialogEnded("RC_DW_UnderTavern_DreamerPipe",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,1)
AND
DB_RC_DW_DreamerHole_OpenCraft(_Player)
THEN
NOT DB_RC_DW_DreamerHole_OpenCraft(_Player);
OpenCraftUI((CHARACTERGUID)_Player,ITEMGUID_S_RC_DW_UnderTavern_DreamPipe_48858acc-7d5a-47ff-97b0-3c8312e08967);

IF
StoryEvent(CHARACTERGUID_S_RC_DW_UnderTavern_Bartender_cb016e2c-2e96-4dd7-bf78-aa1fdefb5424,"RC_DW_UT_EffieAtIntroPoint")
THEN
CharacterLookFromTrigger(CHARACTERGUID_S_RC_DW_UnderTavern_Bartender_cb016e2c-2e96-4dd7-bf78-aa1fdefb5424,TRIGGERGUID_S_RC_DW_UT_Effie_Intro_f49ad071-2bb1-4643-9889-0be79b29e868,0);

IF
DialogEnded("RC_DW_UnderTavern_DreamerPipe",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,1)
AND
DB_RC_DW_DreamerHole_PlayerWantsDreamerPipeIntro(_Player)
THEN
NOT DB_RC_DW_DreamerHole_PlayerWantsDreamerPipeIntro(_Player);
Proc_StartDialog(0,"RC_DW_UnderTavern_DreamerPipe_EffieBusy",_Player);

IF
ObjectFlagSet("RC_DW_UT_SmokeDrudanae",_Player,_)
AND
DB_DrudenaeTemplates(_DrudanaeTemplate)
AND
NOT DB_RC_DW_DreamerHole_FoundASmoke(1)
AND
QryRemoveItemTemplateFromMagicPockets((CHARACTERGUID)_Player,_DrudanaeTemplate,1)
AND
CharacterConsume(_Player,"CON_Herb_Drudanae",_)
THEN
DB_RC_DW_DreamerHole_FoundASmoke(1);
ObjectClearFlag(_Player,"RC_DW_UT_SmokeDrudanae",0);

IF
ObjectFlagSet("RC_DW_UT_SmokeDrudanae",_Player,_)
THEN
NOT DB_RC_DW_DreamerHole_FoundASmoke(1);

//END_REGION


//REGION Using HerbMixes
IF
ItemTemplateCombinedWithItemTemplate(_DrudanaeTemplate,"Quest_DreamerPipe_b43347a7-a763-4791-92e8-0fb623552ade",_,_,_,_Character,_)
AND
DB_DrudenaeTemplates(_DrudanaeTemplate)
AND
CharacterConsume(_Character,"CON_Herb_Drudanae",_)
THEN
DB_NOOP(1);

IF
ItemTemplateCombinedWithItemTemplate(_HerbmixTemplate,"Quest_DreamerPipe_b43347a7-a763-4791-92e8-0fb623552ade",_,_,_,_Character,_)
AND
DB_RC_DW_Herbmix(_,_HerbmixTemplate,_Effect)
THEN
PROC_Herbmix_Effect(_Character,_Effect,1.0);

IF
ItemTemplateCombinedWithItemTemplate(_HerbmixTemplate,"Quest_DreamerPipe_b43347a7-a763-4791-92e8-0fb623552ade",_,_,_,_Character,_)
AND
DB_RC_DW_Herbmix_Potent(_HerbmixTemplate,_Effect)
THEN
PROC_Herbmix_Effect(_Character,_Effect,2.0);

//Regular herbmixes
PROC
PROC_Herbmix_Effect((CHARACTERGUID)_Character,(STRING)_Herbmix,(REAL)_PotentModifier)
AND
RealProduct(300.0,_PotentModifier,_Duration)
THEN
ApplyStatus(_Character,_Herbmix,_Duration);
CloseUI(_Character,"Crafting");
CharacterLookAt(_Character,ITEMGUID_S_RC_DW_UnderTavern_DreamPipe_48858acc-7d5a-47ff-97b0-3c8312e08967);
PlayAnimation(_Character,"Fidget_Forward_01");
Proc_StartDialog(1,"RC_DW_AD_DreamerHole_SmokedRegular",_Character);
//END_REGION

//REGION Debuffing ArenaFighters
IF
DialogStarted("RC_DW_UnderTavern_Bartender",_)
AND
DB_GlobalFlag("Arena_Driftwood_TrialInitiated")
AND
DB_GlobalFlag("RC_DW_Murga_IsDead")
THEN
GlobalSetFlag("RC_DW_UT_DreamerHole_DebuffBlocked");

IF
DialogStarted("RC_DW_UnderTavern_Bartender",_)
AND
DB_GlobalFlag("Arena_Driftwood_TrialInitiated")
AND
DB_GlobalFlag("Arena_Driftwood_ChampionInitiated")
THEN
GlobalSetFlag("RC_DW_UT_DreamerHole_DebuffBlocked");
//END_REGION

//REGION Effies Recipe Book
IF
GameBookInterfaceClosed(S_RC_DW_UT_Effies_Notes_ca656244-9d55-47c2-b627-d95437134e19,_Player)
THEN
CharacterUnlockRecipe(_Player,"CON_Herb_Drudanae_CON_Herb_Farhangite_A",1);
CharacterUnlockRecipe(_Player,"CON_Herb_Drudanae_CON_Herb_Augmentor_A",0);
CharacterUnlockRecipe(_Player,"CON_Herb_Drudanae_CON_Nature_Mushroom_Puffball_A",0);
CharacterUnlockRecipe(_Player,"CON_Herb_Drudanae_CON_Nature_Mushroom_Calocera_A",0);
CharacterUnlockRecipe(_Player,"QUEST_Herbmix_Alertness_FOOD_FishA_Voidwoken",0);
CharacterUnlockRecipe(_Player,"QUEST_Herbmix_Ferocity_FOOD_FishB_Voidwoken",0);
CharacterUnlockRecipe(_Player,"QUEST_Herbmix_Enlightenment_FOOD_FishC_Voidwoken",0);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
