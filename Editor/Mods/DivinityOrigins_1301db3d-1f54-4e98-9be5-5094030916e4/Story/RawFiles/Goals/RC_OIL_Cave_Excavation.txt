Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_RC_OIL_CaveEvents_CombatMagister((CHARACTERGUID)CHARACTERGUID_S_RC_OIL_Cave_Magister_01_3421edf4-9183-4c04-984c-1a92a1edd00c);
DB_RC_OIL_CaveEvents_CombatMagister(CHARACTERGUID_S_RC_OIL_Cave_Magister_02_930f1222-445f-457d-8bfb-d01b91ba6b24);
DB_RC_OIL_CaveEvents_CombatMagister(CHARACTERGUID_S_RC_OIL_Cave_Magister_03_e731333c-c1f8-4704-9684-e7047d522085);
DB_RC_OIL_CaveEvents_CombatMagister(CHARACTERGUID_S_RC_OIL_Cave_MagisterCombatLeader_e0c0e47e-84d1-4e71-8da4-9bae87c1f553);

DB_Dialogs(CHARACTERGUID_S_RC_OIL_Cave_Magister_01_3421edf4-9183-4c04-984c-1a92a1edd00c,"RC_OIL_Cave_SilentMonk1");
DB_Dialogs(CHARACTERGUID_S_RC_OIL_Cave_Magister_02_930f1222-445f-457d-8bfb-d01b91ba6b24,"RC_OIL_Cave_SilentMonk2");
DB_Dialogs(CHARACTERGUID_S_RC_OIL_Cave_Magister_03_e731333c-c1f8-4704-9684-e7047d522085,"RC_OIL_Cave_SilentMonk3");
DB_Dialogs(CHARACTERGUID_S_RC_OIL_Cave_MagisterCombatLeader_e0c0e47e-84d1-4e71-8da4-9bae87c1f553,"RC_OIL_CaveCombatDialog");
DB_Ghosts(CHARACTERGUID_S_RC_OIL_Cave_MagisterCombatLeader_e0c0e47e-84d1-4e71-8da4-9bae87c1f553,CHARACTERGUID_S_RC_OIL_Cave_MagisterCombatLeader_Ghost_b33135a7-6a8f-4530-b937-07f4fdab55cf,"RC_OIL_Cave_ExcavationGhost");
DB_Ghosts(CHARACTERGUID_S_RC_OIL_ExcavationBlackRing_9f001767-e126-4623-bc18-1ed2d6c8b98a,"RC_OIL_ExcavationBlackRing");

DB_RC_OIL_CaveEvents_CombatNPC((CHARACTERGUID)CHARACTERGUID_S_RC_OIL_Cave_Magister_01_3421edf4-9183-4c04-984c-1a92a1edd00c);
DB_RC_OIL_CaveEvents_CombatNPC(CHARACTERGUID_S_RC_OIL_Cave_Magister_02_930f1222-445f-457d-8bfb-d01b91ba6b24);
DB_RC_OIL_CaveEvents_CombatNPC(CHARACTERGUID_S_RC_OIL_Cave_Magister_03_e731333c-c1f8-4704-9684-e7047d522085);
DB_RC_OIL_CaveEvents_CombatNPC(CHARACTERGUID_S_RC_OIL_Cave_MagisterCombatLeader_e0c0e47e-84d1-4e71-8da4-9bae87c1f553);
DB_RC_OIL_CaveEvents_CombatNPC(CHARACTERGUID_S_RC_OIL_Cave_MagisterFight_Spitter_01_f52018a8-6bb3-4253-9e99-d6c9214d4bfb);
DB_RC_OIL_CaveEvents_CombatNPC(CHARACTERGUID_S_RC_OIL_Cave_MagisterFight_Spitter_02_9e94bad2-1e19-48f9-8bf1-47006fd69228);
DB_RC_OIL_CaveEvents_CombatNPC(CHARACTERGUID_S_RC_OIL_Cave_MagisterFight_Spitter_03_f2794f9b-de1e-44aa-885b-64d828b42831);
DB_RC_OIL_CaveEvents_CombatNPC(CHARACTERGUID_S_RC_OIL_Cave_MagisterFight_Spitter_04_c4b282a3-ca84-4bbb-ae9b-017029388e2c);
DB_RC_OIL_CaveEvents_CombatNPC(CHARACTERGUID_S_RC_OIL_Cave_MagisterFight_VampireBat1_f9e3de44-8833-4caa-9973-f2dbbce1f7ec);
DB_RC_OIL_CaveEvents_CombatNPC(CHARACTERGUID_S_RC_OIL_Cave_MagisterFight_VampireBat2_5e45efb6-88a2-4e5b-bb41-3a7a9026a513);

SetOnStage(CHARACTERGUID_S_RC_OIL_Cave_MagisterFight_Spitter_02_9e94bad2-1e19-48f9-8bf1-47006fd69228,0);
SetOnStage(CHARACTERGUID_S_RC_OIL_Cave_MagisterFight_Spitter_03_f2794f9b-de1e-44aa-885b-64d828b42831,0);

DB_Dialogs(ITEMGUID_S_RC_OIL_Cave_EternalsBook1_d435c3a1-81ee-4e25-a52f-fa4df82612a6,"RC_OIL_Cave_EternalsBook1");
DB_Dialogs(ITEMGUID_S_RC_OIL_Cave_EternalsBook2_8966d247-d2a7-48da-a67c-6d0da2353d25,"RC_OIL_Cave_EternalsBook2");

DB_RC_OIL_Cave_EternalsVisions("RC_OIL_Cave_EternalsBook1");
DB_RC_OIL_Cave_EternalsVisions("RC_OIL_Cave_EternalsBook2");

DB_RC_OIL_EternalsArtefacts((ITEMGUID)ITEMGUID_S_RC_OIL_EternalArtefact1_d84d7c8c-821d-4290-9252-41dcd2912fc5,"RC_OIL_EternalArtefact1","RC_OIL_Cave_Recipe1");
DB_RC_OIL_EternalsArtefacts(ITEMGUID_S_RC_OIL_EternalArtefact2_e54b723b-1ede-40a1-9ea4-e8fb3a9cbc2a,"RC_OIL_EternalArtefact2","RC_OIL_Cave_Recipe2");

DB_OneShot_VoiceBarkTrigger(TRIGGERGUID_S_RC_OIL_VB_EternalStone_1_805a5169-1519-46e4-bd57-0bb29d7a3f84,"RC_OIL_VB_EtenalStone1");
DB_OneShot_VoiceBarkTrigger(TRIGGERGUID_S_RC_OIL_VB_EternalStone_2_4a2cecb8-0916-40b6-9b5d-fc8e9d1318ef,"RC_OIL_VB_EtenalStone2");


DB_RC_OIL_EternalsRecipes("RC_OIL_Cave_Recipe1","ARM_UniversalShapeshiftingMask","RC_OIL_VB_UnlockedRecipe1");
DB_RC_OIL_EternalsRecipes("RC_OIL_Cave_Recipe2","Quest_Eternals_Armor","RC_OIL_VB_UnlockedRecipe2");

DB_Dialogs(ITEMGUID_S_RC_OIL_Cave_RuinLamp_3dffefeb-0f1b-4179-a9c6-055f00e711d5,"RC_OIL_EternalsTechLamp");
DB_Dialogs(ITEMGUID_S_RC_OIL_Cave_Pedestal_a6b1c097-ad46-40aa-8dc2-7cda20b27602,"RC_OIL_AeteranPedestal");
PROC_LoopEffect("RS3_FX_GP_ScriptedEvent_ChanelledSource_01",TRIGGERGUID_S_RC_OIL_Excavation_PedestalFX_67b8b961-faa9-4e75-97e4-eec623379bb4,"RC_OIL_PedestalVFX","RC_Main","");
PROC_LoopEffect("RS3_FX_GP_ScriptedEvent_HoECharacterGlow_01",TRIGGERGUID_S_RC_OIL_Excavation_PedestalFX_2_402729e0-5842-492c-874c-d8382ca0857f,"RC_OIL_PedestalVFX","RC_Main","");
KBSECTION
//REGION Combat

IF
DB_RC_OIL_CaveEvents_CombatNPC(_NPC)
THEN
ProcSetInvulnerable(_NPC,1);

IF
CharacterSawCharacter(_Player,_NPC)
AND
DB_IsPlayer(_Player)
AND
DB_RC_OIL_CaveEvents_CombatNPC(_NPC)
AND
QueryOnlyOnce("RC_OIL_CaveCombatStart")
THEN
Proc_RC_OIL_CaveEvents_SetVulnerable();
StartVoiceBark("RC_OIL_VB_CaveCombatHelpOut",_Player);

PROC
Proc_RC_OIL_CaveEvents_SetVulnerable()
AND
DB_RC_OIL_CaveEvents_CombatNPC(_NPC)
THEN
ProcSetInvulnerable(_NPC,0);
NOT DB_RC_OIL_CaveEvents_CombatNPC(_NPC);

IF
DialogEnded("RC_OIL_CaveCombatDialog",_)
AND
GlobalGetFlag("RC_OIL_Cave_ExcavationHostile",1)
THEN
Proc_RC_OIL_CaveEvents_MakeMagistersHostile();

PROC
Proc_RC_OIL_CaveEvents_SetVulnerable()
AND
QueryOnlyOnce("RC_OIL_CaveCombatSpawnBombers")
THEN
CharacterAppear(RC_OIL_Cave_MagisterFight_Spitter_02_9e94bad2-1e19-48f9-8bf1-47006fd69228,1,"");
CharacterAppear(RC_OIL_Cave_MagisterFight_Spitter_03_f2794f9b-de1e-44aa-885b-64d828b42831,1,"");

PROC
Proc_RC_OIL_CaveEvents_MakeMagistersHostile()
THEN
CharacterSetRelationFactionToFaction("RC_OIL_Cave_Excavation","Hero",0);
CharacterSetRelationFactionToFaction("Hero","RC_OIL_Cave_Excavation",0);

IF
CharacterDied(S_RC_OIL_Cave_MagisterCombatLeader_e0c0e47e-84d1-4e71-8da4-9bae87c1f553)
THEN
Proc_RC_OIL_CaveEvents_MakeMagistersHostile();

PROC
Proc_ObjectLeftCombat(_NPC,_Combat)
AND
DB_RC_OIL_CaveEvents_CombatMagister((CHARACTERGUID)_NPC)
AND
DB_WasInCombat(_Player,_Combat)
THEN
ObjectSetFlag(_Player,"RC_OIL_Cave_CombatBonus",0);

PROC
Proc_ObjectLeftCombat(CHARACTERGUID_S_RC_OIL_Cave_MagisterCombatLeader_e0c0e47e-84d1-4e71-8da4-9bae87c1f553,_)
THEN
ObjectSetFlag(CHARACTERGUID_S_RC_OIL_Cave_MagisterCombatLeader_e0c0e47e-84d1-4e71-8da4-9bae87c1f553,"RC_OIL_Cave_ExcavationRestartSpot");

IF
StoryEvent((CHARACTERGUID)_Player,"RC_OIL_Cave_ExcavationSpot")
AND
QRY_IsInRange(_Player,CHARACTERGUID_S_RC_OIL_Cave_MagisterCombatLeader_e0c0e47e-84d1-4e71-8da4-9bae87c1f553,10.0)
THEN
Proc_StartDialog(0,"RC_OIL_CaveCombatDialog",CHARACTERGUID_S_RC_OIL_Cave_MagisterCombatLeader_e0c0e47e-84d1-4e71-8da4-9bae87c1f553,_Player);

IF
StoryEvent((CHARACTERGUID)_Player,"RC_OIL_Cave_ExcavationSpot")
AND
NOT QRY_IsInRange(_Player,CHARACTERGUID_S_RC_OIL_Cave_MagisterCombatLeader_e0c0e47e-84d1-4e71-8da4-9bae87c1f553,10.0)
AND
GetPosition(_Player,_X,_Y,_Z)
AND
RealSubtract(_Y,2.0,_NewY)
THEN
CharacterUseSkillAtPosition(CHARACTERGUID_S_RC_OIL_Cave_MagisterCombatLeader_e0c0e47e-84d1-4e71-8da4-9bae87c1f553,"Jump_EnemyPhoenixDive",_X,_NewY,_Z,1,1);
ProcFaceEachother(CHARACTERGUID_S_RC_OIL_Cave_MagisterCombatLeader_e0c0e47e-84d1-4e71-8da4-9bae87c1f553,_Player);
Proc_StartDialog(0,"RC_OIL_CaveCombatDialog",CHARACTERGUID_S_RC_OIL_Cave_MagisterCombatLeader_e0c0e47e-84d1-4e71-8da4-9bae87c1f553,_Player);

//END_REGION

//REGION Excavation

IF
DialogEnded("RC_OIL_AeteranPedestal",_ID)
AND
DB_DialogPlayers(_ID,_Player,_)
AND
QueryOnlyOnce("RC_OIL_VB_StaffOfDivinityPedestal")
THEN
StartVoiceBark("RC_OIL_VB_StaffOfDivinityPedestal_Generic",(CHARACTERGUID)_Player);

IF
DialogEnded(_Dialog,_ID)
AND
DB_RC_OIL_Cave_EternalsVisions(_Dialog)
AND
DB_DialogPlayers(_ID,_Player,1)
AND
UserGetFlag((CHARACTERGUID)_Player,"RC_OIL_Cave_EternalsCulture",0)
THEN
UserSetFlag(_Player,"RC_OIL_Cave_EternalsCulture");
Proc_StartDialog(1,"RC_OIL_VB_EternalsCulture",_Player);

IF
GameBookInterfaceClosed(ITEMGUID_S_RC_OIL_Cave_ExcavationJournal_8f8c8efc-566f-4268-954c-a6d13b533e2e,_Player)
AND
UserGetFlag(_Player,"RC_OIL_Cave_StaffOfDivinity",0)
THEN
UserSetFlag(_Player,"RC_OIL_Cave_StaffOfDivinity");
Proc_StartDialog(1,"RC_OIL_VB_StaffOfDivinity",_Player);

IF
DialogEnded("RC_OIL_ExcavationBlackRing",_ID)
AND
DB_DialogPlayers(_ID,_Player,1)
AND
UserGetFlag((CHARACTERGUID)_Player,"RC_OIL_Cave_StaffOfDivinity",0)
THEN
UserSetFlag(_Player,"RC_OIL_Cave_StaffOfDivinity");
Proc_StartDialog(1,"RC_OIL_VB_StaffOfDivinity",_Player);

//END_REGION

//REGION 

IF
CharacterUsedItem(_,_Artefact)
AND
DB_RC_OIL_EternalsArtefacts(_Artefact,_,_)
AND
String(_Artefact,_Str)
AND
StringConcatenate("RC_OIL_Cave_EternalPiece",_Str,_Flag)
AND
QueryOnlyOnce(_Flag)
THEN
PROC_LoopEffect("RS3_FX_GP_ScriptedEvent_SourcePillar_01",_Artefact,"RC_OIL_Cave_EternalArtefact","RC_Main","");

IF
DB_RC_OIL_EternalsArtefacts(_Artifact,_Dialog,_)
THEN
DB_Dialogs(_Artifact,_Dialog);

IF
DialogStarted(_Dialog,_ID)
AND
DB_RC_OIL_EternalsArtefacts(_,_Dialog,_)
AND
DB_DialogPlayers(_ID,_Player,_)
AND
CharacterGetSourcePoints((CHARACTERGUID)_Player,_Amount)
AND
_Amount > 0
THEN
ObjectSetFlag(_Player,"RC_OIL_CanUseEternalStone");

IF
DialogEnded(_Dialog,_ID)
AND
DB_RC_OIL_EternalsArtefacts(_,_Dialog,_)
AND
DB_DialogPlayers(_ID,_Player,_)
THEN
ObjectClearFlag(_Player,"RC_OIL_CanUseEternalStone");

IF
DialogEnded(_Dialog,_ID)
AND
DB_RC_OIL_EternalsArtefacts(_,_Dialog,_Recipe)
AND
DB_DialogPlayers(_ID,_Player,_)
AND
ObjectGetFlag(_Player,"RC_OIL_EternalStone_CantUse",1)
AND
ObjectGetFlag(_Player,_Recipe,0)
THEN
ObjectClearFlag(_Player,"RC_OIL_EternalStone_CantUse");
StartVoiceBark("RC_OIL_VB_RuinPillar",(CHARACTERGUID)_Player);

IF
ObjectFlagSet(_RecipeFlag,(CHARACTERGUID)_Player,_)
AND
DB_RC_OIL_EternalsRecipes(_RecipeFlag,_Recipe,_)
THEN
CharacterUnlockRecipe(_Player,_Recipe,1);

IF
DialogEnded(_Dialog,_ID)
AND
DB_RC_OIL_EternalsArtefacts(_,_Dialog,_Flag)
AND
DB_DialogPlayers(_ID,_Player,_)
AND
ObjectGetFlag(_Player,"RC_OIL_EternalStone_CantUse",0)
AND
ObjectGetFlag(_Player,_Flag,1)
AND
DB_RC_OIL_EternalsRecipes(_Flag,_,_VB)
AND
NOT DB_RC_OIL_EternalRecipeCommented(_Flag)
THEN
DB_RC_OIL_EternalRecipeCommented(_Flag);
StartVoiceBark(_VB,(CHARACTERGUID)_Player);

IF
ObjectFlagSet("RemoveOneSourcePoint",_Player,_ID)
AND
DB_DialogName(_Dialog,_ID)
AND
DB_RC_OIL_EternalsArtefacts(_Pillar,_Dialog,_)
THEN
PlayEffect(_Pillar,"RS3_FX_GP_ScriptedEvent_Replenish_SourcePoint_01");

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
