Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_RC_WC_MordusOffice_PrimingItem((ITEMGUID)S_RC_WC_MineSetOffWallExplosion_efbdedc7-922b-420c-b8e4-0038a7a39b56);

DB_RC_WC_MordusOffice_SacrificeAreaTrigger((TRIGGERGUID)S_RC_WC_MordusOffice_DwarfCorpseTrigger_00d79247-9e38-4e53-af75-6c8648889e9e);

DB_RC_WC_MordusOffice_BoatEntranceTrigger((TRIGGERGUID)S_RC_WC_MordusOffice_ShipWreckEnterTrigger_fcc7b294-4143-42c6-8fa5-1b6416d92261);

DB_RC_WC_MordusOffice_SmokeTrigger(0,(TRIGGERGUID)S_RC_WC_MO_SmokeTrigger_000_e5cda19e-9adf-4d90-ac0f-b0b2ca524765,(TRIGGERGUID)TRIGGERGUID_S_RC_WC_MO_SmokeTrigger_BottomLeft_000_381b5d9c-781f-4145-9795-50ca777f23f6,(TRIGGERGUID)TRIGGERGUID_S_RC_WC_MO_SmokeTrigger_TopRight_000_357318fd-05ef-429a-819c-22c1684ccefe,10);
DB_RC_WC_MordusOffice_SmokeTrigger(1,TRIGGERGUID_S_RC_WC_MO_SmokeTrigger_001_d9e3c0ee-22be-4732-9726-c4641aa3ee58,TRIGGERGUID_S_RC_WC_MO_SmokeTrigger_BottomLeft_001_0f1ecdf4-119c-45f5-b33f-96b434dff9e0,TRIGGERGUID_S_RC_WC_MO_SmokeTrigger_TopRight_001_934467eb-43cc-4458-a026-517f0fd7d51b,10);
DB_RC_WC_MordusOffice_SmokeTrigger(2,TRIGGERGUID_S_RC_WC_MO_SmokeTrigger_002_a33dfa16-7fe1-406e-b001-75a01ab4a8c0,TRIGGERGUID_S_RC_WC_MO_SmokeTrigger_BottomLeft_002_01edd66b-220c-4680-b194-1b10029fbe4d,TRIGGERGUID_S_RC_WC_MO_SmokeTrigger_TopRight_002_cdcd3417-f2f1-4b9a-a75e-4dcb28d7c94c,25);

DB_RC_WC_MordusOffice_SmokeTrigger_SplashHelper((TRIGGERGUID)S_RC_WC_MO_SmokeTrigger_000_e5cda19e-9adf-4d90-ac0f-b0b2ca524765,ITEMGUID_RC_WC_MordusOffice_WallSplashHelper_000_ef963fd7-c478-4217-91a2-8567f3f3878d);
DB_RC_WC_MordusOffice_SmokeTrigger_SplashHelper(TRIGGERGUID_S_RC_WC_MO_SmokeTrigger_001_d9e3c0ee-22be-4732-9726-c4641aa3ee58,ITEMGUID_RC_WC_MordusOffice_WallSplashHelper_001_51efed1b-d0b2-413f-8949-05608686391e);

DB_RC_WC_MO_RockFace_Delay(1000);
KBSECTION
//REGION Helpers
PROC
PROC_ShakeCameraForPlayersInTriggerForTime((TRIGGERGUID)_Trigger,(INTEGER)_Time)
AND
DB_IsPlayer(_Player)
AND
ObjectIsInTrigger(_Player,_Trigger,1)
THEN
Proc_ShakeCameraForTime(_Player,_Time);
//END_REGION

//REGION Play smoke effects and splashes
PROC
PROC_RC_WC_MO_SmokeEffectInRandomTrigger()
AND
SysCount("DB_RC_WC_MordusOffice_SmokeTrigger",5,_Entries)
AND
Random(_Entries,_Entry)
AND
DB_RC_WC_MordusOffice_SmokeTrigger(_Entry,_Trigger,_BottomLeft,_TopRight,_MaxScaleOffset)
THEN
PROC_RC_WC_MO_SmokeEffectInTrigger(_Trigger,_BottomLeft,_TopRight,_MaxScaleOffset);

PROC
PROC_RC_WC_MO_SmokeEffectInTrigger((TRIGGERGUID)_Trigger,(TRIGGERGUID)_BottomLeft,(TRIGGERGUID)_TopRight,(INTEGER)_MaxScaleOffset)
AND
QRY_Helper_GetAnyRandomPositionInArea(_BottomLeft,_TopRight)
AND
DB_Helper_GetAnyRandomPositionInArea_Result(_X,_Y,_Z)
AND
Random(_MaxScaleOffset,_ScaleOffsetInt)
AND
Real(_ScaleOffsetInt,_ScaleOffsetReal)
AND
RealDivide(_ScaleOffsetReal,10.0,_ScaleOffset)
AND
RealSum(_ScaleOffset,0.5,_Scale)
THEN
PlayScaledEffectAtPosition("RS3_FX_GP_ScriptedEvent_Smoke_01",_Scale,_X,_Y,_Z);

PROC
PROC_RC_WC_MO_SmokeEffectInTrigger((TRIGGERGUID)_Trigger,(TRIGGERGUID)_BottomLeft,(TRIGGERGUID)_TopRight,(INTEGER)_MaxScaleOffset)
AND
// water splash for this one can up in/behind the rocks
_Trigger != TRIGGERGUID_S_RC_WC_MO_SmokeTrigger_002_a33dfa16-7fe1-406e-b001-75a01ab4a8c0
AND
NOT DB_RC_WC_MO_SplashQueued(_Trigger,_,_,_)
AND
Random(3,0)
AND
GetRandomPositionInTrigger(_Trigger,_X,_Y,_Z)
AND
GetPosition(TRIGGERGUID_S_RC_WC_MO_WallBottomSmokeTrigger_46b981e4-9854-4979-a273-024b86c95425,_,_BottomY,_)
AND
RealSubtract(_Y,_BottomY,_YDelta)
AND
RealDivide(_YDelta,0.962361,_Time)
AND
Integer(_Time,_TimeInt)
AND
DB_RC_WC_MordusOffice_SmokeTrigger_SplashHelper(_Trigger,_SplashHelper)
THEN
DB_RC_WC_MO_SplashQueued(_Trigger,_X,_Y,_Z);
TeleportToPosition(_SplashHelper,_X,_Y,_Z,"",0);
ProcObjectTimerCancel(_SplashHelper,"RC_WC_MO_SplashFinishedTimer");
PROC_StopLoopEffect(_SplashHelper,"WallSmokeSplash");
ProcObjectTimer(_Trigger,"RC_WC_MO_SmokeSplashTimer",_TimeInt);

PROC
ProcObjectTimerFinished(_Trigger,"RC_WC_MO_SmokeSplashTimer")
AND
DB_RC_WC_MO_SplashQueued((TRIGGERGUID)_Trigger,_X,_Y,_Z)
AND
DB_RC_WC_MordusOffice_SmokeTrigger_SplashHelper((TRIGGERGUID)_Trigger,_SplashHelper)
AND
Random(2500,_DeltaDelay)
AND
IntegerSum(2500,_DeltaDelay,_SplashDuration)
THEN
NOT DB_RC_WC_MO_SplashQueued((TRIGGERGUID)_Trigger,_X,_Y,_Z);
PROC_LoopEffect("RS3_FX_Environment_Water_Splash_Sea_01",_SplashHelper,"WallSmokeSplash","RC_Main","Dummy_FX");
ProcObjectTimer(_SplashHelper,"RC_WC_MO_SplashFinishedTimer",_SplashDuration);

PROC
ProcObjectTimerFinished(_SplashHelper,"RC_WC_MO_SplashFinishedTimer")
THEN
PROC_StopLoopEffect(_SplashHelper,"WallSmokeSplash");
//END_REGION

//REGION Priming wall weakening
IF
TextEventSet("RC_WC_MO_StartWallEffects")
THEN
NOT DB_OnlyOnce("RC_WC_MO_StartFiring");
CharacterItemSetEvent((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000,(ITEMGUID)NULL_00000000-0000-0000-0000-000000000000,"RC_WC_MO_PressurePlateTriggered");

IF
TextEventSet("RC_WC_MO_StopWallEffects")
THEN
DB_RC_WC_MO_StopAllPostFireEffects(1);



IF
CharacterItemEvent(_,_,"RC_WC_MO_PressurePlateTriggered")
AND
QueryOnlyOnce("RC_WC_MO_StartFiring")
AND
DB_RC_WC_MordusOffice_SacrificeAreaTrigger(_Trigger)
THEN
CharacterItemSetEvent((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000,(ITEMGUID)NULL_00000000-0000-0000-0000-000000000000,"RC_WC_MO_StartFiring");
TriggerRegisterForPlayers(_Trigger);
TimerLaunch("RC_WC_MO_FiringFirstImpact",1500);
TimerLaunch("RC_WC_MO_StopFiring",4200);


IF
TimerFinished("RC_WC_MO_FiringFirstImpact")
AND
DB_RC_WC_MordusOffice_ShipFlySpeed(_CurrentSpeed,_CurrentAcceleration)
THEN
Proc_ShakeCameraForPlayersInTriggerForTime(TRIGGERGUID_S_RC_WC_MOrdusOffice_Box_a4018fd0-bed4-494c-9da6-3cc425f426e3,4500);
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
NOT DB_RC_WC_MordusOffice_ShipFlySpeed(_CurrentSpeed,_CurrentAcceleration);
DB_RC_WC_MordusOffice_ShipFlySpeed(1.5,-1.0);
PROC_RC_WC_MordusOffice_FlyShip(TRIGGERGUID_S_RC_WC_MordusOffice_ShipWreckSmallMoveTarget_cfacae9f-c682-4cd9-a114-39bf5c50f20e,"");
NOT DB_RC_WC_MordusOffice_ShipFlySpeed(1.5,-1.0);
DB_RC_WC_MordusOffice_ShipFlySpeed(_CurrentSpeed,_CurrentAcceleration);
DB_RC_WC_MO_FiringSmokeTimerContinue(1);
TimerLaunch("RC_WC_MO_FiringSmokeTimer",100);

IF
DB_RC_WC_MO_StopAllPostFireEffects(1)
THEN
NOT DB_RC_WC_MO_FiringSmokeTimerContinue(1);

IF
TimerFinished("RC_WC_MO_FiringSmokeTimer")
AND
DB_RC_WC_MO_FiringSmokeTimerContinue(1)
AND
Random(300,_NextTimer)
THEN
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
TimerLaunch("RC_WC_MO_FiringSmokeTimer",_NextTimer);

IF
DB_RC_WC_MO_StopAllPostFireEffects(1)
THEN
NOT DB_RC_WC_MO_FiringAfterShock(1);

IF
TimerFinished("RC_WC_MO_StopFiring")
THEN
NOT DB_RC_WC_MO_FiringSmokeTimerContinue(1);
CharacterItemSetEvent((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000,(ITEMGUID)NULL_00000000-0000-0000-0000-000000000000,"RC_WC_MO_StopFiring");
//TimerLaunch("RC_WC_MO_FiringStoppedAD",800);
DB_RC_WC_MO_FiringAfterShock(1);
TimerLaunch("RC_WC_MO_FiringAfterShock",2000);

IF
TimerFinished("RC_WC_MO_FiringAfterShock")
AND
DB_RC_WC_MO_FiringAfterShock(1)
THEN
PROC_ShakeCameraForPlayersInTriggerForTime(TRIGGERGUID_S_RC_WC_MOrdusOffice_Box_a4018fd0-bed4-494c-9da6-3cc425f426e3,500);
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
DB_RC_WC_MO_OccasionalRumblesContinue(1);
TimerLaunch("RC_WC_MO_OccasionalRumbles",5000);

IF
DB_RC_WC_MO_StopAllPostFireEffects(1)
THEN
NOT DB_RC_WC_MO_OccasionalRumblesContinue(1);

IF
TimerFinished("RC_WC_MO_OccasionalRumbles")
AND
DB_RC_WC_MO_OccasionalRumblesContinue(1)
AND
Random(10000,_NextTimerOffset)
AND
IntegerSum(5000,_NextTimerOffset,_NextTimer)
THEN
PROC_ShakeCameraForPlayersInTriggerForTime(TRIGGERGUID_S_RC_WC_MOrdusOffice_Box_a4018fd0-bed4-494c-9da6-3cc425f426e3,500);
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
PROC_RC_WC_MO_SmokeEffectInRandomTrigger();
TimerLaunch("RC_WC_MO_OccasionalRumbles",_NextTimer);

IF
DB_RC_WC_MO_StopAllPostFireEffects(1)
THEN
NOT DB_RC_WC_MO_StopAllPostFireEffects(1);

IF
DB_InRegion(_Player,_Trigger)
AND
DB_RC_WC_MordusOffice_SacrificeAreaTrigger(_Trigger)
AND
DB_RC_WC_MordusOffice_BoatEntranceTrigger(_BoatTrigger)
THEN
TriggerUnregisterForPlayers(_Trigger);
TriggerRegisterForPlayers(_BoatTrigger);

IF
CharacterEnteredTrigger(_Player,_BoatTrigger)
AND
DB_RC_WC_MordusOffice_BoatEntranceTrigger(_BoatTrigger)
AND
DB_RC_WC_MO_RockFace(_RockFace,_X,_Y,_Z)
AND
DB_RC_WC_MO_RockFace_Delay(_Time)
THEN
DB_RC_WC_MO_StopAllPostFireEffects(1);
TeleportTo(ITEMGUID_S_RC_WC_MordusOffice_RockFace_SmokeHelper_000_ad244865-e07b-4efb-8798-23df30ff2d5f,_RockFace);
TriggerUnregisterForPlayers(_BoatTrigger);
PlaySound(_RockFace,"SE_RC_LairOfTheAtaraxian_ShipWreck_PreCrash");
TimerLaunch("RC_WC_MordusOffice_StartDestroyRockFace",_Time);

IF
TimerFinished("RC_WC_MordusOffice_StartDestroyRockFace")
THEN
PROC_RC_WC_MordusOffice_Destroy_RockFace();

IF
TextEventSet("rc_wc_mo_rockfacedelay")
AND
GetTextEventParamInteger(1,_Delay)
AND
DB_RC_WC_MO_RockFace_Delay(_PrevDelay)
THEN
NOT DB_RC_WC_MO_RockFace_Delay(_PrevDelay);
DB_RC_WC_MO_RockFace_Delay(_Delay);


PROC
ProcRegionEnded("RC_Main")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "RC_DW_WreckersCaveBase"
