Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_BI_Vault_Possessed_001_Demon");
DB_BI_Vault_DamageDealt(0.0);

DB_ShovelArea(TRIGGERGUID_S_RC_BI_AdventurersSecret_Area_fe96357e-d24a-4ab3-8100-cec235a6759a,"RC_BI_AdventurersSecret",ITEMGUID_S_RC_BI_AdventurersSecret_Pile_8ec45b7b-541c-4530-b146-4a9fd54f7d29);
DB_ShovelRewardItemAppear("RC_BI_AdventurersSecret",ITEMGUID_S_RC_BI_AdventurersSecret_Loot_96bb9a6c-4c4a-4218-9083-694d6690017a,TRIGGERGUID_S_RC_BI_AdventurersSecret_Point_c7d4a56d-594b-4b03-9bc5-8638b4c453dc);

SetOnStage(S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,0);
DB_RC_BI_ParasiteHP(100.0);

DB_RC_BI_Vault_Surfaces((TRIGGERGUID)TRIGGERGUID_S_RC_BI_Vault1_Surface1_3fd04a4f-d9c3-4e39-838a-f29adcfd9308);
DB_RC_BI_Vault_Surfaces(TRIGGERGUID_S_RC_BI_Vault1_Surface2_1b4663b1-9553-4ac2-8793-c4861a14ba37);
DB_RC_BI_Vault_Surfaces(TRIGGERGUID_S_RC_BI_Vault1_Surface3_4005a027-aeae-42e0-b1ca-1c8e57668515);
DB_RC_BI_Vault_Surfaces(TRIGGERGUID_S_RC_BI_Vault1_Surface4_f7b301e4-23be-4909-aee2-4c0fa11eecc2);

ApplyStatus(CHARACTERGUID_S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"DEATH_RESIST",-1.0,1);
KBSECTION
IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_RC_BI_Vault_Prison_001_ed3c583a-2c8d-4022-8cc1-07e15849ad2b)
AND
CharacterIsDead(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,0)
AND
DB_Dialogs(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_BI_Vault_Possessed_001_Demon")
AND
NOT DB_OnlyOnce("RC_BI_Vault_001_DemonSpoked")
THEN
Proc_StartDialog(0,"RC_BI_Vault_Possessed_001_Demon",S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,_Player);
DB_OnlyOnce("RC_BI_Vault_001_DemonSpoked");

IF
AttackedByObject(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,(CHARACTERGUID)_Player,_,_,_)
AND
ObjectGetFlag(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_BI_Vault_ChainsDestroyed",0)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"RC_BI_AttackedDwarf",0)
THEN
ObjectSetFlag(_Player,"RC_BI_AttackedDwarf");
Proc_StartDialog(0,"RC_BI_Vault_Possessed_001_Attacked",S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,_Player);

IF
StoryEvent(S_RC_BI_Vault_Ghost_001_28a0a573-7f60-4516-b68e-537f57e5db84,"RC_BI_Vault_LeverUsed")
THEN
Proc_StartDialog(1,"RC_BI_AD_Vault_Ghost_001_Warning",S_RC_BI_Vault_Ghost_001_28a0a573-7f60-4516-b68e-537f57e5db84);

IF
DB_RC_BI_Vault_Pillars_Count(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,0,_)
THEN
ProcRemoveAllDialogEntriesForSpeaker(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25);
DB_Dialogs(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_BI_Vault_Possessed_001");
CharacterSetReactionPriority(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"Talk",0);
SetFaction(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25, "Evil NPC");


//REGION Damage dealt & demon release

IF
StoryEvent(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_BI_Vault_001_Damaged")
AND
ObjectGetFlag(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_BI_DemonReleased",0)
AND
GetVarFloat(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"LastDamage",_CurPercent)
AND
DB_BI_Vault_DamageDealt((REAL)_LastPercentSum)
AND
RealSum(_LastPercentSum,_CurPercent,_CurPercentSum)
THEN
NOT DB_BI_Vault_DamageDealt(_LastPercentSum);
DB_BI_Vault_DamageDealt(_CurPercentSum);


//Damage dealt
IF
DB_BI_Vault_DamageDealt(_CurPercentSum)
AND
_CurPercentSum > 0.1
AND
_CurPercentSum <= 0.4
THEN
Proc_RC_BI_Vault_001_Damage("RC_BI_DemonHurt_01");

IF
DB_BI_Vault_DamageDealt(_CurPercentSum)
AND
CharacterIsDead(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,0)
AND
_CurPercentSum > 0.4
AND
_CurPercentSum <= 0.7
THEN
Proc_RC_BI_Vault_001_Damage("RC_BI_DemonHurt_02");

IF
DB_BI_Vault_DamageDealt(_CurPercentSum)
AND
CharacterIsDead(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,0)
AND
_CurPercentSum > 0.7
AND
_CurPercentSum <= 1.0
THEN
Proc_RC_BI_Vault_001_Damage("RC_BI_DemonHurt_03");

IF
DB_BI_Vault_DamageDealt(_CurPercentSum)
AND
CharacterIsDead(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,0)
AND
_CurPercentSum > 1.0
AND
_CurPercentSum <= 1.3
THEN
Proc_RC_BI_Vault_001_RemoveDeathResist();
CharacterSetHitpointsPercentage(CHARACTERGUID_S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,100.0);
Proc_RC_BI_Vault_001_Damage("RC_BI_DemonHurt_04");


IF
DB_BI_Vault_DamageDealt(_CurPercentSum)
AND
DB_RC_BI_Vault_001_DemonReady(1)
AND
ObjectGetFlag(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_BI_Vault_ChainsDestroyed",0)
AND
DB_RC_BI_Vault_Pillars(_Pillar,_,_,_Flag,1) //in goal RC_BI_Statues
AND
GetPosition(_Pillar,_X,_Y,_Z)
THEN
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_Source_Absorb_01",_X,_Y,_Z);


// releasing the demon when enough damage has been dealt, when the chains are already destroyed
IF
DB_BI_Vault_DamageDealt(_CurPercentSum)
AND
_CurPercentSum > 1.3
AND
CharacterIsDead(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,0)
AND
ObjectGetFlag(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_BI_DemonReleased",0)
AND
ObjectGetFlag(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_BI_Vault_ChainsDestroyed",1)
THEN
Proc_RC_BI_Vault_001_Damage("RC_BI_DemonReleased");


// the demon is ready to be released (enough damage has been dealt) but the chains are still up
IF
DB_BI_Vault_DamageDealt(_CurPercentSum)
AND
_CurPercentSum > 1.3
AND
CharacterIsDead(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,0)
AND
ObjectGetFlag(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_BI_DemonReleased",0)
AND
ObjectGetFlag(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_BI_Vault_ChainsDestroyed",0)
AND
GetPosition(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,_X,_Y,_Z)
THEN
SetStoryEvent(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"GEN_PeriodicSoloDialog_DelayDialog");
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_RC_BI_Vault_Parasite_01",_X,_Y,_Z); 
Proc_RC_BI_Vault_001_DemonReady(); 


// releasing the demon when the chains are destroyed after dealing enough damage to the dwarf
IF
ObjectFlagSet("RC_BI_Vault_ChainsDestroyed",S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,_)
AND
DB_BI_Vault_DamageDealt(_CurPercentSum)
AND
_CurPercentSum > 1.3
THEN
ObjectSetFlag(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_BI_DemonReleased");


PROC
Proc_RC_BI_Vault_001_Damage((STRING)_Flag)
AND
CharacterIsDead(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,0)
AND
ObjectGetFlag(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,_Flag,0)
THEN
ObjectSetFlag(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,_Flag);
ApplyStatus(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"KNOCKED_DOWN",1.0,1);
SetStoryEvent(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"GEN_PeriodicSoloDialog_DelayDialog");
Proc_StartDialog(1,"RC_BI_AD_Vault_Possessed_001",S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25);


PROC
Proc_RC_BI_Vault_001_DemonReady()
AND 
NOT DB_RC_BI_Vault_001_DemonReady(1)
AND
NOT DB_Dead(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25)
THEN
DB_RC_BI_Vault_001_DemonReady(1);
ApplyStatus(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"KNOCKED_DOWN",-1.0,1);
Proc_StartDialog(1,"RC_BI_AD_Vault_Possessed_001",S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25);
Proc_RC_BI_Vault_001_RemoveDeathResist();


PROC
Proc_RC_BI_Vault_001_RemoveDeathResist()
AND
HasActiveStatus(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"DEATH_RESIST",1)
THEN
RemoveStatus(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"DEATH_RESIST");


// Clean up after the demon is released / the chains are destroyed
IF
ObjectFlagSet("RC_BI_Vault_ChainsDestroyed",S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,_)
THEN
Proc_RC_BI_Vault_001_CleanUp();

IF
ObjectFlagSet("RC_BI_DemonReleased",S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,_)
THEN
Proc_RC_BI_Vault_001_CleanUp();

PROC
Proc_RC_BI_Vault_001_CleanUp()
THEN
Proc_RC_BI_Vault_001_RemoveDeathResist();

PROC
Proc_RC_BI_Vault_001_CleanUp()
AND
DB_BI_Vault_DamageDealt(_CurPercentSum)
AND
_CurPercentSum > 1.3
THEN
NOT DB_BI_Vault_DamageDealt(_CurPercentSum);

PROC
Proc_RC_BI_Vault_001_CleanUp()
AND
DB_RC_BI_Vault_001_DemonReady(1)
AND
HasActiveStatus(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"KNOCKED_DOWN",1)
THEN
RemoveStatus(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"KNOCKED_DOWN");

PROC
Proc_RC_BI_Vault_001_CleanUp()
AND
DB_RC_BI_Vault_001_DemonReady(1)
THEN
NOT DB_RC_BI_Vault_001_DemonReady(1);

//END_REGION

//REGION Possessed died
IF
CharacterDied(CHARACTERGUID_S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25)
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_BI_TheVaults_KilledAdventurer");

IF
CharacterDied(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8)
AND
DB_IsPlayer(_Player)
AND
CharacterIsDead(CHARACTERGUID_S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,0)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_BI_TheVaults_SavedAdventurer");

IF
CharacterDied(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8)
AND
CharacterIsDead(CHARACTERGUID_S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,1)
AND
GetClosestAlivePlayer(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,_Player,_Dist)
AND
_Dist < 15.0
THEN
ProcDefineReflectionDialog("RC_BI_RD_Vault_Possessed_001",_Player);

IF
CharacterDied(CHARACTERGUID_S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25)
AND
ObjectGetFlag(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_BI_DemonReleased",0)
AND
GetClosestAlivePlayer(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,_Player,_Dist)
AND
_Dist < 15.0
THEN
ProcDefineReflectionDialog("RC_BI_RD_Vault_Possessed_001",_Player);

IF
CharacterDying(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8)
AND
DB_CombatCharacters(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8, _ID)
AND
DB_IsPlayer(_Killer)
AND
DB_CombatCharacters(_Killer, _ID)
THEN
ObjectSetFlag(_Killer, "RC_BI_Vault_001_Savior");

//END_REGION

IF
DialogStarted("RC_BI_Vault_Possessed_001",_ID)
AND
DialogGetInvolvedPlayer(_ID, 1, (CHARACTERGUID)_Player)
THEN
PartySetFlag(_Player,"QuestUpdate_RC_BI_TheVaultAdventurer_SavedAdventurer");

IF
DialogEnded("RC_BI_Vault_Possessed_001",_ID)
AND
DialogGetInvolvedPlayer(_ID, 1, (CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"RC_BI_Vault_AdventurersSecret",1)
THEN
PlayEffect(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01");
SetOnStage(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,0);
AddSecret(_Player,"RC_BI_AdventurersSecret");

IF
ObjectTurnStarted(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25)
AND
HasActiveStatus(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"ADRENALINE",0)
THEN
CharacterUseSkill(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"Shout_EnemyAdrenaline",S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,1);

IF
CharacterWentOnStage(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,1)
AND
QueryOnlyOnce("RC_BI_ParasiteOut")
THEN
PlayEffect(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,"RS3_FX_GP_ScriptedEvent_RC_BI_Vault_Parasite_01"); 
GlobalSetFlag("RC_BI_ParasiteOut");

IF
DB_OnlyOnce("RC_BI_ParasiteOut")
AND
NOT DB_Dead(CHARACTERGUID_S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25)
THEN
CharacterSetCustomName(CHARACTERGUID_S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RC_PossessedAdventurer");

IF
AttackedByObject(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,_,_Attacker,_,_)
AND
CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8 != _Attacker
AND
ObjectIsOnStage(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,1)
AND
GlobalGetFlag("RC_BI_ParasiteOut",1)
AND
DB_RC_BI_ParasiteAttackCount(3)
THEN
Proc_RC_BI_ParasiteMadness();

IF
AttackedByObject(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,_,_Attacker,_,_)
AND
CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8 != _Attacker
AND
ObjectIsOnStage(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,1)
AND
GlobalGetFlag("RC_BI_ParasiteOut",1)
AND
DB_RC_BI_ParasiteAttackCount(_Count)
AND
_Count < 3
AND
IntegerSum(_Count,1,_NewSum)
THEN
NOT DB_RC_BI_ParasiteAttackCount(_Count);
DB_RC_BI_ParasiteAttackCount(_NewSum);

PROC
Proc_RC_BI_ParasiteMadness()
AND
GetClosestAlivePlayer(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,_Player,_Dist)
AND
QRY_IsInRange(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,_Player,15.0)
AND
QRY_RC_BI_ParasiteTarget(_Player)
THEN
RemoveStatus(_Player,"CLEAR_MINDED");
ApplyStatus(_Player,"MADNESS",-1.0,1);
ApplyStatus(_Player,"PARASITIC_HEART",-1.0,1);
DB_RC_BI_ParasitePlayer(_Player);
Proc_RC_BI_ParasiteIn();

PROC
Proc_RC_BI_ParasiteMadness()
AND
NOT DB_RC_BI_ParasitePlayer(_)
AND
GetClosestAlivePlayer(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,_Player,_Dist)
AND
QRY_IsInRange(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,_Player,15.0)
THEN
RemoveStatus(_Player,"CLEAR_MINDED");
ApplyStatus(_Player,"MADNESS",-1.0,1);
ApplyStatus(_Player,"PARASITIC_HEART",-1.0,1);
DB_RC_BI_ParasitePlayer(_Player);
Proc_RC_BI_ParasiteIn();

QRY
QRY_RC_BI_ParasiteTarget((CHARACTERGUID)_Player)
AND
HasActiveStatus(_Player,"CHICKEN",0)
AND
HasActiveStatus(_Player,"PETRIFIED",0)
AND
HasActiveStatus(_Player,"KNOCKDOWN",0)
AND
HasActiveStatus(_Player,"STUNNED",0)
AND
HasActiveStatus(_Player,"FEAR",0)
AND
HasActiveStatus(_Player,"MADNESS",0)
THEN
DB_NOOP(1);

IF
CharacterStatusRemoved(_Player,"MADNESS",_)
AND
DB_RC_BI_ParasitePlayer(_Player)
THEN
RemoveStatus(_Player,"CLEAR_MINDED");
ProcObjectTimer(_Player,"Timer_RC_BI_Madness",500);

PROC
ProcObjectTimerFinished(_Player,"Timer_RC_BI_Madness")
THEN
RemoveStatus(_Player,"CLEAR_MINDED");
ApplyStatus(_Player,"MADNESS",-1.0,1);

PROC
Proc_RC_BI_ParasiteIn()
AND
DB_RC_BI_ParasiteHP(_HP)
AND
CharacterGetHitpointsPercentage(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,_NewHP)
THEN
NOT DB_RC_BI_ParasiteHP(_HP);
DB_RC_BI_ParasiteHP(_NewHP);
Poof(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8);
GlobalClearFlag("RC_BI_ParasiteOut");

PROC
Proc_RC_BI_ParasiteIn()
AND
DB_RC_BI_ParasitePlayer(_Player)
AND
NOT QRY_RC_BI_FindSanePlayer(_Player)
THEN
NOT DB_RC_BI_ParasitePlayer(_Player);
Proc_RC_BI_ParasitePop(_Player);

QRY
QRY_RC_BI_FindSanePlayer((CHARACTERGUID)_Player)
AND
DB_IsPlayer(_OtherPlayer)
AND
_OtherPlayer != _Player
AND
CharacterIsDead(_OtherPlayer,0)
AND
HasActiveStatus(_OtherPlayer,"MADNESS",0)
AND
HasActiveStatus(_OtherPlayer,"FEAR",0)
AND
HasActiveStatus(_OtherPlayer,"PARASITIC_HEART",0)
AND
HasActiveStatus(_OtherPlayer,"CHARMED",0)
THEN
DB_NOOP(1);

IF
CharacterDied(_Player)
AND
DB_RC_BI_ParasitePlayer(_OtherPlayer)
AND
_Player != _OtherPlayer
AND
NOT QRY_RC_BI_FindSanePlayer(_OtherPlayer)
THEN
NOT DB_RC_BI_ParasitePlayer(_OtherPlayer);
Proc_RC_BI_ParasitePop(_OtherPlayer);

IF
CharacterDied(_Player)
AND
DB_RC_BI_ParasitePlayer(_Player)
THEN
NOT DB_RC_BI_ParasitePlayer(_Player);
Proc_RC_BI_ParasitePop(_Player);

IF
CharacterReceivedDamage(_Player, _, _)
AND
DB_RC_BI_ParasitePlayer(_Player)
AND
CharacterGetHitpointsPercentage(_Player,_HP)
AND
_HP <= 10.0
THEN
NOT DB_RC_BI_ParasitePlayer(_Player);
Proc_RC_BI_ParasitePop(_Player);

PROC
Proc_RC_BI_ParasitePop((CHARACTERGUID)_Player)
AND
NOT DB_Dead(CHARACTERGUID_S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25)
AND
QRY_IsInRange(CHARACTERGUID_S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,15.0)
THEN
TeleportTo(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,CHARACTERGUID_S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25);

PROC
Proc_RC_BI_ParasitePop((CHARACTERGUID)_Player)
AND
GetPosition(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,_X,_Y,_Z)
THEN
PlayEffect(_Player,"RS3_FX_GP_ScriptedEvent_RC_BI_Vault_Parasite_01"); 
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_RC_BI_Vault_Parasite_01",_X,_Y,_Z);
CharacterAppear(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,1,"");
RemoveStatus(_Player,"MADNESS");
RemoveStatus(_Player,"PARASITIC_HEART");

IF
ObjectEnteredCombat(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,_)
AND
QueryOnlyOnce("RC_BI_Vault1_Surface")
THEN
PlayEffect(S_RC_BI_Vault_PosessedPerson_001_f0d1f343-fc73-4c09-8d13-ed37bf51ae25,"RS3_FX_GP_ScriptedEvent_RC_BI_Vault_Dwarf_01");
Proc_RC_BI_VaultCreateSurface();

IF
ObjectEnteredCombat(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,_)
AND
NOT DB_GlobalFlag("RC_BI_ParasiteOut")
THEN
ProcObjectTimer(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,"Timer_RC_BI_ParasiteHpDamage",3100);

PROC
ProcObjectTimerFinished(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,"Timer_RC_BI_ParasiteHpDamage")
AND
DB_RC_BI_ParasiteHP(_HP)
AND
RealSubtract(_HP,20.0,_NewHP)
THEN
CharacterSetHitpointsPercentage(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,_NewHP);
PlayEffect(CHARACTERGUID_S_RC_BI_Vault_Demon_001_01c5d4d3-425f-42fd-8200-528b14aafec8,"RS3_FX_GP_ScriptedEvent_RC_BI_Vault_Parasite_02");
GlobalSetFlag("RC_BI_ParasiteOut");

IF
GlobalFlagSet("RC_BI_ParasiteOut")
AND
DB_RC_BI_ParasiteAttackCount(_Count)
THEN
NOT DB_RC_BI_ParasiteAttackCount(_Count);
DB_RC_BI_ParasiteAttackCount(0);

IF
GlobalFlagSet("RC_BI_ParasiteOut")
AND
NOT DB_RC_BI_ParasiteAttackCount(_)
THEN
DB_RC_BI_ParasiteAttackCount(0);

IF
TextEventSet("bipara")
AND
DB_IsPLayer(_Player)
THEN
TeleportTo(_Player,TRIGGERGUID_Debug_RC_BI_DemonVault1_62f7ebd5-6d8a-4087-8ee3-95fe98fb0cfd);

IF
TextEventSet("bipara")
AND
DB_RC_BI_Vault_Pillars(_Pillar,_Prisoner,_Ghost,_Flag,_QuestNum)
THEN
PROC_LoopBeamEffect("RS3_FX_GP_ScriptedEvent_ChainedDragon_Beam_01",_Pillar,_Prisoner,"","Dummy_NeckFX",_Flag,"RC_Main");
PROC_LoopEffect("RS3_FX_GP_ScriptedEvent_SourceGlow_Overlay_01",_Pillar,_Flag,"RC_Main","");
PROC_LoopEffect("RS3_FX_GP_ScriptedEvent_SourcePillar_01",_Pillar,_Flag,"RC_Main","");

PROC
Proc_RC_BI_VaultCreateSurface()
AND
DB_RC_BI_Vault_Surfaces(_Surface)
AND
Random(3000,_Time)
THEN
ProcObjectTimer(_Surface,"Timer_RC_BI_Vault_Surface",_Time);

PROC
ProcObjectTimerFinished(_Surface,"Timer_RC_BI_Vault_Surface")
AND
GetPosition(_Surface,_X,_Y,_Z)
THEN
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_RC_BI_Vault_Dwarf_01",_X,_Y,_Z);
CreateSurface(_Surface,"SurfaceWaterFrozenCursed",3.7,-1.0);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
