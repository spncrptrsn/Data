Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_CoS_Temples_Wizard_DoorRunes((ITEMGUID)ITEMGUID_S_CoS_Temples_Wizard_DoorRune_001_2ae7a940-d564-41f2-af0c-8598fa7bff33);
DB_CoS_Temples_Wizard_DoorRunes(ITEMGUID_S_CoS_Temples_Wizard_DoorRune_002_fb774451-94dc-4798-93dc-a8a0043abddb);
DB_CoS_Temples_Wizard_DoorRunes(ITEMGUID_S_CoS_Temples_Wizard_DoorRune_003_ca8243b6-7b9e-4013-872a-8846b1eb1f5c);

DB_Ghosts(CHARACTERGUID_S_CoS_Temples_Wizard_BlackRing_001_Ghost_55be9026-7f00-4249-bf23-4872dcc4d3e8,"CoS_Temples_Wizard_BlackRing_001_Ghost");
CharacterLinkGhost(CHARACTERGUID_S_CoS_Temples_Wizard_BlackRing_001_811a0f82-c1dd-4b0d-8748-309248d72bc9,CHARACTERGUID_S_CoS_Temples_Wizard_BlackRing_001_Ghost_55be9026-7f00-4249-bf23-4872dcc4d3e8);

ProcSetInvulnerable(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,1);
CharacterSetImmortal(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,1);
ProcCharacterDisableAllCrimes(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc);



DB_Dialogs(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,"CoS_Temples_Wizard_KnightOfAmadia");
DB_Dialogs(ITEMGUID_S_CoS_Temples_Wizard_Statue_AmadiaShrine_9ef8ad8b-cd81-418b-9baf-5e59cb2d39b2,"CoS_Temples_Wizard_Statue_AmadiaShrine");
SetHasDialog(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,0);

DB_CoS_Temples_AmadiaRegions((TRIGGERGUID)TRIGGERGUID_S_CoS_Temples_Wizard_BuffTrigger_2f1a00d8-a3cd-455e-be10-88ae3ecc0b77);


DB_CoS_Temples_AmadiaBuffs("CoS_Temples_WizardBuff_Haste","HASTED");
DB_CoS_Temples_AmadiaBuffs("CoS_Temples_WizardBuff_MagicArmor","MAGIC_SHELL");
DB_CoS_Temples_AmadiaBuffs("CoS_Temples_WizardBuff_WingedFeet","ETHEREAL_SOLES");
DB_CoS_Temples_AmadiaBuffs("CoS_Temples_WizardBuff_FireImmune","FIRE_IMMUNITY");
DB_CoS_Temples_AmadiaBuffs("CoS_Temples_WizardBuff_IceImmune","WATER_IMMUNITY");
DB_CoS_Temples_AmadiaBuffs("CoS_Temples_WizardBuff_LightningImmune","AIR_IMMUNITY");

DB_HasStoryEvent((ITEMGUID)ITEMGUID_S_CoS_Temples_Wizard_AmadiaJewel_4e08facd-d84a-40b0-b35e-04d4eb6818e3,"CoS_HasAmadiaKey");

ProcTriggerRegisterForPlayers(TRIGGERGUID_S_CoS_Temples_Wizard_OuterVB_Box_85e1f29a-bf23-430f-a017-5ece32a4a5d0);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_CoS_Temples_Wizard_VB_SanctumPuzzle_Box_f1f08d4e-25a2-4496-b902-3f61b7123af7);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_CoS_Temples_Wizard_BuffTrigger_2f1a00d8-a3cd-455e-be10-88ae3ecc0b77);

ItemSetCanInteract(ITEMGUID_S_CoS_Temples_Wizard_Statue_AmadiaShrine_Door_de956d00-f103-4612-a793-f5f2744e1092,0);
SetOnStage(ITEMGUID_S_CoS_Temples_Wizard_Ladder_57f98b4c-4912-498a-85b0-fa21b58efdb2,0);
SetOnStage(ITEMGUID_S_CoS_Temples_Wizard_SanctumDoor_003_9961d670-1736-4073-9e84-93d05f4e1a3e,0);
SetOnStage(ITEMGUID_S_CoS_Temples_Wizard_SanctumDoor_004_71c3af8b-6961-40c4-a745-6c90909e94a9,0);
SetOnStage(ITEMGUID_S_CoS_Temples_Wizard_PortalToSanctum_ee5bbc24-a9c4-4012-a581-e6689b39cae9,0);
KBSECTION
//REGION VBs
IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_CoS_Temples_Wizard_OuterVB_Box_85e1f29a-bf23-430f-a017-5ece32a4a5d0)
AND
QueryOnlyOnce("CoS_Temples_Wizard_VB_BurningShips_Once")
THEN
StartVoiceBark("CoS_Temples_Wizard_VB_BurningShips",_Player);

//VB entering the Temple
IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_CoS_SUB_Temples_Wizard_b339683b-fb87-4fe5-93ab-599970df44af)
AND
QueryOnlyOnce("CoS_Temples_Wizard_VB_EnteredAmadiaTemple_Once")
THEN
ProcObjectTimer(_Player,"CoS_Temples_Wizard_VB_EnteredAmadiaTemple_Timer",2500);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"CoS_Temples_Wizard_VB_EnteredAmadiaTemple_Timer")
THEN
StartVoiceBark("CoS_Temples_Wizard_VB_EnteredAmadiaTemple",_Player);


IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_CoS_Temples_Wizard_VB_SanctumPuzzle_Box_f1f08d4e-25a2-4496-b902-3f61b7123af7)
AND
QueryOnlyOnce("CoS_Temples_Wizard_VB_SanctumPuzzle_Once")
THEN
StartVoiceBark("CoS_Temples_Wizard_VB_SanctumPuzzle",_Player);
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_CoS_Temples_Wizard_VB_SanctumPuzzle_Box_f1f08d4e-25a2-4496-b902-3f61b7123af7);


IF
CharacterEnteredTrigger((CHARACTERGUID)_Player,TRIGGERGUID_S_CoS_SUB_Temples_Wizard_b339683b-fb87-4fe5-93ab-599970df44af)
THEN
PartySetFlag(_Player,"QuestUpdate_CoS_WizardTemple_Start");


//END_REGION

//REGION Ladder in and out of Temple

IF
CharacterUsedItem(_Player,ITEMGUID_S_CoS_Temples_Wizard_PortalToTemple_c91cae95-8b2e-40ff-8df2-6e9ecf411555)
THEN
ProcObjectTimer(_Player,"CoS_ToAmadiaTemple",2500);

PROC
ProcObjectTimerFinished(_Player,"CoS_ToAmadiaTemple")
THEN
TeleportTo(_Player,TRIGGERGUID_S_CoS_Temples_Wizard_PortalToTemple_Point_5d0456ec-6911-404d-bf38-87d8b730dae8,"",1);

//Down
IF
CharacterUsedItem(_Player,ITEMGUID_S_CoS_Temples_Wizard_PortalToIsland_0e93ce69-a0a5-4731-b7f8-4274f5da7149)
THEN
ProcObjectTimer(_Player,"CoS_AmadiaTempleToIsland",2500);

PROC
ProcObjectTimerFinished(_Player,"CoS_AmadiaTempleToIsland")
THEN
TeleportTo(_Player,TRIGGERGUID_S_CoS_Temples_Wizard_PortalToIsland_Point_b40e6c91-3132-4228-a229-729034dbd473,"",1);



//END_REGION

IF
ObjectFlagSet("QuestUpdate_CoS_WizardTemple_HoveringRunes",_Player,_)
AND
QueryOnlyOnce("QuestUpdate_CoS_WizardTemple_HoveringRunes_AD_Once")
THEN
Proc_StartDialog(1,"GLO_AD_ActivatedSwitch",_Player);


IF
CharacterUsedItem((CHARACTERGUID)_Player,ITEMGUID_S_CoS_Temples_Wizard_DoorToMainRune_77a589a9-5a03-4cd3-8344-6c4c06cf1074)
THEN
GlobalSetFlag("CoS_Temples_Wizard_OuterSanctumPart_1");
Proc_ShakeCameraForTime(_Player,2000);
Proc_StartDialog(1,"GLO_AD_ActivatedSwitch",_Player);
PartySetFlag(_Player,"QuestUpdate_CoS_WizardTemple_HoveringRunes");


IF
GlobalFlagSet("CoS_Temples_Wizard_OuterSanctumPart_1")
THEN
ItemDestroy(ITEMGUID_S_CoS_Temples_Wizard_DoorToMainRoom_000_1d450993-4e1d-4d91-a549-21ed967823d6);

IF
CharacterUsedItem((CHARACTERGUID)_Player,ITEMGUID_S_CoS_Temples_Wizard_DoorRunesToSanctum_001_59938682-d802-4f8d-a2fd-1c71eb68a16a)
THEN
GlobalSetFlag("CoS_Temples_Wizard_OuterSanctumPart_2");
ItemDestroy(ITEMGUID_S_CoS_Temples_Wizard_DoorToMainRoom_001_e060c40d-0b74-4f1e-aa23-cd8b179744a2);
Proc_ShakeCameraForTime(_Player,2000);
PartySetFlag(_Player,"QuestUpdate_CoS_WizardTemple_HoveringRunes");

IF
CharacterUsedItem((CHARACTERGUID)_Player,ITEMGUID_S_CoS_Temples_Wizard_DoorRunesToSanctum_002_4fbf7bbf-5210-4e2f-9c25-23872d7b6f46)
THEN
SetOnStage(ITEMGUID_S_CoS_Temples_Wizard_PortalToSanctum_ee5bbc24-a9c4-4012-a581-e6689b39cae9,1);
PlayEffect(ITEMGUID_S_CoS_Temples_Wizard_PortalToSanctum_ee5bbc24-a9c4-4012-a581-e6689b39cae9,"RS3_FX_Char_Creatures_Shriker_LightningBolt_Impact_01");
GlobalSetFlag("CoS_Temples_Wizard_OuterSanctumPart_3");
ProcObjectTimer(_Player,"CoS_Temples_Wizard_AD_OpenedPortalToSanctum_Timer",1000);
TriggerSetAtmosphere(TRIGGERGUID_AtmosphereTrigger_CoS_Temple_Amadia_FirstHalf_30ca8745-961b-4b46-9726-8f3768fbbaa4,"6b647fc9-1f97-4f53-840e-656506d8d19f");
Proc_ShakeCameraForTime(_Player,2000);
PartySetFlag(_Player,"QuestUpdate_CoS_WizardTemple_HoveringRunes");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"CoS_Temples_Wizard_AD_OpenedPortalToSanctum_Timer")
THEN
Proc_StartDialog(1,"GLO_AD_ActivatedSwitch",_Player);
PartySetFlag(_Player,"QuestUpdate_CoS_WizardTemple_PortalUnlocked");

//REGION Amadia Buffs
//if Flag applied add the buff
IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Player,_)
AND
DB_CoS_Temples_AmadiaBuffs(_Flag,_Buff)
THEN
ApplyStatus(_Player,_Buff,-1.0);
PlayAnimation(_Player,"Kneel_02");

//If a flag is applied, remove other buffs/Flags
IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Player,_)
AND
DB_CoS_Temples_AmadiaBuffs(_Flag,_)
AND
DB_CoS_Temples_AmadiaBuffs(_OtherFlag,_)
AND
_OtherFlag != _Flag
AND
ObjectGetFlag(_Player,_Otherflag,1)
THEN
ObjectClearFlag(_Player,_OtherFlag,0);

//Keep the buff if player remains in the same region
IF
CharacterStatusRemoved((CHARACTERGUID)_Player,_Buff,_)
AND
DB_CoS_Temples_AmadiaBuffs(_Flag,_Buff)
AND
ObjectGetFlag(_Player,_Flag,1)
THEN
Proc_CoS_Temples_Amadia_CheckStilInTemple(_Player,_Flag);

//REGION DOSTWO-25923

IF
CharacterResurrected(_Player)
AND
DB_CoS_Temples_AmadiaBuffs(_Flag,_Buff)
AND
ObjectGetFlag(_Player,_Flag,1)
THEN
Proc_CoS_Temples_Amadia_CheckStilInTemple(_Player,_Flag);

PROC
ProcRegionEnded("CoS_Main")
THEN
GoalCompleted;


//END_REGION

IF
CharacterLeftTrigger(_Player,(TRIGGERGUID)_Trigger)
AND
DB_CoS_Temples_AmadiaRegions(_Trigger)
AND
DB_CoS_Temples_AmadiaBuffs(_Flag,_Buff)
AND
ObjectGetFlag(_Player,_Flag,1)
THEN
RemoveStatus(_Player,_Buff);

PROC
Proc_CoS_Temples_Amadia_CheckStilInTemple((CHARACTERGUID)_Player,(STRING)_Flag)
AND
DB_CoS_Temples_AmadiaRegions((TRIGGERGUID)_Trigger)
AND
DB_InRegion(_Player,_Trigger)
AND
DB_CoS_Temples_AmadiaBuffs(_Flag,_Buff)
AND
ObjectGetFlag(_Player,_Flag,1)
AND
HasActiveStatus(_Player,_Buff,0)
THEN
ApplyStatus(_Player,_Buff,-1.0);

//Re-Entered Temple
IF
CharacterEnteredTrigger(_Player,_Trigger)
AND
DB_CoS_Temples_AmadiaRegions((TRIGGERGUID)_Trigger)
AND
DB_CoS_Temples_AmadiaBuffs((STRING)_Flag,_Buff)
AND
ObjectGetFlag(_Player,_Flag,1)
THEN
ApplyStatus(_Player,_Buff,-1.0);
Proc_CoS_Temples_Amadia_CheckStilInTemple(_Player,_Flag);


IF
ObjectFlagCleared(_Flag,(CHARACTERGUID)_Player,_)
AND
DB_CoS_Temples_AmadiaBuffs(_Flag,_Buff)
THEN
RemoveStatus(_Player,_Buff);


//LoreMaster
IF
DialogStarted("CoS_Temples_Wizard_Statue_AmadiaShrine",_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
CharacterGetAbility(_Player,"Loremaster",_Val)
AND
_Val > 0
THEN
ObjectSetFlag(_Player,"CoS_Temples_Wizard_Loremaster");

//AirSpecialist
IF
DialogStarted("CoS_Temples_Wizard_Statue_AmadiaShrine",_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
CharacterGetAbility(_Player,"AirSpecialist",_Val)
AND
_Val > 0
THEN
ObjectSetFlag(_Player,"CoS_Temples_Wizard_AirSpecialist");

//FireSpecialist
IF
DialogStarted("CoS_Temples_Wizard_Statue_AmadiaShrine",_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
CharacterGetAbility(_Player,"FireSpecialist",_Val)
AND
_Val > 0
THEN
ObjectSetFlag(_Player,"CoS_Temples_Wizard_FireSpecialist");

//WaterSpecialist
IF
DialogStarted("CoS_Temples_Wizard_Statue_AmadiaShrine",_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
CharacterGetAbility(_Player,"WaterSpecialist",_Val)
AND
_Val > 0
THEN
ObjectSetFlag(_Player,"CoS_Temples_Wizard_WaterSpecialist");

IF
DialogEnded("CoS_Temples_Wizard_Statue_AmadiaShrine",_Id)
AND
DialogGetInvolvedPlayer(_Id,1,_Player)
THEN
ObjectClearFlag(_Player,"CoS_Temples_Wizard_Loremaster",0);
ObjectClearFlag(_Player,"CoS_Temples_Wizard_AirSpecialist",0);
ObjectClearFlag(_Player,"CoS_Temples_Wizard_FireSpecialist",0);
ObjectClearFlag(_Player,"CoS_Temples_Wizard_WaterSpecialist",0);


//END_REGION

//REGION Openning the Door
IF
CharacterUsedItem(_Player,(ITEMGUID)_Rune)
AND
DB_CoS_Temples_Wizard_DoorRunes(_Rune)
THEN
NOT DB_CoS_Temples_Wizard_DoorRunes(_Rune);
Proc_CoS_Temples_Wizard_CheckDoorOpen(_Player);
Proc_ShakeCameraForTime(_Player,2000);

PROC
Proc_CoS_Temples_Wizard_CheckDoorOpen((CHARACTERGUID)_Player)
AND
SysCount("DB_CoS_Temples_Wizard_DoorRunes",1,_Int)
AND
_Int == 0
THEN
GlobalSetFlag("CoS_Temples_Wizard_AmadiaAppear");
PartySetFlag(_Player,"QuestUpdate_CoS_WizardTemple_TrapsDisarmed");


IF
CharacterUsedItem(_Player,ITEMGUID_S_CoS_Temples_Wizard_DoorRune_002_fb774451-94dc-4798-93dc-a8a0043abddb)
AND
GetPosition(ITEMGUID_S_CoS_Temples_Wizard_SanctumDoor_001_6df96d87-a634-4ff0-b2eb-af454d4eb8ee,_X,_Y,_Z)
THEN
SetOnStage(ITEMGUID_S_CoS_Temples_Wizard_SanctumDoor_001_6df96d87-a634-4ff0-b2eb-af454d4eb8ee,0);
SetOnStage(ITEMGUID_S_CoS_Temples_Wizard_SanctumDoor_003_9961d670-1736-4073-9e84-93d05f4e1a3e,1);
Proc_ShakeCameraForTime(_Player,2000);

IF
CharacterUsedItem(_Player,ITEMGUID_S_CoS_Temples_Wizard_DoorRune_001_2ae7a940-d564-41f2-af0c-8598fa7bff33)
AND
GetPosition(ITEMGUID_S_CoS_Temples_Wizard_SanctumDoor_002_da52e0a4-2825-446c-9657-04346159c7bf,_X,_Y,_Z)
THEN
SetOnStage(ITEMGUID_S_CoS_Temples_Wizard_SanctumDoor_002_da52e0a4-2825-446c-9657-04346159c7bf,0);
SetOnStage(ITEMGUID_S_CoS_Temples_Wizard_SanctumDoor_004_71c3af8b-6961-40c4-a745-6c90909e94a9,1);
Proc_ShakeCameraForTime(_Player,2000);

IF
GlobalFlagSet("CoS_Temples_Wizard_AmadiaAppear")
THEN
TimerLaunch("CoS_Temples_Wizard_AmadiaAppear_Timer",6000);
ItemDestroy(ITEMGUID_S_CoS_Temples_Wizard_DoorToMainRoom_e060c40d-0b74-4f1e-aa23-cd8b179744a2);
ItemDestroy(ITEMGUID_S_CoS_Temples_Wizard_SanctumDoor_001_6df96d87-a634-4ff0-b2eb-af454d4eb8ee);
ItemDestroy(ITEMGUID_S_CoS_Temples_Wizard_SanctumDoor_002_da52e0a4-2825-446c-9657-04346159c7bf);
ItemDestroy(ITEMGUID_S_CoS_Temples_Wizard_SanctumDoor_003_9961d670-1736-4073-9e84-93d05f4e1a3e);
ItemDestroy(ITEMGUID_S_CoS_Temples_Wizard_SanctumDoor_004_71c3af8b-6961-40c4-a745-6c90909e94a9);
SetOnStage(ITEMGUID_S_CoS_Temples_Wizard_AmadiaSphere_Blocker_bdecac5d-a9ec-47a0-9a0b-08a39c8e83d8,0);
TriggerSetAtmosphere(TRIGGERGUID_AtmosphereTrigger_CoS_Temple_Amadia_1aa19ace-5c70-492f-8075-13c121e8d296,"6b647fc9-1f97-4f53-840e-656506d8d19f");
SetStoryEvent(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,"StartEventOnSight_RestartSpotting");


IF
TimerFinished("CoS_Temples_Wizard_AmadiaAppear_Timer")
THEN
ProcSetInvulnerable(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,0);
CharacterSetImmortal(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,0);
CharacterSetAnimationOverride(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,"");
ProcCharacterEnableAllCrimes(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc);
SetStoryEvent(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,"CoS_RestoreMoveSpeed");

IF
StoryEvent(_Player,"CoS_Temples_Wizard_AmadiaKnightDialogStart")
THEN
Proc_StartDialog(0,"CoS_Temples_Wizard_KnightOfAmadia",CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,_Player);

IF
DialogStarted("CoS_Temples_Wizard_KnightOfAmadia",_)
AND
QueryOnlyOnce("CoS_Wizard_TeleportToGround_Once")
THEN
PlayAnimation(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,"Teleport_Out_01","CoS_Wizard_TeleportToGround");



IF
StoryEvent(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,"CoS_Wizard_TeleportToGround")
THEN
TeleportTo(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,
						TRIGGERGUID_S_CoS_Temples_Wizard_Amadia_DefaultPoint_8c6f14b0-a16b-41e2-941c-edb339588169,"CoS_Wizard_TeleportToGround_Fx",0);

IF
StoryEvent(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,"CoS_Wizard_TeleportToGround_Fx")
THEN
PlayAnimation(CHARACTERGUID_S_CoS_Temples_Wizard_KnightOfAmadia_c2cc3cdf-2119-4eae-a7ef-ed77aec013bc,"Teleport_In_01","");

//END_REGION

IF
ObjectFlagSet("CoS_WizardAltar_MapMarker",(CHARACTERGUID)_Player,_)
THEN
ShowMapMarker(_Player,"CoS_WizardAltar",1);

//REGION Secret Room
IF
GlobalFlagSet("CoS_Temples_Wizard_AmadiaSecretRoomReveal")
THEN
ProcItemLocalMove(ITEMGUID_S_CoS_Temples_Wizard_Statue_AmadiaShrine_9ef8ad8b-cd81-418b-9baf-5e59cb2d39b2,0.0,0.0,-5.5,1.0,0.0);
ItemSetCanInteract(ITEMGUID_S_CoS_Temples_Wizard_Statue_AmadiaShrine_Door_de956d00-f103-4612-a793-f5f2744e1092,1);

IF
GlobalFlagSet("CoS_Temples_Wizard_AmadiaSecretRoomReveal")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
GetDistanceTo(_Player,ITEMGUID_S_CoS_Temples_Wizard_Statue_AmadiaShrine_9ef8ad8b-cd81-418b-9baf-5e59cb2d39b2,_Dist)
AND
_Dist < 8.0
THEN
Proc_ShakeCameraForTime(_Player,3500);

//END_REGION


//Reward from the Amadia Knight
IF
ObjectFlagSet("CoS_Temples_Wizard_Reward",(CHARACTERGUID)_Player,_)
THEN
CharacterGiveReward(_Player,"COS_WizardTempleGloves");
PartySetFlag(_Player,"QuestUpdate_CoS_WizardTemple_KnightOfAmadia");

//REGION Ladder Roll
IF
CharacterUsedItem(_,ITEMGUID_S_CoS_Temples_Wizard_LadderRoll_a10a69df-049b-4651-8ed1-c64c55df4366)
AND
GetPosition(ITEMGUID_S_CoS_Temples_Wizard_LadderRoll_a10a69df-049b-4651-8ed1-c64c55df4366,_X,_Y,_Z)
AND
GetPosition(ITEMGUID_S_CoS_Temples_Wizard_Ladder_57f98b4c-4912-498a-85b0-fa21b58efdb2,_X1,_Y1,_Z1)
THEN
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01",_X,_Y,_Z);
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01",_X1,_Y1,_Z1);
SetOnStage(ITEMGUID_S_CoS_Temples_Wizard_LadderRoll_a10a69df-049b-4651-8ed1-c64c55df4366,0);
SetOnStage(ITEMGUID_S_CoS_Temples_Wizard_Ladder_57f98b4c-4912-498a-85b0-fa21b58efdb2,1);


//END_REGION

//REGION DOS2EE-1681

IF
ObjectFlagSet("CoS_Temples_AmadiaKnockdown",(CHARACTERGUID)_Player,_)
THEN
SetStoryEvent(_Player,"GEN_FallAndLie");
ApplyStatus(_Player,"KNOCKED_DOWN",1.0,1);

IF
DialogEnded("CoS_Temples_Wizard_Altar",_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"CoS_Temples_AmadiaKnockdown",1)
THEN
ObjectClearFlag(_Player,"CoS_Temples_AmadiaKnockdown");
RemoveStatus(_Player,"KNOCKED_DOWN");
CharacterSetAnimationOverride(_Player,"");
PlayAnimation(_Player,"knockdown_getup");

//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "CoS_Temples"
