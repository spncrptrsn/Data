Version 1
SubGoalCombiner SGC_AND
INITSECTION
// https://sites.google.com/a/larian.com/larian-wikisite/home/quest-design-documentation/graveyard---ryker-s-contract
// https://www.lucidchart.com/documents/edit/1a47e3d7-089a-49c7-907a-ba1622929174#
// See also RC_GY_RykersBodyguards, RC_GY_RykersContract

ProcTriggerRegisterForPlayers(TRIGGERGUID_S_RC_GY_RykersRoomArea_5ff7ebd2-30d1-4df9-8196-07432752b3e8);
TriggerRegisterForCharacter(TRIGGERGUID_S_RC_GY_RykersRoomArea_5ff7ebd2-30d1-4df9-8196-07432752b3e8,CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4);

// General flags and useful stuff for Ryker
DB_Dialogs(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,"RC_GY_Ryker");
DB_Ghosts(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,CHARACTERGUID_S_RC_GY_RykerGhost_bc364b87-401f-4f24-a9e2-43eda1e90d7a,"RC_GY_RykerGhost");
DB_DeadEvent(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,"RC_GY_RykerDead");
DB_KilledEvent(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,"RC_GY_RykerKiller");

// Party of the Character DB_RC_GY_RykersContract_CharacterParty();

SetOnStage(ITEMGUID_S_RC_GY_RykersSecretChamber_Hatch_e2f19559-8a6c-479b-8454-189a1dac255a,0);
SetOnStage(ITEMGUID_S_RC_GY_RykersCarpetRolled_7543a9fc-c34a-4fcc-a4da-6289203145b9,0);
SetOnStage(ITEMGUID_S_RC_GY_RykersCarpet_1b69c50c-91ee-4edd-8055-a43898acc00d,1);


// Custom reaction to attacks
DB_CrimeReaction_CustomWarning(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,"Assault","RC_GY_Ryker");
DB_CrimeReaction_CustomWarning(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,"Trespassing","RC_GY_Ryker_TrespassingWarning");

DB_RC_GY_RykerHouse_TalkingDoors((ITEMGUID)ITEMGUID_S_RC_GY_RykerHouse_TalkingDoor_001_33aec338-67f0-498f-9832-1790febd7136);
DB_RC_GY_RykerHouse_TalkingDoors(ITEMGUID_S_RC_GY_RykerHouse_TalkingDoor_002_c704db5e-d663-440f-8622-e9d3dfbe6759);
DB_RC_GY_RykerHouse_TalkingDoors_EffectPoints((TRIGGERGUID)TRIGGERGUID_S_RC_GY_RykerHouse_GargoyleEffectTrigger_000_c6e7edcf-bf0a-453e-a9e5-acaf570fa6e5);
DB_RC_GY_RykerHouse_TalkingDoors_EffectPoints(TRIGGERGUID_S_RC_GY_RykerHouse_GargoyleEffectTrigger_001_e30c612d-fdc1-499f-b24c-57b9ab1b0c7b);
DB_RC_GY_RykerHouse_TalkingDoors_EffectPoints(TRIGGERGUID_S_RC_GY_RykerHouse_GargoyleEffectTrigger_002_d0653a18-9ef2-4ba3-881d-dae8faa96361);
//DB_RC_GY_RykerHouse_SneakerFromCellar(_Character);

DB_OneShotUser_VoicebarkTrigger(TRIGGERGUID_S_RC_GY_RykersSecretChamber_VB_f73dc7d4-a360-42b9-8b19-3367b46fc844,"RC_GY_VB_DiscoverRykersSecretChamber");
DB_OneShotUser_VoicebarkTrigger(TRIGGERGUID_S_RC_GY_RykersSecretChamber_LabVB_64d7dc99-ca92-442a-ab1e-90690b3d38c8,"RC_GY_VB_RykersSecretChamber_Lab");
DB_OneShotUser_VoicebarkTrigger(TRIGGERGUID_S_RC_GY_XP_RykersSecretChamber_Library_127bc331-2aea-4e31-8713-13838371af89,"RC_GY_VB_RykersSecretChamber_Library");

ApplyStatus(ITEMGUID_FUR_Humans_Citz_LongestTable_Poor_A1_002_6ee19fe1-a772-46d5-a207-0bb9e4d83b60,"FROZEN",-1.0);
KBSECTION
//REGION Killer of Ryker
IF
ObjectFlagSet("RC_GY_RykerKiller",_Player,_)
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
Proc_RC_GY_KilledRyker(_Player);
//END_REGION


//REGION Rykers weakness
IF
CharacterStatusAttempt(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,"FORTIFIED",_)
AND
GetVarInteger(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,"FortifiedArmorPercentageDecrease",_Decrease)
AND
Real(_Decrease,_DecreaseFloat)
AND
CharacterGetArmorPercentage(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,_Armor)
AND
RealSubtract(_Armor,_DecreaseFloat,_NewArmor)
THEN
RemoveStatus(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,"FORTIFIED");
CharacterSetArmorPercentage(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,_NewArmor);

IF
CharacterStatusAttempt(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,"FORTIFIED",_Player)
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
Proc_StartDialog(1,"RC_GY_AD_RykerWeakness",CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4);
ProcSetHostileToIndivPlayer(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,_Player);

IF
AutomatedDialogStarted("RC_GY_AD_RykerWeakness",_)
THEN
ObjectSetFlag(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,"RC_GY_RykerSaidWeaknessAD");
//END_REGION


//REGION House entry points
// Talking Doors
PROC
ProcBlockUseOfItem(_Character,_TalkingDoor)
AND
DB_RC_GY_RykerHouse_TalkingDoors(_TalkingDoor)
AND
DB_IsPlayer(_Character)
AND
GlobalGetFlag("RC_GY_RykerHouse_TalkingDoorsUnlocked",0)
THEN
DB_CustomUseItemResponse(_Character,_TalkingDoor,0);
Proc_StartDialog(0,"RC_GY_RykerHouse_TalkingDoors",_TalkingDoor,_Character);

IF
DialogEnded("RC_GY_RykerHouse_TalkingSkull",_DlgInst)
AND
GlobalGetFlag("RC_GY_RykerHouse_UnlockDoors",1)
THEN
Proc_RC_GY_RykerHouse_UnlockTalkingDoors();

IF
DialogEnded("RC_GY_RykerHouse_TalkingDoors",_DlgInst)
AND
GlobalGetFlag("RC_GY_RykerHouse_UnlockDoors",1)
AND
DialogGetInvolvedNPC(_DlgInst,1,_TalkingDoor)
AND
ObjectIsItem(_TalkingDoor,1)
AND
DB_RC_GY_RykerHouse_TalkingDoors((ITEMGUID)_TalkingDoor)
THEN
Proc_RC_GY_RykerHouse_PlayEffectOnUnlockDoors();
Proc_RC_GY_RykerHouse_UnlockTalkingDoors();
ItemOpen(_TalkingDoor);

PROC
Proc_RC_GY_RykerHouse_PlayEffectOnUnlockDoors()
AND
NOT DB_GlobalFlag("RC_GY_RykerHouse_TalkingDoorsUnlocked")
AND
DB_RC_GY_RykerHouse_TalkingDoors_EffectPoints(_Trigger)
THEN
PlayEffect(_Trigger,"RS3_FX_GP_Impacts_Air_LightningBolt_Vertical_01");

PROC
Proc_RC_GY_RykerHouse_UnlockTalkingDoors()
AND
NOT DB_GlobalFlag("RC_GY_RykerHouse_TalkingDoorsUnlocked")
AND
DB_RC_GY_RykerHouse_TalkingDoors(_TalkingDoor)
THEN
ItemUnLock(_TalkingDoor);

PROC
Proc_RC_GY_RykerHouse_UnlockTalkingDoors()
THEN
GlobalSetFlag("RC_GY_RykerHouse_TalkingDoorsUnlocked");

// Talking Skull (entry from cellar)
PROC
ProcBlockUseOfItem(_Player,ITEMGUID_05m_WoodenHouse_A_Stairs_B_Item_000_292d8187-a1a5-4ffe-ab0e-886556800518)
AND
DB_IsPlayer(_Player)
AND
GlobalGetFlag("RC_GY_RykerHouse_TalkingDoorsUnlocked",0)
THEN
DB_CustomUseItemResponse(_Player,ITEMGUID_05m_WoodenHouse_A_Stairs_B_Item_000_292d8187-a1a5-4ffe-ab0e-886556800518,0);
Proc_StartDialog(0,"RC_GY_RykerHouse_TalkingSkull",ITEMGUID_S_RC_GY_RykerHouse_TalkingSkull_f6daa14d-0b30-4230-b5b5-2fde57e1889e,_Player);

IF
CharacterUsedItem(_Player,ITEMGUID_05m_WoodenHouse_A_Stairs_B_Item_000_292d8187-a1a5-4ffe-ab0e-886556800518)
AND
DB_IsPlayer(_Player)
THEN
DB_RC_GY_RykerHouse_SneakerFromCellar(_Player);

IF
DialogEnded("RC_GY_RykerHouse_TalkingSkull",_DlgInst)
AND
DialogGetInvolvedPlayer(_DlgInst,1,_Character)
AND
ObjectIsCharacter(_Character,1)
AND
DB_IsPlayer((CHARACTERGUID)_Character)
AND
ObjectGetFlag(_Character,"RC_GY_RykerHouse_CellarIntruderTeleport",1)
THEN
ObjectClearFlag(_Character,"RC_GY_RykerHouse_CellarIntruderTeleport",0);
TeleportTo(_Character,TRIGGERGUID_S_RC_GY_TP_RykerHouse_CellarIntruder_cbcf602c-ad62-4e9e-820b-7f2ea4bf2f64);
SysClear("DB_RC_GY_RykerHouse_SneakerFromCellar",1);

IF
DialogEnded("RC_GY_RykerHouse_TalkingSkull",_DlgInst)
AND
GlobalGetFlag("RC_GY_RykerHouse_TalkingDoorsUnlocked",1)
AND
DialogGetInvolvedPlayer(_DlgInst,1,_Character)
AND
ObjectIsCharacter(_Character,1)
AND
DB_IsPlayer((CHARACTERGUID)_Character)
THEN
TeleportTo(_Character,TRIGGERGUID_S_RC_GY_TP_RykerHouse_1_FromCellar_ca690e95-3afe-4b01-9e5e-faf8002d9e6d);


IF
GlobalFlagSet("RC_GY_RykerDead")
AND
GlobalGetFlag("RC_GY_RykerHouse_TalkingDoorsUnlocked",0)
THEN
Proc_RC_GY_RykerHouse_UnlockTalkingDoors();
//END_REGION



//REGION Rykers reaction to bodyguard attacked

PROC
Proc_RC_GY_RykersBodyguardAttacked((CHARACTERGUID)_Bodyguard,(CHARACTERGUID)_Player)
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
CharacterIsInCombat(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,0)
AND // only when Ryker sees the assault
CharacterCanSee(CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4,_Player,1)
THEN
Proc_StartDialog(1,"RC_GY_AD_Ryker_OnBodyguardAttacked",CHARACTERGUID_S_RC_GY_Ryker_522b8095-b539-4a82-84f3-bf8e5a74dfe4);

//END_REGION



//REGION Region triggers
IF
CharacterEnteredTrigger(_Character,TRIGGERGUID_S_RC_GY_SUB_RykerHouse_GroundFloor_f685aad8-a811-4d5c-9e90-81e5a66456cc)
THEN
ProcShowMarker(_Character,"RC_GY_RykerHouse");

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_RC_GY_SUB_RykerHouse_GroundFloor_f685aad8-a811-4d5c-9e90-81e5a66456cc)
AND
UserGetFlag(_Player,"RC_GY_DiscoveredRykerHouse",0)
AND
QRY_IsEmptyDB("DB_RC_GY_RykerHouse_SneakerFromCellar",1)
THEN
// Start VB, set flag, add map marker, update quest log
UserSetFlag(_Player,"RC_GY_DiscoveredRykerHouse");
StartVoiceBark("RC_GY_VB_DiscoverRykerHouse",_Player);

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_RC_GY_SUB_RykerHouse_GroundFloor_f685aad8-a811-4d5c-9e90-81e5a66456cc)
AND
UserGetFlag(_Player,"RC_GY_DiscoveredRykerHouse",0)
AND
DB_RC_GY_RykerHouse_SneakerFromCellar(_Char)
AND
CharacterIsInPartyWith(_Player,_Char,0)
THEN
// Start VB, set flag, add map marker, update quest log
UserSetFlag(_Player,"RC_GY_DiscoveredRykerHouse");
StartVoiceBark("RC_GY_VB_DiscoverRykerHouse",_Player);

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_RC_GY_SUB_RykerHouse_GroundFloor_f685aad8-a811-4d5c-9e90-81e5a66456cc)
AND
UserGetFlag(_Player,"RC_GY_DiscoveredRykerHouse",0)
AND
DB_RC_GY_RykerHouse_SneakerFromCellar(_Char)
AND
CharacterIsInPartyWith(_Player,_Char,1)
THEN
// Start VB, set flag, add map marker, update quest log
UserSetFlag(_Player,"RC_GY_DiscoveredRykerHouse");
StartVoiceBark("RC_GY_VB_RykersHouseEnteredFromBasement",_Player);

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_RC_GY_SUB_Graveyard_59ac9cef-edf4-492f-906c-fbde552e68d3)
AND
UserGetFlag(_Player,"RC_GY_DiscoveredGraveyard",0)
THEN
UserSetFlag(_Player,"RC_GY_DiscoveredGraveyard");
StartVoiceBark("RC_GY_VB_DiscoverGraveyard",_Player);
//END_REGION
EXITSECTION
SysClear("DB_RC_GY_RykerHouse_TalkingDoors",1);
SysClear("DB_RC_GY_RykerHouse_TalkingDoors_EffectPoints",1);
ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
