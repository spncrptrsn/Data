Version 1
SubGoalCombiner SGC_AND
INITSECTION
// a.k.a. Cover the Tracks
//DB_Dialogs(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_UnderTavern_DwarfBoss");
//DB_Dialogs(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"RC_DW_UnderTavern_CapturedAssassin");
DB_Dialogs(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"RC_DW_UnderTavern_QuestioningAssassin");
DB_Dialogs(S_RC_DW_UnderTavern_DwarfBossGuard_ec3c854d-f5ef-426a-914e-47ed9147b6f1, "RC_DW_UnderTavern_DwarfBossGuard");

//--- DwarfBoss
ItemToInventory(ITEMGUID_S_RC_DW_MagisterReward_LoharKey_f986a4d5-653b-416e-9a1f-5e153ebef28c,CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784);
DB_HasStoryEvent(ITEMGUID_S_RC_DW_MagisterReward_LoharKey_f986a4d5-653b-416e-9a1f-5e153ebef28c,"RC_DW_HasLoharKey");
DB_GiveItemToEvent(ITEMGUID_S_RC_DW_MagisterReward_LoharKey_f986a4d5-653b-416e-9a1f-5e153ebef28c,"RC_DW_GiveLoharKey");
DB_HasStoryEvent(ITEMGUID_S_QUEST_RC_DW_UnderTavern_LoharRewardDagger_4bf0e673-1c23-41ae-8ae5-7f263942c426,"RC_DW_HasLoharDagger");
DB_GiveItemToEvent(ITEMGUID_S_QUEST_RC_DW_UnderTavern_LoharRewardDagger_4bf0e673-1c23-41ae-8ae5-7f263942c426,"RC_DW_GiveLoharDagger");

DB_Ghosts(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_Ghost_7cb1ebac-b400-44a4-92e6-beaebb5f027c,"RC_DW_UnderTavern_DwarfBossGhost");



TriggerRegisterForItems((TRIGGERGUID)S_RC_DW_AnharPortrait_Box_db78a0a6-09f2-42a1-b98f-6d3dd8b3cb3e);

//--- Thugs
DB_RC_DW_DwarfBoss_Thugs((CHARACTERGUID)S_RC_DW_LilianThug1_3d063a8a-a0a1-409e-907f-ecfb6741935c,(TRIGGERGUID)S_RC_DW_Behavior_LilianThug1_06b8724b-d15a-49be-9b50-83dbf300855a,"RC_DW_UnderTavern_DwarfBossThug_01"); //changed into DB_Dialogs after the intro scene
DB_RC_DW_DwarfBoss_Thugs((CHARACTERGUID)S_RC_DW_LilianThug2_5443de0f-115b-4581-a92a-fee99ca2a2ad,(TRIGGERGUID)S_RC_DW_Behavior_LilianThug2_8e0d2a6a-f54a-41be-a2bc-04bfe8533d6d,"RC_DW_UnderTavern_DwarfBossThug_02");
SetHasDialog(S_RC_DW_LilianThug1_3d063a8a-a0a1-409e-907f-ecfb6741935c,1);
SetHasDialog(S_RC_DW_LilianThug2_5443de0f-115b-4581-a92a-fee99ca2a2ad,1);
DB_OneShotPlayerTrigger(S_RC_DW_LilianThugs_6d63083c-abe3-4fb4-81f6-bd26b227b406);

//Assassin default
CharacterSetAnimationOverride((CHARACTERGUID)S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"Execution_01_Loop");
CharacterSetHitpointsPercentage(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,20.0);
CreateSurface(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"SurfaceBlood",0.5,-1.0);
TriggerRegisterForCharacter(TRIGGERGUID_S_RC_DW_UnderTavern_OwnershipTrigger_DwarfBoss_c8c5300c-a815-48de-ab73-2e49cffc17ef,(CHARACTERGUID)S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2);

//--- Assissin killing after Mordus quest finished
DB_RC_DW_DwarfBossGroup((CHARACTERGUID)S_RC_DW_LilianThug1_3d063a8a-a0a1-409e-907f-ecfb6741935c);
DB_RC_DW_DwarfBossGroup((CHARACTERGUID)S_RC_DW_LilianThug2_5443de0f-115b-4581-a92a-fee99ca2a2ad);
DB_RC_DW_DwarfBossGroup((CHARACTERGUID)S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784);

DB_RC_DW_DwarfBoss_ThugsFinalSeat((CHARACTERGUID)S_RC_DW_LilianThug1_3d063a8a-a0a1-409e-907f-ecfb6741935c,(ITEMGUID)S_RC_DW_UnderTavern_LoharThug1Final_18d9b873-fce2-4a14-91de-8fc4270563d3);
DB_RC_DW_DwarfBoss_ThugsFinalSeat((CHARACTERGUID)S_RC_DW_LilianThug2_5443de0f-115b-4581-a92a-fee99ca2a2ad,(ITEMGUID)S_RC_DW_UnderTavern_LoharThug2Final_2dc8bb26-7b72-451f-83b1-464f1e8f3a9b);

// Assassin's grave once we're done with her - MOVED TO PROC_RC_DW_DisappearAssassin
//DB_ShovelArea(S_RC_DW_LoharAssassinsGrave_ShovelTrigger_4edaf241-7958-4d1e-a9d6-26a82f91fb71,"RC_DW_LoharAssassinsGrave",ITEMGUID_S_RC_DW_LoharAssassinsGrave_DirtPile_4c15a07b-6bc1-49f1-997a-35a80a9db8ae);
// Not DB_ShovelRewardCharacterAppear() because that doesn't work with dead NPCs
//DB_ShovelRewardItemScatter("RC_DW_LoharAssassinsGrave",CHARACTERGUID_S_ARX_Outskirts_DeathFog_ForestPile_Bear_75f4ee4d-bed5-4d02-9fd3-b220eeb83454);
//DB_ShovelRewardEvent("RC_DW_LoharAssassinsGrave","RC_DW_LoharAssassinsGrave_SetDeadAssassinOnStage");
SetOnStage(S_RC_DW_AssassinsGrave_DirtPile_4c15a07b-6bc1-49f1-997a-35a80a9db8ae,0);

//--- Letter
SetOnStage(ITEMGUID_S_RC_DW_DwarfBoss_Letter_37e51ad5-5586-46f0-ae06-495ea62e67d1,0);



//--- Lohar's Head
SetOnStage(S_RC_DW_Limb_LoharsHead_15bb128a-3557-48a0-add6-1da6a41c6b60, 0);

//CharacterSetImmortal(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, 1);
CharacterDisableAllCrimes(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2);

TriggerRegisterForCharacter(TRIGGERGUID_S_RC_DW_UnderTavern_OwnershipTrigger_DwarfBoss_c8c5300c-a815-48de-ab73-2e49cffc17ef,CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2);
DB_DeadEvent(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"RC_DW_CapturedAssassinUnavailable");
DB_DeadEvent(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_LoharDead");

// Dependencies (not just RC_DW_QuestFromJulian, because then this will create dependency chains
// with rules that specify RC_DW_QuestFromJulian as second parameter to this routine
Proc_JDF_CreateCustomFlagsDependency("RC_DW_QuestFromJulian_InvestigateLoharPremise", "RC_DW_QuestFromJulian", "JDF_QuestUpdate_RC_DW_DwarvenCriminals_ContactLohar", "QuestUpdate_RC_DW_DwarvenCriminals_ContactLohar");
Proc_JDF_CreateCustomFlagsDependency("RC_DW_QuestFromJulian_InvestigateLoharPremise", "RC_DW_QuestFromJulian", "QuestUpdate_RC_DW_DwarvenCriminals_CargoHint", "QuestUpdate_RC_DW_DwarvenCriminals_CargoHint");
Proc_JDF_CreateCustomFlagsDependency("RC_DW_QuestFromJulian_InvestigateLoharPremise", "RC_DW_QuestFromJulian", "QuestUpdate_RC_DW_CoverTheTracks_AbandonedHouse_Start", "QuestUpdate_RC_DW_DwarvenCrminals_LoharProblem");
Proc_JDF_CreateCustomFlagsDependency("RC_DW_QuestFromJulian_InvestigateLoharPremise", "RC_DW_QuestFromJulian", "QuestUpdate_RC_DW_WC_AtaraxianLair_KnowAboutDeathFogInCave", "QuestUpdate_RC_DW_DwarvenCriminals_KnowAboutDeathFogInCave");
Proc_JDF_CreateCustomFlagsDependency("RC_DW_QuestFromJulian_InvestigateLoharPremise", "RC_DW_QuestFromJulian", "RC_DW_WC_AskedMordusAboutDeathFogWeapons", "QuestUpdate_RC_DW_DwarvenCriminals_KnowDeathfog");
Proc_JDF_CreateCustomFlagsDependency("RC_DW_QuestFromJulian_InvestigateLoharPremise", "RC_DW_QuestFromJulian", "JDF_QuestUpdate_RC_DW_DwarvenCriminals_FoundWrongLetters", "QuestUpdate_RC_DW_DwarvenCriminals_FoundWrongLetters");

DB_SharedQuestLessFlag("RC_DW_QuestFromJulian_InvestigateLoharPremise");
KBSECTION
//REGION Tests
IF
TextEventSet("lohar_reset")
THEN
ObjectClearFlag(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_DeliverMordusNote",0);
ObjectClearFlag(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_GaveMordusAmulet",0);
GlobalClearFlag("RC_DW_WC_CapturedDwarfIsAtUnderTavetrn");

IF
TextEventSet("lohar_reset")
AND
DB_IsPlayer(_Player)
AND
DB_QuestDef_State("RC_DW_CoverTheTracks",_State)
AND
StringConcatenate("QuestUpdate_RC_DW_CoverTheTracks_",_State,_FullState)
THEN
PartyClearFlag(_Player,_FullState,0);

IF
TextEventSet("lohar_reset")
AND
DB_IsPlayer(_Player)
THEN
ObjectClearFlag(_Player,"QuestAdd_RC_DW_CoverTheTracks",0);
ObjectClearFlag(_Player,"RC_DW_FailedLiliansPersuasion",0);
ObjectClearFlag(_Player,"RC_DW_GotDwarfBossAbandonedHouseJob",0);
ObjectClearFlag(_Player,"RC_DW_GotDwarfBossJob",0);
ObjectClearFlag(_Player,"RC_DW_CoverTheTracks_Completed",0);
ObjectClearFlag(_Player,"QuestUpdate_RC_DW_SurpriseDate_ThugsGaveLoot",0);
ObjectClearFlag(_Player,"RC_DW_DS_LetLilianAssassinGo",0);
ObjectClearFlag(_Player,"RC_DU_ToldLilianAboutCaravan",0);
PartyClearFlag(_Player,"RC_DW_WC_AskedMordusAboutDeathFogWeapons",0);
PartyClearFlag(_Player,"QuestUpdate_RC_DW_CoverTheTracks_AbandonedHouse_NoteRead",0);
PartyClearFlag(_Player,"QuestUpdate_RC_DW_CoverTheTracks_AbandonedHouse_NoteDelivered",0);
PartyClearFlag(_Player,"QuestUpdate_RC_DW_CoverTheTracks_AbandonedHouse_NoteDeliveredSecond",0);
PartyClearFlag(_Player,"RC_DW_MagisterHaveEmpressLetter",0);
UserClearFlag(_Player,"RC_DW_Caravan_Blamed_Dwarf",0);
UserClearFlag(_Player,"RC_DU_WarmaidenToldOfAnhar",0);
UserClearFlag(_Player,"RC_DU_HeardAnharSide",0);

PROC
Proc_RC_DW_Debug_GiveItemToPlayer1((ITEMGUID)_Item)
AND
DB_IsPlayer(_Player)
AND
NOT DB_OnlyOncePlayer(1)
THEN
DB_OnlyOncePlayer(1);
ItemToInventory(_Item,_Player);

PROC
Proc_RC_DW_Debug_SetFlagOnPlayer1((STRING)_Flag)
AND
DB_IsPlayer(_Player)
AND
NOT DB_OnlyOncePlayer(1)
THEN
DB_OnlyOncePlayer(1);
ObjectSetFlag(_Player,_Flag);

IF
TextEventSet("lohar_knowanhar")
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"RC_DU_SeenCaraven");
ObjectSetFlag(_Player,"RC_DU_HeardAnharSide");

IF
TextEventSet("lohar_blame")
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"RC_DW_Caravan_Blamed_Dwarf");

IF
TextEventSet("lohar_magletter")
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"RC_DW_MagisterHaveEmpressLetter");

IF
TextEventSet("lohar_getjob1")
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_DW_CoverTheTracks_AbandonedHouse_Start");
ObjectSetFlag(_Player,"RC_DW_GotDwarfBossAbandonedHouseJob");

IF
TextEventSet("lohar_getjob2")
AND
CharacterGetHostCharacter(_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_DW_CoverTheTracks_AbandonedHouse_Start");
ObjectSetFlag(_Player,"RC_DW_GotDwarfBossAbandonedHouseJob");
PartySetFlag(_Player,"RC_DW_GotDwarfBossJob");
PartySetFlag(_Player,"QuestUpdate_RC_DW_CoverTheTracks_AbandonedHouse_NoteRead");
PartySetFlag(_Player,"QuestUpdate_RC_DW_CoverTheTracks_AbandonedHouse_NoteDelivered");

IF
TextEventSet("lohar_getnotes")
THEN
Proc_RC_DW_Debug_GiveItemToPlayer1((ITEMGUID)ITEMGUID_S_RC_DW_MordusNote_77e74451-9fd9-4a84-a92b-48e9d296cee6);

IF
TextEventSet("lohar_getamulet")
THEN
Proc_RC_DW_Debug_GiveItemToPlayer1((ITEMGUID)QUEST_RC_Amulet_Mordus_000_49de20a2-8ab8-4424-b77e-d871884faaa9);

IF
TextEventSet("lohar_getletter")
THEN
Proc_RC_DW_Debug_GiveItemToPlayer1((ITEMGUID)ITEMGUID_RC_WC_EmpressLetter_ffaa5485-ccc8-4e66-a69b-52316fdc7c82);

IF
TextEventSet("lohar_finished")
AND
CharacterGetHostCharacter(_Player)
THEN
PROC_RC_DW_LoharFinished(_Player);

IF
DB_OnlyOncePlayer(1)
THEN
NOT DB_OnlyOncePlayer(1);
//END_REGION

//REGION Transfer HasMet tag from Lohar to his ghost

IF
DB_HasMet(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,_Player,_)
THEN
ObjectSetFlag(_Player,"RC_DW_HasMet_DwarfBoss");

//END_REGION

//REGION Delivering The Mordus Notes
IF
ObjectFlagSet("RC_DW_DeliverMordusNote", CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,  _ID)
AND
DialogGetInvolvedPlayer(_ID, 1, _Player)
AND
ItemIsInPartyInventory(ITEMGUID_S_RC_DW_MordusNote_77e74451-9fd9-4a84-a92b-48e9d296cee6, (CHARACTERGUID)_Player, 1, 1)
THEN
ItemToInventory(ITEMGUID_S_RC_DW_MordusNote_77e74451-9fd9-4a84-a92b-48e9d296cee6, CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784);

//END_REGION

//REGION Dwarf boss negative attitude adjustment
IF
ObjectFlagSet("RC_DW_LoharDecreaseAttitude",(CHARACTERGUID)_Player,_Id)
THEN
CharacterAddAttitudeTowardsPlayer(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,_Player,-20);
ObjectClearFlag(_Player,"RC_DW_LoharDecreaseAttitude",_Id);
//END_REGION

//REGION Quests rewards & update

IF
ObjectFlagSet("RC_DW_GaveMordusAmulet",CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, _ID)
AND
DialogGetInvolvedPlayer(_ID,1,_Player)
AND
CharacterIsDead(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,0)
AND
QryTakeItemFromMagicPockets(_Player,(ITEMGUID)QUEST_RC_Amulet_Mordus_000_49de20a2-8ab8-4424-b77e-d871884faaa9,CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784)
THEN
PROC_RC_DW_LoharFinished((CHARACTERGUID)_Player);

IF
ObjectFlagSet("RC_DW_GaveMordusAmulet",CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, _ID)
AND
DialogGetInvolvedPlayer(_ID,1,_Player)
AND
CharacterIsDead(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,1)
AND
// Only show, don't give, because this quest can also be completed when Lohar is dead (talking to his ghost),
// and we don't want to transfer the item then to Lohar's corpse
QryTakeItemFromMagicPockets(_Player,(ITEMGUID)QUEST_RC_Amulet_Mordus_000_49de20a2-8ab8-4424-b77e-d871884faaa9,_Player)
THEN
PROC_RC_DW_LoharFinished((CHARACTERGUID)_Player);

PROC
PROC_RC_DW_LoharFinished((CHARACTERGUID)_Player)
THEN
CharacterGiveReward((CHARACTERGUID)_Player, "RC_DW_Lohar_CoverTheTracks_Completed"); // replace with gold & source weapon
PartySetFlag((CHARACTERGUID)_Player,"QuestUpdate_RC_DW_CoverTheTracks_ShowedMordusAmuletToLohar");
PartySetFlag((CHARACTERGUID)_Player,"GLO_SetTag_DWARF-FRIEND");
GlobalSetFlag("RC_DW_Lohar_DealtWith");

// Queen's letter: Lohar destroys it when he gets it, to prevent it from falling in the wrong hands
// (-> stealing back and handing to Julian not possible)
IF
ObjectFlagSet("RC_WC_HasEmpressLetter",S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,_)
THEN
ItemDestroy(RC_WC_EmpressLetter_ffaa5485-ccc8-4e66-a69b-52316fdc7c82);

// Talk to Lohar
IF
DialogStarted("RC_DW_UnderTavern_DwarfBoss",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,_)
THEN
PartySetFlag((CHARACTERGUID)_Player,"JDF_QuestUpdate_RC_DW_DwarvenCriminals_ContactLohar");

IF
DialogStarted("RC_DW_UnderTavern_QuestioningAssassin",_Inst)
AND
DB_DialogPlayers(_Inst,_Player,_)
THEN
PartySetFlag((CHARACTERGUID)_Player,"JDF_QuestUpdate_RC_DW_DwarvenCriminals_ContactLohar");



// Dead Lohar
IF
CharacterSawCharacter(_Player,CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784)
AND
DB_IsPlayer(_Player)
AND
DB_Dead(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784)
THEN
Proc_RC_DW_ShadowOverDriftwood_CheckGive_LoharDead(_Player);

IF
CharacterDied(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784)
AND
DB_IsPlayer(_Player)
AND
CharacterCanSee(_Player,CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,1)
THEN
Proc_RC_DW_ShadowOverDriftwood_CheckGive_LoharDead(_Player);

PROC
Proc_RC_DW_ShadowOverDriftwood_CheckGive_LoharDead((CHARACTERGUID)_Player)
AND
QuestIsClosed(_Player,"RC_DW_ShadowOverDriftwood",0)
AND
QuestAccepted(_Player,"RC_DW_ShadowOverDriftwood",1)
AND
PartyGetFlag(_Player,"QuestUpdate_RC_DW_ShadowOverDriftwood_ShowedMordusAmuletToLohar",0)
AND
PartyGetFlag(_Player,"QuestUpdate_RC_DW_ShadowOverDriftwood_ShowedMordusAmuletToLoharSecond",0)
THEN
PartySetFlag(_Player,"QuestUpdate_RC_DW_ShadowOverDriftwood_LoharDead");

PROC
Proc_RC_DW_ShadowOverDriftwood_CheckGive_LoharDead((CHARACTERGUID)_Player)
AND
QuestAccepted(_Player,"RC_DW_DwarvenCriminals",1)
AND
PartyGetFlag(_Player,"RC_DW_AssassinateLohar",0)
THEN
PartySetFlag(_Player,"QuestUpdate_RC_DW_DwarvenCriminals_KilledLoharTooSoon");


// Read letters
IF
GameBookInterfaceClosed(_Letter,_Player)
AND
DB_RC_DW_DwarvenCriminals_FoundWrongLetters(_Letter)
THEN
PartySetFlag(_Player,"JDF_QuestUpdate_RC_DW_DwarvenCriminals_FoundWrongLetters");


//END_REGION

//REGION Dwarf boss goes hostile

IF
DialogEnded("RC_DW_UnderTavern_DwarfBoss",_Inst)
AND
DB_DialogPlayers(_Inst, _Player, 1)
AND
PartyGetFlag((CHARACTERGUID)_Player,"RC_DW_GotDwarfBossHostile",1)
THEN
CharacterSetTemporaryHostileRelation(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, _Player);

IF
DialogEnded("RC_DW_UnderTavern_DwarfBoss_Beast",_Inst)
AND
DB_DialogPlayers(_Inst, _Player, 1)
AND
PartyGetFlag((CHARACTERGUID)_Player,"RC_DW_GotDwarfBossHostile",1)
THEN
CharacterSetTemporaryHostileRelation(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, _Player);

//END_REGION

//REGION Dwarf boss thugs scene
IF
CharacterLeftTrigger(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,TRIGGERGUID_S_RC_DW_UnderTavern_OwnershipTrigger_DwarfBoss_c8c5300c-a815-48de-ab73-2e49cffc17ef)
THEN
GlobalSetFlag("RC_DW_CapturedAssassinUnavailable");

IF
CharacterEnteredTrigger(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,TRIGGERGUID_S_RC_DW_UnderTavern_OwnershipTrigger_DwarfBoss_c8c5300c-a815-48de-ab73-2e49cffc17ef)
AND
NOT DB_Dead(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2)
THEN
GlobalClearFlag("RC_DW_CapturedAssassinUnavailable");

PROC
ProcOneShotTriggerEntered(_,S_RC_DW_LilianThugs_6d63083c-abe3-4fb4-81f6-bd26b227b406)
AND
GlobalGetFlag("RC_DW_CoverTheTracks_ThugsSceneCancelled",0)
/*AND
QRY_StartDialog(1,"RC_DW_UnderTavern_AD_DwarfBossAndThugs",
					CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,
					CHARACTERGUID_S_RC_DW_LilianThug1_3d063a8a-a0a1-409e-907f-ecfb6741935c,
					CHARACTERGUID_S_RC_DW_LilianThug2_5443de0f-115b-4581-a92a-fee99ca2a2ad,
					CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2)*/
THEN
CharacterSetReactionPriority(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_DwarfBoss_InterrogatingAssassin",1000);
DB_RC_DW_CoverTheTracks_LilianThugsDialog(1);

PROC
ProcOneShotTriggerEntered(_,S_RC_DW_LilianThugs_6d63083c-abe3-4fb4-81f6-bd26b227b406)
AND
NOT DB_RC_DW_CoverTheTracks_LilianThugsDialog(1)
THEN
Proc_RC_DW_CoverTheTracks_MoveThugs();

IF
AutomatedDialogStarted("RC_DW_UnderTavern_AD_DwarfBossAndThugs",_)
THEN
DB_RC_DW_CoverTheTracks_LilianThugsDialog(1);


// Triggering three-way dialog
// (don't use CharacterEnteredTrigger, because DB_RC_DW_CoverTheTracks_LilianThugsDialog(1) can be set
//  after that happens in case you teleport over the S_RC_DW_LilianThugs trigger directly into this one)
// Check for DB_IsPlayer() because the trigger is also registered for the captured assassin
IF
DB_InRegion(_Player,TRIGGERGUID_S_RC_DW_UnderTavern_OwnershipTrigger_DwarfBoss_c8c5300c-a815-48de-ab73-2e49cffc17ef)
AND
DB_IsPlayer(_Player)
AND
DB_RC_DW_CoverTheTracks_LilianThugsDialog(1)
AND
QueryOnlyOnce("RC_DW_UnderTavern_QuestioningAssassin")
THEN
CharacterSetReactionPriority(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_DwarfBoss_InterrogatingAssassin",0);
PROC_RC_DW_CoverTheTracksTryQuestioningDialog(_Player);

PROC
PROC_RC_DW_CoverTheTracksTryQuestioningDialog((CHARACTERGUID)_Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,_Player)
THEN
DB_RC_DW_CoverTheTracks_QuestioningStarted(1);
Proc_StartDialog(0,"RC_DW_UnderTavern_QuestioningAssassin",S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,_Player);

PROC
PROC_RC_DW_CoverTheTracksTryQuestioningDialog((CHARACTERGUID)_Player)
AND
NOT DB_RC_DW_CoverTheTracks_QuestioningStarted(1)
THEN
TimerLaunch("RC_DW_LilianThugsMove",500);
PROC_RC_DW_CoverTheTracksAfterQuestioning();

IF
GlobalFlagSet("RC_DW_Undertavern_MoveLoharThugs")
AND
DB_RC_DW_CoverTheTracks_LilianThugsDialog(1)
THEN
TimerLaunch("RC_DW_LilianThugsMove",800);

// Fallback in case the RC_DW_UnderTavern_QuestioningAssassin(_COM_Beast) dialog got aborted (e.g. by a crime interrogation)
IF
DialogEnded("RC_DW_UnderTavern_QuestioningAssassin",_)
AND
NOT DB_GlobalFlag("RC_DW_Undertavern_MoveLoharThugs")
THEN
TimerLaunch("RC_DW_LilianThugsMove",0);

IF
DialogEnded("RC_DW_UnderTavern_QuestioningAssassin_COM_Beast",_)
AND
NOT DB_GlobalFlag("RC_DW_Undertavern_MoveLoharThugs")
THEN
TimerLaunch("RC_DW_LilianThugsMove",0);

IF
DialogEnded("RC_DW_UnderTavern_QuestioningAssassin",_ID)
AND
DB_DialogPlayers(_ID, _Player, _)
AND
ObjectGetFlag(_Player, "RC_DW_HostileLohar", 1)
THEN
CharacterSetRelationIndivFactionToIndivFaction((CHARACTERGUID)_Player, CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, 0);
CharacterSetRelationIndivFactionToIndivFaction(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, (CHARACTERGUID)_Player, 0);

IF
DialogEnded("RC_DW_UnderTavern_QuestioningAssassin",_)
THEN
PROC_RC_DW_CoverTheTracksAfterQuestioning();

IF
DialogEnded("RC_DW_UnderTavern_QuestioningAssassin_COM_Beast",_)
THEN
PROC_RC_DW_CoverTheTracksAfterQuestioning();

PROC
PROC_RC_DW_CoverTheTracksAfterQuestioning()
THEN
NOT DB_RC_DW_CoverTheTracks_QuestioningStarted(1);
DB_OnlyOnce("RC_DW_DwarfBoss_ReactAfterIntro");
GlobalSetFlag("RC_DW_DwarfBoss_ReactAfterIntro");
NOT DB_RC_DW_CoverTheTracks_LilianThugsDialog(1);
NOT DB_Dialogs(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"RC_DW_UnderTavern_QuestioningAssassin");
PROC_RemoveDialogFromCharacter(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784);
PROC_RemoveDialogFromCharacter(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2);
DB_Dialogs(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_UnderTavern_DwarfBoss");
SetHasDialog(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, 1);
SetHasDialog(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, 1);
DB_Dialogs(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"RC_DW_UnderTavern_CapturedAssassin");

IF
TimerFinished("RC_DW_LilianThugsMove")
THEN
NOT DB_RC_DW_CoverTheTracks_LilianThugsDialog(1);
Proc_RC_DW_CoverTheTracks_MoveThugs();

PROC
Proc_RC_DW_CoverTheTracks_MoveThugs()
AND
DB_RC_DW_DwarfBoss_Thugs(_Thug,_Trigger,_Dialog)
AND
CharacterIsDead(S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,0)
THEN
CharacterSetReactionPriority(_Thug,"Thug_InterrogateAssassin",2000);

PROC
Proc_RC_DW_CoverTheTracks_MoveThugs()
AND
DB_RC_DW_DwarfBoss_Thugs(_Thug,_Trigger,_Dialog)
THEN
DB_Dialogs(_Thug,_Dialog);
DialogRequestStop(_Thug);
SetHasDialog(_Thug,0);
ProcCharacterMoveTo(_Thug,_Trigger,0,"RC_DW_LilianThugArrived");
NOT DB_RC_DW_DwarfBoss_Thugs(_Thug,_Trigger,_Dialog);

IF
StoryEvent((CHARACTERGUID)_Char,"RC_DW_LilianThugArrived")
THEN
SetHasDialog(_Char,1);

//END_REGION 

//REGION Captured assassin
// Teleporting the assassin out of Lohar's room -> stop questioning
IF
CharacterLeftTrigger((CHARACTERGUID)CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,TRIGGERGUID_S_RC_DW_UnderTavern_OwnershipTrigger_DwarfBoss_c8c5300c-a815-48de-ab73-2e49cffc17ef)
AND
DB_RC_DW_DwarfBoss_Thugs(_Thug,_,_)
AND
NOT DB_Dead(_Thug)
THEN
CharacterSetReactionPriority(_Thug,"Thug_InterrogateAssassin",0);

// Could also extend this to have the thugs escort her back to the starting point if she were to re-enter.
IF
CharacterLeftTrigger((CHARACTERGUID)CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,TRIGGERGUID_S_RC_DW_UnderTavern_OwnershipTrigger_DwarfBoss_c8c5300c-a815-48de-ab73-2e49cffc17ef)
THEN
TriggerUnregisterForCharacter(TRIGGERGUID_S_RC_DW_UnderTavern_OwnershipTrigger_DwarfBoss_c8c5300c-a815-48de-ab73-2e49cffc17ef,CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2);

//END_REGION

//REGION Assassin beaten to pulp when coming back from Mordus fight, or back from Abandoned House having read Mordus Note (if anyone left to do that to her)
IF
ObjectFlagSet("QuestUpdate_RC_DW_WC_AtaraxianLair_KilledBoss",_,_)
AND
QueryOnlyOnce("RC_DW_Undertavern_BeatUpAssassin")
THEN
DB_RC_DW_BeatUpAssassin(1);

IF
ObjectFlagSet("QuestUpdate_RC_DW_CoverTheTracks_AbandonedHouse_NoteRead",_,_)
AND
QueryOnlyOnce("RC_DW_Undertavern_BeatUpAssassin")
THEN
DB_RC_DW_BeatUpAssassin(1);


// Make sure we don't interfere with the intro dialog in case it hasn't run yet
IF
DB_RC_DW_BeatUpAssassin(1)
AND
DB_OnlyOnce("RC_DW_DwarfBoss_ReactAfterIntro")
THEN
SetStoryEvent(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"RC_DW_CapturedAssassinWaitTillPlayersAway");

IF
GlobalFlagSet("RC_DW_DwarfBoss_ReactAfterIntro")
AND
DB_RC_DW_BeatUpAssassin(1)
THEN
SetStoryEvent(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"RC_DW_CapturedAssassinWaitTillPlayersAway");


IF
StoryEvent((CHARACTERGUID)S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"RC_DW_CapturedAssassinPlayersAway")
AND
DB_RC_DW_BeatUpAssassin(1)
THEN
NOT DB_RC_DW_BeatUpAssassin(1);
PROC_RC_DW_DwarfBossBeatUpCapturedAssassin();

// Only if at least one thug is still alive, if the captured assassin is stil in the Dwarf boss room,
// not dead yet, and not hurt more already than the amount of health we will leave her with
PROC
PROC_RC_DW_DwarfBossBeatUpCapturedAssassin()
AND
QRY_RC_DW_DwarfBossGroup_AnyAlive()
AND
ObjectIsInTrigger(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,TRIGGERGUID_S_RC_DW_UnderTavern_OwnershipTrigger_DwarfBoss_c8c5300c-a815-48de-ab73-2e49cffc17ef,1)
AND
NOT DB_Dead(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2)
AND
CharacterGetHitpointsPercentage(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,_Percentage)
AND
_Percentage > 1.0
THEN
NOT DB_RC_DW_BeatUpAssassin(1);
CharacterSetHitpointsPercentage(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,1.0);
CreateSurface(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"SurfaceBlood",1.5,-1.0);
ObjectSetFlag(CHARACTERGUID_S_RC_DW_LilianThug1_3d063a8a-a0a1-409e-907f-ecfb6741935c,"RC_DW_UnderTavern_AfterTorture");
ObjectSetFlag(CHARACTERGUID_S_RC_DW_LilianThug2_5443de0f-115b-4581-a92a-fee99ca2a2ad,"RC_DW_UnderTavern_AfterTorture");
ObjectSetFlag(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"RC_DW_UnderTavern_AfterTorture");

//END_REGION

//REGION After Mordus is dead and reported to Lohar: assassin dead & burried (if at least one thug or Lohar is still alive), and move thugs (if alive) to undertavern (DOSTWO-8024)
IF
ObjectFlagSet("RC_DW_GaveMordusAmulet",CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,_)
THEN
// In case we didn't get the chance yet to beat her up
NOT DB_RC_DW_BeatUpAssassin(1);
DB_RC_DW_DisappearAssassin(1);
SetStoryEvent(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"RC_DW_CapturedAssassinWaitTillPlayersAway");

IF
StoryEvent((CHARACTERGUID)S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,"RC_DW_CapturedAssassinPlayersAway")
AND
DB_RC_DW_DisappearAssassin(1)
THEN
NOT DB_RC_DW_DisappearAssassin(1);
PROC_RC_DW_MaybeDisappearAssassin();

// No witnesses and an undertaker available: kill & burry assassin
PROC
PROC_RC_DW_MaybeDisappearAssassin()
AND
QRY_RC_DW_DwarfBossGroup_AnyAlive()
AND
ObjectIsInTrigger(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,TRIGGERGUID_S_RC_DW_UnderTavern_OwnershipTrigger_DwarfBoss_c8c5300c-a815-48de-ab73-2e49cffc17ef,1)
THEN
PROC_RC_DW_DisappearAssassin();

// She may already be dead -> don't kill her again
PROC
PROC_RC_DW_DisappearAssassin()
AND
NOT DB_Dead(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2)
THEN
DialogRequestStop(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2);
CharacterDie(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,0,"DoT");

PROC
PROC_RC_DW_DisappearAssassin()
THEN
// Disappear dead body
//SetOnStage(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,0);
// Make grave appear
TeleportTo(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,S_RC_DW_LoharAssassinsGrave_ShovelTrigger_4edaf241-7958-4d1e-a9d6-26a82f91fb71);
SetOnStage(ITEMGUID_S_RC_DW_LoharAssassinsGrave_DirtPile_4c15a07b-6bc1-49f1-997a-35a80a9db8ae,1);
DB_ShovelArea(S_RC_DW_LoharAssassinsGrave_ShovelTrigger_4edaf241-7958-4d1e-a9d6-26a82f91fb71,"RC_DW_LoharAssassinsGrave",ITEMGUID_S_RC_DW_LoharAssassinsGrave_DirtPile_4c15a07b-6bc1-49f1-997a-35a80a9db8ae);
DB_ShovelRewardItemScatter("RC_DW_LoharAssassinsGrave",CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2);
// Walk thugs to pub
Proc_RC_DW_CoverTheTracks_MoveThugsToUndertavern();
// Clean up
PROC_RC_DW_CoverTheTracks_DeadAssassinCleanup();

PROC
Proc_RC_DW_CoverTheTracks_MoveThugsToUndertavern()
AND
DB_RC_DW_DwarfBoss_ThugsFinalSeat(_Char,_Seat)
THEN
// Remove them from Lohar's combat group, so they don't magically come running back from the
// tavern if Lohar is attacked later
SetCombatGroupID(_Char,"");
SetVarObject(_Char,"Seat",_Seat);

/*IF
GlobalFlagSet("RC_DW_LoharAssassinsGrave_SetDeadAssassinOnStage")
AND
GetPosition(S_RC_DW_AssassinsGrave_DirtPile_4c15a07b-6bc1-49f1-997a-35a80a9db8ae,_X,_Y,_Z)
THEN
TeleportTo(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,S_RC_DW_AssassinsGrave_DirtPile_4c15a07b-6bc1-49f1-997a-35a80a9db8ae);
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01",_X,_Y,_Z);
SetOnStage(CHARACTERGUID_S_RC_DW_UnderTavern_CapturedAssassin_f3a00aa7-0e48-43e5-b324-329de6d584c2,1);
// Clean up
GlobalClearFlag("RC_DW_LoharAssassinsGrave_SetDeadAssassinOnStage");*/

//REGION Clean up
QRY
QRY_RC_DW_DwarfBossGroup_AnyAlive()
AND
DB_RC_DW_DwarfBossGroup(_Char)
AND
CharacterIsDead(_Char,0)
THEN
DB_NOOP(1);

PROC
PROC_RC_DW_CoverTheTracks_DeadAssassinCleanup()
THEN
NOT DB_RC_DW_DisappearAssassin(1);
NOT DB_RC_DW_WaitForAssassinLostSight(1);

PROC
PROC_RC_DW_CoverTheTracks_CleanUpIfDwarfBossGroupDead()
AND
NOT QRY_RC_DW_DwarfBossGroup_AnyAlive()
THEN
PROC_RC_DW_CoverTheTracks_DeadAssassinCleanup();

// Check first time whether there in fact any potential undertaker is alive for the assassin
IF
DB_RC_DW_DisappearAssassin(1)
THEN
PROC_RC_DW_CoverTheTracks_CleanUpIfDwarfBossGroupDead();

// And whenever someone dies afterwards, check whether it wasn't the last possible undertaker
IF
CharacterDied(_Char)
AND
DB_RC_DW_DisappearAssassin(1)
AND
DB_RC_DW_DwarfBossGroup(_Char)
THEN
PROC_RC_DW_CoverTheTracks_CleanUpIfDwarfBossGroupDead();
//END_REGION
//END_REGION


IF
CharacterDying(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784)
THEN
GlobalSetFlag("RC_DW_DwarfBossDead");
PROC_RemoveDialogFromCharacter(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784);
SetHasDialog(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, 1);
SetOnStage(S_RC_DW_Limb_LoharsHead_15bb128a-3557-48a0-add6-1da6a41c6b60, 1);
ItemToInventory(ITEMGUID_S_RC_DW_Limb_LoharsHead_15bb128a-3557-48a0-add6-1da6a41c6b60, CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784);

// Eat Lohar's head
IF
CharacterUsedItem(_Player,S_RC_DW_Limb_LoharsHead_15bb128a-3557-48a0-add6-1da6a41c6b60)
THEN
PartySetFlag(_Player,"QuestUpdate_RC_DW_DwarvenSabotage_AteLoharsHead");

//REGION Dwarf boss Bodyguard
IF
StoryEvent(_Player, "RC_DW_SpotLoharGuard")
THEN
Proc_StartDialog(0, "RC_DW_UnderTavern_DwarfBossGuard", CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBossGuard_ec3c854d-f5ef-426a-914e-47ed9147b6f1, _Player);

PROC
Proc_DialogFlagSetup(_, CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, _Player)
THEN
UserSetFlag((CHARACTERGUID)_Player, "RC_DW_MetDwarfBoss", 0);

IF
ObjectWasTagged(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"HasMet")
THEN
SetStoryEvent(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBossGuard_ec3c854d-f5ef-426a-914e-47ed9147b6f1, "RC_DW_LohardGuardStopSpot");

// Don't talk to players if they first attack us
IF
ObjectEnteredCombat(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBossGuard_ec3c854d-f5ef-426a-914e-47ed9147b6f1,_)
THEN
SetStoryEvent(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBossGuard_ec3c854d-f5ef-426a-914e-47ed9147b6f1, "RC_DW_LohardGuardStopSpot");

//END_REGION

//REGION Dwarf Boss & Anhar Portrait
IF
ItemLeftTrigger((ITEMGUID)S_RC_DW_AnharPortrait_419bc64a-4593-420e-bc9f-9d1f11b8789a,(TRIGGERGUID)S_RC_DW_AnharsPortrait_Box_db78a0a6-09f2-42a1-b98f-6d3dd8b3cb3e,_)
THEN
ObjectSetFlag(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_AnharPortraitMissing");

IF
ItemEnteredTrigger((ITEMGUID)S_RC_DW_AnharPortrait_419bc64a-4593-420e-bc9f-9d1f11b8789a,(TRIGGERGUID)S_RC_DW_AnharsPortrait_Box_db78a0a6-09f2-42a1-b98f-6d3dd8b3cb3e,_)
THEN
ObjectClearFlag(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_AnharPortraitMissing",0);

// Doesn't trigger ItemLeftTrigger
IF
ItemDestroyed((ITEMGUID)S_RC_DW_AnharPortrait_419bc64a-4593-420e-bc9f-9d1f11b8789a)
THEN
ObjectSetFlag(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_AnharPortraitMissing");

IF
DialogEnded("RC_DW_UnderTavern_DwarfBoss",_)
AND
ObjectGetFlag(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_LookAtAnharPortrait",1)
THEN
CharacterSetReactionPriority((CHARACTERGUID)S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_DwarfBoss_LookAtAnharPicture",400);
ObjectClearFlag(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_LookAtAnharPortrait",0);

IF
DialogEnded("RC_DW_UnderTavern_DwarfBoss_Beast",_)
AND
ObjectGetFlag(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_LookAtAnharPortrait",1)
THEN
CharacterSetReactionPriority((CHARACTERGUID)S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_DwarfBoss_LookAtAnharPicture",400);
ObjectClearFlag(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784,"RC_DW_LookAtAnharPortrait",0);
//END_REGION


//REGION Dwarf boss Death
IF
StoryEvent(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, "RC_DW_LoharDown")
THEN
CharacterDisableAllCrimes(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784);
CharacterSetAnimationOverride(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, "knockdown_loop");
DB_DoNotFace(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784);

IF
StoryEvent(_Player, "RC_DW_DeadLoharDialogue")
AND
QueryOnlyOnce("RC_DW_DeadLoharDialogue")
THEN
Proc_StartDialog(0, "RC_DW_DeadLoharDialogue", CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, _Player);

IF
DialogStarted("RC_DW_DeadLoharDialogue_Beast", _)
THEN
ApplyStatus(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, "LIE_DYING", -1.0, 1);

IF
DialogStarted("RC_DW_DeadLoharDialogue", _)
THEN
ApplyStatus(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, "LIE_DYING", -1.0, 1);

IF
DialogEnded("RC_DW_DeadLoharDialogue_COM_Beast", _)
AND
ObjectGetFlag(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978, "RC_DW_LeaveOverLohar", 1)
AND
DB_CompanionAvatarBond(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978, _Player)
THEN
PROC_Origins_CompanionLeavePermanently(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978);
DB_CompanionLeaving(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978, _Player);
ProcCharacterDisappearOutOfSight(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978, 0, 1, "GLO_CompanionLeaves_LowRelation", 1);

IF
ObjectFlagSet("RC_DW_LoharDie", CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, _ID)
THEN
DB_RC_DW_LoharDieDialogue(_ID);

IF
ObjectFlagSet("RC_DW_LoharDie", CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, _ID)
AND
DB_DialogPlayers(_ID,_Player,_)
THEN
ObjectSetFlag(_Player,"GLO_PathOfBlood_MurderedInnocent");

IF
DialogEnded(_, _ID)
AND
DB_RC_DW_LoharDieDialogue(_ID)
THEN
RemoveStatus(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, "UNCONSCIOUS");
RemoveStatus(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, "LIE_DYING");
CharacterSetImmortal(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, 0);
NOT DB_RC_DW_LoharDieDialogue(_ID);
CharacterDie(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, 1, "DoT");

IF
ObjectFlagSet("RC_DW_LoharSpared", CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, _ID)
THEN
DB_RC_DW_LoharSpareDialogue(_ID);

IF
DialogEnded(_, _ID)
AND
DB_RC_DW_LoharSpareDialogue(_ID)
THEN
NOT DB_RC_DW_LoharSpareDialogue(_ID);
RemoveStatus(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, "LIE_DYING");
CharacterSetAnimationOverride(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, "");
ProcCharacterDisappearOutOfSight(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, 0, 1, "", 1);
SetHasDialog(CHARACTERGUID_S_RC_DW_UnderTavern_DwarfBoss_8e58abc6-1010-4e67-97b2-b9686586e784, 0);
//END_REGION

//REGION Micheil Ros journal
IF
TextEventSet("loharstart")
AND
DB_IsPlayer(_Char)
THEN
ObjectSetFlag(_Char,"RC_DW_Lohar_MicheilRos");


IF
ObjectFlagSet("RC_DW_Lohar_MicheilRos",_Char,_)
THEN
ProcQuestSetCategory("RC_ARX_TheImperialDwarves","QCA_Chapter_04");
ObjectSetFlag(_Char,"QuestUpdate_RC_ARX_TheImperialDwarves_LoharStart");

//END_REGION

//REGION JDF flags
// We need to decouple the flag and the quest update trigger/flag so we
// can create a JDF chain
IF
ObjectFlagSet("RC_DW_QuestFromJulian", _Player, _)
THEN
ObjectSetFlag(_Player, "RC_DW_QuestFromJulian_InvestigateLoharPremise");

IF
ObjectFlagShared("RC_DW_QuestFromJulian", _Player, _)
THEN
ObjectSetFlag(_Player, "RC_DW_QuestFromJulian_InvestigateLoharPremise");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
