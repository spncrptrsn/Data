Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_FTJ_SW_DragonShadowSpot((CHARACTERGUID)S_FTJ_SW_FB_Silentmonk_SourceTotem_003_2dc065a4-e0ad-4dac-9e31-bec0bb1e9e33,1500);
DB_FTJ_SW_DragonShadowSpot(CHARACTERGUID_S_FTJ_SW_FB_Silentmonk_SourceTotem_004_aaf5b56f-53f9-4f55-a4de-34d743bca4ce,2800);
DB_FTJ_SW_DragonShadowSpot(CHARACTERGUID_S_FTJ_SW_FB_Silentmonk_SourceTotem_002_b4398dd0-9059-41f6-b6fb-6be6308732dc,3600);
DB_FTJ_SW_DragonShadowSpot(CHARACTERGUID_S_FTJ_SW_FB_Silentmonk_SourceTotem_000_82bb2bd5-10f3-4b35-b472-10896e4bc519,4500);
DB_FTJ_SW_DragonShadowSpot(CHARACTERGUID_S_FTJ_SW_FB_Silentmonk_SourceTotem_001_ac3e4362-5823-4af7-ad9d-bf92e00b752e,5500);
DB_Dialogs(CHARACTERGUID_S_FTJ_SW_TotemRat_bf5baa13-5bc3-43b2-b379-0f836c266b10,"FTJ_SW_TotemRat");
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_FTJ_SW_OneShot_DragonShadow_a3140064-5c4a-4779-ab74-0d3bca61bd86);				
KBSECTION
IF
TextEventSet("dr_s")
AND
DB_IsPlayer(_Player)
AND
QueryOnlyOnce("FTJ_SW_DragonShadowEvent")
THEN
PROC_LoopEffect("RS3_FX_Char_Creatures_DragonShadow_01",ITEMGUID_S_FTJ_SW_DragonShadow_3c22f591-58ad-4a04-a085-24c71a0e245b,"FTJ_SW_DragonShadowFX","FJ_FortJoy_Main","");
TimerLaunch("Timer_FTJ_SW_DragonShadow",12000);
proc_ItemMoveToTrigger(ITEMGUID_S_FTJ_SW_DragonShadow_3c22f591-58ad-4a04-a085-24c71a0e245b,TRIGGERGUID_S_FTJ_SW_DragonShadow1_be152aed-aab3-4b90-b0d4-94b8495fcef7,2.0,1.05,1);
Proc_FTJ_SW_DragonShadowEvent();
Proc_FTJ_SW_DragonCoolingEvent(_Player);
DialogRequestStop(_Player);
StartVoiceBark("FTJ_SW_VB_DragonShadowAppear",_Player);
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_FTJ_SW_OneShot_DragonShadow_a3140064-5c4a-4779-ab74-0d3bca61bd86);
Proc_ShakeCameraForTime(_Player,800);
TimerLaunch("Creatures_Dragon_ABC_Shriekers_Help_AudioEvent",4000);

IF
DialogEnded("FTJ_SW_TotemRat",_)
AND
GlobalGetFlag("FTJ_SW_TotemRatRun",1)
THEN
SetHasDialog(CHARACTERGUID_S_FTJ_SW_TotemRat_bf5baa13-5bc3-43b2-b379-0f836c266b10,0);
CharacterMoveTo(CHARACTERGUID_S_FTJ_SW_TotemRat_bf5baa13-5bc3-43b2-b379-0f836c266b10,TRIGGERGUID_S_FTJ_SW_TotemRatRun_3e9f00ba-4254-4ac7-9923-469381000d2f,1,"",1);

PROC
Proc_FTJ_SW_TotemKillCharacter((CHARACTERGUID)_Totem,CHARACTERGUID_S_FTJ_SW_TotemRat_bf5baa13-5bc3-43b2-b379-0f836c266b10)
AND
DB_FTJ_SW_SourceTotems(_Totem,_,_)
AND
DB_IsPlayer(_Player)
AND
GetDistanceTo(_Player,CHARACTERGUID_S_FTJ_SW_TotemRat_bf5baa13-5bc3-43b2-b379-0f836c266b10,_Dist)
AND
_Dist < 20.0
AND
QueryOnlyOnce("FTJ_SW_TotemRatDead")
THEN
StartVoiceBark("FTJ_SW_VB_TotemRatDead",_Player);

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_FTJ_SW_OneShot_DragonShadow_a3140064-5c4a-4779-ab74-0d3bca61bd86)
AND
// If Red Prince frees Slane, Slane says he hopes to never see him again and promises to kill him next time -> no help
GlobalGetFlag("FTJ_SW_PurgedDragon_FreedByRedPrinceAvatar",0)
AND
PartyGetFlag(_Player,"FTJ_SW_DragonFriend",1)
AND
QueryOnlyOnce("FTJ_SW_DragonShadowEvent")
THEN
PROC_LoopEffect("RS3_FX_Char_Creatures_DragonShadow_01",ITEMGUID_S_FTJ_SW_DragonShadow_3c22f591-58ad-4a04-a085-24c71a0e245b,"FTJ_SW_DragonShadowFX","FJ_FortJoy_Main","");
TimerLaunch("Timer_FTJ_SW_DragonShadow",12000);
proc_ItemMoveToTrigger(ITEMGUID_S_FTJ_SW_DragonShadow_3c22f591-58ad-4a04-a085-24c71a0e245b,TRIGGERGUID_S_FTJ_SW_DragonShadow1_be152aed-aab3-4b90-b0d4-94b8495fcef7,2.0,1.05,1);
Proc_FTJ_SW_DragonShadowEvent();
Proc_FTJ_SW_DragonCoolingEvent(_Player);
ProcObjectTimer(_Player,"Timer_FJT_SW_PurgedDragonBuff",11000);
DialogRequestStop(_Player);
StartVoiceBark("FTJ_SW_VB_DragonShadowAppear",_Player);
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_FTJ_SW_OneShot_DragonShadow_a3140064-5c4a-4779-ab74-0d3bca61bd86);
Proc_ShakeCameraForTime(_Player,800);
TimerLaunch("Creatures_Dragon_ABC_Shriekers_Help_AudioEvent",4000);

IF
TimerFinished("Creatures_Dragon_ABC_Shriekers_Help_AudioEvent")
THEN
PlaySound(ITEMGUID_S_FTJ_SW_DragonShadow_AudioHelper_2f697e6f-2368-452c-b8f7-dc86c12d2fb6,"Creatures_Dragon_ABC_Shriekers_Help");
proc_ItemMoveToTrigger(ITEMGUID_S_FTJ_SW_DragonShadow_AudioHelper_2f697e6f-2368-452c-b8f7-dc86c12d2fb6,TRIGGERGUID_S_FTJ_SW_DragonShadow1_be152aed-aab3-4b90-b0d4-94b8495fcef7,2.0,1.05,1);

IF
TimerFinished("Timer_FTJ_SW_DragonShadow")
THEN
PROC_StopLoopEffect(ITEMGUID_S_FTJ_SW_DragonShadow_3c22f591-58ad-4a04-a085-24c71a0e245b,"FTJ_SW_DragonShadowFX");

PROC
Proc_FTJ_SW_DragonCoolingEvent((CHARACTERGUID)_Player)
THEN
ApplyStatus(_Player,"CHILLED",60.0,1);
Proc_FTJ_SW_DragonChill(_Player);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"Timer_FJT_SW_PurgedDragonBuff")
AND
NOT DB_GlobalFlag("FTJ_SW_PurgedDragon_DestroyedShriker") // All shrikers were already dead
AND
QueryOnlyOnce("FTJ_SW_PurgedDragonBuff")
THEN
ApplyStatus(_Player,"ENCOURAGED",300.0,1);
ObjectSetFlag(_Player,"QuestUpdate_FTJ_SW_PurgedDragon_DragonHelpBuff");
UserSetFlag(_Player,"QuestUpdate_FTJ_Escape_Island_DragonHelp");
Proc_FTJ_SW_DragonBuff(_Player);

PROC
Proc_FTJ_SW_DragonBuff((CHARACTERGUID)_Player)
AND
DB_IsPlayer(_OtherPlayer)
AND
_Player != _OtherPlayer
AND
GetDistanceTo(_Player,_OtherPlayer,_Dist)
AND
_Dist <= 25.0
THEN
ApplyStatus(_OtherPlayer,"ENCOURAGED",300.0,1);

PROC
Proc_FTJ_SW_DragonChill((CHARACTERGUID)_Player)
AND
DB_IsPlayer(_OtherPlayer)
AND
_Player != _OtherPlayer
AND
GetDistanceTo(_Player,_OtherPlayer,_Dist)
AND
_Dist <= 25.0
THEN
ApplyStatus(_OtherPlayer,"CHILLED",60.0,1);

PROC
Proc_FTJ_SW_DragonShadowEvent()
AND
DB_FTJ_SW_DragonShadowSpot(_Totem,_Time)
THEN
ProcObjectTimer(_Totem,"Timer_FTJ_SW_DragonWinterStrike",_Time);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Totem,"Timer_FTJ_SW_DragonWinterStrike")
AND
DB_FTJ_SW_DragonShadowSpot(_Totem,_)
AND
CharacterIsDead(_Totem,0) 
AND
GetPosition(_Totem,_X,_Y,_Z)
THEN
GlobalSetFlag("FTJ_SW_PurgedDragon_DestroyedShriker");
CreateExplosionAtPosition(_X,_Y,_Z,"Projectile_SlaneIceShard",-1);
PlayScaledEffectAtPosition("RS3_FX_GP_ScriptedEvent_FTJ_SlaneVsShriekers_Impact_01",2.5,_X,_Y,_Z);
CharacterDie(_Totem,1,"DoT");
CreateSurfaceAtPosition(_X,_Y,_Z,"SurfaceWaterFrozen",2.0,60.0);

IF
CharacterDying(_Totem)
AND
DB_FTJ_SW_DragonShadowSpot(_Totem,_)
AND
GetPosition(_Totem,_X,_Y,_Z)
THEN
PlayScaledEffectAtPosition("RS3_FX_GP_ScriptedEvent_SourceJar_Break_01",2.0,_X,_Y,_Z);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Totem,"Timer_FTJ_SW_DragonWinterStrike")
AND
DB_FTJ_SW_DragonShadowSpot(_Totem,_)
AND
GlobalGetFlag("FTJ_SW_PurgedDragon_DestroyedShriker",1)
AND
QueryOnlyOnce("FTJ_SW_Shriekers_SlaneShriekerUpdate")
AND
DB_IsPlayer(_Player)
AND
GetDistanceTo(_Totem,_Player,_Dist)
AND
_Dist < 100.0
THEN
Proc_ShakeCameraForTime(_Player,800);
UserSetFlag(_Player,"QuestUpdate_FTJ_SW_Shriekers_SlaneShrieker");

PROC
ProcObjectTimerFinished(CHARACTERGUID_S_FTJ_SW_FB_Silentmonk_SourceTotem_001_ac3e4362-5823-4af7-ad9d-bf92e00b752e,"Timer_FTJ_SW_DragonWinterStrike")
THEN
GlobalSetFlag("FTJ_SW_CallToArms_PathIsCleared");

PROC
ProcObjectTimerFinished(_,"Timer_FTJ_SW_DragonWinterStrike")
AND
GlobalGetFlag("FTJ_SW_PurgedDragon_DestroyedShriker",1)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"FTJ_SW_DragonFriend",1)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_SW_PurgedDragon_DragonHelp");
UserSetFlag(_Player,"QuestUpdate_FTJ_Escape_Island_DragonHelp");

IF
CharacterDied(_Totem)
AND
DB_FTJ_SW_DragonShadowSpot(_Totem,_Time)
THEN
NOT DB_FTJ_SW_DragonShadowSpot(_Totem,_Time);

IF
TextEventSet("Creatures_Dragon_ABC_Shriekers_Help_Text")
THEN
TeleportToPosition(ITEMGUID_S_FTJ_SW_DragonShadow_AudioHelper_2f697e6f-2368-452c-b8f7-dc86c12d2fb6,582.97,-7.74,271.02,"",0);
PlaySound(ITEMGUID_S_FTJ_SW_DragonShadow_AudioHelper_2f697e6f-2368-452c-b8f7-dc86c12d2fb6,"Creatures_Dragon_ABC_Shriekers_Help");
proc_ItemMoveToTrigger(ITEMGUID_S_FTJ_SW_DragonShadow_AudioHelper_2f697e6f-2368-452c-b8f7-dc86c12d2fb6,TRIGGERGUID_S_FTJ_SW_DragonShadow1_be152aed-aab3-4b90-b0d4-94b8495fcef7,2.0,1.05,1);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "FortJoy"
