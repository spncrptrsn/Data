Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"CoS_Temples_SallowMan");

DB_Dialogs(CHARACTERGUID_S_CoS_Temples_SallowManMinion_001_a092534c-ec79-46db-af07-d852313eb1d6,"CoS_Temples_SallowManMinion_001");
DB_Dialogs(CHARACTERGUID_S_CoS_Temples_SallowManMinion_002_f5307c31-f32a-44ee-85fa-7b413c2957f3,"CoS_Temples_SallowManMinion_002");
DB_Dialogs(CHARACTERGUID_S_CoS_Temples_SallowManMinion_003_7c282499-a74d-4ae6-aee7-0e210a6695b4,"CoS_Temples_SallowManMinion_003");

DB_Ghosts(CHARACTERGUID_S_CoS_Temples_SallowMan_MagisterGhost_001_b7d06753-1718-4396-bedc-e769024c36e2,"CoS_Temples_SallowMan_MagisterGhost_001");
DB_Ghosts(CHARACTERGUID_S_CoS_Temples_SallowMan_MagisterGhost_002_d0f8a877-1810-409c-8edc-5d294817bbb1,"CoS_Temples_SallowMan_MagisterGhost_002");

DB_DeadEvent(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"QuestUpdate_FTJ_Hunted_KillSallowMan");
DB_KilledEvent(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"QuestUpdate_FTJ_Hunted_KillSallowMan");

DB_Dialogs(ITEMGUID_S_CoS_Temples_SallowManMirror_001_3d324483-7bbf-8fc6-5d9a-6fdcda72bc98,"CoS_Temples_SallowManMirror_001"); //Forktongue
DB_Dialogs(ITEMGUID_S_CoS_Temples_SallowManMirror_002_2806e431-ae8c-83a8-5568-222a576e677c,"CoS_Temples_SallowManMirror_002"); //Kemm
DB_Dialogs(ITEMGUID_S_CoS_Temples_SallowManMirror_003_89a3d489-4c4b-8ca1-3fc2-41b34dabdc7e,"CoS_Temples_SallowManMirror_003"); //Q
DB_Dialogs(ITEMGUID_S_CoS_Temples_SallowManMirror_004_caad1808-b39e-4469-8472-89d27cac6cf0,"CoS_Temples_SallowManMirror_004"); //Tarquin
DB_Dialogs(ITEMGUID_S_CoS_Temples_SallowManMirror_005_a9694bd6-97f0-497f-ac2d-9378c4decc39,"CoS_Temples_SallowManMirror_005"); //Sadha

DB_HasStoryEvent((ITEMGUID)ITEMGUID_S_CoS_Temples_Capacitor_001_7dde44fb-f35e-4c13-afd6-8ab23654d797,"CoS_HasSallowManCapacitor");
DB_GiveItemToEvent((ITEMGUID)ITEMGUID_S_CoS_Temples_Capacitor_001_7dde44fb-f35e-4c13-afd6-8ab23654d797,"CoS_SallowManCapacitorTo");


DB_IsNotMessingAround(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af);

DB_Dialogs(ITEMGUID_S_CoS_Temples_SallowManAltar_5ef271db-b53c-4607-91cd-8145699ab648,"CoS_Temples_SallowManAltar");

SetOnStage(ITEMGUID_S_CoS_Temples_SallowMan_Limb_cdc447f5-ef16-4deb-9f79-177f1de1837d,0);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_CoS_Temples_BloodAltarVB_Box_5e3420cf-c6a3-47e3-9bf0-63069e8139f8);


DB_CoS_SallowManIllusions(ITEMGUID_S_CoS_Temples_BlackRingIllusion_001_50e4f4e5-3e1b-4394-b1c9-1fa3a68f9189);
DB_CoS_SallowManIllusions(ITEMGUID_S_CoS_Temples_BlackRingIllusion_002_a40deb28-5795-4ee0-bd30-64a8c7cda3b7);
DB_CoS_SallowManIllusions(ITEMGUID_S_CoS_Temples_BlackRingIllusion_003_e4102122-8cc3-4bcd-b259-20bec7dec77a);


DB_CoS_BlackRingFactions("CoS_BlackRing");
DB_CoS_BlackRingFactions("CoS_BRCamp");
DB_CoS_BlackRingFactions("CoS_BlackRing_ElfTemple");
DB_CoS_BlackRingFactions("CoS_BlackRing_HumanCamp");
DB_CoS_BlackRingFactions("CoS_BlackRing_LizardCamp");
DB_CoS_BlackRingFactions("CoS_BlackRing_OrcCamp");
DB_CoS_BlackRingFactions("CoS_Temples_BlackRingHub");
DB_CoS_BlackRingFactions("CoS_BlackRing_OrcTempleEntrance");
DB_CoS_BlackRingFactions("CoS_Temples_BlackRingHub_HighPriest");
DB_CoS_BlackRingFactions("CoS_Temples_BlackRingHub_Troll");

DB_Ghosts(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,
				CHARACTERGUID_S_CoS_Temples_SallowMan_Ghost_5d8e25b9-65c0-41b1-aed7-58e933927bbf,"CoS_Temples_SallowMan_Ghost");
DB_NoLowAttitudeDialog(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af);
CharacterSetImmortal(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,1);
SetOnStage(ITEMGUID_S_CoS_Temples_BlackRingIllusion_003_Treasure_fda3843c-0ae4-4cee-936e-4a2080753b85,0);

DB_CoS_SallowManCombatants(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af);
DB_CoS_SallowManCombatants(CHARACTERGUID_S_CoS_Temples_SallowManMinion_001_a092534c-ec79-46db-af07-d852313eb1d6);
DB_CoS_SallowManCombatants(CHARACTERGUID_S_CoS_Temples_SallowManMinion_002_f5307c31-f32a-44ee-85fa-7b413c2957f3);
DB_CoS_SallowManCombatants(CHARACTERGUID_S_CoS_Temples_SallowManMinion_003_7c282499-a74d-4ae6-aee7-0e210a6695b4);

Proc_JDF_CreateCustomFlagsDependency("QuestUpdate_CoS_HelpingTheSallowMan_Start_Mirror","CoS_HasAlexandarsHead","QuestUpdate_CoS_Temples_GotHead");
Proc_JDF_CreateCustomFlagsDependency("QuestUpdate_CoS_HelpingTheSallowMan_Start_Mirror","QuestUpdate_CoS_HelpingTheSallowMan_AteAlexandarHead","QuestUpdate_CoS_Temples_AteHead");
KBSECTION
/////Sallow Man dead quest updates
IF
GlobalFlagSet("CoS_SallowMan_IsDead")
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"CoS_SallowManIsDeadQuestUpdater");
SetOnStage(ITEMGUID_S_CoS_Temples_SallowMan_Limb_cdc447f5-ef16-4deb-9f79-177f1de1837d,1);
ItemToInventory(ITEMGUID_S_CoS_Temples_SallowMan_Limb_cdc447f5-ef16-4deb-9f79-177f1de1837d,CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af);

IF
DB_Dead(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af)
AND
DB_Sees(_Player,CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af)
AND
DB_IsPlayer(_Player)
AND
QuestAccepted(_Player,"CoS_HelpingTheSallowMan",1)
THEN
PartySetFlag(_Player,"QuestUpdate_CoS_HelpingTheSallowMan_Fail_SallowManDead");


//Add to main journal
IF
ObjectFlagSet("QuestUpdate_CoS_HelpingTheSallowMan_Start_Mirror",_Player,_)
THEN
ObjectSetFlag(_Player,"QuestUpdate_CoS_BlackRing_SallowMan");

IF
ObjectFlagSet("CoS_Temples_SallowMan_HasMet",(CHARACTERGUID)_Player,_)
AND
UserGetFlag(_Player,"QuestUpdate_FTJ_Hunted_LearnedSallowMan",1)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Hunted_MetSallowMan");

//REGION Dark Mirrors - Black Ring

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player,"QUEST_DarkMirror_BlackRing_a3da854d-5b28-47cc-b7da-e7075d0f97f3",_Mirror)
AND
ItemIsInCharacterInventory(_Mirror,_Player,0)
THEN
ItemToInventory(_Mirror,_Player,1,0);

IF
CharacterUsedItemTemplate(_Player,"QUEST_DarkMirror_BlackRing_a3da854d-5b28-47cc-b7da-e7075d0f97f3",_Mirror)
THEN
Proc_StartDialog(0,"CoS_Temples_BlackRingMirror",_Mirror,_Player);

//Chill effect when using Altars
IF
ObjectFlagSet("CoS_Temples_Altar_ChilledStatus",(CHARACTERGUID)_Player,_)
THEN
ApplyStatus(_Player,"CHILLED",30.0);
ObjectClearFlag(_Player,"CoS_Temples_Altar_ChilledStatus");

////Fail Mirror persuade, all BR go Hostile
IF
ObjectFlagSet("CoS_Temples_AllBlackRingHostileTo",_Player,_)
THEN
DB_CoS_Temples_AllBlackRingHostileTo_AfterDialog(_Player);

IF
DialogEnded("CoS_Temples_BlackRingMirror",_Id)
AND
DB_CoS_Temples_AllBlackRingHostileTo_AfterDialog(_Player)
AND
GetFaction(_Player,_PlayerFaction)
AND
DB_CoS_BlackRingFactions(_Faction)
THEN
CharacterSetRelationFactionToFaction(_Faction,_PlayerFaction,0);
CharacterSetRelationFactionToFaction(_PlayerFaction,_Faction,0);
NOT DB_CoS_Temples_AllBlackRingHostileTo_AfterDialog(_Player);

IF
DB_CoS_Temples_AllBlackRingHostileTo_AfterDialog(_Player)
THEN
GlobalSetFlag("CoS_Temples_SallowManOrderedPlayersDeath");
//CoS_Temples_AD_BlackRingCaptains_AttackPlayers - AD in first turnsdd

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_CoS_SUB_Temples_BlackRingEncampment_3e419709-30ca-4c37-af1b-2b9bbedd0a33)
THEN
ObjectSetFlag(_Player,"CoS_Temples_EnteredTheSallowMansCamp");


//END_REGION

//REGION Openning the Camp

//VB
IF
CharacterEnteredTrigger((CHARACTERGUID)_Player,TRIGGERGUID_S_CoS_Temples_BloodAltarVB_Box_5e3420cf-c6a3-47e3-9bf0-63069e8139f8)
AND
QueryOnlyOnce("CoS_Temples_BloodAltarVB_Once")
THEN
StartVoiceBark("CoS_Temples_VB_BloodAltar",_Player);
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_CoS_Temples_BloodAltarVB_Box_5e3420cf-c6a3-47e3-9bf0-63069e8139f8);

IF
TextEventSet("brdispel")
AND
CharacterGetHostCharacter((CHARACTERGUID)_Player)
THEN
CharacterAddSkill(_Player,"Cone_Quest_DispelIllusion");

//REGION Put the Head on the Altar

IF
ObjectFlagSet("CoS_Temples_SallowManAltar_PlaceHead",_Player,_)
THEN
TeleportTo(ITEMGUID_S_CoS_Temples_AlexandarsHead_e7097370-d786-47e8-8ab4-76528939a1c0,TRIGGERGUID_S_CoS_Temples_SallowManAltar_HeadPoint_77364145-5667-4fd7-9745-99240601f671,"",0);
ItemSetCanInteract(ITEMGUID_S_CoS_Temples_AlexandarsHead_e7097370-d786-47e8-8ab4-76528939a1c0,0);

IF
DialogEnded("CoS_Temples_SallowManAltar",_Id)
AND
DB_DialogPlayers(_Id,_Player,_)
AND
ObjectGetFlag(_Player,"CoS_Temples_SallowManAltar_PlaceHead",1)
THEN
ItemToInventory(ITEMGUID_S_CoS_Temples_AlexandarsHead_e7097370-d786-47e8-8ab4-76528939a1c0,_Player);
ItemSetCanInteract(ITEMGUID_S_CoS_Temples_AlexandarsHead_e7097370-d786-47e8-8ab4-76528939a1c0,1);
ObjectClearFlag(_Player,"CoS_Temples_SallowManAltar_PlaceHead");

//END_REGION

IF
CharacterUsedSkillOnZoneWithTarget(_Player,_Illusion,"Cone_Quest_DispelIllusion",_,_)
AND
DB_CoS_SallowManIllusions(_Illusion)
THEN
Proc_CoS_Temples_RemoveBRIllusion(_Illusion,_Player);

PROC
Proc_CoS_Temples_RemoveBRIllusion(ITEMGUID_S_CoS_Temples_BlackRingIllusion_001_50e4f4e5-3e1b-4394-b1c9-1fa3a68f9189)
THEN
GlobalSetFlag("CoS_Temples_CampEntranceIllusionDispelled");

//The Illusion at the front near the Altar
IF
GlobalFlagSet("CoS_Temples_CampEntranceIllusionDispelled")
THEN
AutoSave();
UnlockSecretRegion(TRIGGERGUID_S_CoS_SallowManCave_SecretRegionTrigger_f083ec99-3f87-49b3-a432-caeea4a0c680);
Proc_CoS_Temples_RemoveBRIllusion(ITEMGUID_S_CoS_Temples_BlackRingIllusion_001_50e4f4e5-3e1b-4394-b1c9-1fa3a68f9189);

PROC
Proc_CoS_Temples_RemoveBRIllusion((GUIDSTRING)_Illusion,(CHARACTERGUID)_Caster)
AND
DB_CoS_SallowManIllusions((ITEMGUID)_Illusion)
THEN
Proc_CoS_Temples_RemoveBRIllusion(_Illusion);

PROC
Proc_CoS_Temples_RemoveBRIllusion((GUIDSTRING)_Illusion)
AND
DB_CoS_SallowManIllusions(_Illusion)
THEN
ProcObjectTimer(_Illusion,"SallowManIllusion_Remove",1600);

PROC
ProcObjectTimerFinished(_Illusion,"SallowManIllusion_Remove")
AND
DB_CoS_SallowManIllusions((ITEMGUID)_Illusion)
THEN
PlayEffect(_Illusion,"RS3_FX_GP_ScriptedEvent_BlackRingIllusion_01");
ProcObjectTimer(_Illusion,"SallowManIllusion_Remove_1",1800);

PROC
ProcObjectTimerFinished(_Illusion,"SallowManIllusion_Remove_1")
AND
DB_CoS_SallowManIllusions((ITEMGUID)_Illusion)
THEN
NOT DB_CoS_SallowManIllusions(_Illusion);
ItemDestroy(_Illusion);


//Check BR hostility
PROC
Proc_CoS_Temples_RemoveBRIllusion((GUIDSTRING)_Illusion,(CHARACTERGUID)_Caster)
AND
CharacterCanSee(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,_Caster,1)
THEN
CharacterSetRelationIndivFactionToIndivFaction(_Caster,CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,0);
CharacterSetRelationIndivFactionToIndivFaction(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,_Caster,0);



PROC
Proc_CoS_Temples_RemoveBRIllusion(ITEMGUID_S_CoS_Temples_BlackRingIllusion_003_e4102122-8cc3-4bcd-b259-20bec7dec77a,_)
THEN
SetOnStage(ITEMGUID_S_CoS_Temples_BlackRingIllusion_003_Treasure_fda3843c-0ae4-4cee-936e-4a2080753b85,1);



//END_REGION

//Start Dialog on first sight with a Player
IF
StoryEvent((CHARACTERGUID)_Player,"CoS_SallowMan_DialogStart")
AND
NOT QRY_CoS_Temples_Sallowman_TryLohse(_Player)
THEN
Proc_StartDialog(0,"CoS_Temples_SallowMan",CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,_Player);
SetVarFixedString(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"currentState","State_Default");

QRY
QRY_CoS_Temples_Sallowman_TryLohse((CHARACTERGUID)_Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295, _Player)
AND
QRY_StartDialog(0, "CoS_Temples_SallowMan_OM_Lohse", CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af, CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295)
THEN
DB_NOOP(1);

IF
DialogEnded("CoS_Temples_SallowMan_OM_Lohse",_)
THEN
EnterCombat(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af, CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295);

//Eating Sallowmans's Limb
IF
CharacterItemEvent((CHARACTERGUID)_Player,ITEMGUID_S_CoS_Temples_SallowMan_Limb_cdc447f5-ef16-4deb-9f79-177f1de1837d,"GLO_AteBodyPart")
THEN
Proc_StartDialog(0,"CoS_Temples_Sallowman_Limb",_Player);

IF
DialogEnded("CoS_Temples_Sallowman_Limb",_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
THEN
ApplyStatus(_Player,"INFECTIOUS_DISEASED",120.0,1);

//REGION Returning the Head

IF
ObjectFlagSet("CoS_Temples_GaveSallowManAlexandarsHead",(CHARACTERGUID)_Player,_)
AND
QueryOnlyOnce("SallowManEatsAlexandarHead_Once")
THEN
ItemRemove(ITEMGUID_S_CoS_Temples_AlexandarsHead_e7097370-d786-47e8-8ab4-76528939a1c0);
CreateSurface(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"SurfaceBlood",1.0,-1.0);
PlayEffect(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"RS3_FX_GP_Combat_Hit_Blood_01_Large");
//PlayAnimation(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"use_eat");
ObjectSetFlag(_Player,"QuestUpdate_CoS_HelpingTheSallowMan_ReturnAlexandarHead");


//Swap dialog
IF
DialogEnded("CoS_Temples_SallowMan",_)
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"CoS_Temples_GaveSallowManAlexandarsHead",1)
AND
QueryOnlyOnce("SallowMan_SwapDialogAfterAlexadnarKillQuest_Once")
THEN
PROC_RemoveDialogFromCharacter(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af);
DB_Dialogs(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"CoS_Temples_SallowMan_CompanionKiller");


//END_REGION

//REGION Kill Companions Quest
//GLO_Promise_AcceptedPromise

IF
DialogEnded("CoS_Temples_SallowMan_CompanionKiller",_)
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"CoS_PlayerRenounceParty",1)
AND
NOT DB_CoS_PlayerRenounceParty(_Player)
THEN
Proc_CoS_PlayerRenounceParty(_Player);
DB_CoS_PlayerRenounceParty(_Player);

PROC
Proc_CoS_PlayerRenounceParty((CHARACTERGUID)_Player)
THEN
CharacterSetRelationIndivFactionToIndivFaction(_Player,CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,100);
CharacterSetRelationIndivFactionToIndivFaction(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,_Player,100);


PROC
Proc_CoS_PlayerRenounceParty((CHARACTERGUID)_Player)
AND
DB_GLO_PartyMembers_RecruiteeAvatarBond(_Companion,(CHARACTERGUID)_Player)
AND
IsTagged(_Companion,"HENCHMAN",0)
THEN
PROC_Origins_CompanionLeavePermanently(_Companion);
CharacterSetCorpseLootable(_Companion,1);
CharacterSetRelationIndivFactionToIndivFaction(_Companion,_Player,0);
CharacterSetRelationIndivFactionToIndivFaction(_Player,_Companion,0);

// Remove other avatars controlled by the same user in case of a multiplayer that became single player
// (only an avatar player can renounce the party, so _Player is guaranteed to be an avatar)
PROC
Proc_CoS_PlayerRenounceParty((CHARACTERGUID)_Player)
AND
CharacterGetReservedUserID(_Player,_UserID)
AND
DB_Avatars(_AllAvatars)
AND
_Player != _AllAvatars
AND
CharacterGetReservedUserID(_AllAvatars,_UserID)
THEN
DB_GLO_PartyMembers_BlockReturnToRecruitmentPosition(_AllAvatars);
PROC_GLO_PartyMembers_MakeNPC(_AllAvatars);
ClearTag(_AllAvatars,"AVATAR");
CharacterSetCorpseLootable(_AllAvatars,1);
CharacterSetRelationIndivFactionToIndivFaction(_AllAvatars,_Player,0);
CharacterSetRelationIndivFactionToIndivFaction(_Player,_AllAvatars,0);

//Avatar Leave Party
PROC
Proc_CoS_PlayerRenounceParty((CHARACTERGUID)_Player)
AND
CharacterGetReservedUserID(_Player,_UserId)
THEN
LeaveParty(_UserId);

//Avatar Leave Party
PROC
Proc_CoS_PlayerRenounceParty((CHARACTERGUID)_Player)
AND
DB_Avatars(_Player)
AND
DB_Avatars(_AllAvatars)
AND
_Player != _AllAvatars
THEN
MakeWar(_Player,_AllAvatars,1);


PROC
Proc_CoS_PlayerRenounceParty((CHARACTERGUID)_Player)
AND
DB_OriginNPCAlignment(_Origin,_)
AND
ObjectGetFlag((CHARACTERGUID)_Origin,"CoS_PlayerRenounceParty",0)
THEN
ProcSetHostileToIndivPlayer(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,_Origin);

//END_REGION

//Give Player Purging Rod
IF
ObjectFlagSet("QuestUpdate_CoS_CompanionCulling_PurgingRod",(CHARACTERGUID)_Player,_)
THEN
CharacterGiveReward(_Player,"COS_ResurrectionPreventionStaff");

IF
TextEventSet("rod")
AND
CharacterGetHostCharacter(_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_CoS_CompanionCulling_PurgingRod");

//A Player was purged, do Culled Player Check
IF
ObjectWasTagged((CHARACTERGUID)_Origin,"BLOCK_RESURRECTION")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
_Origin != _Player
AND
ObjectGetFlag(_Player,"QuestUpdate_CoS_CompanionCulling_Start",1)
THEN
Proc_CoS_CheckCulledPlayers(_Player);

//If a Player has accepted the Promise check culled Players
IF
ObjectWasTagged((CHARACTERGUID)_Origin,"PROMISED")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
_Origin != _Player
AND
ObjectGetFlag(_Player,"QuestUpdate_CoS_CompanionCulling_Start",1)
THEN
Proc_CoS_CheckCulledPlayers(_Player);


IF
DialogStarted("CoS_Temples_SallowMan_CompanionKiller",_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
THEN
Proc_CoS_CheckCulledPlayers(_Player);

//REGION Culled Player Check
PROC
Proc_CoS_CheckCulledPlayers((CHARACTERGUID)_Player)
THEN
ProcDeclareCounter("CoS_CulledPlayerCount");

PROC
Proc_CoS_CheckCulledPlayers((CHARACTERGUID)_Player)
AND
DB_OriginNPCAlignment(_Origin,_)
AND
_Origin != _Player
AND
GetRegion(_Origin,_Region)
AND
_Region == "CoS_Main"
AND
IsTagged((CHARACTERGUID)_Origin,"BLOCK_RESURRECTION",0)
AND												
IsTagged(_Origin,"PROMISED",0)
THEN
ProcIncreaseCounter("CoS_CulledPlayerCount");

PROC
Proc_CoS_CheckCulledPlayers((CHARACTERGUID)_Player)
AND
DB_GlobalCounter("CoS_CulledPlayerCount",_Count)
AND
_Count == 0
THEN
ObjectSetFlag(_Player,"QuestUpdate_CoS_CompanionCulling_CompanionsCulled");

PROC
Proc_CoS_CheckCulledPlayers((CHARACTERGUID)_Player)
THEN
ProcRemoveCounter("CoS_CulledPlayerCount");

//END_REGION

//REGION Promise Breaker remove after dialog
IF
ObjectFlagCleared("GLO_Promise_Relieved",(CHARACTERGUID)_Player,_Inst)
AND
DialogGetInvolvedPlayer(_Inst,1,_Player)
AND
DialogGetInvolvedNPC(_Inst,1,_Breaker)
THEN
DB_CoS_PromiseBreakerRemoveItemAfterDialog(1);
/*
IF
DialogEnded("CoS_Temples_PromiseBreaker",_)
AND
DB_CoS_PromiseBreakerRemoveItemAfterDialog(1)
THEN
ItemRemove(ITEMGUID_S_CoS_Temples_PromiseBreaker_ad5823d0-2b93-413b-af20-39f72fff3140);
NOT DB_CoS_PromiseBreakerRemoveItemAfterDialog(1);
*/

//END_REGION

IF
CharacterItemEvent((CHARACTERGUID)_Player,ITEMGUID_S_CoS_Temples_BRCave_Limb_001_7ec0c64c-8b7e-4193-8790-1fe13764258c,"GLO_AteBodyPart")
THEN
Proc_StartDialog(0,"CoS_Temples_BRCave_Limb_001",_Player);

IF
CharacterItemEvent((CHARACTERGUID)_Player,ITEMGUID_S_CoS_Temples_BRCave_Limb_002_e3ed2417-c793-4012-9f4f-db6797718cdf,"GLO_AteBodyPart")
THEN
Proc_StartDialog(0,"CoS_Temples_BRCave_Limb_002",_Player);

//Q Missive Flag
IF
CharacterUsedItem(_Player,ITEMGUID_S_CoS_Temples_SallowManMissive_001_2d9b04c1-dfb4-470f-9085-4da2ebf453bd)
THEN
ObjectSetFlag(_Player,"CoS_Temples_ReadMissiveFromQ");


//REGION Sallow Man Resurrect

IF
CharacterDied(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af)
THEN
GlobalSetFlag("CoS_SallowMan_IsDead");

IF
AttackedByObject(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,_,_,_,_)
AND
CharacterGetHitpointsPercentage(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,_Per)
AND
_Per < 1.0
AND
GlobalGetFlag("CoS_SallowMan_KilledOnce",0)
AND
QueryOnlyOnce("CoS_SallowManDeadState_Once")
THEN
PlayAnimation(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"die_dot","CoS_SallowManDeadState");
SetHasDialog(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,0);

IF
StoryEvent(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"CoS_SallowManDeadState")
THEN
PROC_LoopEffect("RS3_FX_GP_ScriptedEvent_Resurrection_01",CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,
					"SallowManDeadFx","CoS_Main","");
RemoveHarmfulStatuses(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af);
ProcSetInvulnerable(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,1);
CharacterSetAnimationOverride(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"die_dot");
CharacterSetReactionPriority(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"Sleep_InCombat",10000);
CharacterSetArmorPercentage(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,0.0);
CharacterSetMagicArmorPercentage(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,0.0);
RemoveStatus(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"DEFLECTING");
GlobalSetFlag("CoS_SallowMan_KilledOnce");
ApplyStatus(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"UNCONSCIOUS",1.0,1);

IF
GlobalFlagSet("CoS_SallowMan_KilledOnce")
AND
DB_IsPlayer(_Player)
THEN
CharacterSetRelationFactionToIndivFaction("CoS_BRCamp",_Player,0);
CharacterSetRelationIndivFactionToFaction(_Player,"CoS_BRCamp",0);


IF
ObjectTurnStarted(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af)
AND
GlobalGetFlag("CoS_SallowMan_KilledOnce",1)
AND
GetPosition(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,_X,_Y,_Z)
AND
QueryOnlyOnce("CoS_SallowManResurrect_Resurrect_Once")
THEN
RemoveStatus(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"UNCONSCIOUS");
PROC_StopLoopEffect(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"SallowManDeadFx");
CharacterSetAnimationOverride(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"");
SetTag(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"UNDEAD");
CharacterAddTalent(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"Zombie");
CharacterSetReactionPriority(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"Sleep_InCombat",0);
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_Lightning_Vertical_01",_X,_Y,_Z); 
PlayEffectAtPosition("RS3_FX_Skills_Source_Resurrect_Target_Cast",_X,_Y,_Z);
ProcSetInvulnerable(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,0);
PlayAnimation(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"resurrect");
CharacterSetHitpointsPercentage(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,50.0);
CharacterSetArmorPercentage(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,50.0);
CharacterSetMagicArmorPercentage(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,50.0);
CharacterSetImmortal(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,0);
ApplyStatus(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"HASTED",-1.0,1);
ApplyStatus(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"POISON_SKIN",-1.0,1);
ApplyStatus(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"ENRAGED",-1.0,1);
SetTag(CHARACTERGUID_S_CoS_Temples_SallowMan_d07e0f6b-c473-47f2-9d1c-e1f6f0ef61af,"AI_CANUSESKILL");


//END_REGION


//REGION Sallow Man Betrayal Update
IF
CharacterKilledBy(_Player,_SallowManCombat,_)
AND
DB_CoS_SallowManCombatants(_SallowManCombat)
AND
ObjectGetFlag(_Player,"CoS_QueueSallowManBetryalUpdate",1)
THEN
ObjectSetFlag(_Player,"QuestUpdate_CoS_BlackRing_Betrayal");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "CoS_Temples"
