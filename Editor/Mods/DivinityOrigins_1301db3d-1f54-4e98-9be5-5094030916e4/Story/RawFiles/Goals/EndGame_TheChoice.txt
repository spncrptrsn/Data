Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_EG_GlobalDialogs("EG_TheChoice");
DB_EG_GlobalDialogs("EG_Companion_TheChoice");
DB_EG_GlobalDialogs("EG_NooneChoseDivinity");

DB_EG_CutsceneFlags("EG_Ending1_NewDivine","CS_Ending01");
DB_EG_CutsceneFlags("EG_Ending2_SourceForAll","CS_Ending01");
DB_EG_CutsceneFlags("EG_Ending3_Purge","CS_Ending01");
DB_EG_CutsceneFlags("EG_Ending4_GodKing","CS_Ending01");
DB_EG_CutsceneFlags("EG_Ending_5_Demon","CS_Ending01");

ItemSetCanInteract(ITEMGUID_S_EG_Aeteran_07405202-99b6-4516-ab57-a20e7fcd3083, 0);
PROC_LoopEffect("RS3_FX_GP_ScriptedEvent_SourceGlow_Overlay_01", ITEMGUID_S_EG_Aeteran_07405202-99b6-4516-ab57-a20e7fcd3083, "EG_AeteranGlow","ARX_EndGame","");

DB_EG_DivineJournalUpdates(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f, "QuestUpdate_FTJ_COM_Ifan_Crypt_Divine", "QuestUpdate_FTJ_Ifan_DarkFaction_ChoseCompanionDivine");
DB_EG_DivineJournalUpdates(CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295, "QuestUpdate_FTJ_COM_Lohse_Crypt_Divine", "QuestUpdate_FTJ_OriginLohse_ChoseCompanionDivine");
DB_EG_DivineJournalUpdates(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb, "QuestUpdate_FTJ_COM_Fane_Crypt_Divine", "QuestUpdate_FTJ_OriginFane_ChoseCompanionDivine");
DB_EG_DivineJournalUpdates(CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c, "QuestUpdate_FTJ_COM_Sebille_Crypt_Divine", "QuestUpdate_FTJ_OriginSebille_ChoseCompanionDivine");
DB_EG_DivineJournalUpdates(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978, "QuestUpdate_FTJ_COM_Beast_Crypt_Divine", "QuestUpdate_FTJ_OriginBeast_ChoseCompanionDivine");
DB_EG_DivineJournalUpdates(CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f, "QuestUpdate_FTJ_COM_RedPrince_Crypt_Divine", "QuestUpdate_FTJ_OriginRedPrince_ChoseCompanionDivine");


KBSECTION
//REGION Start the choice dialog
PROC
PROC_EG_MakeTheChoice(_)
AND
DB_IsPlayer(_Player)
AND
DB_IsPlayer(_Player2)
AND
_Player != _Player2
THEN
MakePeace(_Player, _Player2, 0);

PROC
PROC_EG_MakeTheChoice(_)
AND
DB_EG_DivinityCompetitor(_Competitor)
AND
NOT DB_IsPlayer(_Competitor)
THEN
SetInArena(_Competitor, 0);
LeaveCombat(_Competitor);
ProcForceStopDialog(_Competitor);

PROC
PROC_EG_MakeTheChoice(_)
AND
DB_IsPlayer(_Player)
THEN
SetInArena(_Player, 0);
LeaveCombat(_Player);
ProcForceStopDialog(_Player);

PROC
PROC_EG_MakeTheChoice(_)
THEN
CreateSurface(TRIGGERGUID_S_EG_TheChoicePosition_660fda5a-ae95-4aaf-a044-a18b234a48d7, "SurfaceNone", 5.0, -1.0);
CreateSurface(TRIGGERGUID_S_EG_TheChoicePosition_B_87f4c6f5-15df-4521-9ca8-9939fcb73378, "SurfaceNone", 5.0, -1.0);

PROC
PROC_EG_MakeTheChoice(_Player)
AND
DB_GlobalFlag("EndGame_BraccusTransformStarted")
AND
GetDistanceTo(_Player, TRIGGERGUID_S_EG_TheChoicePosition_B_87f4c6f5-15df-4521-9ca8-9939fcb73378, _Dist)
AND
_Dist >= 15.0
THEN
CharacterTeleportPartiesToTrigger(TRIGGERGUID_S_EG_TheChoicePosition_B_87f4c6f5-15df-4521-9ca8-9939fcb73378, "");

PROC
PROC_EG_MakeTheChoice(_Player)
AND
NOT DB_GlobalFlag("EndGame_BraccusTransformStarted")
AND
GetDistanceTo(_Player, TRIGGERGUID_S_EG_TheChoicePosition_660fda5a-ae95-4aaf-a044-a18b234a48d7, _Dist)
AND
_Dist >= 15.0
THEN
CharacterTeleportPartiesToTrigger(TRIGGERGUID_S_EG_TheChoicePosition_660fda5a-ae95-4aaf-a044-a18b234a48d7, "");

PROC
PROC_EG_MakeTheChoice((CHARACTERGUID)_Player)
AND
NOT DB_GlobalFlag("EG_Ending4_GodKing")
THEN
ObjectSetFlag(_Player,"EG_TheChoice_MadeIt");
DB_EG_Decider(_Player);

PROC
PROC_EG_MakeTheChoice((CHARACTERGUID)_Character)
THEN
TimerLaunch("EG_TheChoiceTimer", 300);
DB_EG_MakingTheChoice(_Character);

IF
TimerFinished("EG_TheChoiceTimer")
AND
DB_EG_MakingTheChoice(_Player)
AND
NOT DB_GlobalFlag("EG_Ending4_GodKing")
AND
DB_IsPlayer(_Player)
THEN
Proc_StartDialog(0, "EG_TheChoice", ITEMGUID_S_EG_Aeteran_07405202-99b6-4516-ab57-a20e7fcd3083, _Player);

IF
TimerFinished("EG_TheChoiceTimer")
AND
DB_EG_MakingTheChoice(_NPC)
AND
NOT DB_GlobalFlag("EG_Ending4_GodKing")
AND
NOT DB_IsPlayer(_NPC)
AND
GetClosestAlivePlayer(_NPC, _Player, _)
THEN
Proc_StartDialog(0, "EG_Companion_TheChoice", _NPC, _Player);

IF
TimerFinished("EG_TheChoiceTimer")
AND
DB_EG_MakingTheChoice(_Character)
AND
NOT DB_GlobalFlag("EG_Ending4_GodKing")
AND
HasActiveStatus(CHARACTERGUID_S_ARX_EG_Lucian_62d0ca66-b6db-4a10-a457-effe0b64aca0, "SITTING", 0)
THEN
RemoveHarmfulStatuses(_Character);
CharacterUseItem(_Character, ITEMGUID_S_ARX_EG_LucianChair_a807a420-ccf9-4aa6-aa54-bb691229be55, "");


IF
TimerFinished("EG_TheChoiceTimer")
AND
DB_EG_MakingTheChoice(_Player)
AND
DB_GlobalFlag("EG_Ending4_GodKing")
THEN
TeleportTo(ITEMGUID_S_GLO_EighthPromiseHelper_000_9c8c6eb9-84bb-47a6-91cb-4ef33442df06, _Player);
Proc_StartDialog(0, "EG_NooneChoseDivinity", ITEMGUID_S_GLO_EighthPromiseHelper_000_9c8c6eb9-84bb-47a6-91cb-4ef33442df06, _Player);

IF
TimerFinished("EG_TheChoiceTimer")
THEN
DB_EG_EpilogCanStart(1);

PROC
Proc_ObjectLeftCombat(_Player,_)
AND
DB_EG_MakingTheChoice((CHARACTERGUID)_Player)
AND
NOT DB_GlobalFlag("EG_Ending1_NewDivine")
AND
NOT DB_GlobalFlag("EG_Ending2_SourceForAll")
AND
NOT DB_GlobalFlag("EG_Ending3_Purge")
AND
NOT DB_GlobalFlag("EG_Ending4_GodKing")
AND
DB_IsPlayer(_Player)
THEN
Proc_StartDialog(0, "EG_TheChoice", ITEMGUID_S_EG_Aeteran_07405202-99b6-4516-ab57-a20e7fcd3083, _Player);

PROC
Proc_ObjectLeftCombat(_NPC,_)
AND
DB_EG_MakingTheChoice((CHARACTERGUID)_NPC)
AND
NOT DB_GlobalFlag("EG_Ending1_NewDivine")
AND
NOT DB_GlobalFlag("EG_Ending2_SourceForAll")
AND
NOT DB_GlobalFlag("EG_Ending3_Purge")
AND
NOT DB_GlobalFlag("EG_Ending4_GodKing")
AND
NOT DB_IsPlayer(_NPC)
AND
GetClosestAlivePlayer(_NPC, _Player, _)
THEN
Proc_StartDialog(0, "EG_Companion_TheChoice", _NPC, _Player);
//END_REGION

//REGION People should not be able to sit in the big chair mid-combat
PROC
ProcBlockUseOfItem(_Char, ITEMGUID_S_ARX_EG_LucianChair_a807a420-ccf9-4aa6-aa54-bb691229be55)
AND
CharacterIsInCombat(_Char, 1)
THEN
DB_CustomUseItemResponse(_Char,ITEMGUID_S_ARX_EG_LucianChair_a807a420-ccf9-4aa6-aa54-bb691229be55,0);

PROC
ProcBlockUseOfItem(_Char, ITEMGUID_S_ARX_EG_LucianChair_a807a420-ccf9-4aa6-aa54-bb691229be55)
AND
CharacterIsInCombat(_Char, 1)
AND
DB_IsPlayer(_Char)
THEN
Proc_StartDialog(1,"GLO_AD_CannotUseNow", _Char);
//END_REGION

//REGION Cutscenes
IF
DialogEnded(_EndgameDialog,_)
AND
DB_EG_CutsceneFlags(_Flag,_)
AND
GlobalGetFlag(_Flag,1)
AND
DB_EG_GlobalDialogs(_EndgameDialog)
AND
DB_IsPlayer(_Char)
THEN
PROC_EG_TransitionToEpilog(_Char);

PROC
PROC_EG_TransitionToEpilog((CHARACTERGUID)_Char)
THEN
PlayMovieForDialog(_Char,"EG_Epilogue_WindegoCutScene","Windego_Cutscene");

PROC
PROC_EG_TransitionToEpilog((CHARACTERGUID)_Char)
AND
QueryOnlyOnce("EG_EpilogueStarted")
THEN
PROC_EG_Epilogue_Start();

IF
MoviePlaylistFinished("Windego_Cutscene")
AND
QueryOnlyOnce("EG_EpilogueStarted")
THEN
PROC_EG_Epilogue_Start();

//END_REGION

//REGION Divine Aura
PROC
PROC_EG_Epilogue_Start()
AND
DB_GlobalFlag("EG_Ending1_NewDivine")
AND
DB_EG_Decider(_Player)
THEN
PROC_LoopEffect("RS3_FX_GP_ScriptedEvent_GodsAura_01",_Player,"DivineAura","ARX_Endgame","Dummy_StatusFX");

//END_REGION

//REGION Journal entries
PROC
PROC_EG_MakeTheChoice(_Companion)
AND
IsTagged(_Companion, "AVATAR", 0)
AND
DB_EG_DivineJournalUpdates(_Companion, _Update,_)
THEN
UserSetFlag(_Companion, _Update);


PROC
PROC_EG_MakeTheChoice(_Companion)
AND
IsTagged(_Companion, "AVATAR", 0)
AND
// Companions have been removed from the party here for (potentially) fighting in the
// final arena -> can't use DB_CompanionAvatarBond()
DB_Companion_FixatedFlag(_Companion, _Flag)
AND
DB_Avatars(_Player)
AND
ObjectGetFlag(_Player, _Flag, 1)
AND
DB_EG_DivineJournalUpdates(_Player, _, _Update)
THEN
ObjectSetFlag(_Player, _Update);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "EndGame"
