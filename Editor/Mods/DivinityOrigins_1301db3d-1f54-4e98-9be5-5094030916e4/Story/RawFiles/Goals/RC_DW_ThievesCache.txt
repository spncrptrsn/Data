Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_IsNotMessingAround(S_RC_DW_Gardener_Overseer_95a3ef01-dbc0-44a2-ba17-922ddc1bd3de);
DB_IsNotMessingAround(S_RC_DW_Human_Gardener_Addict_000_faf9467d-7f43-47ce-8bf4-3ac213deb768);
DB_IsNotMessingAround(S_RC_DW_Human_Gardener_Addict_001_603d0ca9-650b-48b6-993b-453c28f9238c);
DB_IsNotMessingAround(S_RC_DW_Human_Gardener_Addict_002_063d7dfc-e5de-4495-a749-9a4443a156f2);

// Dialogs
DB_Dialogs(CHARACTERGUID_S_RC_DW_Gardener_Overseer_95a3ef01-dbc0-44a2-ba17-922ddc1bd3de,"RC_DW_ThievesCache_Overseer");
DB_Dialogs(CHARACTERGUID_S_RC_DW_Gardener_Druid_786900c9-336b-40c2-878b-de2cd3c88135,"RC_DW_ThievesCache_Druid");
DB_Dialogs(CHARACTERGUID_S_RC_DW_Human_Gardener_Addict_000_faf9467d-7f43-47ce-8bf4-3ac213deb768,"RC_DW_ThievesCache_Addict");
DB_Dialogs(CHARACTERGUID_S_RC_DW_Human_Gardener_Addict_001_603d0ca9-650b-48b6-993b-453c28f9238c,"RC_DW_ThievesCache_Addict2");
DB_Dialogs(CHARACTERGUID_S_RC_DW_Human_Gardener_Addict_002_063d7dfc-e5de-4495-a749-9a4443a156f2,"RC_DW_ThievesCache_Addict3");

// Grave digging
DB_Ghosts((CHARACTERGUID)S_RC_DW_ThievesCache_MobVictim_000_73b79d85-9c7d-46f2-b8cf-003abe768e59,(CHARACTERGUID)S_RC_DW_ThievesCache_MobVictim_000_Ghost_521501a6-e777-4b7b-8397-1ef81639cf03,"RC_DW_ThievesCache_MobVictimGhost");
SetOnStage(S_RC_DW_ThievesCache_MobVictim_000_Ghost_521501a6-e777-4b7b-8397-1ef81639cf03,0);
DB_ShovelArea((TRIGGERGUID)S_RC_DW_ThievesCache_Grave_BoxTrig_2a69009a-8d69-42f0-ada2-155c9f369aaf,"RC_DW_ThievesCache_Grave",(ITEMGUID)S_RC_DW_ThievesCache_Grave_d7a9508e-4d67-4a24-a575-a611eb4bf013);
SetOnStage(S_RC_DW_ThievesCache_MobVictim_000_73b79d85-9c7d-46f2-b8cf-003abe768e59,0);

// Trespassing
// Ideally, both would be tied to the overseer, but a character can only have one sneak trigger
// AdvancedSneakTriggerSpotter supports multiple triggers, but does not appear to work -> use two
// different characters
DB_SneakTriggerSpotter(TRIGGERGUID_S_RC_DW_ThievesCache_Overseer_TalkBox_fda31140-97a9-4295-acda-9582f8d6a7e1,CHARACTERGUID_S_RC_DW_Human_Gardener_Addict_002_063d7dfc-e5de-4495-a749-9a4443a156f2);
DB_SneakTriggerSpotter(TRIGGERGUID_S_RC_DW_ThievesCache_Overseer_CombatBox_2203e619-65f9-451b-8c8a-5697da4e8d28,CHARACTERGUID_S_RC_DW_Gardener_Overseer_95a3ef01-dbc0-44a2-ba17-922ddc1bd3de);

CrimeAreaSetTensionModifier(TRIGGERGUID_S_RC_DW_CrimeAreaTrigger_ThievesCache_79b78188-79c3-4ed4-a413-6a3afaf569e0,4);

// ADs for addicts
DB_RC_DW_ThievesCache_Addict((CHARACTERGUID)S_RC_DW_Human_Gardener_Addict_000_faf9467d-7f43-47ce-8bf4-3ac213deb768);
DB_RC_DW_ThievesCache_Addict((CHARACTERGUID)S_RC_DW_Human_Gardener_Addict_001_603d0ca9-650b-48b6-993b-453c28f9238c);
DB_RC_DW_ThievesCache_Addict((CHARACTERGUID)S_RC_DW_Human_Gardener_Addict_002_063d7dfc-e5de-4495-a749-9a4443a156f2);

DB_RC_DW_ThievesCache_Addict_Action("Rake_01_Loop","RC_DW_ThievesCache_Raking");
DB_RC_DW_ThievesCache_Addict_Action("Fidget_Low_01","RC_DW_ThievesCache_Fidgeting");
DB_RC_DW_ThievesCache_Addict_Action("Fidget_Low_02","RC_DW_ThievesCache_Fidgeting");
DB_RC_DW_ThievesCache_Addict_Action("Fidget_Low_03","RC_DW_ThievesCache_Fidgeting");
DB_RC_DW_ThievesCache_Addict_Action("Stare_01_Loop","RC_DW_ThievesCache_StaringAhead");
DB_RC_DW_ThievesCache_Addict_Action("Insane_01_Loop","RC_DW_ThievesCache_Breakdown");
DB_RC_DW_ThievesCache_Addict_Action("Look_Down_Long_01","RC_DW_ThievesCache_StaringDown");

// Storage items
ItemTemplateAddTo("GRN_Grenade_Molotov_A_5208b121-64fa-4704-8924-c61c576e1ac5",ITEMGUID_S_RC_DW_ThievesCache_SunkenCrate_b6b69039-075c-4842-b5eb-9d2b29f262c2,2);
ItemTemplateAddTo("GRN_Grenade_Nailbomb_A_c2dac4cd-724b-4179-b72b-84e0d9ffa7a1",ITEMGUID_S_RC_DW_ThievesCache_SunkenCrate_b6b69039-075c-4842-b5eb-9d2b29f262c2,2);
ItemTemplateAddTo("GRN_Grenade_Molotov_A_5208b121-64fa-4704-8924-c61c576e1ac5",ITEMGUID_S_RC_DW_ThievesCache_SunkenCrate_b6b69039-075c-4842-b5eb-9d2b29f262c2,1);
ItemTemplateAddTo("GRN_Grenade_Flashbang_A_a71888d9-9d3f-4004-96c3-bbc1a158c3f6",ITEMGUID_S_RC_DW_ThievesCache_RockCrate_8e91934f-9953-48e1-a2b3-62d802bdaf9f,1);

PROC_RC_DW_ThievesCache_CustomCrimeInit();
KBSECTION
//REGION Debug
IF
TextEventSet("rc_dw_tc_reset")
AND
CharacterGetHostCharacter(_Player)
THEN
PartyClearFlag(_Player,"RC_DW_ThievesCache_OverseerPersuaded",0);
PartyClearFlag(_Player,"RC_DW_ThievesCace_OverseerMadeHostilePreviously",0);
PartyClearFlag(_Player,"RC_DW_ThievesCache_OverseerWarned",0);

IF
TextEventSet("rc_dw_tc_canlie")
AND
CharacterGetHostCharacter(_Player)
THEN
ObjectSetFlag(_Player,"RC_DW_GotDwarfBossJob");

//END_REGION

// If a character is spotted in the talk box, Bark will talk to them.
// If a character is spotted in the combat box and permission was not yet obtained
// from Bark, combat will commence.
//REGION Bark dialog
PROC
ProcCharInTriggerSpotted((CHARACTERGUID)_Player,TRIGGERGUID_S_RC_DW_ThievesCache_Overseer_TalkBox_fda31140-97a9-4295-acda-9582f8d6a7e1)
AND
QRY_SpeakerIsAvailable(CHARACTERGUID_S_RC_DW_Gardener_Overseer_95a3ef01-dbc0-44a2-ba17-922ddc1bd3de,1)
AND
ObjectGetFlag(_Player,"RC_DW_ThievesCache_OverseerPersuaded",0)
AND
ObjectGetFlag(_Player,"RC_DW_ThievesCache_OverseerWarned",0)
THEN
Proc_StartDialog(0,"RC_DW_ThievesCache_Overseer",CHARACTERGUID_S_RC_DW_Gardener_Overseer_95a3ef01-dbc0-44a2-ba17-922ddc1bd3de,_Player);

// Mark everyone nearby as warned (so we don't trigger the warning for them too)
IF
ObjectFlagSet("RC_DW_ThievesCache_OverseerWarned",(CHARACTERGUID)_Player,_)
AND
DB_IsPlayer(_OtherPlayer)
AND
ObjectGetFlag(_OtherPlayer,"RC_DW_ThievesCache_OverseerWarned",0)
AND
GetDistanceTo(_Player,_OtherPlayer,_Distance)
AND
_Distance <= 6.0
THEN
ObjectSetFlag(_OtherPlayer,"RC_DW_ThievesCache_OverseerWarned");

IF
ObjectFlagSet("RC_DW_ThievesCache_OverseerDecline",(CHARACTERGUID)_Player,_)
THEN
CharacterAddAttitudeTowardsPlayer((CHARACTERGUID)S_RC_DW_Gardener_Overseer_95a3ef01-dbc0-44a2-ba17-922ddc1bd3de,_Player,-15);
// Reset so we can decline more in the future!
ObjectClearFlag(_Player,"RC_DW_ThievesCache_OverseerDecline",0);

// Make Bark & addicts attack
PROC
Proc_RC_DW_ThievesCache_OverseerGoHostile((CHARACTERGUID)_Player)
THEN
SetStoryEvent(CHARACTERGUID_S_RC_DW_Gardener_Overseer_95a3ef01-dbc0-44a2-ba17-922ddc1bd3de,"RC_DW_GoHostile");
ObjectSetFlag(_Player,"RC_DW_ThievesCace_OverseerMadeHostilePreviously");
ProcForceStopDialog(CHARACTERGUID_S_RC_DW_Gardener_Overseer_95a3ef01-dbc0-44a2-ba17-922ddc1bd3de);

// Hostile because of tresspassing
PROC
ProcCharInTriggerSpotted((CHARACTERGUID)_Player,TRIGGERGUID_S_RC_DW_ThievesCache_Overseer_CombatBox_2203e619-65f9-451b-8c8a-5697da4e8d28)
AND
ObjectGetFlag(_Player,"RC_DW_ThievesCache_OverseerPersuaded",0)
THEN
Proc_RC_DW_ThievesCache_OverseerGoHostile(_Player);

// Hostile because attacked during dialog
IF
DialogEnded("RC_DW_ThievesCache_Overseer",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,_Player)
AND
ObjectGetFlag(_Player,"RC_DW_ThievesCache_OverseerHostileAfterDialog",1)
THEN
Proc_RC_DW_ThievesCache_OverseerGoHostile((CHARACTERGUID)_Player);

//END_REGION

//REGION Addict ADs (to avoid the AD blocking the animation in the charscript
IF
StoryEvent((CHARACTERGUID)_Addict,_Event)
AND
DB_RC_DW_ThievesCache_Addict(_Addict)
AND
DB_RC_DW_ThievesCache_Addict_Action(_Event,_Action)
THEN
DB_RC_ThievesCache_AddictAction(_Addict,_Action);
ObjectSetFlag(_Addict,_Action);
Proc_StartDialog(1,"RC_DW_AD_ThievesCache_Addict_Tending",_Addict);

IF
AutomatedDialogEnded("RC_DW_AD_ThievesCache_Addict_Tending",_ID)
AND
DialogGetInvolvedNPC(_ID,1,_Addict)
AND
DB_RC_ThievesCache_AddictAction((CHARACTERGUID)_Addict,_Action)
THEN
ObjectClearFlag(_Addict,_Action,0);
//END_REGION

//REGION Digging up mob victim
PROC
ProcShovelRewards(_Player,"RC_DW_ThievesCache_Grave")
AND
GetPosition(S_RC_DW_ThievesCache_Grave_d7a9508e-4d67-4a24-a575-a611eb4bf013,_X,_Y,_Z)
THEN
// Not DB_ShovelRewardCharacterAppear() because that doesn't work with dead NPCs
TeleportTo(S_RC_DW_ThievesCache_MobVictim_000_Ghost_521501a6-e777-4b7b-8397-1ef81639cf03,S_RC_DW_ThievesCache_Grave_d7a9508e-4d67-4a24-a575-a611eb4bf013);
SetOnStage(S_RC_DW_ThievesCache_MobVictim_000_Ghost_521501a6-e777-4b7b-8397-1ef81639cf03,1);
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01",_X,_Y,_Z);
SetOnStage(S_RC_DW_ThievesCache_MobVictim_000_73b79d85-9c7d-46f2-b8cf-003abe768e59,1);

PROC
ProcShovelRewards(_Player,"RC_DW_ThievesCache_Grave")
AND
DB_IsPlayer(_MeOrOtherPlayer)
AND
QRY_SpeakerIsAvailableAndInDialogRange(_Player,_MeOrOtherPlayer)
AND
QueryOnlyOnce("RC_DW_ThievesCache_Grave_Comment")
THEN
Proc_StartDialog(1,"RC_DW_AD_ThievesCache_DigUpMobVictim",_MeOrOtherPlayer);
//END_REGION

//REGION Addicts enter combat

//REGION Addicts clean off their shovel when overseer and druid are dead, and fight(s) are over
IF
DB_Dead(CHARACTERGUID_S_RC_DW_Gardener_Overseer_95a3ef01-dbc0-44a2-ba17-922ddc1bd3de)
AND
DB_Dead(CHARACTERGUID_S_RC_DW_Gardener_Druid_786900c9-336b-40c2-878b-de2cd3c88135)
AND
DB_RC_DW_ThievesCache_Addict(_Addict)
AND
NOT DB_Dead(_Addict)
THEN
SetHasDialog(_Addict,0);
LeaveCombat(_Addict);
CharacterSetReactionPriority(_Addict,"GetDrudanaeBeforeLeaving",1100);
CharacterSetReactionPriority(_Addict,"Leave",1000);

//END_REGION

//REGION Custom crime reactions (DOSTWO-22355)
PROC
PROC_RC_DW_ThievesCache_CustomCrimeInit()
AND
DB_RC_DW_ThievesCache_Addict(_Addict)
THEN
// The addicts are too far gone
ProcCharacterDisableAllCrimes(_Addict);

// Since the addicts don't react to crimes, we need a custom assault behaviour
IF
CharacterReceivedDamage(_Addict,_,_)
AND
DB_RC_DW_ThievesCache_Addict(_Addict)
THEN
// Works even if the overseer is dead
SetStoryEvent(CHARACTERGUID_S_RC_DW_Gardener_Overseer_95a3ef01-dbc0-44a2-ba17-922ddc1bd3de,"RC_DW_GoHostile");
//END_REGION

//REGION Clean up
PROC
Proc_RC_DW_ThievesCache_CleanUpTalkBox()
THEN
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_RC_DW_ThievesCache_Overseer_TalkBox_fda31140-97a9-4295-acda-9582f8d6a7e1);
ProcCleanUpSneakTrigger(CHARACTERGUID_S_RC_DW_Human_Gardener_Addict_002_063d7dfc-e5de-4495-a749-9a4443a156f2);

PROC
Proc_RC_DW_ThievesCache_CleanUpCombatBox()
THEN
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_RC_DW_ThievesCache_Overseer_CombatBox_2203e619-65f9-451b-8c8a-5697da4e8d28);
ProcCleanUpSneakTrigger(CHARACTERGUID_S_RC_DW_Gardener_Overseer_95a3ef01-dbc0-44a2-ba17-922ddc1bd3de);

PROC
ProcRegionEnded("RC_Main")
THEN
Proc_RC_DW_ThievesCache_CleanUpTalkBox();
Proc_RC_DW_ThievesCache_CleanUpCombatBox();
NOT DB_OnlyOnce("RC_DW_ThievesCache_Grave_Comment");
GoalCompleted;
//END_REGION
EXITSECTION
NOT DB_IsNotMessingAround(S_RC_DW_Gardener_Overseer_95a3ef01-dbc0-44a2-ba17-922ddc1bd3de);
NOT DB_IsNotMessingAround(S_RC_DW_Human_Gardener_Addict_000_faf9467d-7f43-47ce-8bf4-3ac213deb768);
NOT DB_IsNotMessingAround(S_RC_DW_Human_Gardener_Addict_001_603d0ca9-650b-48b6-993b-453c28f9238c);
NOT DB_IsNotMessingAround(S_RC_DW_Human_Gardener_Addict_002_063d7dfc-e5de-4495-a749-9a4443a156f2);

// Dialogs
NOT DB_Dialogs(CHARACTERGUID_S_RC_DW_Gardener_Overseer_95a3ef01-dbc0-44a2-ba17-922ddc1bd3de,"RC_DW_ThievesCache_Overseer");
NOT DB_Dialogs(CHARACTERGUID_S_RC_DW_Human_Gardener_Addict_000_faf9467d-7f43-47ce-8bf4-3ac213deb768,"RC_DW_ThievesCache_Addict");
NOT DB_Dialogs(CHARACTERGUID_S_RC_DW_Human_Gardener_Addict_001_603d0ca9-650b-48b6-993b-453c28f9238c,"RC_DW_ThievesCache_Addict");
NOT DB_Dialogs(CHARACTERGUID_S_RC_DW_Human_Gardener_Addict_002_063d7dfc-e5de-4495-a749-9a4443a156f2,"RC_DW_ThievesCache_Addict");

// Grave digging
NOT DB_Ghosts((CHARACTERGUID)S_RC_DW_ThievesCache_MobVictim_000_73b79d85-9c7d-46f2-b8cf-003abe768e59,(CHARACTERGUID)S_RC_DW_ThievesCache_MobVictim_000_Ghost_521501a6-e777-4b7b-8397-1ef81639cf03,"RC_DW_ThievesCache_MobVictimGhost");
NOT DB_ShovelArea((TRIGGERGUID)S_RC_DW_ThievesCache_Grave_BoxTrig_2a69009a-8d69-42f0-ada2-155c9f369aaf,"RC_DW_ThievesCache_Grave",(ITEMGUID)S_RC_DW_ThievesCache_Grave_d7a9508e-4d67-4a24-a575-a611eb4bf013);
NOT DB_ShovelRewardCharacterAppear("RC_DW_ThievesCache_Grave",(CHARACTERGUID)S_RC_DW_ThievesCache_MobVictim_000_Ghost_521501a6-e777-4b7b-8397-1ef81639cf03);

// ADs for addicts
NOT DB_RC_DW_ThievesCache_Addict((CHARACTERGUID)S_RC_DW_Human_Gardener_Addict_000_faf9467d-7f43-47ce-8bf4-3ac213deb768);
NOT DB_RC_DW_ThievesCache_Addict((CHARACTERGUID)S_RC_DW_Human_Gardener_Addict_001_603d0ca9-650b-48b6-993b-453c28f9238c);
NOT DB_RC_DW_ThievesCache_Addict((CHARACTERGUID)S_RC_DW_Human_Gardener_Addict_002_063d7dfc-e5de-4495-a749-9a4443a156f2);

NOT DB_RC_DW_ThievesCache_Addict_Action("Rake_01_Loop","RC_DW_ThievesCache_Raking");
NOT DB_RC_DW_ThievesCache_Addict_Action("Fidget_Low_01","RC_DW_ThievesCache_Fidgeting");
NOT DB_RC_DW_ThievesCache_Addict_Action("Fidget_Low_02","RC_DW_ThievesCache_Fidgeting");
NOT DB_RC_DW_ThievesCache_Addict_Action("Fidget_Low_03","RC_DW_ThievesCache_Fidgeting");
NOT DB_RC_DW_ThievesCache_Addict_Action("Stare_01_Loop","RC_DW_ThievesCache_StaringAhead");
NOT DB_RC_DW_ThievesCache_Addict_Action("Insane_01_Loop","RC_DW_ThievesCache_Breakdown");
NOT DB_RC_DW_ThievesCache_Addict_Action("Look_Down_Long_01","RC_DW_ThievesCache_StaringDown");
ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
