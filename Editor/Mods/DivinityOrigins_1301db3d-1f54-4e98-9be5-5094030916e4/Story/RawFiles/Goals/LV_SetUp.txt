Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,"FTJ_SW_River");
DB_CanUseSkillCheck("FTJ_SW_River","Target_Bless",1);
ItemSetCanInteract(ITEMGUID_S_FTJ_BoatToShip_001_63914b60-eea0-4d17-8081-898b58ef9cc0,0);
KBSECTION
//REGION River
PROC
ReactOnKillCounter("FTJ_FinalBattle")
THEN
SetOnStage(ITEMGUID_S_FTJ_BoatToShip_001_63914b60-eea0-4d17-8081-898b58ef9cc0,1);
SetOnStage(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,1);
//AttachVisualTo(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,ITEMGUID_S_FTJ_BoatToShip_001_63914b60-eea0-4d17-8081-898b58ef9cc0,"BLD_Humans_Boat_Small_Deck_A");
TimerLaunch("FTJ_SW_RiverAppear_Timer",4000);

IF
TimerFinished("FTJ_SW_RiverAppear_Timer")
AND
GlobalGetFlag("FTJ_LV_SetUp",0)
THEN
TeleportTo(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,TRIGGERGUID_S_FTJ_SW_RiverJumpTo_Point_d8964eef-17d9-49ba-8aeb-b60bc89b3933);
SetVarFixedString(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,"currentState","State_FTJ_SW_EndOfAct1");
CreateSurface(TRIGGERGUID_S_FTJ_SW_RiverJumpTo_Point_d8964eef-17d9-49ba-8aeb-b60bc89b3933,"SurfaceNone",6.5,1.0);
CreateSurface(TRIGGERGUID_S_FTJ_SW_RverDefault_Point_52146de3-d50a-4dcd-9997-3156ba43ac43,"SurfaceNone",6.5,1.0);
TimerLaunch("FTJ_SW_BoatToLV_Splash_Stop",300);
TriggerResetAtmosphere(TRIGGERGUID_S_AtmosphereTrigger_FTJ_FinalBattleBeach_d4987042-6d48-4c3d-8ac5-763d0c400902);

//Dialog move to
IF
ObjectFlagSet("FTJ_SW_MaladyMoveToPlayer",_Player,_)
THEN
ProcCharacterMoveTo(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,_Player,1,"");
ObjectClearFlag(_Player,"FTJ_SW_MaladyMoveToPlayer",0);


//REGION Can leave check
IF
DialogStarted("FTJ_SW_River",_)
THEN
PROC_CheckCanLeaveAct1();

IF
DialogStarted("FTJ_SW_River",_Id)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_MetRiver");

PROC
PROC_CheckCanLeaveAct1()
THEN
ProcDeclareCounter("FTJ_CanLeaveAct1Players");

PROC
PROC_CheckCanLeaveAct1()
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player,"AVATAR",1)
AND
ObjectGetFlag(_Player,"FTJ_SW_HoEReturnToShrine",1)
THEN
ProcIncreaseCounter("FTJ_CanLeaveAct1Players",1);
/*
PROC
PROC_CheckCanLeaveAct1()
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player,"AVATAR",0)
AND
ObjectGetFlag(_Player,"FTJ_SW_ShrineSendToHoE",1)
THEN
ProcIncreaseCounter("FTJ_CanLeaveAct1Players",1);
*/
PROC
PROC_CheckCanLeaveAct1()
AND
SysCount("DB_Avatars",1,_AvatarCount)
AND
DB_GlobalCounter("FTJ_CanLeaveAct1Players",_TotalCanLeave)
AND
_AvatarCount == _TotalCanLeave
THEN
GlobalSetFlag("FTJ_SW_PartyCanLeaveAct1");

PROC
PROC_CheckCanLeaveAct1()
AND
SysCount("DB_Avatars",1,_AvatarCount)
AND
DB_GlobalCounter("FTJ_CanLeaveAct1Players",_TotalCanLeave)
AND
_AvatarCount != _TotalCanLeave
THEN
GlobalClearFlag("FTJ_SW_PartyCanLeaveAct1");

PROC
PROC_CheckCanLeaveAct1()
THEN
ProcRemoveCounter("FTJ_CanLeaveAct1Players");
//END_REGION

IF
ObjectFlagSet("AddSourcePoint",_Player,_ID)
AND
DB_DialogName("FTJ_SW_River",_ID)
THEN
PlayEffect(_Player,"RS3_FX_GP_ScriptedEvent_Replenish_SourcePoint_01");


IF
ObjectFlagSet("FTJ_SW_PlayerUseBlessOnRiver",(CHARACTERGUID)_Player,_)
THEN
CharacterUseSkill(_Player,"Target_Bless",CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e);

IF
ObjectFlagSet("FTJ_SW_PlayerUseBlessOnSelf",(CHARACTERGUID)_Player,_)
THEN
CharacterUseSkill(_Player,"Target_Bless",_Player);


IF
StoryEvent((CHARACTERGUID)_Player,"FTJ_RiverStartDialog")
AND
QueryOnlyOnce("FTJ_SW_River_StartDialog_Once")
THEN
Proc_StartDialog(0,"FTJ_SW_River",CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,_Player);

IF
DialogStarted("FTJ_SW_River",_DlgInst)
THEN
DB_GLO_AllDialogObjectFlagsShared(_DlgInst);


//Special set up for River and sending companions to the HoE
PROC
Proc_FTJ_SW_HoeStatue_StunCompanions((CHARACTERGUID)_Player,CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e)
AND
CharacterGetReservedUserID(_Player,_User)
AND
DB_IsPlayer((CHARACTERGUID)_Char)
AND
CharacterGetReservedUserID(_Char,_User)
AND
_Char != _Player
AND
IsTagged(_Char,"AVATAR",0)
AND
ObjectGetFlag(_Char,"FTJ_SW_ShrineSendToHoE",0)
THEN
ProcForceStopDialog(_Char);
ObjectSetFlag(_Char,"FTJ_SW_ShrineSendToHoE",0);
CharacterPurgeQueue(_Char);
ApplyStatus(_Char,"SLEEPING",15.0,1);
DB_FTJ_SW_CompanionHadHoEExperience(_Char);

//END_REGION

IF
GlobalFlagSet("FTJ_LV_RiverSetUp_Debug")
AND
CharacterGetHostCharacter((CHARACTERGUID)_Player)
THEN
TeleportTo(_Player,TRIGGERGUID_S_FTJ_SW_RiverJumpTo_Point_d8964eef-17d9-49ba-8aeb-b60bc89b3933);
ReactOnKillCounter("FTJ_FinalBattle");

IF
GlobalFlagSet("FTJ_LV_SetUp")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
CharacterResurrect(_Player);

IF
GlobalFlagSet("FTJ_LV_SetUp")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_LadyVengeance");
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Island_EscapeLadyVengeance");

IF
DialogEnded("FTJ_SW_River",_)
THEN
ProcClearStoryMove(CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e);

IF
DialogEnded("FTJ_SW_River",_ID)
AND
DB_DialogPlayers(_ID,_Player,_)
AND
GlobalGetFlag("FTJ_LV_PartyToLV",1)
THEN
GlobalClearFlag("FTJ_LV_PartyToLV");
ReadyCheckStart(_Player,"FTJ_LV_PartyToLV");

IF
ReadyCheckPassed("FTJ_LV_PartyToLV")
THEN
GlobalSetFlag("FTJ_LV_SetUp");
//ProcSetFlagOnAll("QuestClose_FTJ_SW_BatteredAndCornered");

IF
GlobalFlagSet("FTJ_LV_SetUp")
THEN
CharacterTeleportPartiesToTriggerWithMovie(TRIGGERGUID_S_FTJ_LV_BoatToLV_Point_d54d0088-0784-4e92-b5da-34d8eb1ef4ab,"LV_Main_StartQuest","CS_FJtoLVmov");

IF
GlobalFlagSet("FTJ_LV_SetUp")
AND
DB_OriginRecruitmentDialog((CHARACTERGUID)_Player,_)
THEN
CharacterSetRelationIndivFactionToFaction(_Player,"RC_Magister",0);
CharacterSetRelationFactionToIndivFaction("RC_Magister",_Player,0);
ObjectSetFlag(_Player,"FTJ_RemoveSourceCollar");
CharacterAddSkill(_Player,"Target_Bless");

IF
StoryEvent((CHARACTERGUID)_Player,"LV_Main_StartQuest")
THEN
PROC_LV_Main_StartQuest(_Player);

PROC
ProcRegionEnded("FJ_FortJoy_Main")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "FortJoy"
