Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
// Wake up origins when player hears screams on top deck
IF
TimerFinished("TUT_BreakCrate")
AND
QueryOnlyOnce("TUT_OriginsGetUp")
AND
// NPCs can't be revived -> no problem that QueryOnlyOnce() is above
QRY_TUT_AnyRescueOriginNPCAlive()
THEN
GlobalSetFlag("TUT_OriginsWokenUp");
// First origin wakes up immediately (to make congruent with fact that players hear screaming immediately),
// rest will wake up later
DB_TUT_OriginWakeUpRandom(1);
DB_TUT_OriginWakeUpRandomOffset(0);
PROC_TUT_WakeUpOrigins();

PROC
PROC_TUT_WakeUpOrigins()
AND
DB_TUT_OriginsToRescue(_Char)
AND
DB_TUT_LowerDeck_WindegoKnockedDown(_Char)
AND
NOT DB_Dead(_Char)
AND
NOT DB_TUT_OriginWokenUp(_Char)
AND
DB_TUT_OriginWakeUpRandom(_Rand)
AND
DB_TUT_OriginWakeUpRandomOffset(_Offset)
AND
Random(_Rand,_Delay1)
AND
IntegerSum(_Rand,_Delay1,_Delay)
THEN
DB_TUT_OriginWokenUp(_Char);
ProcObjectTimer(_Char,"TUT_LowerDeck_OriginGetUp",_Delay);
NOT DB_TUT_OriginWakeUpRandom(_Rand);
NOT DB_TUT_OriginWakeUpRandomOffset(_Offset);
DB_TUT_OriginWakeUpRandom(3000);
DB_TUT_OriginWakeUpRandomOffset(500);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char,"TUT_LowerDeck_OriginGetUp")
THEN
SetVarInteger(_Char, "CanSpeak", 1);
PROC_TUT_RemoveWindegoKnockDownEffects(_Char);
Proc_StartDialog(1,"TUT_AD_LowerDeck_OriginsWakeUpAfterWindego",_Char);
//END_REGION

PROC
ProcRegionEnded("TUT_Tutorial_A")
AND
DB_OriginRecruitmentLocation_Region("TUT_Tutorial_A",_Char,_,_)
THEN
SetVarInteger(_Char, "CanSpeak", 1);

PROC
ProcRegionEnded("TUT_Tutorial_A")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "TUT_LowerDeck"
