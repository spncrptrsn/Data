Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION Global


//REGION Character creation by broken mods
// DOSTWO-24607
IF
SavegameLoaded(_,_,_,_)
AND
DB_IsPlayer(_Player)
AND
DB_CharacterCreationDummy(_Player)
THEN
NOT DB_IsPlayer(_Player);
CharacterRemoveFromParty(_Player);
CharacterMakeNPC(_Player);
SetOnStage(_Player,0);

//END_REGION

// No version check, because cannot find how this can happen and hence cannot fix the cause
// (DOSTWO-26872)
IF
SavegameLoaded(_,_,_,_)
AND
GlobalGetFlag("GLO_ShipFreed",1)
AND
GlobalGetFlag("GLO_ShipSubjugated",1)
THEN
GlobalClearFlag("GLO_ShipSubjugated");

//END_REGION

//REGION Bugfix DOSTWO-24247 (Somebody left Dreamscene unoficially)
IF
SavegameLoaded(_,_,_,_)
AND
DB_RC_DB_Meistr_Dream_Occupied(_Trigger,_Player,_X,_Y,_Z)
AND
NOT DB_InRegion(_Player,S_RC_DW_Meistr_DreamScene_f206d532-488e-469f-bb2a-5fb4270aa438)
THEN
NOT DB_RC_DB_Meistr_Dream_Occupied(_Trigger,_Player,_X,_Y,_Z);
PROC_RC_DW_Meistr_Completed_DreamSequence(_Player);
DB_BUGFIX_Marker(_Player,"DOSTWO-24247");
//END_REGION

//REGION Weird avatar+companion bug (DOSTWO-22497)
//Someone sent in a savegame where companion Fane became an Avatar. Could not find how this happened so can only fix the symptom.
IF
SavegameLoaded(_,_,_,_)
AND
DB_Avatars(_Companion)
AND
DB_CompanionAvatarBond(_Companion,_)
AND
DB_OriginInPartyDialog(_Companion,_Dialog)
THEN
ClearTag(_Companion,"AVATAR");
NOT DB_Avatars(_Companion);
NOT DB_GLO_PartyMembers_InPartyDialog(_Companion,"");
PROC_GLO_PartyMembers_SetInpartyDialog(_Companion,_Dialog);
DB_BUGFIX_Marker(_Companion,"DOSTWO-22497");

//END_REGION


//REGION //To prevent players from missing out on journal entries if they left the tutorial without ever goin up to the lower deck. (DOS2EE-3729)

IF //Grants quests to players who did not receive them
SavegameLoaded(_Major,_Minor,_Rev,_Build)
AND
QRY_VersionIsOlderThan(_Major,_Minor,_Rev,_Build, 3, 5, 1, 0)
AND
DB_IsPlayer(_Player)
AND
UserGetFlag(_Player,"QuestUpdate_CORE_Chapter1_TUT_Start",0)
AND
GetRegion(_Player,"FJ_FortJoy_Main")
THEN
UserSetFlag(_Player,"QuestUpdate_CORE_Chapter1_TUT_Start");
UserSetFlag(_Player,"QuestUpdate_CORE_Chapter1_TUT_Lore");
UserSetFlag(_Player,"QuestUpdate_CORE_Chapter1_TUT_FTJ");
UserSetFlag(_Player,"QuestUpdate_CORE_Magisters_InitialSetup");
PartySetFlag(_Player,"QuestUpdate_TUT_ShipMurder_WakeUp");

IF //Grants quests to players who did not receive them
SavegameLoaded(_Major,_Minor,_Rev,_Build)
AND
QRY_VersionIsOlderThan(_Major,_Minor,_Rev,_Build, 3, 5, 1, 0)
AND
DB_IsPlayer(_Player)
AND
DB_GLO_SourceCollars_OncePerNPC(_Player)
AND
IsTagged(_Player,"AVATAR",1)
AND
ObjectGetFlag(_Player,"QuestUpdate_RC_FTJ_SourceCollar_StartSourceCollar",0)
AND
GetRegion(_Player,"FJ_FortJoy_Main")
THEN
ObjectSetFlag(_Player,"QuestUpdate_RC_FTJ_SourceCollar_StartSourceCollar");

IF //Grants quests to players who did not receive them
SavegameLoaded(_Major,_Minor,_Rev,_Build)
AND
QRY_VersionIsOlderThan(_Major,_Minor,_Rev,_Build, 3, 5, 1, 0)
AND
DB_TUT_OriginQuestStarts(_Player,_AddQuest,_FirstUpdate)
AND
ObjectGetFlag(_Player,_AddQuest,0)
AND
IsTagged(_Player,"AVATAR",1)
AND
GetRegion(_Player,"FJ_FortJoy_Main")
THEN
ObjectSetFlag(_Player,_AddQuest);
ObjectSetFlag(_Player,_FirstUpdate);

//END_REGION


//REGION Helpers
QRY
QRY_Patching_CurrentLevelAnyOf((STRING)_Level1,(STRING)_Level2,(STRING)_Level3)
AND
DB_CurrentLevel(_Level3)
THEN
DB_NOOP(1);

QRY
QRY_Patching_CurrentLevelAnyOf((STRING)_Level1,(STRING)_Level2,(STRING)_Level3)
AND
QRY_Patching_CurrentLevelAnyOf(_Level1,_Level2)
THEN
DB_NOOP(1);

QRY
QRY_Patching_CurrentLevelAnyOf((STRING)_Level1,(STRING)_Level2)
AND
DB_CurrentLevel(_Level2)
THEN
DB_NOOP(1);

QRY
QRY_Patching_CurrentLevelAnyOf((STRING)_Level1,(STRING)_Level2)
AND
QRY_Patching_CurrentLevelAnyOf(_Level1)
THEN
DB_NOOP(1);

QRY
QRY_Patching_CurrentLevelAnyOf((STRING)_Level1)
AND
DB_CurrentLevel(_Level1)
THEN
DB_NOOP(1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start"
