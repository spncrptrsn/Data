Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Dialog

DB_Ghosts(S_RC_ARX_Prison_TorturedMagister_Ghost_376f7ed3-85df-455e-b650-e2b25568a6e7,"RC_ARX_Barracks_TorturedMagister_Ghost");

DB_Dialogs(CHARACTERGUID_S_RC_ARX_Prison_Warden_63b53c11-2f0d-40b7-8771-05df0e4f264d,"RC_ARX_Prison_Warden");
ItemToInventory(S_ARX_PrisonCells_Key_33488677-ea0c-4b0f-8bf6-d51627c5ea0f,CHARACTERGUID_S_RC_ARX_Prison_Warden_63b53c11-2f0d-40b7-8771-05df0e4f264d);

DB_Dialogs(CHARACTERGUID_S_RC_ARX_Prison_Guard_01_06ccfe8f-07fe-432a-8f69-9e6c153ad7c9,"RC_ARX_Prison_Guard_01");
DB_Dialogs(CHARACTERGUID_S_RC_ARX_Prison_Guard_02_cd12ddda-06ab-4310-9db5-7a15ae9ddd5b,"RC_ARX_Prison_Guard_02");
DB_Dialogs(CHARACTERGUID_S_RC_ARX_Prison_Guard_04_488baaed-6298-4155-a7e1-3e0f094da5d9,"RC_ARX_Prison_Guard_04");

ProcCharacterDisableCrime(S_ARX_Prison_Arhus_Keeper_43570f67-d127-4636-bac4-d898001afb9e,"UseForbiddenItem");

DB_ARX_Prison_DoorsOnwership_Characters(CHARACTERGUID_S_RC_ARX_Prison_Warden_63b53c11-2f0d-40b7-8771-05df0e4f264d);
DB_ARX_Prison_DoorsOnwership_Characters(CHARACTERGUID_S_RC_ARX_Prison_Guard_01_06ccfe8f-07fe-432a-8f69-9e6c153ad7c9);
DB_ARX_Prison_DoorsOnwership_Characters(CHARACTERGUID_S_RC_ARX_Prison_Guard_02_cd12ddda-06ab-4310-9db5-7a15ae9ddd5b);
DB_ARX_Prison_DoorsOnwership_Characters(CHARACTERGUID_S_RC_ARX_Prison_Guard_04_488baaed-6298-4155-a7e1-3e0f094da5d9);
DB_ARX_Prison_DoorsOnwership_Characters(CHARACTERGUID_S_ARX_Prison_ToWindegoGuard2_9b70b655-d8f4-4690-9da5-39c364ed3c77);
DB_ARX_Prison_DoorsOnwership_Characters(CHARACTERGUID_S_ARX_Prison_ToWindegoGuard1_e58ded34-c56c-4959-9c7b-159529273705);

DB_ARX_Prison_DoorsOnwership_Doors(ITEMGUID_S_ARX_Prison_PlayerCell_Door_3a6d9404-27b0-8dbc-199a-5b83156d3943);
DB_ARX_Prison_DoorsOnwership_Doors(ITEMGUID_S_BLD_Humans_Prison_Grating_Door_B_000_e1204a91-5dd3-4138-80c2-e254cf955503);
DB_ARX_Prison_DoorsOnwership_Doors(ITEMGUID_S_BLD_Humans_Prison_Grating_Door_B_002_b149b405-f50a-4341-aea3-ed7fa585872a);
DB_ARX_Prison_DoorsOnwership_Doors(ITEMGUID_S_BLD_Humans_Prison_Grating_Door_B_003_9f22c602-ff29-4701-8c21-3bbf68e3a095);


//END_REGION 

//REGION 2 Magister Prisoners 
DB_Dialogs(CHARACTERGUID_S_ARX_Prison_Arhus_Keeper_43570f67-d127-4636-bac4-d898001afb9e,"ARX_Prison_Magister_Prisoner");
DB_Dialogs(S_ARX_Prison_Magister_Prisoner02_c0aa5b34-a755-46b1-bd17-5df848fe2be4,"ARX_Prison_Magister_Prisoner02");

DB_ARX_Prison_Magister_Prisoners(S_ARX_Prison_Arhus_Keeper_43570f67-d127-4636-bac4-d898001afb9e);
DB_ARX_Prison_Magister_Prisoners(S_ARX_Prison_Magister_Prisoner02_c0aa5b34-a755-46b1-bd17-5df848fe2be4);

ProcCharacterDisableAllCrimes(CHARACTERGUID_S_ARX_Prison_Magister_Prisoner02_c0aa5b34-a755-46b1-bd17-5df848fe2be4);
ProcCharacterDisableAllCrimes(CHARACTERGUID_S_ARX_Prison_Magister_Prisoner01_43570f67-d127-4636-bac4-d898001afb9e);

ProcCharacterEnableCrime(CHARACTERGUID_S_ARX_Prison_Magister_Prisoner02_c0aa5b34-a755-46b1-bd17-5df848fe2be4,"Assault");
ProcCharacterEnableCrime(CHARACTERGUID_S_ARX_Prison_Magister_Prisoner01_43570f67-d127-4636-bac4-d898001afb9e,"Assault");


//END_REGION

//REGION Destructable Hatch
DB_ARX_Prison_DestructibleGradingCanUse((ITEMGUID)S_ARX_Prison_SecretPath_FromPlayerPrison_057e7643-c45f-4456-b058-c2c7f2cb4570,"FromPlayerCell");
DB_ARX_Prison_DestructibleGradingCanUse((ITEMGUID)S_ARX_Prison_SecretPath_ToPlayerPrison_7235b773-54b2-4085-9d42-f74f0a5bec4d,"ToPlayerCell");

DB_ARX_Prison_DestructibleGradingCantUse((ITEMGUID)S_ARX_Prison_SecretPath_FromPlayerPrison_CantUse_3ec7489e-f9d9-4b33-a814-cabd77745bec,"FromPlayerCell");
DB_ARX_Prison_DestructibleGradingCantUse((ITEMGUID)S_ARX_Prison_SecretPath_ToPlayerPrison_CantUse_e42c6b0b-94be-47a0-b01f-ab82e32b1cf5,"ToPlayerCell");

DB_ARX_Prison_DestructibleGradingCantUseDestructivewood(S_ARX_Prison_SecretPath_ToPlayerPrison_CantUse_e42c6b0b-94be-47a0-b01f-ab82e32b1cf5,S_ARX_Prison_SecretPath_ToPlayerPrison_WoodBlock_b9665f3b-5f06-4b6d-af7a-24e42a55b8c8);
DB_ARX_Prison_DestructibleGradingCantUseDestructivewood(S_ARX_Prison_SecretPath_FromPlayerPrison_CantUse_3ec7489e-f9d9-4b33-a814-cabd77745bec,S_ARX_Prison_SecretPath_FromPlayerPrison_WoodBlock_b9924613-06d1-43fb-ba07-8909f93d35a6);

//END_REGION

//REGION Missing Prisoners
DB_QuestDef_State("ARX_MissingPrisoners","NoticedMissingPrisoners",1);
DB_QuestDef_State("ARX_MissingPrisoners","FoundDeadPrisoners");
DB_QuestDef_State("ARX_MissingPrisoners","FoundPrisonerGhost"); //QuestUpdate_ARX_MissingPrisoners_FoundPrisonerGhost
DB_QuestDef_State("ARX_MissingPrisoners","KnowsMagistersWereInvestigating");
DB_QuestDef_State("ARX_MissingPrisoners","PrisonerWereUsedForSource");
DB_QuestDef_State("ARX_MissingPrisoners","ReportedTotheWarden",-1);
DB_QuestDef_State("ARX_MissingPrisoners","CloseLeftARX",-1);
DB_QuestDef_CloseEvent("ARX_MissingPrisoners","ARX_Prison_MissingPrisoners_CloseJournal");

DB_SeenDeadNPCPartyFlag(CHARACTERGUID_S_RC_ARX_Prison_Warden_63b53c11-2f0d-40b7-8771-05df0e4f264d,"ARX_Parison_SawDeadWarden");
//END_REGION
KBSECTION
//REGION Magister Prisoners

IF
ItemOpened(BLD_Humans_Prison_Grating_Door_B_000_e1204a91-5dd3-4138-80c2-e254cf955503)
AND
DB_ARX_Prison_Magister_Prisoners(_MagisterPrisoner)
AND
CharacterIsDead((CHARACTERGUID)_MagisterPrisoner,0)
THEN
ObjectSetFlag(_MagisterPrisoner,"ARX_Prison_ArhusKeeperFleeWhenPossible");
ProcForceStopDialog(_MagisterPrisoner);

IF
ItemDestroyed(BLD_Humans_Prison_Grating_Door_B_000_e1204a91-5dd3-4138-80c2-e254cf955503)
AND
DB_ARX_Prison_Magister_Prisoners(_MagisterPrisoner)
AND
CharacterIsDead((CHARACTERGUID)_MagisterPrisoner,0)
THEN
ObjectSetFlag(_MagisterPrisoner,"ARX_Prison_ArhusKeeperFleeWhenPossible");
ProcForceStopDialog(_MagisterPrisoner);

IF
ObjectFlagSet("ARX_Prison_ArhusKeeper_Disapear",_NPC,_)
THEN
ProcCharacterDisappearOutOfSight((CHARACTERGUID)_NPC,270,1,"",0);

IF
AttackedByObject(S_ARX_Prison_Arhus_Keeper_43570f67-d127-4636-bac4-d898001afb9e,S_ARX_Prison_Magister_Prisoner02_c0aa5b34-a755-46b1-bd17-5df848fe2be4,_,_,_)
AND
GlobalGetFlag("ARX_Prison_Magister_Prisoner_ToldAboutWhiteMagisters",1)
AND
CharacterIsInCombat(S_ARX_Prison_Arhus_Keeper_43570f67-d127-4636-bac4-d898001afb9e,0)
THEN
CharacterDie(S_ARX_Prison_Arhus_Keeper_43570f67-d127-4636-bac4-d898001afb9e,1,"Physical");

IF
ObjectEnteredCombat(S_ARX_Prison_Magister_Prisoner02_c0aa5b34-a755-46b1-bd17-5df848fe2be4,_ID)
AND
GlobalGetFlag("ARX_Prison_Magister_Prisoner_ToldAboutWhiteMagisters",1)
THEN
Proc_CharacterSetTemporaryHostileRelation(S_ARX_Prison_Magister_Prisoner02_c0aa5b34-a755-46b1-bd17-5df848fe2be4,S_ARX_Prison_Arhus_Keeper_43570f67-d127-4636-bac4-d898001afb9e);


IF
ObjectFlagSet("ARX_Prison_Magister_Prisoner_ToldAboutWhiteMagisters",_Player,_ID)
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
ProcShowMarker(_Player,"ARX_Barracks_Storage");

//END_REGION

//REGION Destructable Hatch

IF
CharacterUsedItem(_Player,_Item)
AND
NOT DB_ARX_Prison_DestructibleGradingCantUse_PlayedAD(_Player,_Item)
AND
DB_ARX_Prison_DestructibleGradingCantUse(_Item,_)
THEN
Proc_StartDialog(1,"ARX_AD_Prison_DestructibleGradingCantUse",_Player);
DB_ARX_Prison_DestructibleGradingCantUse_PlayedAD(_Player,_Item);
ProcObjectTimer(_Player,"ARX_AD_Prison_DestructibleGradingCantUse",4000);


PROC
ProcObjectTimerFinished(_Player,"ARX_AD_Prison_DestructibleGradingCantUse") //Anti Spam and Cleanup
AND
DB_ARX_Prison_DestructibleGradingCantUse_PlayedAD((CHARACTERGUID)_Player,_Item)
THEN
NOT DB_ARX_Prison_DestructibleGradingCantUse_PlayedAD(_Player,_Item);

//Prison Escape ARX
IF
DB_ARX_Prison_DestructibleGradingCanUse(_Item,_String)
THEN
SetOnStage(_Item,0);

IF
ItemDestroying(_ItemCantUse)
AND
DB_ARX_Prison_DestructibleGradingCantUse((ITEMGUID)_ItemCantUse,_String)
AND
DB_ARX_Prison_DestructibleGradingCanUse(_ItemCanUse,_String)
AND
DB_ARX_Prison_DestructibleGradingCantUseDestructivewood(_ItemCantUse,_Wood)
THEN
SetOnStage(_ItemCantUse,0);
SetOnStage(_ItemCanUse,1);
SetOnStage(_Wood,0);
GlobalSetFlag("ARX_Prison_GratingIsBorken");

IF
CharacterUsedItem(_Player,_ItemCanUse)
AND
DB_IsPlayer(_Player)
AND
DB_ARX_Prison_DestructibleGradingCanUse((ITEMGUID)_ItemCanUse,_)
THEN
ARX_Prison_DestructibleGradingSwapAll();

/*
IF
ItemWentOnStage(_ItemCantUse,0)
AND
DB_ARX_Prison_DestructibleGradingCantUseDestructivewood(_ItemCantUse,_Wood)
THEN
SetOnStage(_Wood,0);

IF
ItemWentOnStage(_ItemCantUse,1)
AND
DB_ARX_Prison_DestructibleGradingCantUseDestructivewood(_ItemCantUse,_Wood)
THEN
SetOnStage(_Wood,1);
*/
PROC
ARX_Prison_DestructibleGradingSwapAll()
AND
DB_ARX_Prison_DestructibleGradingCantUse(_ItemCantUse,_)
THEN
SetOnStage(_ItemCantUse,0);

PROC
ARX_Prison_DestructibleGradingSwapAll()
AND
DB_ARX_Prison_DestructibleGradingCantUseDestructivewood(_ItemCantUse,_Wood)
THEN
SetOnStage(_Wood,0);


PROC
ARX_Prison_DestructibleGradingSwapAll()
AND
DB_ARX_Prison_DestructibleGradingCanUse((ITEMGUID)_ItemCanUse,_)
THEN
SetOnStage(_ItemCanUse,1);
/* // Moved the a new location with the mistake
IF 
CharacterLeftTrigger(_Char,S_RC_ARX_Prison_12cea110-e5b9-4362-9699-69fdc1baa917) 
AND
DB_CurrentLevel(_)
AND
DB_IsPlayer(_Char)
AND
NOT DB_InRegion(_,S_RC_ARX_Prison_12cea110-e5b9-4362-9699-69fdc1baa917)
AND
GlobalGetFlag("ARX_Prison_GratingIsBorken",1)
THEN
GlobalClearFlag("ARX_Prison_GratingIsBorken");
ARX_Prison_DestructibleGrading_Reset();
*/
PROC
ARX_Prison_DestructibleGrading_Reset()
AND
DB_ARX_Prison_DestructibleGradingCanUse((ITEMGUID)_ItemCanUse,_)
THEN
SetOnStage(_ItemCanUse,0);

PROC
ARX_Prison_DestructibleGrading_Reset()
AND
DB_ARX_Prison_DestructibleGradingCantUse(_ItemCantUse,_)
THEN
SetOnStage(_ItemCantUse,1);


//END_REGION

//REGION Know About Q and Prisoners 

IF
GameBookInterfaceClosed(S_ARX_Sewers_QsResearchNotesInStudy_01_c7c4cc72-ef25-4a3a-91ac-a0465fac1087,_Player)
THEN
PartySetFlag(_Player,"QuestUpdate_ARX_MissingPrisoners_PrisonerWereUsedForSource"); 

IF
GameBookInterfaceClosed(S_ARX_Sewers_QsResearchNotesInStudy_01_c7c4cc72-ef25-4a3a-91ac-a0465fac1087,_Player)
AND
CharacterIsDead(CHARACTERGUID_S_RC_ARX_Prison_Warden_63b53c11-2f0d-40b7-8771-05df0e4f264d,1)
THEN
PartySetFlag(_Player,"ARX_Prison_MissingPrisoners_CloseJournal"); 

IF
ObjectFlagSet("ARX_Parison_SawDeadWarden",(CHARACTERGUID)_Player,_ID)
AND
ObjectGetFlag(_Player,"QuestUpdate_ARX_MissingPrisoners_PrisonerWereUsedForSource",1)
THEN
PartySetFlag(_Player,"ARX_Prison_MissingPrisoners_CloseJournal"); 

//END_REGION

//REGION Turture Room

IF
GameBookInterfaceClosed(S_ARX_Prison_MagisterConfesion_983c7492-9656-4d4c-9e73-14ea954f5cc7,_Player)
THEN
UserSetFlag(_Player,"ARX_Prison_MagisterTimot_ReadNotes");
//END_REGION

//REGION Switching Onwers 

IF
CharacterDying(_Char)
AND
DB_ARX_Prison_DoorsOnwership_Characters(_Char)
AND
DB_ARX_Prison_DoorsOnwership_Doors(_Item)
AND
ItemGetOwner((ITEMGUID)_Item,NULL_00000000-0000-0000-0000-000000000000) //Onwer is already cleared here
THEN
NOT DB_ARX_Prison_DoorsOnwership_Characters(_Char);
Proc_ARX_Prison_SetNewDoorOnwer(_Item);

PROC
Proc_ARX_Prison_SetNewDoorOnwer((ITEMGUID)_Item)
AND
DB_ARX_Prison_DoorsOnwership_Characters(_NewOnwer)
AND
CharacterIsDead((CHARACTERGUID)_NewOnwer,0)
AND
ObjectGetFlag(_Item,"ARX_Prison_SetNewDoorOnwer",0)
THEN
ObjectSetFlag(_Item,"ARX_Prison_SetNewDoorOnwer");
ItemSetOwner(_Item,_NewOnwer);

PROC
Proc_ARX_Prison_SetNewDoorOnwer((ITEMGUID)_Item)
THEN
ObjectClearFlag(_Item,"ARX_Prison_SetNewDoorOnwer");



//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ARX"
