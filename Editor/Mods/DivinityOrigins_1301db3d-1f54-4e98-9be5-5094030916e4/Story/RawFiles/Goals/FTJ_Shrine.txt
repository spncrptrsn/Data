Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(RC_FTJ_ShrineToTheSeven_a7c71478-416b-4b7a-bd97-446f03708443,"FTJ_ShrineToTheSeven");
DB_Dialogs(RC_FTJ_ShrineWorshipper_bd8a4344-814a-4904-8399-326a5a3f88d1,"FTJ_ShrineWorshipper");

DB_GLO_Attribute_Check_AgainstLevel("FTJ_ShrineToTheSeven",1,"Wits","Hard",2,2);

SetOnStage(RC_FTJ_HatchToCellar_64188e1e-3160-48b1-8bb5-77edabf907c5,0);
ItemClearOwner(ITEMGUID_S_FTJ_LucianShrinePainting_fe1ef6ab-a059-40ab-8320-086f17067890);
ItemClearOwner(ITEMGUID_S_FTJ_HatchToCellar_64188e1e-3160-48b1-8bb5-77edabf907c5);
//http://fileserver-dmz/mediawiki/index.php/The_Shrine_to_the_Seven
KBSECTION
IF
DialogEnded("FTJ_ShrineToTheSeven",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
QueryOnlyOnce("FTJ_ShrineVB")
THEN
StartVoiceBark("FTJ_VB_Shrine",_Player);

//Worshipper sees shrine appear
IF
DialogEnded("FTJ_ShrineToTheSeven",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
GlobalGetFlag("RC_FTJ_ShrineHatchAppear",1)
AND
GetDistanceTo(CHARACTERGUID_S_FTJ_ShrineWorshipper_bd8a4344-814a-4904-8399-326a5a3f88d1,_Player,_Dist)
AND
_Dist < 12.0
AND
QRY_SpeakerIsAvailable(CHARACTERGUID_S_FTJ_ShrineWorshipper_bd8a4344-814a-4904-8399-326a5a3f88d1)
AND
QueryOnlyOnce("FTJ_WorsipperSawShrineAppear")
THEN 
CharacterLookAt(CHARACTERGUID_S_FTJ_ShrineWorshipper_bd8a4344-814a-4904-8399-326a5a3f88d1,ITEMGUID_S_FTJ_HatchToCellar_64188e1e-3160-48b1-8bb5-77edabf907c5);
Proc_StartDialog(1,"FTJ_AD_WorshipperSawShrineOpen",CHARACTERGUID_S_FTJ_ShrineWorshipper_bd8a4344-814a-4904-8399-326a5a3f88d1);


IF
ObjectFlagSet("FTJ_SoulJar_Accepted",(CHARACTERGUID)_Player,_)
THEN
ObjectSetFlag(_Player,"FTJ_ToldAboutShrineButton",1);
UserSetFlag(_Player,"QuestUpdate_FTJ_Escape_WithermooreDeal");

IF
ObjectFlagShared("FTJ_SoulJar_Accepted",_Player,1)
THEN
ObjectShareFlag(_Player,"FTJ_ToldAboutShrineButton");

//Trigger if DivineStatue hatch appear and was instructed by Withermoore to go there
IF
ObjectFlagSet("FTJ_Escape_ActivateHatch",(CHARACTERGUID)_Player,_)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_WithermooreDeal",1)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Escape_DivineStatueInfo");



//Trigger DivineStatue_Passage entry if Player was instructed to use Shrine by Withermoore
IF
CharacterUsedItem((CHARACTERGUID)_Player,ITEMGUID_S_FTJ_HatchToCellar_64188e1e-3160-48b1-8bb5-77edabf907c5)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_WithermooreDeal",1)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Escape_DivineStatue_Passage");

//Trigger Free_Own if Player opened Shrine & walked in by themselves
IF
CharacterUsedItem((CHARACTERGUID)_Player,ITEMGUID_S_FTJ_HatchToCellar_64188e1e-3160-48b1-8bb5-77edabf907c5)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_Free_Own",0)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_WithermooreDeal",0)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_EnterUnderground",0)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_Yarrow_KeyUsed",0)
AND
ObjectGetFlag(_Player,"IsInPrison",0)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Escape_Free_Own");

IF
GlobalFlagSet("RC_FTJ_ShrineHatchAppear")
THEN
SetOnStage(RC_FTJ_HatchToCellar_64188e1e-3160-48b1-8bb5-77edabf907c5,1);
PlayEffect(RC_FTJ_HatchToCellar_64188e1e-3160-48b1-8bb5-77edabf907c5,"RS3_FX_Skills_Earth_Cast_Ground_Earth_Root_01");

PROC
ProcBlockUseOfItem((CHARACTERGUID)_Player,(ITEMGUID)ITEMGUID_S_FTJ_LadderToOlgo_b3eb4099-662f-4ff9-a133-7bf1ff500649)
AND
DB_IsPlayer(_Player)
AND
GlobalGetFlag("RC_FTJ_ShrineHatchAppear",0)
THEN
StartVoiceBark("FTJ_VB_ShrineLadderCannotUse",_Player);
DB_CustomUseItemResponse(_Player,ITEMGUID_S_FTJ_LadderToOlgo_b3eb4099-662f-4ff9-a133-7bf1ff500649,0);


//underground door
IF
CharacterUsedItem(_,RC_FTJ_UndergroundShrineDoorLever_f45d3339-1afc-4a4e-b9b6-a9b2e84c3cad)
THEN
ItemOpen(RC_FTJ_DoorToShrineUnderground_38bd937c-d73f-470b-91df-880d5142b550);
ItemUnLock(ITEMGUID_S_FTJ_DoorToShrineUnderground_38bd937c-d73f-470b-91df-880d5142b550);

IF
ObjectFlagSet("FTJ_PrayedToShrineOfSeven",(CHARACTERGUID)_Player,_)
THEN
ObjectClearFlag(_Player,"FTJ_PrayedToShrineOfSeven",0);
PlayAnimation(_Player,"Kneel_02");

PROC
ProcRegionEnded("FJ_FortJoy_Main")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "FortJoy"
