Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_GLO_SystemicTagsFlags("GLO_SetTag_HERO","HERO","TUT_TagHero");
DB_GLO_SystemicTagsFlags("GLO_SetTag_VILLAIN","VILLAIN","TUT_TagVillain");

DB_GLO_SystemicRaceTagsFlags("GLO_SetTag_DWARF-FRIEND","DWARF-FRIEND");
DB_GLO_SystemicRaceTagsFlags("GLO_SetTag_ELF-FRIEND","ELF-FRIEND");

// Ignore animal killing for the villain tag (yes, we're cruel like that)
DB_GLO_SystemicMurderTypesForVillain("Murder");
DB_GLO_SystemicMurderTypesForVillain("SneakMurder");
KBSECTION
//REGION race-friend

IF
ObjectFlagSet(_Flag,_Player,_)
AND
DB_GLO_SystemicRaceTagsFlags(_Flag,_Tag)
THEN
ObjectClearFlag(_Player,_Flag);
SetTag(_Player,_Tag);

//END_REGION


//REGION GLO_PathOfBlood_MurderedInnocent, GLO_StoleItem, GLO_PathOfBlood_DisrespectedSoul

//Checked in the Path of Blood in ARX
IF
CharacterKilledBy(_Defender,_Attacker,_AttackerOwner)
AND
DB_IsPlayer(_AttackerOwner)
AND
IsTagged(_Attacker,"SUMMON",0)
AND
GetTemplate(_Defender,_Template)
AND
_Template != "Creatures_Kraken_Tentacle_HarbourFight_A_2f7ef632-e033-4541-a4cf-fac29469430c"
THEN
ObjectSetFlag(_AttackerOwner,"GLO_PathOfBlood_MurderedInnocent"); //it is intended to be set on any kill.


IF
CharacterStoleItem(_Player,_,_,_,_,_,_,_)
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"GLO_StoleItem");


IF
ObjectFlagSet("GLO_UsedSourceVampirism",_Player,_)
THEN
ObjectSetFlag(_Player,"GLO_PathOfBlood_DisrespectedSoul"); //also set in GLO_SoulJars


//END_REGION


//REGION Killing 3 innocents

//--- Excluding invaling characters
PROC
PROC_GLO_SystemicTags_CheckMurder((CHARACTERGUID)_Player)
AND
NOT DB_GLO_SystemicTags_TimeKilledInnocent(_Player,3)
AND
IsTagged(_Player,"VILLAIN",0)
THEN
PROC_GLO_SystemicTags_ProcessMurder(_Player);


//--- Couting the murders
PROC
ProcCrimeCreateMurder((CHARACTERGUID)_Killer,(CHARACTERGUID)_Victim,(INTEGER)_CrimeID)
AND
QryCrimeMurderGetCrimeType(_Killer,_Victim)
AND
DB_CrimeMurderCrimeType(_CrimeType)
AND
DB_GLO_SystemicMurderTypesForVillain(_CrimeType)
THEN
PROC_GLO_SystemicTags_ProcessMurder(_Killer);

PROC
PROC_GLO_SystemicTags_ProcessMurder((CHARACTERGUID)_Player)
AND
NOT DB_GLO_SystemicTags_TimeKilledInnocent(_Player,_)
THEN
DB_GLO_SystemicTags_TimeKilledInnocent(_Player,0);

PROC
PROC_GLO_SystemicTags_ProcessMurder((CHARACTERGUID)_Player)
AND
DB_GLO_SystemicTags_TimeKilledInnocent(_Player,_Num)
AND
IntegerSum(_Num,1,_Sum)
THEN
NOT DB_GLO_SystemicTags_TimeKilledInnocent(_Player,_Num);
DB_GLO_SystemicTags_TimeKilledInnocent(_Player,_Sum);
PROC_GLO_SystemicTags_CheckNumKilledInnocent(_Player,_Sum);


//--- Setting the flag on third murder
PROC
PROC_GLO_SystemicTags_CheckNumKilledInnocent((CHARACTERGUID)_Player,(INTEGER)_Sum)
AND
_Sum == 3
THEN
ObjectSetFlag(_Player,"GLO_SetTag_VILLAIN");

//END_REGION


//REGION Used SV flag

//--- PoB checks
IF
CharacterUsedSkillOnTarget(_Player,(CHARACTERGUID)_Target,_Skill,_,_)
AND
DB_IsPlayer(_Player)
AND
DB_SourceVampirism_Skills(_Skill)
AND
NOT DB_RC_DB_Meistr_Dream(_,_,_Target)
AND
NOT DB_KemmVault_ArhusPrison_Beam(_Target,_)
AND
IsTagged(_Target,"GHOST",1)
AND
IsTagged(_Target,"CONSTRUCT",0)
THEN
ObjectSetFlag(_Player,"GLO_UsedSourceVampirism");
PROC_GLO_SystemicTags_CheckSourceSucking(_Player);


//--- Counting towards the VILLAIN tag:
PROC
PROC_GLO_SystemicTags_CheckSourceSucking((CHARACTERGUID)_Player)
AND
NOT DB_GLO_SystemicTags_TimeSuckedSource(_Player,3)
AND
IsTagged(_Player,"VILLAIN",0)
THEN
PROC_GLO_SystemicTags_ProcessSourceSucking((CHARACTERGUID)_Player);


//--- Couting the source vampirism
PROC
PROC_GLO_SystemicTags_ProcessSourceSucking((CHARACTERGUID)_Player)
AND
NOT DB_GLO_SystemicTags_TimeSuckedSource(_Player,_)
THEN
DB_GLO_SystemicTags_TimeSuckedSource(_Player,0);

PROC
PROC_GLO_SystemicTags_ProcessSourceSucking((CHARACTERGUID)_Player)
AND
DB_GLO_SystemicTags_TimeSuckedSource(_Player,_Num)
AND
IntegerSum(_Num,1,_Sum)
THEN
NOT DB_GLO_SystemicTags_TimeSuckedSource(_Player,_Num);
DB_GLO_SystemicTags_TimeSuckedSource(_Player,_Sum);
PROC_GLO_SystemicTags_CheckNumSuckedSource(_Player,_Sum);

PROC
PROC_GLO_SystemicTags_CheckNumSuckedSource((CHARACTERGUID)_Player,(INTEGER)_Sum)
AND
_Sum == 3
THEN
ObjectSetFlag(_Player,"GLO_SetTag_VILLAIN");

//END_REGION


//REGION Getting HERO and VILLAIN

IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Player,_)
AND
DB_GLO_SystemicTagsFlags(_Flag,_Tag,_)
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player,_Tag,0)
THEN
ObjectClearFlag(_Player,_Flag);
PROC_GLO_SystemicTags_ProcessTagged(_Player,_Tag);

PROC
PROC_GLO_SystemicTags_ProcessTagged((CHARACTERGUID)_Player,(STRING)_Tag)
AND
NOT DB_GLO_SystemicTags_TimeTagged(_Player,_Tag,_)
THEN
DB_GLO_SystemicTags_TimeTagged(_Player,_Tag,0);

PROC
PROC_GLO_SystemicTags_ProcessTagged((CHARACTERGUID)_Player,(STRING)_Tag)
AND
DB_GLO_SystemicTags_TimeTagged(_Player,_Tag,_Num)
AND
IntegerSum(_Num,1,_Sum)
THEN
NOT DB_GLO_SystemicTags_TimeTagged(_Player,_Tag,_Num);
DB_GLO_SystemicTags_TimeTagged(_Player,_Tag,_Sum);
PROC_GLO_SystemicTags_CheckNumSetTag(_Player,_Tag,_Sum);

PROC
PROC_GLO_SystemicTags_CheckNumSetTag((CHARACTERGUID)_Player,(STRING)_Tag,(INTEGER)_Sum)
AND
_Sum >= 3
THEN
SetTag(_Player,_Tag);

IF
ObjectWasTagged((CHARACTERGUID)_Player,_Tag)
AND
DB_GLO_SystemicTagsFlags(_,_Tag,_)
THEN
CharacterReceivedTag(_Player,_Tag);

IF
ObjectWasTagged((CHARACTERGUID)_Player,_Tag)
AND
DB_GLO_SystemicTagsFlags(_,_Tag,_Tut)
AND
_Tut != ""
THEN
//ProcPlayTut(_Player,_Tut);
OpenMessageBox(_Player,_Tut);

//END_REGION




EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOS2ModWrapper"
