Version 1
SubGoalCombiner SGC_AND
INITSECTION
//https://sites.google.com/a/larian.com/larian-wikisite/home/quest-design-documentation/vengeful-spirits

DB_Ghosts(CHARACTERGUID_S_RC_GY_VengefulSpirits_DowagerGhost_01b023e7-c079-4d45-a7d8-b88e44135569,"RC_GY_VengefulSpirits_DowagerGhost");

//REGION Moving Wall
DB_RC_GY_VengefulSpirits_WallButtons(ITEMGUID_S_RC_GY_VengefulSpirits_Button01_12ccc2b6-565b-45a8-9c05-d3fd0e3c2675,"RC_GY_VengefulSpirits_Button01Pressed");
DB_RC_GY_VengefulSpirits_WallButtons(ITEMGUID_S_RC_GY_VengefulSpirits_Button02_305f8f94-554f-44cb-ad73-2666b29c1336,"RC_GY_VengefulSpirits_Button02Pressed");

ProcTriggerRegisterForPlayers(TRIGGERGUID_S_RC_GY_VengefulSpirits_PlayerWallBox_f77cc8db-00d2-475c-92ee-46bdf874a53b);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_RC_GY_VengefulSpirits_Treasury_19cc53ef-72c1-4d01-b8af-a60cfe6af7ed);

//END_REGION

//REGION Puzzle
DB_GY_VengefulSpirits_SmokesOnPipes((ITEMGUID)ITEMGUID_S_RC_GY_FX_VengefulSpirits_Smoke_01_96220053-36ca-4fa5-81f0-e1311c8c0448);
DB_GY_VengefulSpirits_SmokesOnPipes(ITEMGUID_S_RC_GY_FX_VengefulSpirits_Smoke_02_a9ee981e-becb-4c73-8927-80cb5137f224);
DB_GY_VengefulSpirits_SmokesOnPipes(ITEMGUID_S_RC_GY_FX_VengefulSpirits_Smoke_03_eafe9e97-9d74-44c4-ba03-2c606fcbf575);
DB_GY_VengefulSpirits_SmokesOnPipes(ITEMGUID_S_RC_GY_FX_VengefulSpirits_Smoke_04_ca0e4ec3-3bfb-4fdc-991b-b8c230618c73);
DB_GY_VengefulSpirits_SmokesOnPipes(ITEMGUID_S_RC_GY_FX_VengefulSpirits_Smoke_05_92432bc5-e43d-4845-b6a4-02b8e7354bc1);

DB_GY_VengefulSpirits_Levers((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_Lever01_bd7f4d15-1452-4868-902d-e03fe31f551c,"SurfaceOil","Projectile_Grenade_Taser","SurfaceBloodElectrifiedCursed");
DB_GY_VengefulSpirits_Levers(ITEMGUID_S_RC_GY_VengefulSpirits_Lever02_ad842ea4-63f1-44be-87cc-cc6639369693,"SurfaceWater","SurfacePoison","SurfacePoisonCursed");
DB_GY_VengefulSpirits_Levers(ITEMGUID_S_RC_GY_VengefulSpirits_Lever03_198f826b-3317-410b-81e6-c2f9f9aee2b7,"SurfaceBlood","SurfaceFire","SurfaceFireCursed");

DB_GY_VengefulSpirits_LeversNum(0);
DB_GY_VengefulSpirits_PlateLocked(1);

//END_REGION

SetOnStage(ITEMGUID_S_RC_GY_VengefulSpirits_TreasuryHatch_cd72c405-b1a9-4d28-bb62-6bcdbd7a69e9,0);

DB_OneShotPlayerTrigger(TRIGGERGUID_S_RC_GY_VengefulSpirits_PressuPlate_Box_ef7d1c1b-9392-4024-a581-4934941d83dd);
KBSECTION
IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_GY_VengefulSpirits_TreasureNote_6e61a829-5b9e-4e94-ada4-502984f618ce)
THEN
PartySetFlag(_Player,"QuestUpdate_RC_GY_Godslayer_FoundNote");

//REGION Smoke on the Pipes
IF
DB_GY_VengefulSpirits_SmokesOnPipes(_SmokeFX)
THEN
SetOnStage(_SmokeFX,0);

PROC
Proc_RC_GY_VengefulSpirits_SecretPlate_UnPressed()
AND
DB_GY_VengefulSpirits_SmokesOnPipes(_SmokeFX)
THEN
SetOnStage(_SmokeFX,0);

PROC
Proc_RC_GY_VengefulSpirits_SecretPlate_Pressed()
AND
DB_GY_VengefulSpirits_SmokesOnPipes(_SmokeFX)
THEN
SetOnStage(_SmokeFX,1);

//END_REGION


//REGION VBs
IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_GY_VengefulSpirits_TombEntranceDoor_9cc2c824-c0be-4d95-9056-a1baee548646)
AND
ItemIsLocked(ITEMGUID_S_RC_GY_VengefulSpirits_TombEntranceDoor_9cc2c824-c0be-4d95-9056-a1baee548646,0)
AND
QueryOnlyOnce("RC_GY_VengefulSpirits_EnteredTomb")
THEN
StartVoiceBark("RC_GY_VengefulSpirits_VB_EnteredTomb",_Player);

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_RC_GY_VengefulSpirits_Treasury_19cc53ef-72c1-4d01-b8af-a60cfe6af7ed)
AND
QueryOnlyOnce("RC_GY_VengefulSpirits_EnteredTreasury")
THEN
StartVoiceBark("RC_GY_VengefulSpirits_VB_EnteredTreasury",_Player);

PROC
ProcOneShotTriggerEntered(_Player,TRIGGERGUID_S_RC_GY_VengefulSpirits_PressuPlate_Box_ef7d1c1b-9392-4024-a581-4934941d83dd)
THEN
StartVoiceBark("RC_GY_VengefulSpirits_VB_SteppedOnPlate",_Player);

//Show a message if a lever is used when stepped on the pressure plate
IF
CharacterUsedItem(_Player,_Lever)
AND
NOT DB_GY_VengefulSpirits_PuzzleSolved(_)
AND
NOT DB_GY_VengefulSpirits_PlateLocked(_)
AND
DB_GY_VengefulSpirits_Levers(_Lever,_,_,_)
THEN
StartVoiceBark("RC_GY_VengefulSpirits_VB_LeverUsed",_Player);

//Show a message if a lever is used when not on the pressure plate
IF
CharacterUsedItem(_Player,_Lever)
AND
NOT DB_GY_VengefulSpirits_PuzzleSolved(_)
AND
DB_GY_VengefulSpirits_PlateLocked(_)
AND
DB_GY_VengefulSpirits_Levers(_Lever,_,_,_)
THEN
StartVoiceBark("RC_GY_VengefulSpirits_VB_LockedLeverUsed",_Player);

//Show a message when a lever is used after puzzle solved
IF
CharacterUsedItem(_Player,_Lever)
AND
DB_GY_VengefulSpirits_PuzzleSolved(_)
AND
DB_GY_VengefulSpirits_Levers(_Lever,_,_,_)
THEN
StartVoiceBark("RC_GY_VengefulSpirits_VB_SolvedLeverUsed",_Player);

//Show a message when player uses tomb, and hasn't moved it via levers yet
IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_GY_VengefulSpirits_MovingTomb_413fe398-d55e-4246-b8f7-c48e83080dce)
AND
NOT DB_GY_VengefulSpirits_PuzzleSolved(_)
THEN
StartVoiceBark("RC_GY_VengefulSpirits_VB_MovingTomb",_Player);

//END_REGION


//REGION Moving Wall
//VoiceBark on first time button used
IF
CharacterItemEvent(_Player,_Button,_Event)
AND
DB_RC_GY_VengefulSpirits_WallButtons(_Button,_Event)
AND
QueryOnlyOnce("RC_GY_VengefulSpirits_OneButtonPressed")
THEN
StartVoiceBark("RC_GY_VengefulSpirits_VB_OneButtonPressed",_Player);

//VoiceBark on second button used and wall moving
PROC
Proc_RC_GY_VengefulSpirits_CheckForButtons((CHARACTERGUID)_Player)
AND
SysCount("DB_RC_GY_VengefulSpirits_UsedButtons",1,2)
AND
QueryOnlyOnce("RC_GY_VengefulSpirits_WallMoved")
THEN
StartVoiceBark("RC_GY_VengefulSpirits_VB_WallMoved",_Player);

IF
CharacterItemEvent(_Player,_Button,_Event)
AND
DB_RC_GY_VengefulSpirits_WallButtons(_Button,_Event)
AND
NOT DB_RC_GY_VengefulSpirits_UsedButtons(_Button)
THEN
DB_RC_GY_VengefulSpirits_UsedButtons(_Button);
//ItemSetCanInteract(_Button,0);
Proc_RC_GY_VengefulSpirits_CheckForButtons(_Player);

PROC
Proc_RC_GY_VengefulSpirits_CheckForButtons((CHARACTERGUID)_Player)
AND
SysCount("DB_RC_GY_VengefulSpirits_UsedButtons",1,2)
AND
NOT DB_RC_GY_VengefulSpirits_WallMoved(_)
THEN
proc_ItemMoveToTrigger(ITEMGUID_S_RC_GY_VengefulSpirits_MovingWall_8b93cfcc-2019-48dd-98b5-06a6c22e7fe4,TRIGGERGUID_S_RC_GY_VengefulSpirits_WallPoint_9ce65d2a-51dc-43ee-b580-f36197a94dbe,1.0,1.0,0);
Proc_ShakeCameraForTime(_Player,500);
Proc_RC_GY_VengefulSpirits_CleanupButtons();
DB_RC_GY_VengefulSpirits_WallMoved(1);

PROC
Proc_RC_GY_VengefulSpirits_CheckForButtons((CHARACTERGUID)_Player)
AND
SysCount("DB_RC_GY_VengefulSpirits_UsedButtons",1,2)
AND
DB_RC_GY_VengefulSpirits_WallMoved(1)
THEN
proc_ItemMoveToTrigger(ITEMGUID_S_RC_GY_VengefulSpirits_MovingWall_8b93cfcc-2019-48dd-98b5-06a6c22e7fe4,TRIGGERGUID_S_RC_GY_VengefulSpirits_WallPointStart_43e71a8e-b13d-4663-b10d-c37f14d803b9,1.0,1.0,0);
Proc_ShakeCameraForTime(_Player,500);
Proc_RC_GY_VengefulSpirits_CleanupButtons();
NOT DB_RC_GY_VengefulSpirits_WallMoved(1);

PROC
Proc_RC_GY_VengefulSpirits_CleanupButtons()
AND
DB_RC_GY_VengefulSpirits_UsedButtons(_Button)
THEN
//ItemSetCanInteract(_Button,1);
NOT DB_RC_GY_VengefulSpirits_UsedButtons(_Button);

IF
ItemMoved(ITEMGUID_S_RC_GY_VengefulSpirits_MovingWall_8b93cfcc-2019-48dd-98b5-06a6c22e7fe4)
AND
DB_IsPlayer(_Char)
AND
DB_InRegion(_Char,TRIGGERGUID_S_RC_GY_VengefulSpirits_PlayerWallBox_f77cc8db-00d2-475c-92ee-46bdf874a53b)
THEN
TeleportTo(_Char,TRIGGERGUID_S_RC_GY_VengefulSpirits_PlayerWallPoint_7885398b-69b2-411f-bf66-72dc78d93cfa);

//END_REGION


//REGION Secret Room
//Succesful puzzle result
IF
DB_GY_VengefulSpirits_PuzzleSolved(_)
AND
DB_GY_VengefulSpirits_PlayerUsedLever(_Player)
AND
QueryOnlyOnce("RC_GY_VengefulSpirits_PuzzleResult")
THEN
SetOnStage(ITEMGUID_S_RC_GY_VengefulSpirits_TreasuryHatch_cd72c405-b1a9-4d28-bb62-6bcdbd7a69e9,1);
PlayEffect(ITEMGUID_S_RC_GY_VengefulSpirits_TreasuryHatch_cd72c405-b1a9-4d28-bb62-6bcdbd7a69e9,"RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01");
ItemUnlockAndOpen((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_GrateDoor_24f218b6-a589-47a6-abe3-8f4447ca5253);
proc_ItemMoveToTrigger(ITEMGUID_S_RC_GY_VengefulSpirits_MovingTomb_413fe398-d55e-4246-b8f7-c48e83080dce,TRIGGERGUID_S_RC_GY_VengefulSpirits_TombPoint_5d31d609-3da1-4f2c-bb8c-40eb7d52107c,1.0,1.0,0);
Proc_ShakeCameraForTime(_Player,500);
ItemSetCanInteract(ITEMGUID_S_RC_GY_VengefulSpirits_MovingTomb_413fe398-d55e-4246-b8f7-c48e83080dce,0);
StartVoiceBark("RC_GY_VengefulSpirits_VB_TreasuryDoorOpened",_Player);
GlobalSetFlag("RC_GY_VengefulSpirits_LeverLock");
NOT DB_GY_VengefulSpirits_PlayerUsedLever(_Player);

IF
DB_GY_VengefulSpirits_PuzzleSolved(_)
AND
DB_GY_VengefulSpirits_Levers(_Lever,_,_,_)
THEN
PROC_StopLoopEffect(_Lever,"RC_GY_VengefulSpirits_LeversWork");

//END_REGION


//REGION Uses of the pressure plate and levers
IF
CharacterItemEvent(_Player,ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,"RC_GY_VengefulSpirits_SecretPlate_UnPressed")
THEN
Proc_RC_GY_VengefulSpirits_SecretPlate_UnPressed();

IF
CharacterItemEvent(_Player,ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,"RC_GY_VengefulSpirits_SecretPlate_Pressed")
THEN
Proc_RC_GY_VengefulSpirits_SecretPlate_Pressed();

PROC
Proc_RC_GY_VengefulSpirits_SecretPlate_UnPressed()
AND
DB_GY_VengefulSpirits_Levers(_Lever,_,_,_)
THEN
PROC_StopLoopEffect(_Lever,"RC_GY_VengefulSpirits_LeversWork");

PROC
Proc_RC_GY_VengefulSpirits_SecretPlate_UnPressed()
THEN
ItemUnlockAndOpen((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_GrateDoor_24f218b6-a589-47a6-abe3-8f4447ca5253);

PROC
Proc_RC_GY_VengefulSpirits_SecretPlate_UnPressed()
AND
NOT DB_GY_VengefulSpirits_PuzzleSolved(_)
THEN
DB_GY_VengefulSpirits_PlateLocked(1);
GlobalSetFlag("RC_GY_VengefulSpirits_LeverLock");

PROC
Proc_RC_GY_VengefulSpirits_SecretPlate_Pressed()
AND
NOT DB_GY_VengefulSpirits_PuzzleSolved(_)
AND
DB_GY_VengefulSpirits_Levers(_Lever,_,_,_)
THEN
PROC_LoopEffect("RS3_FX_Environment_Fire_Green_01",_Lever,"RC_GY_VengefulSpirits_LeversWork","RC_Main","Bone_PUZ_Lever_Wall_C_Lever");

PROC
Proc_RC_GY_VengefulSpirits_SecretPlate_Pressed()
AND
NOT DB_GY_VengefulSpirits_PuzzleSolved(_)
THEN
NOT DB_GY_VengefulSpirits_PlateLocked(1);
ItemCloseAndLock(ITEMGUID_S_RC_GY_VengefulSpirits_GrateDoor_24f218b6-a589-47a6-abe3-8f4447ca5253,"NO_KEY");
GlobalSetFlag("RC_GY_VengefulSpirits_LeverUnLock");

//Lever Puzzle
IF
CharacterUsedItem(_Player,_Lever)
AND
DB_GY_VengefulSpirits_Levers(_Lever,_,_,_)
THEN
Proc_RC_GY_VengefulSpirits_Lever_Puzzle(_Player,_Lever);

PROC
Proc_RC_GY_VengefulSpirits_Lever_Puzzle((CHARACTERGUID)_Player,(ITEMGUID)_Lever)
AND
NOT DB_GY_VengefulSpirits_PuzzleSolved(_)
AND
NOT DB_GY_VengefulSpirits_PlateLocked(_)
AND
DB_GY_VengefulSpirits_LeversNum(_Idx)
AND
IntegerSum(_Idx,1,_NewIdx)
THEN
DB_GY_VengefulSpirits_UsedLevers(_Lever,_NewIdx);
NOT DB_GY_VengefulSpirits_LeversNum(_Idx);
DB_GY_VengefulSpirits_LeversNum(_NewIdx);
Proc_GY_VengefulSpirits_LeverDamage(_Lever);
Proc_GY_VengefulSpirits_CheckLevers(_Player,_Lever);

//END_REGION

/*
//REGION Spirit levers
IF
DB_GY_VengefulSpirits_Levers(_Lever,_,_,_)
THEN
SetOnStage(_Lever,0);

IF
CharacterUsedSkill(_Player,"Shout_SpiritVision",_,_)
AND
DB_InRegion(_Player,TRIGGERGUID_S_RC_GY_VengefulSpirits_Tomb_6d6e43ee-e286-4c71-ae62-c84cb7a917dd)
THEN
ProcObjectTimer(_Player,"RC_GY_VengefulSpirits_ShowLevers",900);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"RC_GY_VengefulSpirits_ShowLevers")
THEN
Proc_RC_GY_VengefulSpirits_LeversReveal(_Player);

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_RC_GY_VengefulSpirits_Tomb_6d6e43ee-e286-4c71-ae62-c84cb7a917dd)
AND
HasActiveStatus(_Player,"SPIRIT_VISION",1)
THEN
Proc_RC_GY_VengefulSpirits_LeversReveal(_Player);

PROC
Proc_RC_GY_VengefulSpirits_LeversReveal((CHARACTERGUID)_Player)
AND
DB_GY_VengefulSpirits_Levers(_Lever,_,_,_)
AND
CharacterCanSee(_Player,_Lever,0)
THEN
SetOnStage(_Lever,1);

//END_REGION
*/

//REGION Lever puzzle
//Correct combo
PROC
Proc_GY_VengefulSpirits_CheckLevers((CHARACTERGUID)_Player,(ITEMGUID)_Lever)
AND
SysCount("DB_GY_VengefulSpirits_UsedLevers",2,3)
AND
DB_GY_VengefulSpirits_UsedLevers(ITEMGUID_S_RC_GY_VengefulSpirits_Lever01_bd7f4d15-1452-4868-902d-e03fe31f551c,2)
AND
DB_GY_VengefulSpirits_UsedLevers(ITEMGUID_S_RC_GY_VengefulSpirits_Lever02_ad842ea4-63f1-44be-87cc-cc6639369693,1)
AND
DB_GY_VengefulSpirits_UsedLevers(ITEMGUID_S_RC_GY_VengefulSpirits_Lever03_198f826b-3317-410b-81e6-c2f9f9aee2b7,3)
THEN
DB_GY_VengefulSpirits_PuzzleSolved(1);
DB_GY_VengefulSpirits_PlayerUsedLever(_Player);
CreateSurface((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,"SurfaceNone",9.0,-1.0);
CreateSurface((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,"SurfaceFireBlessed",9.0,-1.0);
//CreatePuddle((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,"SurfaceNone",60,60,900,900,0.02);
//CreatePuddle((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,"SurfaceFireBlessed",60,60,600,600,0.02);

//Wrong combo
PROC
Proc_GY_VengefulSpirits_CheckLevers((CHARACTERGUID)_Player,(ITEMGUID)_Lever)
AND
SysCount("DB_GY_VengefulSpirits_UsedLevers",2,3)
AND
NOT DB_GY_VengefulSpirits_PuzzleSolved(1)
THEN
Proc_GY_VengefulSpirits_ResetLevers();
StartVoiceBark("RC_GY_VengefulSpirits_VB_WrongCombo",_Player);

//Reset Levers
PROC
Proc_GY_VengefulSpirits_ResetLevers()
AND
DB_GY_VengefulSpirits_UsedLevers(_Lever,_Idx)
THEN
NOT DB_GY_VengefulSpirits_UsedLevers(_Lever,_Idx);

//Reset Number of uses
PROC
Proc_GY_VengefulSpirits_ResetLevers()
AND
DB_GY_VengefulSpirits_LeversNum(_Idx)
THEN
NOT DB_GY_VengefulSpirits_LeversNum(_Idx);
DB_GY_VengefulSpirits_LeversNum(0);

//END_REGION


//REGION Lever damages
PROC
Proc_GY_VengefulSpirits_LeverDamage((ITEMGUID)_Lever)
AND
DB_GY_VengefulSpirits_LeversNum(_Idx)
AND
_Idx == 1
AND
DB_GY_VengefulSpirits_Levers(_Lever,_Surface,_,_)
THEN
CreateSurface((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,"SurfaceNone",9.0,-1.0);
CreateSurface((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,_Surface,9.0,-1.0);
//CreatePuddle((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,"SurfaceNone",60,60,900,900,0.02);
//CreatePuddle((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,_Surface,60,60,600,600,0.02);

PROC
Proc_GY_VengefulSpirits_LeverDamage((ITEMGUID)_Lever)
AND
DB_GY_VengefulSpirits_LeversNum(_Idx)
AND
_Idx == 2
AND
DB_GY_VengefulSpirits_Levers(_Lever,_,_Surface,_)
AND
NOT _Lever == ITEMGUID_S_RC_GY_VengefulSpirits_Lever01_bd7f4d15-1452-4868-902d-e03fe31f551c
THEN
CreateSurface((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,_Surface,9.0,-1.0);
//CreatePuddle((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,_Surface,60,60,600,600,0.02);

PROC
Proc_GY_VengefulSpirits_LeverDamage((ITEMGUID)_Lever)
AND
DB_GY_VengefulSpirits_LeversNum(_Idx)
AND
_Idx == 2
AND
DB_GY_VengefulSpirits_Levers(_Lever,_,_Skill,_)
AND
_Lever == ITEMGUID_S_RC_GY_VengefulSpirits_Lever01_bd7f4d15-1452-4868-902d-e03fe31f551c
THEN
CreateExplosion((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,_Skill,12);

PROC
Proc_GY_VengefulSpirits_LeverDamage((ITEMGUID)_Lever)
AND
DB_GY_VengefulSpirits_LeversNum(_Idx)
AND
_Idx == 3
AND
DB_GY_VengefulSpirits_Levers(_Lever,_,_,_Surface)
THEN
CreateSurface((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,_Surface,9.0,-1.0);
//CreatePuddle((ITEMGUID)ITEMGUID_S_RC_GY_VengefulSpirits_PressurePlate_9d876f70-53c8-48cd-b15c-75cf1a5161c4,_Surface,60,60,600,600,0.02);

//END_REGION
EXITSECTION
NOT DB_GY_VengefulSpirits_SmokesOnPipes(ITEMGUID_S_RC_GY_FX_VengefulSpirits_Smoke_01_96220053-36ca-4fa5-81f0-e1311c8c0448);
NOT DB_GY_VengefulSpirits_SmokesOnPipes(ITEMGUID_S_RC_GY_FX_VengefulSpirits_Smoke_02_a9ee981e-becb-4c73-8927-80cb5137f224);
NOT DB_GY_VengefulSpirits_SmokesOnPipes(ITEMGUID_S_RC_GY_FX_VengefulSpirits_Smoke_03_eafe9e97-9d74-44c4-ba03-2c606fcbf575);
NOT DB_GY_VengefulSpirits_SmokesOnPipes(ITEMGUID_S_RC_GY_FX_VengefulSpirits_Smoke_04_ca0e4ec3-3bfb-4fdc-991b-b8c230618c73);
NOT DB_GY_VengefulSpirits_SmokesOnPipes(ITEMGUID_S_RC_GY_FX_VengefulSpirits_Smoke_05_92432bc5-e43d-4845-b6a4-02b8e7354bc1);
NOT DB_GY_VengefulSpirits_SmokesOnPipes(ITEMGUID_S_RC_GY_FX_VengefulSpirits_Smoke_06_0b029b1d-3f2a-428b-a2eb-a110459ac2bb);
ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
