Version 1
SubGoalCombiner SGC_AND
INITSECTION
//_Flag, _DebugProfile
DB_QA_DebugFlags("QA_Debug_Act2","RC_Main");

//_DebugProfile,_PartyPreset
DB_QA_PartyPreset("RC_Main","2.0_QA_IfanPrinceLohseSebille_lvl9");

//_DebugProfile,_LevelName,_TeleportTarget
DB_QA_RegionLevel("RC_Main","RC_Main",(TRIGGERGUID)StartPoint_000_e30fe0c4-9b40-4040-9670-e8edd53a34ce);

//REGION Quests
//_DebugProfile,_QuestName, _QuestFlag,_DstCharacter
DB_QA_AddQuestThroughFlag("RC_Main","TUT_ShipMurder", "QuestUpdate_TUT_ShipMurder_WakeUp",(CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000);

//_DebugProfile,_QuestName,_DstCharacter
DB_QA_AddQuest("RC_Main","RC_FTJ_SourceCollar",(CHARACTERGUID)CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f);
DB_QA_AddQuest("RC_Main","RC_FTJ_SourceCollar",CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c);
DB_QA_AddQuest("RC_Main","RC_FTJ_SourceCollar",CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295);
DB_QA_AddQuest("RC_Main","RC_FTJ_SourceCollar",CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f);
DB_QA_AddQuest("RC_Main","RC_FTJ_SourceCollar",CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978);
DB_QA_AddQuest("RC_Main","RC_FTJ_SourceCollar",CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb);

//_DebugProfile,_QuestName,_UpdateFlag ,_DstCharacter
//DB_QA_UpdateQuestThroughFlag("RC_Main","","",(CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000);

//_DebugProfile,_QuestName,_Update
DB_QA_UpdateQuest("RC_Main","TUT_ShipMurder","Failed");

//_DebugProfile,_QuestName, _QuestFlag,_DstCharacter
DB_QA_CloseQuestThroughFlag("RC_Main","RC_FTJ_SourceCollar","QuestUpdate_RC_FTJ_SourceCollar_EndSourceCollar",(CHARACTERGUID)CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f);
DB_QA_CloseQuestThroughFlag("RC_Main","RC_FTJ_SourceCollar","QuestUpdate_RC_FTJ_SourceCollar_EndSourceCollar",CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c);
DB_QA_CloseQuestThroughFlag("RC_Main","RC_FTJ_SourceCollar","QuestUpdate_RC_FTJ_SourceCollar_EndSourceCollar",CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295);
DB_QA_CloseQuestThroughFlag("RC_Main","RC_FTJ_SourceCollar","QuestUpdate_RC_FTJ_SourceCollar_EndSourceCollar",CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f);
DB_QA_CloseQuestThroughFlag("RC_Main","RC_FTJ_SourceCollar","QuestUpdate_RC_FTJ_SourceCollar_EndSourceCollar",CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978);
DB_QA_CloseQuestThroughFlag("RC_Main","RC_FTJ_SourceCollar","QuestUpdate_RC_FTJ_SourceCollar_EndSourceCollar",CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb);

//_DebugProfile,_QuestName
DB_QA_CloseQuest("RC_Main","");
//END_REGION

//DebugProfile,_Value
DB_QA_DefaultPartyAttitude("RC_Main",0);

//DebugProfile,_Companion,_Value
DB_QA_DefaultPartyAttitudeForCharacter("RC_Main",(CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000,0);

//Debugprofile,_maxslots
DB_QA_NumSourceslots("RC_Main",1);

//Debugprofile,goal
DB_QA_CloseStoryGoals("RC_Main","Fortjoy");
KBSECTION
IF
CharacterUsedItemTemplate(_Player,"GLO_QA_Debug_Item_6dce3a97-e7b2-4a6d-a0d6-009237669bab",_)
THEN
Proc_StartDialog(0,"GEN_QA_Debug_Item",_Player);

IF
ObjectFlagSet(_Flag,_Player,_)
AND
DB_QA_DebugFlags((STRING)_Flag,(STRING)_RegionEvent)
AND
CharacterGetReservedUserID((CHARACTERGUID)_Player,_User)
THEN
DB_QA_DebugUser(_User);
ObjectClearFlag(_Player,_Flag);
ProcQASetupRegion(_RegionEvent);

IF
GameStarted(_LevelName,_)
AND
DB_PerformLevelLoad(_Region)
AND
DB_QA_RegionLevel(_Region,(STRING)_LevelName,(TRIGGERGUID)_)
THEN
NOT DB_PerformLevelLoad(_Region);
ProcQASetupRegion(_Region);

PROC
ProcQASetupRegion((STRING)_Region)
AND
DB_QA_RegionLevel(_Region,(STRING)_LevelName,(TRIGGERGUID)_)
AND
NOT DB_CurrentLevel(_LevelName)
THEN
DB_PerformLevelLoad(_Region);

PROC
ProcQASetupRegion((STRING)_Region)
THEN
ProcQASetupStoryGoals(_Region);

PROC
ProcQASetupRegion((STRING)_Region)
AND
NOT DB_PerformLevelLoad(_Region)
AND
DB_QA_PartyPreset(_Region,(STRING)_Preset)
THEN
ProcQALoadPartyPreset(_Region);

PROC
ProcQASetupRegion((STRING)_Region)
AND
NOT DB_PerformLevelLoad(_Region)
THEN
ProcDoDebugSetup(_Region);

PROC
ProcQASetupRegion((STRING)_Region)
AND
DB_PerformLevelLoad(_Region)
AND
DB_QA_RegionLevel(_Region,(STRING)_LevelName,_Target)
THEN
CharacterTeleportPartiesToTrigger(_Target,"QA_Teleport");

PROC
ProcQASetupRegion((STRING)_Region)
THEN
ProcQASetupJournal(_Region);
ProcQASetupPartyAttitude(_Region);
ProcQASetupSourceSlots(_Region);


//REGION QA PARTY PRESET
PROC
ProcQALoadPartyPreset((STRING)_Region)
AND
DB_QA_RegionLevel(_Region,_,_Dest)
AND
DB_QA_PartyPreset(_Region,(STRING)_Preset)
THEN
LoadPartyPreset(_Preset,_Dest);

PROC
ProcQALoadPartyPreset((STRING)_Region)
AND
NOT DB_QA_RegionLevel(_Region,_,_)
AND
DB_QA_PartyPreset(_Region,(STRING)_Preset)
THEN
LoadPartyPreset(_Preset,NULL_00000000-0000-0000-0000-000000000000);

PROC
ProcQALoadPartyPreset(_)
THEN
FireOsirisEvents();

PROC
ProcQALoadPartyPreset(_)
AND
DB_IsPlayer(_Player)
THEN
ItemTemplateRemoveFrom("GLO_Debug_Item_bcf8f28d-9ed1-43c0-bfc6-284aff5a6d19",_Player,666);

PROC
ProcQALoadPartyPreset(_)
AND
CharacterGetHostCharacter(_Player)
THEN
ItemTemplateAddTo("GLO_QA_Debug_Item_6dce3a97-e7b2-4a6d-a0d6-009237669bab",_Player,1);

PROC
ProcQALoadPartyPreset(_)
THEN
NOT DB_QA_DEBUG_AssignedHost(1);

PROC
ProcQALoadPartyPreset(_)
AND
DB_QA_DebugUser(_User)
THEN
NOT DB_QA_DebugUser(_User);

IF
CharacterLoadedInPreset(_Character)
AND
DB_QA_DebugUser(_User)
AND
NOT DB_QA_DEBUG_AssignedHost(1)
THEN
DB_QA_DEBUG_AssignedHost(1);
CharacterAssignToUser(_User,_Character);

//END_REGION

//REGION QA PARTY ATTITUDE
PROC
ProcQASetupPartyAttitude((STRING)_Region)
AND
DB_QA_DefaultPartyAttitude(_Region,_Attitude)
AND
CharacterGetHostCharacter(_Player)
AND
DB_CompanionAvatarBond((CHARACTERGUID)_Companion,_Player)
THEN
ProcQASetAttitudeTowards(_Companion,_Player,_Attitude);

PROC
ProcQASetupPartyAttitude((STRING)_Region)
AND
CharacterGetHostCharacter(_Player)
AND
DB_CompanionAvatarBond((CHARACTERGUID)_Companion,_Player)
AND
DB_QA_DefaultPartyAttitudeForCharacter(_Region,_Companion,_Attitude)
THEN
ProcQASetAttitudeTowards(_Companion,_Player,_Attitude);

PROC
ProcQASetAttitudeTowards((CHARACTERGUID)_Companion,(CHARACTERGUID)_Player,(INTEGER)_Attitude)
AND
CharacterGetAttitudeTowardsPlayer(_Companion,_Player,_OldAttitude)
AND
IntegerSubtract(_Attitude,_OldAttitude,_Delta)
AND
_Delta!=0
THEN
CharacterAddAttitudeTowardsPlayer(_Companion,_Player,_Delta);

//END_REGION

//REGION QA JOURNAL

PROC
ProcQASetupJournal((STRING)_Region)
AND
DB_QA_AddQuestThroughFlag(_Region,_Quest,_Flag,_Character)
AND
QuestGetBroadcastLevel(_Quest,_Level)
THEN
ProcQASetQuestFlag(_Flag,_Level,_Character);

PROC
ProcQASetupJournal((STRING)_Region)
AND
DB_QA_AddQuest(_Region,_Quest,_Character)
AND
QuestGetBroadcastLevel(_Quest,_Level)
AND
StringConcatenate("QuestAdd_",_Quest,_AddEvent)
THEN
ProcQASetQuestFlag(_AddEvent,_Level,_Character);

PROC
ProcQASetupJournal((STRING)_Region)
AND
DB_QA_UpdateQuestThroughFlag(_Region,(STRING)_Quest,(STRING)_Flag,(CHARACTERGUID)_Character)
AND
QuestGetBroadcastLevel(_Quest,_Level)
THEN
ProcQASetQuestFlag(_Flag,_Level,_Character);

PROC
ProcQASetupJournal((STRING)_Region)
AND
DB_QA_UpdateQuest(_Region,_Quest,_Update)
AND
QuestGetBroadcastLevel(_Quest,_Level)
AND
StringConcatenate("QuestUpdate_",_Quest,_String1)
AND
StringConcatenate(_String1,"_",_String2)
AND
StringConcatenate(_String2,_Update,_UpdateEvent)
THEN
ProcQASetQuestFlag(_UpdateEvent,_Level,NULL_00000000-0000-0000-0000-000000000000);

PROC
ProcQASetupJournal((STRING)_Region)
AND
DB_QA_CloseQuestThroughFlag(_Region,_Quest,_Flag,_Character)
AND
QuestGetBroadcastLevel(_Quest,_Level)
THEN
ProcQASetQuestFlag(_Flag,_Level,_Character);

PROC
ProcQASetupJournal((STRING)_Region)
AND
DB_QA_CloseQuest(_Region,_Quest)
AND
QuestGetBroadcastLevel(_Quest,_Level)
AND
StringConcatenate("QuestClose_",_Quest,_CloseEvent)
THEN
ProcQASetQuestFlag(_CloseEvent,_Level,NULL_00000000-0000-0000-0000-000000000000);

PROC
ProcQASetQuestFlag((STRING)_Flag,"Character",(CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
THEN
DebugBreak("Setting a Character quest flag on NULL!");

PROC
ProcQASetQuestFlag(_Flag,"Character",_Char)
AND
_Char!=NULL_00000000-0000-0000-0000-000000000000
THEN
ObjectSetFlag(_Char,_Flag);

PROC
ProcQASetQuestFlag((STRING)_Flag,"User",_)
AND
CharacterGetHostCharacter(_Char)
THEN
UserSetFlag(_Char,_Flag);

PROC
ProcQASetQuestFlag((STRING)_Flag,"Party",_)
AND
CharacterGetHostCharacter(_Char)
THEN
PartySetFlag(_Char,_Flag);
//END_REGION

//REGION QA SOURCE SLOTS
PROC
ProcQASetupSourceSlots((STRING)_Region)
AND
DB_QA_NumSourceslots(_Region,_Slots)
AND
DB_IsPlayer(_Player)
THEN
CharacterOverrideMaxSourcePoints(_Player,_Slots);


//END_REGION

//REGION QA STORY GOALS

PROC
ProcQASetupStoryGoals((STRING)_Region)
AND
DB_QA_CloseStoryGoals(_Region,_Goal)
AND
NOT SysIsCompleted(_Goal)
THEN
SysCompleteGoal(_Goal);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOS2ModWrapper"
