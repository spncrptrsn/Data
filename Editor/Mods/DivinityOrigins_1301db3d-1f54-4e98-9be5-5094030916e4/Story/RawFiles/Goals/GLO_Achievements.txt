Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_ProgressAchievement("",_Total);
DB_ProgressAchievement("DOS2_Quest40",99); //Destroy every Voidwoken egg in Reaper's Cove
DB_ProgressAchievement("DOS2_Quest48",7); //Pray at every altar on the Nameless Isle
DB_ProgressAchievement("DOS2_Quest81",4); //Shapeshift into every race

KBSECTION
//REGION SAVEGAME PATCH
// - Preview build
IF
SavegameLoaded(_Major,_Minor,_Rev,_Build)
AND
QRY_VersionIsOlderThan(_Major,_Minor,_Rev,_Build, 3, 5, 1, 0)
AND
DB_CurrentGameMode("Campaign")
AND
DB_IsPlayer(_Player)
AND
GetRegion(_Player,_Region)
AND
_Region != "TUT_Tutorial_A"
THEN
UnlockAchievement("DOS2_Quest1",_Player);


// - Patch 0
IF
SavegameLoaded(_Major,_Minor,_Rev,_Build)
AND
QRY_VersionIsOlderThan(_Major,_Minor,_Rev,_Build, 3, 6, 0, 1)
AND
DB_IsPlayer(_Player)
AND
Qry_Achievement_PatchCheckGive_DOS2_Quest4(_Player)
THEN
UnlockAchievement("DOS2_Quest4",_Player);

QRY
Qry_Achievement_PatchCheckGive_DOS2_Quest4((CHARACTERGUID)_Player)
AND
GetRegion(_Player, "FJ_FortJoy_Main")
AND
ObjectIsInTrigger(_Player, TRIGGERGUID_S_FTJ_DallisEventAutoEnd_Poly_c66c0ceb-84d0-42f2-af77-854d178a58d5, 1)
THEN
DB_NOOP(1);

QRY
Qry_Achievement_PatchCheckGive_DOS2_Quest4((CHARACTERGUID)_Player)
AND
GetRegion(_Player, _Region)
AND
_Region != "TUT_Tutorial_A"
AND
_Region != "FJ_FortJoy_Main"
THEN
DB_NOOP(1);

//END_REGION

//REGION Procs
PROC
Proc_UnlockAchievement((STRING)_Achievement)
THEN
UnlockAchievement(_Achievement,NULL_00000000-0000-0000-0000-000000000000);

PROC
UnlockAchievement((STRING)_Achievement)
THEN
Proc_UnlockAchievement(_Achievement);
//END_REGION

//REGION Progress achievement

PROC
Proc_ProgressAchievement((CHARACTERGUID)_Player,(STRING)_Achievement)
AND
NOT DB_ProgressAchievement_AchievementCompleted(_Player,_Achievement)
THEN
Proc_IncrementProgressAchievement((CHARACTERGUID)_Player,(STRING)_Achievement);


PROC
Proc_IncrementProgressAchievement((CHARACTERGUID)_Player,(STRING)_Achievement)
AND
NOT DB_ProgressAchievement_PlayerProgression(_Player,_Achievement,_)
THEN
DB_ProgressAchievement_PlayerProgression(_Player,_Achievement,0);


PROC
Proc_IncrementProgressAchievement((CHARACTERGUID)_Player,(STRING)_Achievement)
AND
DB_ProgressAchievement_PlayerProgression(_Player,_Achievement,_OldActual)
AND
IntegerSum(_OldActual,1,_NewActual)
THEN
NOT DB_ProgressAchievement_PlayerProgression(_Player,_Achievement,_OldActual);
DB_ProgressAchievement_PlayerProgression(_Player,_Achievement,_NewActual);


PROC
Proc_IncrementProgressAchievement((CHARACTERGUID)_NullPlayer,(STRING)_Achievement)
AND
_NullPlayer == NULL_00000000-0000-0000-0000-000000000000
AND
DB_ProgressAchievement_PlayerProgression(_NullPlayer,_Achievement,_NewActual)
THEN
SetAchievementProgress(_Achievement,_NewActual);


PROC
Proc_IncrementProgressAchievement((CHARACTERGUID)_Player,(STRING)_Achievement)
AND
_Player != NULL_00000000-0000-0000-0000-000000000000
AND
DB_ProgressAchievement_PlayerProgression(_Player,_Achievement,_NewActual)
THEN
SetAchievementProgress(/*_Player,*/_Achievement,_NewActual);


PROC
Proc_IncrementProgressAchievement((CHARACTERGUID)_Player,(STRING)_Achievement)
AND
DB_ProgressAchievement_PlayerProgression(_Player,_Achievement,_NewActual)
AND
DB_ProgressAchievement(_Achievement,_Total)
AND
_Total <= _NewActual
THEN
NOT DB_ProgressAchievement_AchievementCompleted(_Player,_Achievement);

//END_REGION

//REGION TUT_Tutorial_A

//Escape the Merryweather
PROC
Proc_FTJ_CheckPlayersWokenUp((CHARACTERGUID)_Player)
THEN
UnlockAchievement("DOS2_Quest1",_Player);

//END_REGION

//REGION FJ_FortJoy_Main

//Fort Joy
PROC
Proc_FTJ_CheckPlayersWokenUp((CHARACTERGUID)_Player)
THEN
UnlockAchievement("DOS2_Quest2",_Player);


//FJ Champion
IF
ObjectWasTagged(_Player,"CHAMPION_FTJ")
THEN
UnlockAchievement("DOS2_Quest3",(CHARACTERGUID)_Player);


//Escaped Fort Joy
IF
CharacterEnteredTrigger((CHARACTERGUID)_Player,TRIGGERGUID_S_FTJ_DallisEventAutoEnd_Poly_c66c0ceb-84d0-42f2-af77-854d178a58d5)
THEN
UnlockAchievement("DOS2_Quest4",(CHARACTERGUID)_Player);


//Removed Source Collar
IF
ObjectFlagSet("FTJ_RemoveSourceCollar",(CHARACTERGUID)_Player,_)
AND
DB_IsPlayer(_Player)
THEN
UnlockAchievement("DOS2_Quest5",(CHARACTERGUID)_Player);


//Meet your God
IF
StoryEvent((CHARACTERGUID)_Player,"FTJ_DoHoEReturnRD")
THEN
UnlockAchievement("DOS2_Quest6",_Player);


//Arrived at Shelter
IF
CharacterEnteredTrigger((CHARACTERGUID)_Player,TRIGGERGUID_S_FTJ_SW_SUB_Shelter_51c3953f-c756-472d-a7ca-dc2a6232b386)
AND
DB_IsPlayer(_Player)
THEN
UnlockAchievement("DOS2_Quest7",_Player);


//Save Gareth
IF
ObjectFlagSet("FTJ_SW_FreedCorneredPaladin",_Player,_)
THEN
UnlockAchievement("DOS2_Quest8",(CHARACTERGUID)_Player);


//Defeated Alexandar 
PROC
Proc_ObjectLeftCombat(CHARACTERGUID_S_GLO_Alexandar_03e6345f-1bd3-403c-80e2-a443a74f6349,_CombatID)
AND
DB_Dead(CHARACTERGUID_S_GLO_Alexandar_03e6345f-1bd3-403c-80e2-a443a74f6349)
AND
GetRegion(CHARACTERGUID_S_GLO_Alexandar_03e6345f-1bd3-403c-80e2-a443a74f6349,"FJ_FortJoy_Main")
AND
DB_IsPlayer(_Player)
AND
QRY_Achievements_CharacterIsOrWasInCombat(_Player,_CombatID)
THEN
UnlockAchievement("DOS2_Quest9",(CHARACTERGUID)_Player);

IF
CharacterKilledBy(CHARACTERGUID_S_GLO_Alexandar_03e6345f-1bd3-403c-80e2-a443a74f6349,_Player,_)
AND
DB_IsPLayer(_Player)
AND
GetRegion(_Player,"FJ_FortJoy_Main")
THEN
UnlockAchievement("DOS2_Quest9",_Player);


//The Black Cat
IF
ObjectFlagSet("QuestUpdate_FTJ_Escape_BlackCat_Saved",_Player,_)
THEN
UnlockAchievement("DOS2_Quest10",(CHARACTERGUID)_Player);


//Dallis Defeated
IF
GlobalFlagSet("FTJ_DallisDefeatedInGhetto")
THEN
UnlockAchievement("DOS2_Quest11");


//The Witched and the Purged Dragon
PROC
Proc_ObjectLeftCombat(CHARACTERGUID_S_FTJ_SW_PurgedDragon_c099caa6-1938-4b4f-9365-d0881c611e71,_CombatID)
AND
DB_Dead(CHARACTERGUID_S_FTJ_SW_PurgedDragon_c099caa6-1938-4b4f-9365-d0881c611e71)
AND
GetRegion(CHARACTERGUID_S_FTJ_SW_PurgedDragon_c099caa6-1938-4b4f-9365-d0881c611e71,"FJ_FortJoy_Main")
AND
DB_IsPlayer(_Player)
AND
QRY_Achievements_CharacterIsOrWasInCombat(_Player,_CombatID)
THEN
UnlockAchievement("DOS2_Quest12",(CHARACTERGUID)_Player);

IF
CharacterKilledBy(CHARACTERGUID_S_FTJ_SW_PurgedDragon_c099caa6-1938-4b4f-9365-d0881c611e71,_Player,_)
AND
DB_IsPLayer(_Player)
AND
GetRegion(_Player,"FJ_FortJoy_Main")
THEN
UnlockAchievement("DOS2_Quest12",_Player);


//The Witch And Purged Dragon
PROC
Proc_ObjectLeftCombat(CHARACTERGUID_S_FTJ_SW_Witch_4014aee0-56f1-47e0-a8eb-89c4b5a1da83,_CombatID)
AND
DB_Dead(CHARACTERGUID_S_FTJ_SW_Witch_4014aee0-56f1-47e0-a8eb-89c4b5a1da83)
AND
DB_IsPlayer(_Player)
AND
QRY_Achievements_CharacterIsOrWasInCombat(_Player,_CombatID)
THEN
UnlockAchievement("DOS2_Quest13",(CHARACTERGUID)_Player);

IF
CharacterKilledBy(CHARACTERGUID_S_FTJ_SW_Witch_4014aee0-56f1-47e0-a8eb-89c4b5a1da83,_Player,_)
AND
DB_IsPLayer(_Player)
THEN
UnlockAchievement("DOS2_Quest13",_Player);

//END_REGION

//REGION LV_HoE_Main

//Sails That Glance in the Sun -- Take command of the Lady Vengeance
IF
GlobalFlagSet("FTJ_LV_RiverDoReadyCheck")
THEN
UnlockAchievement("DOS2_Quest14");


//Scholar of Secret Arts -- Welcome Tarquin to the Seekers' service
IF
ObjectFlagSet("LV_PDD_SeekerMerchant_Peace",_Player,_)
THEN
UnlockAchievement("DOS2_Quest15",(CHARACTERGUID)_Player);


//Seascape -- Escape Dallis' siege on the Lady Vengeance without killing every minion
IF
GlobalFlagSet("FTJ_LV_StartHoELevel")
AND
NOT DB_OnlyOnce("LV_FinishAssaultEarly_Once")
THEN
UnlockAchievement("DOS2_Quest16");


//Heaven's Gate -- Enter the Hall of Echoes
IF
MovieFinished("CS_LV-Battle_to_HOE")
THEN
UnlockAchievement("DOS2_Quest17");


//Swashbuckler -- Escape Dallis' siege on the Lady Vengeance after killing every minion
PROC
Proc_LV_FinishAssaultEarly()
THEN
UnlockAchievement("DOS2_Quest18");

//END_REGION

//REGION RC_Main

//Arrived in Reaper's Coast
IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_LV_TP_Beach_9128c91c-6302-4574-9d0e-0595ff211b6b)
AND
DB_IsPlayer(_Player)
THEN
UnlockAchievement("DOS2_Quest19",_Player);


//Meistr Siva
IF
ObjectFlagSet("RC_DW_Meistr_Hasmet",_Player,_)
THEN
UnlockAchievement("DOS2_Quest20",(CHARACTERGUID)_Player);


//Driftwood Champion
IF
ObjectWasTagged(_Player,"CHAMPION_DW")
THEN
UnlockAchievement("DOS2_Quest21",(CHARACTERGUID)_Player);


//Use your first source skill
IF
CharacterUsedSkill(_Player,_Skill,_,_)
AND
DB_IsPlayer(_Player)
AND
IsSourceSkill(_Skill,1)
THEN
UnlockAchievement("DOS2_Quest22",_Player); //Use your first source skill


//Unlock your second source container
IF
ObjectFlagSet("GLO_Has2MaxMP",_Player,_)
THEN
UnlockAchievement("DOS2_Quest23",(CHARACTERGUID)_Player);


//Unlock your third source container
IF
ObjectFlagSet("GLO_Has3MaxMP",_Player,_)
THEN
UnlockAchievement("DOS2_Quest24",(CHARACTERGUID)_Player);


//Gareth's Vengeance
IF
ObjectFlagSet("QuestUpdate_RC_FL_GarethFarm_ConvincedKillJonathan",_Player,_)
THEN
UnlockAchievement("DOS2_Quest25",(CHARACTERGUID)_Player);


//Gareth's Compassion
IF
ObjectFlagSet("RC_FL_ChoseJonathanRelease",_Player,_)
THEN
UnlockAchievement("DOS2_Quest26",(CHARACTERGUID)_Player);


//Thank Gods for the Council of Seven
IF
ObjectFlagSet("RC_DW_Meistr_KnowCoS",_,_)
THEN
UnlockAchievement("DOS2_Quest27");


//Thank Malady for the Council of Seven
IF
ObjectFlagSet("RC_DW_River_KnowCoS",_,_)
THEN
UnlockAchievement("DOS2_Quest28");


//Thank the Doctor for the Council of Seven
IF
ObjectFlagSet("RC_BI_ThePresence_KnowCoS",_,_)
THEN
UnlockAchievement("DOS2_Quest29");


//Name of the Tree
IF
ObjectFlagSet("QuestUpdate_RC_BI_TheTruth_FoundOutDemonsName",_Player,_)
THEN
UnlockAchievement("DOS2_Quest30",(CHARACTERGUID)_Player);


//Existential Skeleton
IF
ObjectFlagSet("QuestUpdate_RC_GY_WronglyBuried_SkellyExploded",_Player,_)
THEN
UnlockAchievement("DOS2_Quest31",(CHARACTERGUID)_Player);


//Bank (You successfully lockpicked the skull lock in Mordus' basement)
//DOS2_Quest32 //--- Set in: RC_DW_AbandonedHouse


//God Slayer
IF
ObjectFlagSet("ARX_LV_Tarquin_GiveGodslayer",(CHARACTERGUID)_Player,_)
THEN
UnlockAchievement("DOS2_Quest33",_Player);


//Sawmill (You freed Saheila without initiating combat against the Lone Wolves.)
IF
ObjectFlagSet("RC_MIL_RescuedSaheila",_Player,_)
AND
NOT DB_GlobalFlag("RC_MIL_Achievement_Sawmill_Failed") //set in RC_MIL_SaheilaRescue
THEN
UnlockAchievement("DOS2_Quest34",(CHARACTERGUID)_Player);


//The Ferryman
PROC
Proc_RC_BF_DeathBoat_Arrived()
AND
DB_RC_BF_DeathBoat_SailFromIsland(0)
AND
DB_RC_BF_DeathBoat_PlayersInTrip(_Player)
AND
CharacterIsDead(_Player,0)
AND
IsTagged(_Player,"UNDEAD",1)
THEN
UnlockAchievement("DOS2_Quest35",_Player);


//Faithful Eagle
IF
SkillAdded(_Player,"Summon_Condor",_)
AND
DB_IsPlayer(_Player)
THEN
UnlockAchievement("DOS2_Quest36",_Player);


//Heroic Rescue
IF
ObjectFlagSet("RC_DU_RescuedHeroicWife",_Player,_)
THEN
UnlockAchievement("DOS2_Quest37",(CHARACTERGUID)_Player);


//Loyal Dog
PROC
Proc_ObjectLeftCombat(CHARACTERGUID_S_RC_GY_LoyalDog_46e022c5-f2d4-465c-a5f0-db0e9760fcce,_CombatID)
AND
DB_Dead(CHARACTERGUID_S_RC_GY_LoyalDog_46e022c5-f2d4-465c-a5f0-db0e9760fcce)
AND
DB_IsPlayer(_Player)
AND
QRY_Achievements_CharacterIsOrWasInCombat(_Player,_CombatID)
THEN
UnlockAchievement("DOS2_Quest38",_Player);

IF
CharacterKilledBy(CHARACTERGUID_S_RC_GY_LoyalDog_46e022c5-f2d4-465c-a5f0-db0e9760fcce,_Player,_)
AND
DB_IsPLayer(_Player)
THEN
UnlockAchievement("DOS2_Quest38",_Player);


//Purged Daughter
IF
ObjectFlagSet("QuestUpdate_RC_GY_PurgedDaughter_OperationSuccessful",_Player,_)
THEN
UnlockAchievement("DOS2_Quest39",(CHARACTERGUID)_Player);


//The Lair of the Eternals
//Destroy all void eggs in Reaper Cove (aka Wreckers Cave)
//DOS2_Quest40


//Funny Meat
IF
ObjectFlagSet("RC_ManagedToRemoveTaintedFunnyMeatStew",_Player,_)
THEN
UnlockAchievement("DOS2_Quest41",(CHARACTERGUID)_Player);


//Spider's Kiss
IF
ObjectFlagSet("QuestUpdate_RC_DW_SpidersKiss_Completed",_Player,_)
THEN
UnlockAchievement("DOS2_Quest42",(CHARACTERGUID)_Player);


//Mommy Voidwoken
IF
ObjectFlagSet("QuestUpdate_RC_DW_VoidwokenChicks_BecameMama",_Player,_)
THEN
UnlockAchievement("DOS2_Quest43",(CHARACTERGUID)_Player);

//END_REGION

//REGION CoS_Main

//Council of Seven
IF
CharacterUsedItem(_Player,ITEMGUID_S_RC_LV_TP_Beach_000_1f46931a-4c58-4055-9b2d-cfdeaddb968e)
AND
DB_IsPlayer(_Player)
THEN
UnlockAchievement("DOS2_Quest44",_Player);

// Sebille kills spy master
PROC
Proc_ObjectLeftCombat(CHARACTERGUID_S_CoS_Temples_SpyMaster_787c6c4a-6e5a-4044-9616-27e21e5a171e,_CombatID)
AND
DB_Dead(CHARACTERGUID_S_CoS_Temples_SpyMaster_787c6c4a-6e5a-4044-9616-27e21e5a171e)
AND
DB_IsPlayer(CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c)
AND
QRY_Achievements_CharacterIsOrWasInCombat(CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,_CombatID)
THEN
UnlockAchievement("DOS2_Quest100",CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c);


//The Academy
IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_CoS_SUB_Academy_51ddb828-18a6-4b6e-9f5a-e2b2883862e1)
AND
DB_IsPlayer(_Player)
THEN
UnlockAchievement("DOS2_Quest45",_Player);


//Entered the Arena of the One
IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_CoS_AotO_SUB_Arena_46aab122-a2f8-42f2-a67b-25062e1a8847)
AND
DB_IsPlayer(_Player)
THEN
UnlockAchievement("DOS2_Quest46",_Player);


//Killed your Gods
IF
ObjectWasTagged(_Player,"GODSLAYER")
THEN
UnlockAchievement("DOS2_Quest47",(CHARACTERGUID)_Player);


//The Seven Altars (Pray at all of the altars)
//DOS2_Quest48 - Set in goal: CoS_Temples_GodAltars


//Charm Alexandar
IF
ObjectFlagSet("QuestUpdate_CoS_Academy_ConvincedAlexandar",_Player,_)
THEN
UnlockAchievement("DOS2_Quest49",(CHARACTERGUID)_Player);


//The Chosen One
//UnlockAchievement("DOS2_Quest50");


//Academy Backdoor
IF
ObjectFlagSet("CoS_Temples_KnowsHowToEnterAcademy_BackEntrance",_Player,_)
THEN
UnlockAchievement("DOS2_Quest51",(CHARACTERGUID)_Player);


//The One Champion
IF
ObjectWasTagged(_Player,"THE_ONE")
THEN
UnlockAchievement("DOS2_Quest52",(CHARACTERGUID)_Player);


//Promise Breaker
IF
ItemTemplateCombinedWithItemTemplate(_,_,_,_,_,_Player,_Crafted)
AND
DB_IsPlayer(_Player)
AND
GetTemplate(_Crafted,"WPN_Ataraxians_Scythe_2H_A_3b45c978-5a42-40b5-a7aa-183852616910")
THEN
UnlockAchievement("DOS2_Quest53",_Player);


//The Promise
IF
ObjectFlagSet("GLO_Promise_AcceptedPromise",_Player,_)
THEN
UnlockAchievement("DOS2_Quest54",(CHARACTERGUID)_Player);

//END_REGION

//REGION ARX_Main

//Crashed in Arx
//PROC
//ProcRegionStarted("ARX_Main")
IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_ARX_TheCrash_CrashSite_e8316fd4-b022-4332-8c7f-bf82b0001521)
AND
DB_IsPlayer(_Player)
THEN
UnlockAchievement("DOS2_Quest55",_Player);


//Cathedral Crypt
IF
ObjectFlagSet("QuestUpdate_ARX_HuntingForDallis_TrickedPoB",_Player,_)
THEN
UnlockAchievement("DOS2_Quest56",(CHARACTERGUID)_Player);


//Path of Blood
IF
ObjectFlagSet("ARX_Cathedral_DivineTomb_PoB_Accepted",_Player,_)
THEN
UnlockAchievement("DOS2_Quest57",(CHARACTERGUID)_Player);


//Death to the Kraken
IF
CharacterDied(CHARACTERGUID_S_EndGame_Kraken_ae15de17-4238-4ae0-af43-48cd0f72f8f5)
THEN
UnlockAchievement("DOS2_Quest58");


//Ending 1 - New Divine
IF
GlobalFlagSet("EG_Ending1_NewDivine")
THEN
UnlockAchievement("DOS2_Quest59");


//Ending 2 - Source for All
IF
GlobalFlagSet("EG_Ending2_SourceForAll")
THEN
UnlockAchievement("DOS2_Quest60");


//Ending 3 - Purge
IF
GlobalFlagSet("EG_Ending3_Purge")
THEN
UnlockAchievement("DOS2_Quest61");


//Ending 4 - God King
IF
GlobalFlagSet("EG_Ending4_GodKing")
THEN
UnlockAchievement("DOS2_Quest62");


//Ending 5 - Demon
IF
GlobalFlagSet("EG_Ending_5_Demon")
THEN
UnlockAchievement("DOS2_Quest63");


// Sebille serves the mother tree
IF
GlobalFlagSet("GLO_SebilleRooted")
THEN
UnlockAchievement("DOS2_Quest68",CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c);

IF
ObjectFlagSet("QuestUpdate_FTJ_COM_Sebille_KilledMotherTree",CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,_)
THEN
UnlockAchievement("DOS2_Quest69",CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c);

IF
ObjectFlagSet("QuestUpdate_FTJ_OriginSebille_KilledMotherTree",CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,_)
THEN
UnlockAchievement("DOS2_Quest69",CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c);

IF
ObjectFlagSet("QuestUpdate_FTJ_COM_Sebille_KilledMotherTree_DeathFog",CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,_)
THEN
UnlockAchievement("DOS2_Quest69",CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c);

// Fane becomes new divine and restores eternals
IF
GlobalFlagSet("EG_Ending1A_EternalsReturn")
AND
DB_IsPlayer(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb)
THEN
UnlockAchievement("DOS2_Quest70",CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb);

// Fane chooses purge -> gave up restoring eternals
IF
GlobalFlagSet("EG_Ending3_Purge")
AND
DB_Avatars(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb)
AND
NOT DB_Dead(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb)
THEN
UnlockAchievement("DOS2_Quest71",CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb);

// Fane gives source to everyone -> gave up restoring eternals
IF
GlobalFlagSet("EG_Ending2_SourceForAll")
AND
DB_Avatars(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb)
AND
NOT DB_Dead(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb)
THEN
UnlockAchievement("DOS2_Quest71",CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb);


//Ifan kills Lucian
IF
ObjectFlagSet("QuestUpdate_FTJ_Ifan_DarkFaction_KilledLucianCrypt",CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,_)
THEN
UnlockAchievement("DOS2_Quest72",CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f);

IF
ObjectFlagSet("QuestUpdate_FTJ_COM_Ifan_KilledLucianCrypt",_Player,_)
THEN
UnlockAchievement("DOS2_Quest72",(CHARACTERGUID)_Player);

// Ifan forgives Lucian
IF
ObjectFlagSet("ARX_EndGame_ForgaveLucian",CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,_)
THEN
UnlockAchievement("DOS2_Quest73",CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f);


//Beast assassinates the queen
IF
ObjectFlagSet("ARX_Sewers_TheQueen_AssassinationAttempt",CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,_)
THEN
UnlockAchievement("DOS2_Quest74",CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978);


//Beast and queen reconcile their differences
IF
ObjectFlagSet("QuestUpdate_FTJ_OriginBeast_Resolved",CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,_)
THEN
UnlockAchievement("DOS2_Quest75",CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978);

IF
ObjectFlagSet("QuestUpdate_FTJ_COM_Beast_Resolved",_Player,_)
THEN
UnlockAchievement("DOS2_Quest75",(CHARACTERGUID)_Player);


//Creepy Amulet
IF
ObjectFlagSet("ARX_CreepyCraftsman_Give_SourceAmulet",(CHARACTERGUID)_Player,_)
AND
DB_IsPlayer(_Player)
THEN
UnlockAchievement("DOS2_Quest76",_Player);


//Sneaky Reimond
IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_ARX_Barracks_WhiteMagisterVault_RegionTrigger_c03d300b-0c04-425e-8af0-8644cb5a1c15)
AND
DB_IsPlayer(_Player)
THEN
UnlockAchievement("DOS2_Quest77",(CHARACTERGUID)_Player);


//Save Arhu
IF
ObjectFlagSet("ARX_KemmVault_Arhu_PostKemmDialog",_Player,_)
THEN
UnlockAchievement("DOS2_Quest78",(CHARACTERGUID)_Player);


//Death to the Doctor
PROC
Proc_ObjectLeftCombat(CHARACTERGUID_S_ARX_DoctorsHouse_TheDoctor_83083470-c543-45b3-ac8a-108df0daca8d,_CombatID)
AND
DB_Dead(CHARACTERGUID_S_ARX_DoctorsHouse_TheDoctor_83083470-c543-45b3-ac8a-108df0daca8d)
AND
DB_IsPLayer(_Player)
AND
QRY_Achievements_CharacterIsOrWasInCombat(_Player,_CombatID)
THEN
UnlockAchievement("DOS2_Quest79",_Player);

IF
CharacterKilledBy(CHARACTERGUID_S_ARX_DoctorsHouse_TheDoctor_83083470-c543-45b3-ac8a-108df0daca8d,_Player,_)
AND
DB_IsPLayer(_Player)
THEN
UnlockAchievement("DOS2_Quest79",_Player);


//Deal with the Doctor
IF
ObjectFlagSet("QuestUpdate_ARX_TheDoctor_Pact_Accepted",_Player,_)
THEN
UnlockAchievement("DOS2_Quest80",(CHARACTERGUID)_Player);

//END_REGION

//REGION ARX_Main - Origins

//Red Prince - Killed Sadha
IF
ObjectFlagSet("ARX_HorrorSleep_Sunset_KilledPrincess",CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f,_ID)
THEN
UnlockAchievement("DOS2_Quest64",CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f);


//Red Prince - Have a heir
IF
SkillAdded(CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f,"Summon_Quest_SummonNewt",_Learned)
THEN
UnlockAchievement("DOS2_Quest65",CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f);

IF
GlobalFlagSet("ARX_HorrorSleep_Closed")
AND
NOT DB_Dead(CHARACTERGUID_S_ARX_HorrorSleep_Sunset_Newt_1f46750f-1d62-4676-aaa7-9ec226c96428)
AND
DB_IsPlayer(CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f)
THEN
UnlockAchievement("DOS2_Quest65",CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f);


//Lohse - Embraced the Doctor
IF
CharacterUsedSkillOnTarget(CHARACTERGUID_S_ARX_DoctorsHouse_TheDoctor_83083470-c543-45b3-ac8a-108df0daca8d,CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,"Target_Quest_DemonicPossession_Kill",_,_)
THEN
UnlockAchievement("DOS2_Quest66",CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295);

IF
CharacterUsedSkillOnTarget(CHARACTERGUID_S_ARX_DoctorsHouse_TheDoctor_83083470-c543-45b3-ac8a-108df0daca8d,CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,"Target_Quest_DemonicPossession",_,_)
THEN
UnlockAchievement("DOS2_Quest66",CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295);


//Lohse - Free herself from the Doctor
IF
ObjectFlagSet("QuestUpdate_FTJ_COM_Lohse_DoctorDefeat",_Player,_)
THEN
UnlockAchievement("DOS2_Quest67",(CHARACTERGUID)_Player);

IF
ObjectFlagSet("QuestUpdate_FTJ_OriginLohse_DoctorDefeat",CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,_)
THEN
UnlockAchievement("DOS2_Quest67",CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295);



//END_REGION

//REGION Any

//Shapeshifted into every race
IF
CharacterPolymorphedInto(_Char,_Race)
AND
DB_IsPlayer(_Char)
AND
NOT DB_GLO_Achievement_ShapeshiftedRace(_Char,_Race)
THEN
DB_GLO_Achievement_ShapeshiftedRace(_Char,_Race);
Proc_ProgressAchievement(_Char,"DOS2_Quest81");


//Killer Teleport - Killed somebody by "Teleportation"
//DOS2_Quest86 //CODE


//Craft a skill (Successfully crafted a skill) 
//DOS2_Quest87 //CODE


//Rune Master (Fill up a piece of equipment's rune slots) 
//DOS2_Quest88 //CODE


//Animal Ghost Whisperer
IF
DialogStarted(_,_Inst)
AND
DB_DialogNPCs(_Inst,_Char,_)
AND
IsTagged(_Char,"GHOST",1)
AND
IsTagged(_Char,"ANIMAL",1)
AND
DB_DialogPlayers(_Inst,_Player,_)
THEN
UnlockAchievement("DOS2_Quest89",(CHARACTERGUID)_Player);


//Curse
IF
SkillCast(_Player,"Target_Curse",_,_)
AND
DB_IsPlayer(_Player)
THEN
UnlockAchievement("DOS2_Quest90",_Player);


//Hero
IF
ObjectWasTagged(_Player,"HERO")
THEN
UnlockAchievement("DOS2_Quest91",(CHARACTERGUID)_Player);


//Villain
IF
ObjectWasTagged(_Player,"VILLAIN")
THEN
UnlockAchievement("DOS2_Quest92",(CHARACTERGUID)_Player);


//Persuasive
IF
PersuasionResult(_Player,1,_)
AND
DB_IsPlayer(_Player)
THEN
UnlockAchievement("DOS2_Quest93",_Player);


//Thief
IF
CharacterStoleItem(_Player,_,_,_,_,_,_,_)
AND
DB_IsPlayer(_Player)
THEN
UnlockAchievement("DOS2_Quest94",_Player);


//Craft a Grenade 
//DOS2_Quest95 //CODE


//Going to jail
PROC
ProcCrimeTeleportCharacterToPrison((CHARACTERGUID)_Player,(TRIGGERGUID)_)
THEN
UnlockAchievement("DOS2_Quest96",_Player);


//END_REGION

//REGION Queries

QRY
QRY_Achievements_CharacterIsOrWasInCombat((CHARACTERGUID)_Char,(INTEGER)_CombatID)
AND
DB_CombatCharacters(_Char,_CombatID)
THEN
DB_NOOP(1);


QRY
QRY_Achievements_CharacterIsOrWasInCombat((CHARACTERGUID)_Char,(INTEGER)_CombatID)
AND
DB_WasInCombat(_Char,_CombatID)
THEN
DB_NOOP(1);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "DOS2ModWrapper"
