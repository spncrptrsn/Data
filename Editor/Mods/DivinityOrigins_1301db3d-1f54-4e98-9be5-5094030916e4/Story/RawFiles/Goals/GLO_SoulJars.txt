Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_BlockSoulJarDestroyVB(ITEMGUID_S_FTJ_SoulJarTrap_001_f1048dcf-98bb-4cd1-9de8-c11363bf3f27);
DB_BlockSoulJarDestroyVB(ITEMGUID_S_FTJ_SoulJarTrap_002_af88fbb9-143d-4663-8f74-a889bc7686ad);
DB_BlockSoulJarDestroyVB(ITEMGUID_S_FTJ_SoulJarTrap_003_a843c58b-25f8-44bd-a0dc-9702f146d357);
DB_BlockSoulJarDestroyVB(ITEMGUID_S_FTJ_SoulJarTrap_004_fcea4277-68af-410f-93c8-29693cb13cf7);


DB_GLO_SourcePointDialog("FTJ_WithermooreSoulJar");
DB_GLO_SourcePointDialog("FTJ_SW_SoulJar_GuardUndead1");
DB_GLO_SourcePointDialog("FTJ_SW_SoulJar_GuardUndead2");
DB_GLO_SourcePointDialog("FTJ_SW_SoulJar_GuardUndead3");
DB_GLO_SourcePointDialog("FTJ_SW_SoulJar_ShelterUndead");
DB_GLO_SourcePointDialog("FTJ_SW_SoulJar_Illusionist");
DB_GLO_SourcePointDialog("FTJ_SW_River");



KBSECTION
IF
ItemDestroying((ITEMGUID)_SoulJar)
AND
IsTagged(_SoulJar,"SOULJAR",1)
AND
NOT DB_FTJ_SoulJar_Removed(_SoulJar)
AND
ObjectGetFlag(_SoulJar,"Absorb_SoulJar",0)
THEN
//PlayEffect(_SoulJar,"RS3_FX_GP_ScriptedEvent_SourceJar_Break_01");
PROC_GLO_SoulJar_PlayDestroyEffect(_SoulJar);

IF
DialogEnded(_,_ID)
AND
DialogGetInvolvedNPC(_ID,1,(ITEMGUID)_SoulJar)
AND
IsTagged(_SoulJar,"SOULJAR",1)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
ItemIsInPartyInventory(_SoulJar,_Player,0,1)
AND
ObjectGetFlag(_SoulJar,"Destroy_SoulJar",1)
THEN
PlayEffect(_Player,"RS3_FX_GP_ScriptedEvent_SourceJar_Break_01");
PROC_GLO_SoulJar_PlayDestroyEffect(_SoulJar);
DB_FTJ_SoulJar_Removed(_SoulJar);
ItemRemove(_SoulJar);
DB_DestroyedItems(_SoulJar);

IF
DialogEnded(_,_ID)
AND
DialogGetInvolvedNPC(_ID,1,(ITEMGUID)_SoulJar)
AND
IsTagged(_SoulJar,"SOULJAR",1)
AND
ObjectGetFlag(_SoulJar,"Destroy_SoulJar",1)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
ItemIsInCharacterInventory(_SoulJar,_Player,0)
THEN
ItemDestroy(_SoulJar);
ProcForceStopDialog(_Player);

IF
DialogEnded(_,_ID)
AND
DialogGetInvolvedNPC(_ID,1,(ITEMGUID)_SoulJar)
AND
IsTagged(_SoulJar,"SOULJAR",1)
AND
ObjectGetFlag(_SoulJar,"Absorb_SoulJar",1)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
DB_Dialogs(_SoulJar, _Dialog)
THEN
Proc_AddSourcePoint(_Player);
Transform(_SoulJar,"PUZ_Source_Jar_Empty_A_f5a33e40-171c-4cf0-9f86-9343fd5df646",0);
PROC_GLO_SoulJar_PlayDestroyEffect(_SoulJar);
PlayAnimation(_Player,"use_activate");
PlayEffect(_Player,"RS3_FX_GP_ScriptedEvent_Source_Absorb_01");
NOT DB_Dialogs(_SoulJar, _Dialog);
ObjectSetFlag(_Player,"GLO_PathOfBlood_DisrespectedSoul");
Proc_AbsorbedSoulJar(_Player,_SoulJar);

PROC
Proc_AbsorbedSoulJar((CHARACTERGUID)_Player,(ITEMGUID)_SoulJar)
THEN
DB_NOOP(1);

IF
ObjectFlagSet("Pickup_SoulJar",(CHARACTERGUID)_Player,_Inst)
AND
DialogGetInvolvedNPC(_Inst,1,(ITEMGUID)_Jar)
THEN
DB_SoulJarPickup(_Player,_Jar,_Inst);
ObjectClearFlag(_Player,"Pickup_SoulJar",_Inst);

IF
DialogEnded(_,_ID)
AND
DB_SoulJarPickup(_Player,_Jar,_ID)
THEN
CharacterPickupItem(_Player,_Jar,"");
NOT DB_SoulJarPickup(_Player,_Jar,_ID);

IF
ItemAddedToCharacter(_SoulJar,_)
AND
ObjectExists(_Souljar,1)
AND
IsTagged(_SoulJar,"SOULJAR",1)
AND
ObjectGetFlag(_SoulJar,"Jar_Pickedup",0)
THEN
ObjectSetFlag(_SoulJar,"Jar_Pickedup");

PROC
PROC_GLO_SoulJar_PlayDestroyEffect((ITEMGUID)_SoulJar)
AND
ItemIsInInventory(_SoulJar, 0)
AND
GetPosition(_SoulJar, _X, _Y, _Z)
THEN
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_SourceJar_Death_launch_01", _X, _Y, _Z);

PROC
PROC_GLO_SoulJar_PlayDestroyEffect((ITEMGUID)_SoulJar)
AND
ItemIsInInventory(_SoulJar, 1)
AND
GetInventoryOwner(_SoulJar, _Character)
THEN
PlayEffect(_Character,"RS3_FX_GP_ScriptedEvent_SourceJar_Death_launch_01");

//REGION Check if character can absorb a jar

IF
DialogStarted(_Dialog,_Id)
AND
DB_GLO_SourcePointDialog(_Dialog)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
AND
Qry_GLO_RemoveBlockDialogSourceAbsorbtion(_Player)
THEN
ObjectSetFlag(_Player,"GLO_CanAbsorbSourceInDialog");

IF
DialogEnded(_Dialog,_Id)
AND
DB_GLO_SourcePointDialog(_Dialog)
AND
DialogGetInvolvedPlayer(_Id,1,(CHARACTERGUID)_Player)
THEN
ObjectClearFlag(_Player,"GLO_CanAbsorbSourceInDialog");


QRY
Qry_GLO_RemoveBlockDialogSourceAbsorbtion((CHARACTERGUID)_Char)
AND
HasActiveStatus(_Char,"SOURCE_MUTED",0)
AND
CharacterGetMaxSourcePoints(_Char,_CountMax)
AND
CharacterGetSourcePoints(_Char,_Count)
AND
_Count < _CountMax
THEN
DB_NOOP(1);


//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start"
