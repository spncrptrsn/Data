Version 1
SubGoalCombiner SGC_AND
INITSECTION
ProcTriggerRegisterForPlayers(RC_FTJ_DrownedCorpseReaction_Box_0110bfbc-c045-48c6-a4d9-68faad0553da);
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_FTJ_TUT_SaveLoad_df13b935-3930-43a5-8739-2ba60a4060be);

DB_Dialogs(RC_FTJ_BeachCrab_ba6d2b58-c978-4ce9-a99b-3aef542dd2e7,"FTJ_BeachCrab");
DB_DeadEvent(CHARACTERGUID_S_FTJ_BeachCrab_ba6d2b58-c978-4ce9-a99b-3aef542dd2e7,"FTJ_SeptaIsDead");
DB_Dialogs(CHARACTERGUID_S_FTJ_StartingAreaChild_39cbb883-7e80-445c-b5f8-0e42d2283d73,"FTJ_StartingAreaChild_001");

DB_FTJ_SA_Voidlings((CHARACTERGUID)CHARACTERGUID_S_FTJ_BeachVw_001_08348b3a-bded-4811-92ce-f127aa4310e0);
DB_FTJ_SA_Voidlings((CHARACTERGUID)CHARACTERGUID_S_FTJ_BeachVw_002_1832a661-0e21-421f-acaa-a7e66e813b14);

DB_KillCounter_Internal(CHARACTERGUID_S_FTJ_BeachVw_001_08348b3a-bded-4811-92ce-f127aa4310e0,"FTJ_BeachVw");
DB_KillCounter_Internal(CHARACTERGUID_S_FTJ_BeachVw_002_1832a661-0e21-421f-acaa-a7e66e813b14,"FTJ_BeachVw");
DB_KillCounter("FTJ_BeachVw",2);

DB_ShovelArea(RC_FTJ_DirtMound_004_Box_e3d16b82-c559-45c7-8e19-26126fe13032,"FTJ_DirtPileCombat",RC_FTJ_DirtPile_004_69f1cc0b-61ef-4ea4-9f21-ff5d86ea1f1d);
DB_ShovelRewardCharacterAppear("FTJ_DirtPileCombat",CHARACTERGUID_S_FTJ_DirtMoundZombie_eb499ae9-21df-41e9-9d89-968a88ceb3ad);

DB_IsSign(ITEMGUID_S_FTJ_WoodenCross_001_7e039004-997c-4946-be37-70f3586fd270,"FTJ_Signs","FTJ_WoodenCross_001");

ProcTriggerRegisterForPlayers(TRIGGERGUID_S_FTJ_TUT_SecretPassages_6c725690-fb06-43e6-a27b-c64d08fbd78c);

Proc_FTJ_StartingAreaStart();
KBSECTION
IF
ObjectEnteredCombat((CHARACTERGUID)_NPC,_)
AND
DB_FTJ_SA_Voidlings((CHARACTERGUID)_NPC)
AND
QueryOnlyOnce("FTJ_CombatSave_Voidlings")
THEN
AutoSave();

//All players out of cc get main quest
PROC 
Proc_FTJ_StartingAreaStart()
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
ObjectSetFlag(_Player,"QuestAdd_FTJ_Escape");
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_StartEscape");
ObjectSetFlag(_Player,"QuestUpdate_RC_FTJ_SourceCollar_CloseEscapeShip");

//Add main quest for players recently recruited
PROC
PROC_GLO_PartyMembers_Add(_Companion,_)
AND
GetRegion(_Companion,_Region)
AND
_Region == "FJ_FortJoy_Main"
THEN
ObjectSetFlag(_Companion,"QuestAdd_FTJ_Escape");


//REGION Voidlings
IF
CharacterSawCharacter((CHARACTERGUID)_Player,(CHARACTERGUID)_Voidling)
AND
DB_IsPlayer(_Player)
AND
DB_FTJ_SA_Voidlings(_Voidling)
AND
GlobalGetFlag("FTJ_BeachVoidlingsDefeated",0)
AND
QRY_SpeakerIsAvailable(_Player)
AND
QueryOnlyOnce("FTJ_SA_VoidlingVB_Once")
THEN
StartVoiceBark("FTJ_VB_SawBeachVoidling",_Player);

PROC
ReactOnKillCounter("FTJ_BeachVw")
THEN 
GlobalSetFlag("FTJ_BeachVoidlingsDefeated");

IF
TextEventSet("xpadd")
AND
CharacterGetHostCharacter(_Char)
THEN
PartyAddExperience(_Char,1,1,100);


PROC
ReactOnKillCounter("FTJ_BeachVw")
AND
GetClosestAlivePlayer(CHARACTERGUID_S_FTJ_BeachVw_001_08348b3a-bded-4811-92ce-f127aa4310e0,_Player,_)
AND
QRY_SpeakerIsAvailable(_Player)
AND
GetClosestAlivePlayer(_Player,_OtherPlayer,_Dist)
AND
_OtherPlayer != _Player
AND
_Dist <= 15.0
AND
QueryOnlyOnce("FTJ_BeachVwDeathReaction")
THEN
ProcDefineReflectionDialog("FTJ_RD_DefeatedBeachVoidlings",_Player);

PROC
ReactOnKillCounter("FTJ_BeachVw")
AND
GetClosestAlivePlayer(CHARACTERGUID_S_FTJ_BeachVw_001_08348b3a-bded-4811-92ce-f127aa4310e0,_Player,_)
AND
QRY_SpeakerIsAvailable(_Player)
AND
QueryOnlyOnce("FTJ_BeachVwDeathReaction")
THEN
StartVoiceBark("FTJ_VB_DefeatedBeachVw",_Player);

PROC
ReactOnKillCounter("FTJ_BeachVw")
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_BeachVoidlings");

IF
CharacterLootedCharacterCorpse((CHARACTERGUID)_Player,CHARACTERGUID_S_FTJ_DeadVwNPC_ccce2ec7-04da-4e22-8bdd-5b3c058fbba9)
AND
QRY_SpeakerIsAvailable(_PLayer)
AND
QueryOnlyOnce("FTJ_VB_DeadBeachMagister_Once")
THEN
StartVoiceBark("FTJ_VB_DeadBeachMagister",_Player);

//Voidling AD
IF
ObjectTurnStarted((CHARACTERGUID)_Voidling)
AND
DB_FTJ_SA_Voidlings(_Voidling)
AND
GetClosestAlivePlayer(_Voidling,(CHARACTERGUID)_Player,_)
AND
CharacterIsPolymorphInteractionDisabled(_Player,0)
AND
CharacterCanSee(_Voidling,_Player,1)
AND
DB_CombatCharacters(_Voidling, _ID)
AND
DB_CombatCharacters(_Player, _ID)
AND
QueryOnlyOnce("FTJ_SA_VoidlingsNamePlayer")
THEN
Proc_StartDialog(1,"FTJ_AD_VoidlingNamesPlayer",_Voidling,_Player);
PlayAnimation(_Voidling,"idle1");


//END_REGION

//REGION Game Start Players Get-Up + VB
IF
GameStarted("FJ_FortJoy_Main",0)
AND
QueryOnlyOnce("FTJ_Game_Intro")
THEN
Proc_FTJ_StartGame();
ProcDeclareCounter("FTJ_PlayersWokenUp");

PROC
Proc_FTJ_StartGame()
AND
DB_IsPlayer(_Player)
THEN
FadeToBlack(_Player,0.0,0,"Placeholder");
CharacterSetAnimationOverride(_Player,"knockdown_loop");
CharacterFreeze(_Player);
ProcObjectTimer(_Player,"FTJ_GameStart_FadeIn",1000);
ApplyStatus(_Player,"WET",12.0);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"FTJ_GameStart_FadeIn")
THEN
FadeToBlack(_Player,4.5,1,"Placeholder");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"FTJ_GameStart_FadeIn")
AND
Random(3000,_Rand)
AND
IntegerSum(6000,_Rand,_Timer)
THEN
ProcObjectTimer(_Player,"FTJ_WakeUpTimer",_Timer);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"FTJ_WakeUpTimer")
THEN
PROC_UnlockWaypoint("WAYP_FTJ_BeachStatue",_Player);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"FTJ_WakeUpTimer")
THEN
CharacterSetAnimationOverride(_Player,"");
PlayAnimation(_Player,"knockdown_getup","");
ProcIncreaseCounter("FTJ_PlayersWokenUp");
Proc_FTJ_CheckPlayersWokenUp(_Player);

PROC
Proc_FTJ_CheckPlayersWokenUp((CHARACTERGUID)_Player)
AND
SysCount("DB_IsPlayer",1,_TotalPlayers)
AND
DB_GlobalCounter("FTJ_PlayersWokenUp",_Count)
AND
_Count == _TotalPlayers
THEN
PROC_FTJ_StartWakeUpVoicebark(_Player);
Proc_FTJ_UnfreezePlayers();
UserSetFlag(_Player,"QuestUpdate_FTJ_Voice_TUT_Voice");

PROC
PROC_FTJ_StartWakeUpVoicebark((CHARACTERGUID)_Player)
AND
DB_GlobalFlag("FTJ_BST_BeastEndedTutWithHat")
THEN
StartVoiceBark("FTJ_VB_WakeUpOnBeach",_Player);

PROC
PROC_FTJ_StartWakeUpVoicebark((CHARACTERGUID)_Player)
AND
NOT DB_GlobalFlag("FTJ_BST_BeastEndedTutWithHat")
THEN
StartVoiceBark("FTJ_VB_WakeUpOnBeachNoHat",_Player);

IF
VoiceBarkStarted("FTJ_VB_WakeUpOnBeach",_)
THEN
DB_GLO_Tutorial_StartMovementTutorial(1);

PROC
Proc_FTJ_UnfreezePlayers()
AND
DB_IsPlayer(_Player)
THEN
CharacterUnfreeze(_Player);

//END_REGION

IF 
ObjectFlagSet("FTJ_BeachPrisoner_001_ShowInquire_04",(CHARACTERGUID)_Player,_)
THEN 
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_ChapelInfo1");


IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_FTJ_XP_MountainTop_c10e42e6-e49a-44dc-aa9b-83c24a879510)
AND
QRY_SpeakerIsAvailable(_Player)
AND
QueryOnlyOnce("FTJ_MountainTopVB")
THEN
StartVoiceBark("FTJ_VB_MountainTopWarriors",_Player);


//Starting Area Statue
// No need to track WaypointLock, cause it won't happen until level switch
PROC
PROC_WaypointUnlocked_Event(_Player)
AND
NOT DB_FTJ_NumUnlockedWaypoints(_Player,_)
THEN
DB_FTJ_NumUnlockedWaypoints(_Player,0);

PROC
PROC_WaypointUnlocked_Event(_Player)
AND
DB_FTJ_NumUnlockedWaypoints(_Player,_OldCount)
AND
IntegerSum(_OldCount,1,_NewCount)
THEN
NOT DB_FTJ_NumUnlockedWaypoints(_Player,_OldCount);
DB_FTJ_NumUnlockedWaypoints(_Player,_NewCount);

IF 
CharacterUsedItem((CHARACTERGUID)_Player,ITEMGUID_S_FTJ_BeachStatue_465c7929-cc0f-4f58-8c2c-24d3e65e2fb0)
AND 
QRY_SpeakerIsAvailable(_Player)
AND
DB_FTJ_NumUnlockedWaypoints(_Player,_Count)
AND
_Count <= 1
THEN
StartVoiceBark("FTJ_VB_BeachStatue",_Player);

IF 
CharacterUsedItem((CHARACTERGUID)_Player,ITEMGUID_S_FTJ_BeachStatue_465c7929-cc0f-4f58-8c2c-24d3e65e2fb0)
AND 
QRY_SpeakerIsAvailable(_Player)
AND
DB_FTJ_NumUnlockedWaypoints(_Player,_Count)
AND
_Count > 1
THEN
Proc_StartDialog(0, "FTJ_BeachStatue", ITEMGUID_S_FTJ_BeachStatue_465c7929-cc0f-4f58-8c2c-24d3e65e2fb0, _Player);




PROC
ProcRegionEnded("FJ_FortJoy_Main")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "FortJoy"
