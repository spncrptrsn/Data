Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_CoS_Temples_GodAltars(ITEMGUID_S_CoS_Temples_Elf_Altar_4fb7a143-46e4-4ca5-b620-82de5188b20a,"CoS_Temples_Elf_Altar","Elf","CoS_Temples_LunarElf_Sequence","Moon");
DB_CoS_Temples_GodAltars(ITEMGUID_S_CoS_Temples_Orc_Altar_67205e75-78e2-48c7-9b28-30fdb0e5650e,"CoS_Temples_Orc_Altar","Orc","CoS_Temples_LunarOrc_Sequence","Sun");
DB_CoS_Temples_GodAltars(ITEMGUID_S_CoS_Temples_Human_Altar_ab5fc8e8-7861-4709-9fda-d5bb04b11ac0,"CoS_Temples_Human_Altar","Human","CoS_Temples_LunarHuman_Sequence","Sun");
DB_CoS_Temples_GodAltars(ITEMGUID_S_CoS_Temples_Dwarf_Altar_5c4011fe-70ad-4088-bc20-a2b100436a09,"CoS_Temples_Dwarf_Altar","Dwarf","CoS_Temples_LunarDwarf_Sequence","Moon");
DB_CoS_Temples_GodAltars(ITEMGUID_S_CoS_Temples_Lizard_Altar_c2becf8c-27b0-4b2e-a221-c8df35766c9c,"CoS_Temples_Lizard_Altar","Lizard","CoS_Temples_LunarLizard_Sequence","Sun");
DB_CoS_Temples_GodAltars(ITEMGUID_S_CoS_Temples_Imp_StatueToXantezza_27793291-e162-4c85-b3cc-f811f62d53e5,"CoS_Temples_Imp_StatueToXantezza","Imp","CoS_Temples_LunarImp_Sequence","Moon");
DB_CoS_Temples_GodAltars(ITEMGUID_S_CoS_Temples_Wizard_Altar_013d54ae-d7da-4336-959a-0bdd1493c407,"CoS_Temples_Wizard_Altar","Wizard","CoS_Temples_LunarWizard_Sequence","Moon");

DB_CoS_Temples_GodAltars_FxObject((ITEMGUID)ITEMGUID_S_CoS_Temples_Elf_Altar_4fb7a143-46e4-4ca5-b620-82de5188b20a,(ITEMGUID)ITEMGUID_S_CoS_Temples_Elf_Altar_FxHelper_b5a837c1-8fa8-4394-b30c-8a17115b1224);

DB_CanUseSkillCheck("CoS_Temples_Dwarf_Altar","Target_Bless",1);

//CoS_Temples_GodAltars_GodIntervened

DB_CoS_SunMoonEffects("Sun","RS3_FX_GP_ScriptedEvent_RunePuzzle_Icon_G_01");
DB_CoS_SunMoonEffects("Moon","RS3_FX_GP_ScriptedEvent_RunePuzzle_Icon_H_01");

Proc_CoS_Temples_Achievement_SevenAltars_FillFact();
KBSECTION
//REGION Achievement - The Seven Altars (Pray at all of the altars)

IF
ObjectFlagSet(_Flag,_Player,_Inst)
AND
DB_CoS_Temples_Achievement_SevenAltars(_Dialog,_Flag)
AND
DB_DialogName(_Dialog,_Inst)
THEN
Proc_ProgressAchievement((CHARACTERGUID)_Player,"DOS2_Quest48");

PROC
Proc_CoS_Temples_Achievement_SevenAltars_FillFact()
THEN
DB_CoS_Temples_Achievement_SevenAltars("CoS_Temples_Elf_Altar","CoS_Temples_AltarAchievement_Elf");
DB_CoS_Temples_Achievement_SevenAltars("CoS_Temples_Orc_Altar","CoS_Temples_AltarAchievement_Orc");
DB_CoS_Temples_Achievement_SevenAltars("CoS_Temples_Human_Altar","CoS_Temples_AltarAchievement_Human");
DB_CoS_Temples_Achievement_SevenAltars("CoS_Temples_Dwarf_Altar","CoS_Temples_AltarAchievement_Dwarf");
DB_CoS_Temples_Achievement_SevenAltars("CoS_Temples_Lizard_Altar","CoS_Temples_AltarAchievement_Lizard");
DB_CoS_Temples_Achievement_SevenAltars("CoS_Temples_Imp_StatueToXantezza","CoS_Temples_AltarAchievement_Imp");
DB_CoS_Temples_Achievement_SevenAltars("CoS_Temples_Wizard_Altar","CoS_Temples_AltarAchievement_Wizard");

//END_REGION

//REGION Journal update

//IF PLAYER MADE SACRIFICE AT ONE ALTAR
IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Player,_)
AND
DB_IsPlayer(_Player)
AND
DB_CoS_Temples_GodAltars(_,_,_,_Flag,_)
THEN
UserSetFlag(_Player,"QuestUpdate_CoS_Temples_PrayedAltar");
UserSetFlag(_Player,"QuestUpdate_CORE_Chapter5_COS_Altar_PrayedOnce");


//IF PLAYER MADE SACRIFICE AT ALL ALTARS
IF
DialogEnded(_Dialog,_Inst)
AND
DB_CoS_Temples_GodAltars(_,_Dialog,_,_,_)
AND
DB_DialogPlayers(_Inst,_Player,_)
AND
NOT QRY_CoS_Temples_AltarLeft((CHARACTERGUID)_Player)
THEN
UserSetFlag(_Player,"QuestUpdate_CoS_Temples_SacrificeAllAltar");
UserSetFlag(_Player,"QuestUpdate_CORE_Chapter5_COS_Altar_PrayedAll");

QRY
QRY_CoS_Temples_AltarLeft((CHARACTERGUID)_Player)
AND
DB_CoS_Temples_GodAltars(_,_,_,_Flag,_)
AND
UserGetFlag(_Player,_Flag,0)
THEN
DB_NOOP(1);


//END_REGION

IF
DB_CoS_Temples_GodAltars(_Altar,_Dialog,_,_,_)
THEN
DB_Dialogs(_Altar,_Dialog);

IF
ObjectFlagSet("CoS_Temples_GodAltar_SacrificeAccepted",(CHARACTERGUID)_Player,_Id)
AND
DialogGetInvolvedPlayer(_Id,1,_Player)
AND
DialogGetInvolvedNPC(_Id,1,_Altar)
AND
DB_CoS_Temples_GodAltars(_Altar,_Dialog,_Sacrifice,_,_)
THEN
ObjectClearFlag(_Player,"CoS_Temples_GodAltar_SacrificeAccepted");
Proc_CoS_Temples_GodAltars_ActivateSacrifice(_Player,_Sacrifice);

IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Player,_Id)
AND
DialogGetInvolvedNPC(_Id,1,(ITEMGUID)_Altar)
AND
DB_CoS_Temples_GodAltars(_Altar,_,_,_Flag,_Sequence)
AND
DB_CoS_SunMoonEffects(_Sequence,_Fx)
THEN
PROC_LoopEffect(_Fx,_Altar,"Altar_LunarSequence","CoS_Main","");


PROC
Proc_CoS_Temples_GodAltars_ActivateSacrifice((CHARACTERGUID)_Player,"Human")
THEN
ApplyStatus(_Player,"PERMANENT_BLIND",-1.0,1);
GlobalSetFlag("CoS_AotO_Rhalic_Buff");

PROC
Proc_CoS_Temples_GodAltars_ActivateSacrifice((CHARACTERGUID)_Player,"Orc")
THEN
ApplyStatus(_Player,"CURSED",-1.0,1);

PROC
Proc_CoS_Temples_GodAltars_ActivateSacrifice((CHARACTERGUID)_Player,"Wizard")
THEN
ObjectSetFlag(_Player,"RemoveOneSourcePoint");
GlobalSetFlag("CoS_AotO_Amadia_Buff");

PROC
Proc_CoS_Temples_GodAltars_ActivateSacrifice((CHARACTERGUID)_Player,"LIZARD")
AND
GetPosition(_Player,_X,_Y,_Z)
THEN
CharacterMakeStoryNpc(_Player,0);
CreateExplosionAtPosition(_X,_Y,_Z,"Projectile_TrapFireballMineWithSurface",16);
ApplyStatus(_Player,"BURNING",-1.0,1);
ApplyDamage(_Player,100,"Fire");
CreateSurfaceAtPosition(_X,_Y,_Z,"SurfaceFire",3.0,-1.0);
CharacterMakeStoryNpc(_Player,1);
GlobalSetFlag("CoS_AotO_Zorl_Buff");

PROC
Proc_CoS_Temples_GodAltars_ActivateSacrifice((CHARACTERGUID)_Player,"Imp")
THEN
ObjectSetFlag(_Player,"CoS_Temples_GodAltar_RenouncedGod");

PROC
Proc_CoS_Temples_GodAltars_ActivateSacrifice((CHARACTERGUID)_Player,"Elf")
THEN
CharacterPurgeQueue(_Player);
CharacterMakeStoryNpc(_Player,0);
CharacterRemoveAttribute(_Player,"Constitution",2);
CharacterSetHitpointsPercentage(_Player,3.0);
PlayAnimation(_Player,"hit");
GlobalSetFlag("CoS_AotO_Tir_Buff");
CharacterMakeStoryNpc(_Player,1);

PROC
Proc_CoS_Temples_GodAltars_ActivateSacrifice((CHARACTERGUID)_Player,"Elf")
AND
IsTagged(_Player,"UNDEAD",0)
AND
GetPosition(_Player,_X,_Y,_Z)
THEN
PlayEffect(_Player,"RS3_FX_GP_Combat_Hit_Blood_01_Large");
PlayEffect(_Player,"RS3_FX_Skills_Voodoo_Cast_Aoe_Voodoo_Blood_Root_01");
CreateSurfaceAtPosition(_X,_Y,_Z,"SurfaceBlood",3.0,-1.0);

PROC
Proc_CoS_Temples_GodAltars_ActivateSacrifice((CHARACTERGUID)_Player,"Elf")
AND
IsTagged(_Player,"UNDEAD",1)
THEN
PlayEffect(_Player,"RS3_FX_Skills_Voodoo_Cast_Aoe_Voodoo_Blood_Root_01");

PROC
Proc_CoS_Temples_GodAltars_ActivateSacrifice((CHARACTERGUID)_Player,"Dwarf")
THEN
CharacterPurgeQueue(_Player);
CharacterResetCooldowns(_Player);
CharacterUseSkill(_Player,"Target_Bless",ITEMGUID_S_CoS_Temples_Dwarf_Altar_5c4011fe-70ad-4088-bc20-a2b100436a09,1);
GlobalSetFlag("CoS_AotO_Duna_Buff");

//If Player used Bless on the Altar out of dialog
IF
CharacterUsedSkillOnTarget(_Player,ITEMGUID_S_CoS_Temples_Dwarf_Altar_5c4011fe-70ad-4088-bc20-a2b100436a09,"Target_Bless",_,_)
AND
ObjectGetFlag(_Player,"QuestUpdate_CoS_Temples_Lunar_Dwarf",0)
AND
QRY_SpeakerIsAvailable(_Player)
AND
QRY_SpeakerIsAvailable(ITEMGUID_S_CoS_Temples_Dwarf_Altar_5c4011fe-70ad-4088-bc20-a2b100436a09)
THEN
ObjectSetFlag(_Player,"CoS_Temples_BlessedAltarOutOfDialog");
Proc_StartDialog(0,"CoS_Temples_Dwarf_Altar",ITEMGUID_S_CoS_Temples_Dwarf_Altar_5c4011fe-70ad-4088-bc20-a2b100436a09,_Player);



IF
DialogStarted("CoS_Temples_Wizard_Altar",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
CharacterGetSourcePoints(_Player,_Int)
AND
_Int > 0
THEN
ObjectSetFlag(_Player,"CoS_Temples_Wizard_Altar_HasSourcePoint");

IF
DialogEnded("CoS_Temples_Wizard_Altar",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
THEN
ObjectClearFlag(_Player,"CoS_Temples_Wizard_Altar_HasSourcePoint");



//REGION God Wisp appear during dialog

IF
ObjectFlagSet("CoS_Temples_AltarWispEffect_On",_Altar,_)
AND
DB_CoS_Temples_GodAltars(_Altar,_Dialog,_,_,_)
AND
NOT DB_CoS_Temples_GodAltars_FxObject((ITEMGUID)_Altar,_)
THEN
DB_CoS_Temples_GodAltars_FxObject(_Altar,_Altar,_Dialog);

//DB_CoS_Temples_GodAltars_FxObject used for altars with unusual scalings to fix Fx positioning
IF
ObjectFlagSet("CoS_Temples_AltarWispEffect_On",_Altar,_)
AND
DB_CoS_Temples_GodAltars(_Altar,_Dialog,_,_,_)
AND
DB_CoS_Temples_GodAltars_FxObject((ITEMGUID)_Altar,_FxObject)
THEN
DB_CoS_Temples_GodAltars_FxObject(_Altar,_FxObject,_Dialog);

IF
DB_CoS_Temples_GodAltars_FxObject(_Altar,_FxObject,_)
THEN
PROC_LoopEffect("RS3_FX_GP_ScriptedEvent_CoS_Altar_Wisp_01",_FxObject,"CoS_Temples_AltarWispEffect","CoS_Main","");
ObjectClearFlag(_Altar,"CoS_Temples_AltarWispEffect_On");

IF
ObjectFlagSet("CoS_Temples_AltarWispEffect_Off",_Altar,_)
AND
DB_CoS_Temples_GodAltars_FxObject((ITEMGUID)_Altar,(ITEMGUID)_FxObject,(STRING)_Dialog)
THEN
PROC_StopLoopEffect(_FxObject,"CoS_Temples_AltarWispEffect");
ObjectClearFlag(_Altar,"CoS_Temples_AltarWispEffect_Off");
NOT DB_CoS_Temples_GodAltars_FxObject(_Altar,_FxObject,_Dialog);


IF
DialogEnded(_Dialog,_)
AND
DB_CoS_Temples_GodAltars_FxObject(_Altar,_FxObject,_Dialog)
THEN
PROC_StopLoopEffect(_FxObject,"CoS_Temples_AltarWispEffect");
ObjectClearFlag(_Altar,"CoS_Temples_AltarWispEffect_On");
NOT DB_CoS_Temples_GodAltars_FxObject(_Altar,_FxObject,_Dialog);


//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "CoS_Temples"
