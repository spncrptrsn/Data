Version 1
SubGoalCombiner SGC_AND
INITSECTION
ItemToInventory(S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446,S_TUT_LohseSongBoy1_835a993e-1bf1-4e6f-8922-20dd8b99bca4);
DB_GiveItemToEventWithClear((ITEMGUID)S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446,"TUT_GiveBall");
DB_HasStoryEvent((ITEMGUID)S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446,"TUT_HaveBall");

PROC_TUT_InitPlayingBall();
KBSECTION
IF
TextEventSet("tut_giveball")
THEN
ItemToInventory(ITEMGUID_S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446,S_TUT_LohseSongBoy1_835a993e-1bf1-4e6f-8922-20dd8b99bca4);

IF
TextEventSet("tut_getball")
AND
CharacterGetHostCharacter(_Player)
THEN
ItemToInventory(ITEMGUID_S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446,_Player);

// Ensure they move back to their starting position after running away from e.g. crimes so they can keep playing ball
PROC
PROC_TUT_InitPlayingBall()
AND
DB_TUT_LowerDeck_Child(_Child)
THEN
CharacterSetReactionPriority(_Child,"GoHome",1200);

QRY
QRY_TUT_LowerDeck_KidHasBall((CHARACTERGUID)_Kid)
AND
ItemIsInInventory(ITEMGUID_S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446,0)
AND
GetDistanceTo(_Kid,ITEMGUID_S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446,_Dist)
AND
_Dist < 0.3
THEN
DB_NOOP(1);

IF
CharacterCharacterEvent(_Child1,_Child2,"TUT_KickBall")
AND
NOT QRY_TUT_LowerDeck_KidHasBall(_Child1)
THEN
PROC_TUT_BallMissing();

IF
CharacterCharacterEvent(_Child1,_Child2,"TUT_KickBall")
AND
QRY_TUT_LowerDeck_KidHasBall(_Child1)
AND
GetPosition(_Child1,_X,_Y,_Z)
THEN
ObjectSetFlag(_Child1,"TUT_PlayingBall");
ObjectSetFlag(_Child2,"TUT_PlayingBall");
GlobalClearFlag("TUT_BallMissing");
// Prevent character script from trying to kick the ball while we are handling it
GlobalSetFlag("TUT_StoryBallKickHandling");
// Child1 looks at Child2 with its charscript
CharacterLookAt(_Child2,_Child1);
DB_TUT_Kickball(_Child1,_Child2);
// Wait for start of "kick" animation from charscript
ProcObjectTimer(_Child1,"TUT_KickBall",4300);

// Character got into dialog while it started kicking
IF
DialogStarted(_,_ID)
AND
DB_TUT_Kickball(_Child1,_Child2)
AND
DB_DialogNPCs(_ID,_Child1,_)
THEN
PROC_TUT_AbortOngoingKick();

// Player moved/picked up ball while it was on the ground
IF
ItemMoved(ITEMGUID_S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446)
AND
DB_TUT_Kickball(_Child1,_Child2)
THEN
PROC_TUT_BallMissing();

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Child1,"TUT_KickBall")
AND
DB_TUT_Kickball(_Child1,_Child2)
AND
GetVarFloat3(_Child1,"BallTargetPos",_X,_Y,_Z)
THEN
NOT DB_TUT_Kickball(_Child1,_Child2);
DB_TUT_BallKicked(_Child1,_Child2);
PlaySound(ITEMGUID_S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446,"TUT_Scripted_Kids_Ballplay_Kick");
ItemMoveToPosition(ITEMGUID_S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446,_X,_Y,_Z,4.5,-1.0,"",0);
ObjectClearFlag(_Child1,"TUT_PlayingBall");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Child1,"TUT_KickBall")
AND
DB_TUT_Kickball(_Child1,_Child2)
AND
Random(4,_Chance)
AND
_Chance == 0
THEN
Proc_StartDialog(1,"TUT_AD_LohseKidKickBall",_Child1);

IF
ItemMoved(ITEMGUID_S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446)
AND
DB_TUT_BallKicked(_Child1,_Child2)
THEN
NOT DB_TUT_BallKicked(_Child1,_Child2);
GlobalClearFlag("TUT_StoryBallKickHandling");
PROC_TUT_CheckBallCaught(_Child2);

// For sound effect only. The children check in their behaviour script whether the ball is
// close enough to kick it again
PROC
PROC_TUT_CheckBallCaught((CHARACTERGUID)_Child)
AND
ItemIsInInventory(ITEMGUID_S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446,0)
AND
GetDistanceTo(ITEMGUID_S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446,_Child,_Dist)
AND
_Dist < 0.5
THEN
PlaySound(ITEMGUID_S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446,"TUT_Scripted_Kids_Ballplay_Catch");

IF
ItemAddedToCharacter(ITEMGUID_S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446,_Player)
AND
DB_IsPlayer(_Player)
THEN
PROC_TUT_BallMissing();

PROC
PROC_TUT_AbortOngoingKick()
AND
DB_TUT_Kickball(_Child1,_Child2)
THEN
ObjectClearFlag(_Child1,"TUT_PlayingBall");
ObjectClearFlag(_Child2,"TUT_PlayingBall");
ProcObjectTimerCancel(_Child1,"TUT_KickBall");
NOT DB_TUT_Kickball(_Child1,_Child2);

PROC
PROC_TUT_AbortOngoingKick()
AND
DB_TUT_BallKicked(_Child1,_Child2)
THEN
NOT DB_TUT_BallKicked(_Child1,_Child2);

PROC
PROC_TUT_AbortOngoingKick()
THEN
GlobalClearFlag("TUT_StoryBallKickHandling");

PROC
PROC_TUT_BallMissing()
THEN
PROC_TUT_AbortOngoingKick();
GlobalSetFlag("TUT_BallMissing");

// Stop playing after Windego caused the explosions
IF
GlobalFlagSet("TUT_LowerDeck_WindegoHasCastSpell")
THEN
GlobalSetFlag("TUT_StoryBallKickHandling");
ItemClearOwner(ITEMGUID_S_TUT_ChildrenBall_04ba6013-fc72-47be-97b7-87a0d7fa8446);
GoalCompleted;

// For debugging
IF
GlobalFlagCleared("TUT_LowerDeck_WindegoHasCastSpell")
THEN
GlobalClearFlag("TUT_StoryBallKickHandling");

PROC
ProcRegionEnded("TUT_Tutorial_A")
AND
DB_TUT_LowerDeck_Child(_Child)
THEN
CharacterSetReactionPriority(_Child,"GoHome",0);

PROC
ProcRegionEnded("TUT_Tutorial_A")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "TUT_LowerDeck"
