Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Ghosts(CHARACTERGUID_S_RC_MIL_SariasGhost_09a853c2-cd62-4f7b-9e36-160b455ccaed, "RC_MIL_SariasGhost");

DB_ScorePersuasion_Dialog("RC_MIL_SariasRitual",5);

DB_RC_MIL_RitualParticipants((CHARACTERGUID)CHARACTERGUID_S_RC_MIL_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a);
DB_RC_MIL_RitualParticipants(CHARACTERGUID_S_RC_MIL_ElvenTank_21cf95b4-c4a7-4a54-9beb-71a9ba711aba);
DB_RC_MIL_RitualParticipants(CHARACTERGUID_S_RC_MIL_ElfShaman_ce67ffa8-7919-4ec3-8440-23ed94b7cf5b);
DB_RC_MIL_RitualParticipants(CHARACTERGUID_S_RC_MIL_ElvenTank2_55dc1982-2f24-4ece-a592-ad3dda6272cb);

CharacterSetHitpointsPercentage(CHARACTERGUID_S_RC_MIL_ElvenTank_21cf95b4-c4a7-4a54-9beb-71a9ba711aba, 80.0);
CharacterSetHitpointsPercentage(CHARACTERGUID_S_RC_MIL_ElvenTank2_55dc1982-2f24-4ece-a592-ad3dda6272cb, 80.0);

DB_QuestDef_State("RC_MIL_ElvenBurial","ArriveCamp", 1);
DB_QuestDef_State("RC_MIL_ElvenBurial","JoinRitual", 1);
DB_QuestDef_State("RC_MIL_ElvenBurial","DidnotParticipate",-1);

DB_QuestDef_UpdateEvent("RC_MIL_ElvenBurial", "AllowedCamp", "RC_MIL_AllowedInCamp");
DB_QuestDef_UpdateEvent("RC_MIL_ElvenBurial", "PerformedRitual_Well", "RC_MIL_PerformedRitual_Well");
DB_QuestDef_UpdateEvent("RC_MIL_ElvenBurial", "PerformedRitual_Mediocre", "RC_MIL_PerformedRitual_Mediocre");
DB_QuestDef_UpdateEvent("RC_MIL_ElvenBurial", "PerformedRitual_Poorly", "RC_MIL_PerformedRitual_Poorly");
DB_QuestDef_CloseEvent("RC_MIL_ElvenBurial", "RC_MIL_PerformedRitual_Well");
DB_QuestDef_CloseEvent("RC_MIL_ElvenBurial", "RC_MIL_PerformedRitual_Mediocre");
DB_QuestDef_CloseEvent("RC_MIL_ElvenBurial", "RC_MIL_PerformedRitual_Poorly");
DB_QuestDef_CloseEvent("RC_MIL_ElvenBurial", "RC_MIL_ElvenBurial_Close");
DB_QuestDef_State("RC_MIL_ElvenBurial", "TovahDead",-1);

DB_PossessedItem(ITEMGUID_S_RC_MIL_AncestorTree_ElfSiteTree_32ebb430-26c5-46ec-a9f0-97625f68cd26, ITEMGUID_S_RC_MIL_AncestorTree_ElfSiteTree_Ghost_b4472ca3-0c21-4dda-ba79-96b36a46fb9e, "RC_MIL_AncestorTree_ElfSiteTree", "RC_MIL_AncestorTree_SeeAura");
CharacterEquipItem(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, S_RC_MIL_SaheilaRewardSpear_590b4847-e424-45fc-b7b2-a9a2606b6888);
KBSECTION
// Traditional elves cannot be bribed
QRY
QRY_CrimeBribes_DisableBribe((INTEGER)_CrimeID,(CHARACTERGUID)_Interrogator,(CHARACTERGUID)_Criminal,(STRING)_Dialog)
AND
IsTagged(_Interrogator,"TRADIONAL_ELF",1)
THEN
DB_NOOP(1);

IF
StoryEvent(_Player, "RC_MIL_StartSariasRitual")
AND
GlobalGetFlag("RC_MIL_SariasRitualStarted", 0)
AND
NOT QRY_RC_MIL_TovahAddress_FarFromCorpse()
AND
NOT QRY_RC_MIL_TovahAddressesSebille(_Player)
AND
NOT QRY_StartDialog(0, "RC_MIL_SariasRitual", CHARACTERGUID_S_RC_MIL_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, ITEMGUID_S_RC_MIL_SariasCorpse_37211be0-ea49-4689-a517-1f7aa7d0bf27, _Player)
THEN
ObjectSetFlag(CHARACTERGUID_S_RC_MIL_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, "RestartSpotting", 0);

QRY
QRY_RC_MIL_TovahAddress_FarFromCorpse()
AND
GetDistanceTo(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, TRIGGERGUID_S_RC_MIL_SariasCorpseStartTrigger_a3be0dfb-47fd-48c3-8992-27c7b68c4057, _Dist)
AND
_Dist > 4.0
THEN
ObjectSetFlag(CHARACTERGUID_S_RC_MIL_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, "RestartSpotting", 0);

//REGION When Saheila returns, Sarias burial is canceled.
IF
DialogEnded("RC_MIL_TovahWithSaheila", _)
AND
GlobalGetFlag("RC_MIL_SariasRitualStarted", 0)
THEN
Proc_StartDialog(1, "RC_MIL_AD_BurySarias", CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a);
//END_REGION

//REGION Tovah prefers Sebille for the ritual.
QRY
QRY_RC_MIL_TovahAddressesSebille((CHARACTERGUID)_Player)
AND
CharacterIsInPartyWith(_Player, CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c, 1)
AND
IsTagged(CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c, "SEBILLE", 1) //Accounts for possibility of Sebille being disguised.
AND
CharacterCanSee(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c, 1)
AND
QRY_StartDialog(0,"RC_MIL_SariasRitual", CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, ITEMGUID_S_RC_MIL_SariasCorpse_37211be0-ea49-4689-a517-1f7aa7d0bf27, CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c)
THEN
DB_NOOP(1);
//END_REGION

PROC
Proc_DialogFlagSetup("RC_MIL_SariasRitual", _, _, _)
THEN
GlobalSetFlag("RC_MIL_SariasRitualStarted");

PROC
Proc_DialogFlagSetup("RC_MIL_AD_SariasRitual", _)
THEN
GlobalSetFlag("RC_MIL_SariasRitualStarted");

//REGION Animations during burial
IF
GlobalFlagSet("RC_MIL_ElvenPrayer")
AND
DB_RC_MIL_RitualParticipants(_Elf)
AND
QRY_RC_MIL_DoRitualEvent((CHARACTERGUID)_Elf)
THEN
CharacterLookAt(_Elf, ITEMGUID_S_RC_MIL_SariasCorpse_37211be0-ea49-4689-a517-1f7aa7d0bf27);
PlayAnimation(_Elf, "Pray_01");

IF
GlobalFlagSet("RC_MIL_ElvesBleed")
THEN
ProcCharacterMoveTo(CHARACTERGUID_S_RC_MIL_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, ITEMGUID_S_RC_MIL_SariasCorpse_37211be0-ea49-4689-a517-1f7aa7d0bf27, 0, "RC_MIL_BleedSarias");

IF
StoryEvent(CHARACTERGUID_S_RC_MIL_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, "RC_MIL_BleedSarias")
THEN
CreatePuddle(ITEMGUID_S_RC_MIL_SariasCorpse_37211be0-ea49-4689-a517-1f7aa7d0bf27, "SurfaceBlood", 10, 25, 2, 5, 0.2);

IF
StoryEvent(_Character, "RC_MIL_BleedSarias")
THEN
CharacterLookAt((CHARACTERGUID)_Character, ITEMGUID_S_RC_MIL_SariasCorpse_37211be0-ea49-4689-a517-1f7aa7d0bf27);
PlayAnimation(_Character, "Fidget_Low_02");
PlayEffect(ITEMGUID_S_RC_MIL_SariasCorpse_37211be0-ea49-4689-a517-1f7aa7d0bf27, "RS3_FX_GP_Combat_Hit_Blood_01_Small");

IF
GlobalFlagSet("RC_MIL_ElvesEat")
AND
DB_RC_MIL_RitualParticipants(_Elf)
AND
QRY_RC_MIL_DoRitualEvent(_Elf)
THEN
CharacterPurgeQueue(_Elf);
ProcCharacterMoveTo(_Elf, ITEMGUID_S_RC_MIL_SariasCorpse_37211be0-ea49-4689-a517-1f7aa7d0bf27, 0, "RC_MIL_EatSarias");

IF
StoryEvent(_Elf, "RC_MIL_EatSarias")
AND
Random(500, _Rand)
AND
IntegerSum(_Rand, 2500, _Timer)
THEN
CharacterPurgeQueue((CHARACTERGUID)_Elf);
CharacterLookAt(_Elf, ITEMGUID_S_RC_MIL_SariasCorpse_37211be0-ea49-4689-a517-1f7aa7d0bf27);
PlayAnimation(_Elf, "use_loot");
ProcObjectTimer(_Elf, "RC_MIL_EatSarias", _Timer);

PROC
ProcObjectTimerFinished(_Character, "RC_MIL_EatSarias")
THEN
ProcClearStoryMove((CHARACTERGUID)_Character);
PlayAnimation(_Character, "use_eat");
PlayEffect(_Character, "RS3_FX_GP_Combat_Hit_Blood_01_Medium");

IF
GlobalFlagSet("RC_MIL_BurySarias")
THEN
CharacterMoveItemToTrigger(CHARACTERGUID_S_RC_MIL_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, ITEMGUID_S_RC_MIL_SariasCorpse_37211be0-ea49-4689-a517-1f7aa7d0bf27, TRIGGERGUID_S_RC_MIL_SariasBuriedTrigger_349f907c-4c7a-46d3-b4f1-9078ac829234, 1, "RC_MIL_SariasCorpseBuried");

IF
StoryEvent(_, "RC_MIL_SariasCorpseBuried")
THEN
ItemSetCanMove(ITEMGUID_S_RC_MIL_SariasCorpse_37211be0-ea49-4689-a517-1f7aa7d0bf27, 0);

QRY
QRY_RC_MIL_DoRitualEvent((CHARACTERGUID)_Elf)
AND
QRY_SpeakerIsAvailable(_Elf)
THEN
DB_NOOP(1);

QRY
QRY_RC_MIL_DoRitualEvent(CHARACTERGUID_S_RC_MIL_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a)
THEN
DB_NOOP(1);
//END_REGION

//REGION Player behaviour during burial
IF
ObjectFlagSet("RC_MIL_ElvesBleed", _Player, _)
THEN
CharacterPurgeQueue((CHARACTERGUID)_Player);
CharacterMoveTo((CHARACTERGUID)_Player, ITEMGUID_S_RC_MIL_SariasCorpse_37211be0-ea49-4689-a517-1f7aa7d0bf27, 0, "RC_MIL_BleedSarias", 0);

IF
DialogEnded("RC_MIL_SariasRitual", _ID)
AND
DialogGetInvolvedPlayer(_ID, 1, _Player)
THEN
CharacterPurgeQueue((CHARACTERGUID)_Player);
ProcObjectTimerCancel(_Player, "RC_MIL_EatSarias");

IF
ObjectFlagSet("RC_MIL_ElvenPrayer", _Player, _)
THEN
PlayAnimation(_Player, "Pray_01");

IF
ObjectFlagSet("RC_MIL_ElvesEat", _Player, _)
THEN
CharacterMoveTo((CHARACTERGUID)_Player, ITEMGUID_S_RC_MIL_SariasCorpse_37211be0-ea49-4689-a517-1f7aa7d0bf27, 0, "RC_MIL_EatSarias", 0);

IF
DialogEnded("RC_MIL_SariasRitual", _ID)
AND
DialogGetInvolvedPlayer(_ID, 1, _Player)
THEN
CharacterPurgeQueue((CHARACTERGUID)_Player);
//END_REGION

//REGION Elves can be spoken to after ritual
IF
DialogEnded("RC_MIL_SariasRitual", _)
THEN
PROC_RC_MIL_AfterRitualDialogs();

IF
AutomatedDialogEnded("RC_MIL_AD_BurySarias", _)
THEN
PROC_RC_MIL_AfterRitualDialogs();

PROC
PROC_RC_MIL_AfterRitualDialogs()
AND
GlobalGetFlag("RC_MIL_TovahElvesCanLeave", 0)
THEN
DB_Dialogs(S_RC_MIL_ElfShaman_ce67ffa8-7919-4ec3-8440-23ed94b7cf5b, "RC_MIL_ElfShaman");
DB_Dialogs(S_RC_MIL_ElvenTank_21cf95b4-c4a7-4a54-9beb-71a9ba711aba, "RC_MIL_ElvenTank");
DB_Dialogs(CHARACTERGUID_S_RC_MIL_ElvenTank2_55dc1982-2f24-4ece-a592-ad3dda6272cb, "RC_MIL_ElvenTank2");


PROC
PROC_RC_MIL_AfterRitualDialogs()
AND
GlobalGetFlag("RC_MIL_SaheilaDeadAtMillStart", 0)
AND
GlobalGetFlag("RC_MIL_TovahElvesCanLeave", 0)
AND
GlobalGetFlag("RC_MIL_SaheilaIsAtCamp", 0)
THEN
DB_Dialogs(CHARACTERGUID_S_RC_MIL_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, "RC_MIL_TovahWithSaheila");

PROC
PROC_RC_MIL_AfterRitualDialogs()
AND
GlobalGetFlag("RC_MIL_SaheilaDeadAtMillStart", 1)
AND
GlobalGetFlag("RC_MIL_TovahElvesCanLeave", 0)
THEN
DB_Dialogs(CHARACTERGUID_S_RC_MIL_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, "RC_MIL_TovahNoSaheila");
//END_REGION

//REGION Eating part of Sarias
IF
DialogEnded("RC_MIL_SariasRitual", _ID)
AND
DialogGetInvolvedPlayer(_ID, 1, _Player)
AND
ObjectGetFlag(_Player, "RC_MIL_ElvesEat", 1)
AND
QRY_RC_MIL_CanEatCorpse(_Player)
AND
CharacterConsume((CHARACTERGUID)_Player, "CON_BodyPart_Heal", _)
THEN
DB_NOOP(1);

IF
DialogEnded("RC_MIL_SariasRitual", _ID)
AND
DialogGetInvolvedPlayer(_ID, 1, _Player)
AND
ObjectGetFlag(_Player, "RC_MIL_ElvesEat", 1)
AND
NOT QRY_RC_MIL_CanEatCorpse(_Player)
AND
CharacterConsume((CHARACTERGUID)_Player, "CON_BodyPart_Harm", _)
THEN
DB_NOOP(1);

QRY
QRY_RC_MIL_CanEatCorpse((GUIDSTRING)_Player)
AND
IsTagged(_Player, "ELF", 1)
THEN
DB_NOOP(1);

QRY
QRY_RC_MIL_CanEatCorpse((GUIDSTRING)_Player)
AND
IsTagged(_Player, "UNDEAD", 1)
THEN
DB_NOOP(1);
//END_REGION

//REGION Journal closing if the ritual is already done when player is allowed in.
IF
ObjectFlagSet("RC_MIL_AllowedInCamp", _Player, _)
AND
GlobalGetFlag("RC_MIL_BurySarias", 1)
THEN
ObjectSetFlag(_Player,"RC_MIL_ElvenBurial_Close");

//END_REGION

//REGION Sarias ghost
IF
GlobalFlagSet("RC_MIL_BurySarias")
THEN
DialogRequestStop(CHARACTERGUID_S_RC_MIL_SariasGhost_09a853c2-cd62-4f7b-9e36-160b455ccaed);
DB_DoNotFace(CHARACTERGUID_S_RC_MIL_SariasGhost_09a853c2-cd62-4f7b-9e36-160b455ccaed);
SetOnStage(S_RC_MIL_SariasGraveBlocker_d2d8ec58-5d34-48a0-bfa9-8bc065d0d428, 0);
SetOnStage(CHARACTERGUID_S_RC_MIL_SariasGhost_09a853c2-cd62-4f7b-9e36-160b455ccaed, 0);
TeleportTo(CHARACTERGUID_S_RC_MIL_SariasGhost_09a853c2-cd62-4f7b-9e36-160b455ccaed, S_RC_MIL_SariasGhostTrigger_259f08d1-dfa8-45c5-9dc4-26f0d87ad199, "", 0);
CharacterLookFromTrigger(CHARACTERGUID_S_RC_MIL_SariasGhost_09a853c2-cd62-4f7b-9e36-160b455ccaed, S_RC_MIL_SariasGhostTrigger_259f08d1-dfa8-45c5-9dc4-26f0d87ad199, 1);
TimerLaunch("RC_MIL_SariasAtTreeSpot", 300);


IF
TimerFinished("RC_MIL_SariasAtTreeSpot")
THEN
CharacterAppear(CHARACTERGUID_S_RC_MIL_SariasGhost_09a853c2-cd62-4f7b-9e36-160b455ccaed, 1, "");
//END_REGION

IF
CharacterDied(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a)
THEN
ProcSetFlagOnAll("QuestUpdate_RC_MIL_ElvenBurial_TovahDead");

//REGION Tovah wields the spear during the ritual.
IF
ItemRemovedFromCharacter(S_RC_MIL_SaheilaRewardSpear_590b4847-e424-45fc-b7b2-a9a2606b6888, CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a)
AND
CharacterIsDead(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, 0)
AND
GetItemForItemTemplateInInventory(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, "WPN_Elves_Staff_2H_A_30f84930-88f0-48dd-8ab7-1bb6fe0b7437", _Staff)
AND
QueryOnlyOnce("RC_MIL_TovahEquipStaff")
THEN
CharacterEquipItem(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, _Staff);

IF
GlobalFlagSet("RC_MIL_BurySarias")
AND
GetItemForItemTemplateInInventory(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, "WPN_Elves_Staff_2H_A_30f84930-88f0-48dd-8ab7-1bb6fe0b7437", _Staff)
AND
QueryOnlyOnce("RC_MIL_TovahEquipStaff")
THEN
CharacterEquipItem(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, _Staff);

IF
ObjectEnteredCombat(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, _)
AND
CharacterGetEquippedWeapon(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, S_RC_MIL_SaheilaRewardSpear_590b4847-e424-45fc-b7b2-a9a2606b6888)
AND
GetItemForItemTemplateInInventory(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, "WPN_Elves_Staff_2H_A_30f84930-88f0-48dd-8ab7-1bb6fe0b7437", _Staff)
THEN
CharacterEquipItem(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, _Staff);

IF
CharacterDied(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a)
AND
CharacterGetEquippedWeapon(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, S_RC_MIL_SaheilaRewardSpear_590b4847-e424-45fc-b7b2-a9a2606b6888)
THEN
CharacterUnequipItem(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, S_RC_MIL_SaheilaRewardSpear_590b4847-e424-45fc-b7b2-a9a2606b6888);
//END_REGION

IF
ObjectEnteredCombat(_Elf, _)
AND
DB_RC_MIL_RitualParticipants((CHARACTERGUID)_Elf)
AND
DB_RC_MIL_RitualParticipants((CHARACTERGUID)_OtherElf)
AND
CharacterCanSee(_OtherElf, _Elf, 1)
THEN
ProcForceStopDialog(_OtherElf);

IF
CharacterDying(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a)
AND
CharacterCanSee(CHARACTERGUID_S_RC_MIL_ElfShaman_ce67ffa8-7919-4ec3-8440-23ed94b7cf5b, CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, 1)
AND
QueryOnlyOnce("RC_MIL_AD_TovahDying")
THEN
CharacterLookAt(CHARACTERGUID_S_RC_MIL_ElfShaman_ce67ffa8-7919-4ec3-8440-23ed94b7cf5b, CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a);
Proc_StartDialog(1, "RC_MIL_AD_TovahDying", CHARACTERGUID_S_RC_MIL_ElfShaman_ce67ffa8-7919-4ec3-8440-23ed94b7cf5b);

IF
CharacterDying(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a)
AND
DB_CombatCharacters(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, _ID)
AND
DB_CombatCharacters(CHARACTERGUID_S_RC_MIL_ElfShaman_ce67ffa8-7919-4ec3-8440-23ed94b7cf5b, _ID)
AND
QueryOnlyOnce("RC_MIL_AD_TovahDying")
THEN
CharacterLookAt(CHARACTERGUID_S_RC_MIL_ElfShaman_ce67ffa8-7919-4ec3-8440-23ed94b7cf5b, CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a);
Proc_StartDialog(1, "RC_MIL_AD_TovahDying", CHARACTERGUID_S_RC_MIL_ElfShaman_ce67ffa8-7919-4ec3-8440-23ed94b7cf5b);


IF
DB_OnlyOnce("RC_MIL_FreeSaheila")
AND
NOT DB_GlobalFlag("RC_MIL_SariasRitualStarted")
AND
QRY_SpeakerIsAvailable(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a)
THEN
Proc_StartDialog(1, "RC_MIL_AD_BurySarias", CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a);
SetHasDialog(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, 0);


IF
ObjectLeftCombat(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, _)
AND
DB_OnlyOnce("RC_MIL_FreeSaheila")
AND
NOT DB_GlobalFlag("RC_MIL_SariasRitualStarted")
AND
QRY_SpeakerIsAvailable(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a)
THEN
Proc_StartDialog(1, "RC_MIL_AD_BurySarias", CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a);
SetHasDialog(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, 0);

IF
DialogEnded(_, _ID)
AND
DB_DialogNPCs(_, CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, _)
AND
DB_OnlyOnce("RC_MIL_FreeSaheila")
AND
NOT DB_GlobalFlag("RC_MIL_SariasRitualStarted")
AND
QRY_SpeakerIsAvailable(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a)
THEN
Proc_StartDialog(1, "RC_MIL_AD_BurySarias", CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a);
SetHasDialog(CHARACTERGUID_S_GLO_Tovah_69c31947-b004-44aa-b856-2a9a96ff218a, 0);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
