Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_RC_TenebriumChest((ITEMGUID)ITEMGUID_S_RC_TenebriumChestRyker_c551014b-1194-4c40-a014-23669ce6bb19);
DB_RC_TenebriumChest(ITEMGUID_S_RC_TenebriumChestArchive_ca4cfb15-a9e2-411b-823f-b6221013f2ea);

// Call this proc to setup disabled pyramids, cause they are added through Sidebar and the above databases are not filled when the events are fired
Proc_RC_CheckTenebriumChests();
KBSECTION
PROC
Proc_RC_CheckTenebriumChests()
AND
DB_TeleporterPyramid(_Pyramid)
AND
GetInventoryOwner(_Pyramid,_TenebriumChest)
AND
DB_RC_TenebriumChest((ITEMGUID)_TenebriumChest)
THEN
Proc_PyramidCustomBlockAdd(_Pyramid,"RC_TenebriumChest");



IF
DB_RC_TenebriumChest(_Chest)
THEN
ItemClearOwner(_Chest);

IF
CharacterUsedItem(_Player,_TenebriumChest)
AND
DB_RC_TenebriumChest(_TenebriumChest)
THEN
Proc_RC_TenebriumChestDrain(_Player);

IF
AttackedByObject((ITEMGUID)_TenebriumChest,(CHARACTERGUID)_Player,_,_,_)
AND
DB_IsPlayer(_Player)
AND
DB_RC_TenebriumChest(_TenebriumChest)
THEN
Proc_RC_TenebriumChestDrain(_Player);

PROC
Proc_RC_TenebriumChestDrain((CHARACTERGUID)_Player)
THEN
UserSetFlag(_Player,"GLO_UsedTenebriumChest");
PlayEffect(_Player,"RS3_FX_GP_ScriptedEvent_Source_Absorb_01");
CharacterAddSourcePoints(_Player,-1);
StartVoiceBark("RC_GLO_VB_TenebriumChest",_Player);


// Rely on the fact that tenebrium chests are immovable.
IF
ItemAddedToContainer(_Pyramid,_Container)
AND
DB_TeleporterPyramid(_Pyramid)
AND
GetInventoryOwner(_Pyramid,_TenebriumChest)
AND
DB_RC_TenebriumChest((ITEMGUID)_TenebriumChest)
THEN
Proc_PyramidCustomBlockAdd(_Pyramid,"RC_TenebriumChest");

IF
ItemAddedToContainer(_Pyramid,_Container)
AND
DB_TeleporterPyramid(_Pyramid)
AND
GetInventoryOwner(_Pyramid,_TopCont)
AND
NOT DB_RC_TenebriumChest((ITEMGUID)_TopCont)
AND
DB_PyramidCustomBlock(_Pyramid,"RC_TenebriumChest")
THEN
Proc_PyramidCustomBlockRemove(_Pyramid,"RC_TenebriumChest");

IF
ItemRemovedFromContainer(_Pyramid,_TenebriumChest)
AND
DB_RC_TenebriumChest(_TenebriumChest)
AND
DB_TeleporterPyramid(_Pyramid)
THEN
Proc_PyramidCustomBlockRemove(_Pyramid,"RC_TenebriumChest");

IF
ItemAddedToCharacter(_Pyramid,_Char)
AND
DB_PyramidCustomBlock(_Pyramid,"RC_TenebriumChest")
THEN
Proc_PyramidCustomBlockRemove(_Pyramid,"RC_TenebriumChest");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
