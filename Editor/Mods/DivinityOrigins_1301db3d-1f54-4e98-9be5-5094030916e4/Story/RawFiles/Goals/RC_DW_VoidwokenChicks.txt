Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_Dialogs(S_RC_DW_Chicken_001_9053f0be-7d06-4e88-ab20-f411e2e790d0,S_RC_DW_Chicken_002_d8699c1d-3ca1-444b-b985-95de1dc8c708,S_RC_DW_Chicken_003_bcd34f89-8ff2-4d94-8c30-fe09bdbc1163,"RC_DW_VC_ChickenNormal");
DB_RC_DW_VC_Chickens((CHARACTERGUID)S_RC_DW_VC_Chicken_001_9053f0be-7d06-4e88-ab20-f411e2e790d0,"RC_DW_VC_Chicken1","RC_DW_VC_AD_Chicken1",0,(TRIGGERGUID)S_RC_DW_VC_Chicken01_DeadTrigger_51b9f0bc-04bf-4f68-9e0d-43058d4a4580); //Last int is if sitting on egg
DB_RC_DW_VC_Chickens(S_RC_DW_VC_Chicken_002_d8699c1d-3ca1-444b-b985-95de1dc8c708,"RC_DW_VC_Chicken2","RC_DW_VC_AD_Chicken2",0,S_RC_DW_VC_Chicken02_DeadTrigger_629dcb32-e251-48b8-8a33-e710dda8c3fc);
DB_RC_DW_VC_Chickens(S_RC_DW_VC_Chicken_003_bcd34f89-8ff2-4d94-8c30-fe09bdbc1163,"RC_DW_VC_Chicken3","RC_DW_VC_AD_Chicken3",0,S_RC_DW_VC_Chicken03_DeadTrigger_72400c76-d222-4996-bef1-b787f449fcc2);
DB_RC_DW_VC_Chickens(S_RC_DW_VC_Chicken_004_66188471-2b54-4f53-8efa-8278125e2c60,"RC_DW_VC_Chicken4","RC_DW_VC_AD_Chicken4",0,S_RC_DW_VC_Chicken04_DeadTrigger_7d824686-06ce-472c-a4ce-cd85300f57b6);
DB_RC_DW_VC_Chickens(S_RC_DW_VC_Chicken_Scared_81ef9840-7b6e-4d3d-9b60-4c7ed7a286a7,"RC_DW_VC_ChickenScared","RC_DW_VC_AD_ChickenScared",0,S_RC_DW_VC_ScaredChicken_DeadTrigger_65e32575-6cd8-44dc-9af3-ded6982d034f);

//Hatch Trigger
DB_OneShotPlayerTrigger(S_RC_DW_VC_HatchTrigger_9cacc4e1-9aae-4347-97b2-0cf0d4c7dc2e);

//Item event
//DB_GiveItemToEvent DOES NOT NEED GIVE JUST SET THE EGG IN THE WORLD ON QUEST FLAG
DB_HasStoryEvent(S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa, "RC_DW_VC_HasHelplessEgg");

//Ghost
DB_Ghosts(S_RC_DW_VC_Chicken_Scared_81ef9840-7b6e-4d3d-9b60-4c7ed7a286a7,S_RC_DW_VC_Chicken_Scared_Ghost_1e3a6b3d-84cf-4458-b741-71df3c19614b,"RC_DW_VC_ChickenScared_Ghost");

//Rooster
SetHasDialog(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0,1);

//Voidwoken Chick
DB_Dialogs(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"RC_DW_VC_VoidwokenChick");
SetOnStage(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,0);

//City chickens
DB_Dialogs(S_RC_DW_VC_CityChicken_01_f4fae5ab-639e-4bad-890e-abc2c86f1293,"RC_DW_VC_CityChicken1");
DB_Dialogs(S_RC_DW_VC_CityChicken_02_3fd71e16-04bc-4943-9c59-525f709f150a,"RC_DW_VC_CityChicken2");

//City chickens grave
DB_ShovelArea(S_RC_DW_VC_CatGraveTrigger_3f162e90-eb5a-428b-8c09-770a4def9e8c,"RC_WC_VC_DiggingUpCatGrave",S_RC_DW_VC_CatGrave_49d3f4d6-0ed2-4f84-88e5-c5933bebca99);
DB_ShovelRewardItemScatter("RC_WC_VC_DiggingUpCatGrave",CHARACTERGUID_S_RC_DW_VC_CatSkeleton_5f1081e7-c93b-404a-aa49-3717cf12b10f);
//SetOnStage(S_RC_DW_VC_CatSkeleton_5f1081e7-c93b-404a-aa49-3717cf12b10f,0);

//Chicken reward dirt
DB_ShovelArea(S_RC_DW_VC_ChickenRewardDigTrigger_d372f610-49e8-4a2d-863a-8a6cae89ff93,"RC_WC_VC_DugChickenReward",S_RC_DW_VC_ReturnEggReward_2d1e494d-d11c-4370-beed-a26c9427d903);
DB_ShovelRewardItemAppear("RC_WC_VC_DugChickenReward",(ITEMGUID)S_RC_DW_VC_BroughtEggReward_8804b6ed-3fca-4767-b0df-cfc431921625,(TRIGGERGUID)S_RC_DW_VC_BroughtEggReward_DestTrigger_204241f2-e2c8-434a-affa-52982c8a6cf7);


//Rooster check chick trigger
TriggerRegisterForCharacter(S_RC_DW_VC_RoosterCheckChickTrigger_33774bdb-125d-4bbf-93fe-542950abba31,S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d);

//Rooster reward Chest
SetOnStage(S_RC_DW_VC_MagicRoosterChest_f93a85e3-7588-4e0f-a1af-d1e9c63f6a87,0);

//Rooster Teleport Triggers 
DB_RC_DW_VC_TeleportTriggers((TRIGGERGUID)S_RC_DW_VC_RoosterTpLocationA_e55fb9e8-b104-4004-aabf-b486cf806cd9,0); 
DB_RC_DW_VC_TeleportTriggers(S_RC_DW_VC_RoosterTpLocationB_35888442-6c8a-4333-b5ba-a420d92cfa0a,1);
DB_RC_DW_VC_TeleportTriggers(S_RC_DW_VC_RoosterTpLocationC_98adfa0c-910c-4f36-8e5a-54db1b26f7e8,2);
DB_RC_DW_VC_TeleportTriggers(S_RC_DW_VC_RoosterTpLocationD_b61c6fc6-dcfd-4921-b44c-2a3f0078250f,3);


//Disable default logics chick and rooster
DB_BlockThreatenedDialog(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d);
CharacterDisableAllCrimes(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d);
CharacterDisableAllCrimes(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0);
DB_BlockThreatenedDialog(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0);

//Coops
ItemSetCanInteract(S_RC_DW_VC_HenHouse_PanicChicken_4f5ebd83-b09f-4eb9-bf42-9dfb55dc0b24,1);
ItemSetCanInteract(S_RC_DW_VC_HenHouse_CityChicken_5f73cf44-c3a4-4ffd-9ccf-27dae3741ad9,1);

DB_RC_DW_VC_CorruptedEggs(S_RC_DW_VC_CorruptedEgg_006_00f6c493-5a90-4802-b9d1-f67950abb75a);
DB_RC_DW_VC_CorruptedEggs(S_RC_DW_VC_CorruptedEgg_000_af56b59a-f12c-4707-a607-4a23ca645579);
DB_RC_DW_VC_CorruptedEggs(S_RC_DW_VC_CorruptedEgg_001_39b00143-aa44-4ce9-974c-4c3338e3ca8f);
DB_RC_DW_VC_CorruptedEggs(S_RC_DW_VC_CorruptedEgg_003_c5979e7c-e425-418c-98de-bcb579c2e86e);
DB_RC_DW_VC_CorruptedEggs(S_RC_DW_VC_CorruptedEgg_004_6d76f2ce-b43d-4e27-bf74-725cac3fed4b);
DB_RC_DW_VC_CorruptedEggs(S_RC_DW_VC_CorruptedEgg_002_04342d92-a424-47b0-b2a0-0bfe7d232cab);

DB_Followers(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"RC_DW_VC_IsOwnerOfChick");

DB_IgnoreAssault(CHARACTERGUID_S_GLO_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d);
DB_DontCreateMurder(CHARACTERGUID_S_GLO_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d);

//Init
Proc_RC_DW_VC_Init();
KBSECTION
//REGION Debug
/*
IF
ItemEnteredTrigger(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,TRIGGERGUID_S_RC_DW_VC_NestArea_425c31d0-77d0-4020-a9e4-2d8a338f609f,_)
AND
CharacterGetHostCharacter(_Player)
THEN
DebugText(_Player,"Triggered entry");

IF
ItemLeftTrigger(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,TRIGGERGUID_S_RC_DW_VC_NestArea_425c31d0-77d0-4020-a9e4-2d8a338f609f,_)
AND
CharacterGetHostCharacter(_Player)
THEN
DebugText(_Player,"Triggered exit");
*/

IF
TextEventSet("rc_dw_vc_getitemtoinventory")
AND
CharacterGetHostCharacter(_Player)
THEN
ItemToInventory(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,_Player);

IF
TextEventSet("rc_dw_vc_teleportto")
THEN
TeleportTo(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,TRIGGERGUID_S_RC_DW_VC_HelplessEggLocation_92d4c501-c342-4e25-b08e-32fb7d396ee9,"ThisEventWillNeverTrigger",0);

IF
TextEventSet("rc_dw_vc_dragto")
THEN
ItemDragToTrigger(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,TRIGGERGUID_S_RC_DW_VC_HelplessEggLocation_92d4c501-c342-4e25-b08e-32fb7d396ee9);

IF
StoryEvent(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,"ThisEventWillNeverTrigger")
THEN
DebugBreak("ThisEventWillNeverTrigger has triggered, DOSTWO-9488 is fixed!");

IF
TextEventSet("rc_dw_vc_dropteleport")
AND
CharacterGetHostCharacter(_Player)
THEN
ItemToInventory(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,_Player);
ItemDrop(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa);
// This teleport won't do anything
TeleportTo(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,TRIGGERGUID_S_RC_DW_VC_HelplessEggLocation_92d4c501-c342-4e25-b08e-32fb7d396ee9);

IF
TextEventSet("rc_dw_vc_dropdrag")
AND
CharacterGetHostCharacter(_Player)
THEN
ItemToInventory(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,_Player);
ItemDrop(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa);
// This drag will do something, but the egg won't end up at the trigger
ItemDragToTrigger(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,TRIGGERGUID_S_RC_DW_VC_HelplessEggLocation_92d4c501-c342-4e25-b08e-32fb7d396ee9);

IF
TextEventSet("rc_dw_vc_transform_peeper")
AND
ObjectIsOnStage(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,0)
THEN
SetOnStage(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,1);

IF
TextEventSet("rc_dw_vc_transform_peeper")
AND
CharacterGetHostCharacter(_Player)
THEN
TeleportTo(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,_Player);
Transform(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"QUEST_RC_DW_VC_SmallInsect_f01630e8-de8f-471a-9c36-007d4f50186e",0,1);

IF
TextEventSet("chickorama")
AND
ObjectIsOnStage(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,0)
THEN
SetOnStage(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,1);

IF
TextEventSet("chickorama")
AND
CharacterGetHostCharacter(_Player)
THEN
TeleportTo(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,_Player);
ObjectSetFlag(_Player,"RC_DW_VC_DontKillPeeper");
Proc_RC_DW_VC_PuttingDownPeeper(_Player);


//REGION Moving an item after dropping it (at various stages)

// 1. Drop the item after adding it to the inventory (common)
// Note that ItemToInventory() does not result in an ItemMoved() event
// (if that were to be changed, these tests will fail to function as
//  designed in case the ItemMoved() event gets triggered after the
//  ItemAddedToCharacter() event)
IF
ItemAddedToCharacter(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,_)
AND
DB_RC_DW_VC_DebugDropAndMove(1)
THEN
NOT DB_RC_DW_VC_DebugDropAndMove(1);
ItemDrop(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa);

PROC
PROC_RC_DW_VC_DebugAddEggToInventoryIfNeededAndDropIt((CHARACTERGUID)_Player)
AND
ItemIsInCharacterInventory(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,_Player,0)
THEN
DB_RC_DW_VC_DebugDropAndMove(1);
ItemToInventory(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,_Player);
// Event above will drop it once added

PROC
PROC_RC_DW_VC_DebugAddEggToInventoryIfNeededAndDropIt(_Player)
AND
ItemIsInCharacterInventory(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,_Player,1)
THEN
ItemDrop(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa);

// 2. The various tests (each in their own region)

//REGION Teleport after itemdropped event
IF
TextEventSet("rc_dw_vc_droppedteleport")
AND
CharacterGetHostCharacter(_Player)
THEN
DB_RC_DW_VC_DebugDroppedTeleport(1);
PROC_RC_DW_VC_DebugAddEggToInventoryIfNeededAndDropIt(_Player);

IF
ItemDropped(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa)
AND
DB_RC_DW_VC_DebugDroppedTeleport(1)
THEN
NOT DB_RC_DW_VC_DebugDroppedTeleport(1);
// This teleport won't do anything
TeleportTo(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,TRIGGERGUID_S_RC_DW_VC_HelplessEggLocation_92d4c501-c342-4e25-b08e-32fb7d396ee9);
//END_REGION

//REGION Drag after itemdropped event
IF
TextEventSet("rc_dw_vc_droppeddrag")
AND
CharacterGetHostCharacter(_Player)
AND
GetPosition(TRIGGERGUID_S_RC_DW_VC_HelplessEggLocation_92d4c501-c342-4e25-b08e-32fb7d396ee9,_X,_Y,_Z)
THEN
DB_RC_DW_VC_DebugDroppedDrag(1);
PROC_RC_DW_VC_DebugAddEggToInventoryIfNeededAndDropIt(_Player);

IF
ItemDropped(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa)
AND
DB_RC_DW_VC_DebugDroppedDrag(1)
THEN
NOT DB_RC_DW_VC_DebugDroppedDrag(1);
// This drag will do something, but the egg won't end up at the trigger
ItemDragToTrigger(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,TRIGGERGUID_S_RC_DW_VC_HelplessEggLocation_92d4c501-c342-4e25-b08e-32fb7d396ee9);
//END_REGION

//REGION Teleport after itemmoved event
IF
TextEventSet("rc_dw_vc_movedteleport")
AND
CharacterGetHostCharacter(_Player)
THEN
DB_RC_DW_VC_DebugMovedTeleport(1);
PROC_RC_DW_VC_DebugAddEggToInventoryIfNeededAndDropIt(_Player);

IF
ItemMoved(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa)
AND
DB_RC_DW_VC_DebugMovedTeleport(1)
THEN
NOT DB_RC_DW_VC_DebugMovedTeleport(1);
// This teleport will usually work (but not always)
TeleportTo(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,TRIGGERGUID_S_RC_DW_VC_HelplessEggLocation_92d4c501-c342-4e25-b08e-32fb7d396ee9);
//END_REGION

//REGION Drag after itemmoved event
IF
TextEventSet("rc_dw_vc_moveddrag")
AND
CharacterGetHostCharacter(_Player)
THEN
DB_RC_DW_VC_DebugMovedDrag(1);
PROC_RC_DW_VC_DebugAddEggToInventoryIfNeededAndDropIt(_Player);

IF
ItemMoved(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa)
AND
DB_RC_DW_VC_DebugMovedDrag(1)
THEN
NOT DB_RC_DW_VC_DebugMovedDrag(1);
// This drag will do something, but the egg won't end up at the trigger
ItemDragToTrigger(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,TRIGGERGUID_S_RC_DW_VC_HelplessEggLocation_92d4c501-c342-4e25-b08e-32fb7d396ee9);
//END_REGION

//END_REGION

//END_REGION

//REGION On Init
PROC
Proc_RC_DW_VC_Init()
AND
DB_RC_DW_VC_Chickens(_chicken,_dialog,_,_,_)
THEN
DB_Dialogs(_chicken,(STRING)_dialog);

PROC
Proc_RC_DW_VC_Init()
AND
DB_RC_DW_VC_CorruptedEggs(_corruptedEgg)
THEN
SetCombatGroupID(_corruptedEgg,"RC_DW_VC_HatchedVoidwoken");

PROC
Proc_RC_DW_VC_Init()
THEN
SetOnStage(S_RC_DW_VC_EggThiefVoidwoken_059fbb9c-0967-481c-a732-faacf13ab9a6,0);
SetCombatGroupID(CHARACTERGUID_S_RC_DW_VC_EggThiefVoidwoken_059fbb9c-0967-481c-a732-faacf13ab9a6,"RC_DW_VC_HatchedVoidwoken");

PROC
Proc_RC_DW_VC_Init()
AND
SysCount("DB_RC_DW_VC_CorruptedEggs",1,_EggCount)
THEN
ProcDeclareCounter("RC_DW_VC_CorruptedEggs");
ProcIncreaseCounter("RC_DW_VC_CorruptedEggs",_EggCount);
//END_REGION

//REGION On Chicken Death
IF
DB_Dead(S_RC_DW_VC_Chicken_Scared_81ef9840-7b6e-4d3d-9b60-4c7ed7a286a7)
THEN
GlobalSetFlag("RC_DW_VC_ScaredChickenDead");


//Current Brooding chicken died

// 1) chicken was still walking to/putting the egg in the nest -> panic
//    (otherwise we have to make the new hatching chicken pick up the egg,
//     deal with the fact that a player could pick up or destroy the egg
//     before the new chicken gets to it, ...)
IF
DB_Dead(_chicken)
AND
DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,1,_trigger)
AND
GlobalGetFlag("RC_DW_VC_ReturnedEggGone",0)
AND
DB_RC_DW_VC_DroppingEgg(_chicken,_)
THEN
// Not 100% true, but close enough
GlobalSetFlag("RC_DW_VC_ReturnedEggGone");

// 2) the chicken was hatching the egg and the egg was not stolen from under it -> new hatching chicken
IF
DB_Dead(_chicken)
AND
DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,1,_trigger)
AND
// only assign a new chicken to hatch the egg if the egg was not stolen from under the previous chicken
GlobalGetFlag("RC_DW_VC_ReturnedEggGone",0)
THEN
Proc_RC_DW_VC_SetNewBroodChicken();

// 3) the chicken was hatching the egg and the egg was stolen from under it -> the others will now notice that the egg is gone
IF
DB_Dead(_chicken)
AND
DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,1,_trigger)
AND
GlobalGetFlag("RC_DW_VC_ReturnedEggGone",1)
THEN
GlobalSetFlag("RC_DW_VC_ChickensAreUpset");

// The dead chicken is no longer hatching the egg
IF
DB_Dead(_chicken)
AND
DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,1,_trigger)
THEN
NOT DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,1,_trigger);
DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,0,_trigger);

//Set new Brood Chicken
PROC
Proc_RC_DW_VC_SetNewBroodChicken()
AND
GlobalGetFlag("RC_DW_VC_EggWasReturned",1)
AND
DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,0,_trigger)
AND
CharacterIsDead((CHARACTERGUID)_chicken,0)
AND
NOT DB_RC_DW_VC_DoneOnce(1)
THEN
DB_RC_DW_VC_DoneOnce(1);
NOT DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,0,_trigger);
DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,1,_trigger);
DB_RC_DW_VC_DroppingEgg(_chicken,"0_GoingToEggAsNewHatchingChicken");
ProcCharacterMoveTo(_chicken,TRIGGERGUID_S_RC_DW_VC_HelplessEggLocation_92d4c501-c342-4e25-b08e-32fb7d396ee9,0,"RC_DW_VC_ChickenWalkToEgg");
CharacterSetReactionPriority(_chicken,"DefaultWanderInTrigger",0);


//Delete DB
PROC
Proc_RC_DW_VC_SetNewBroodChicken()
AND
DB_RC_DW_VC_DoneOnce(_int)
THEN
NOT DB_RC_DW_VC_DoneOnce(_int);

//END_REGION

//REGION Hatching voidwoken eggs
//Hatch eggs and set journal 
PROC
ProcOneShotTriggerEntered(_player,S_RC_DW_VC_HatchTrigger_9cacc4e1-9aae-4347-97b2-0cf0d4c7dc2e)
THEN
Proc_RC_DW_VC_PlayerFoundEggs((CHARACTERGUID)_player);


//Play ad and set journal
PROC
Proc_RC_DW_VC_PlayerFoundEggs((CHARACTERGUID)_player)
AND
DB_IsPlayer(_player)
AND
QueryOnlyOnce("RC_DW_VC_VoidlingsHatch")
THEN
GlobalSetFlag("RC_DW_VC_HatchVoidWoken");
Proc_RC_DW_VC_StartQuestSafely((CHARACTERGUID)_player);

QRY
QRY_RC_DW_VoidwokenChicks_CanStartQuest()
AND
GlobalGetFlag("RC_DW_VC_MargeGhostGone",0)
THEN
DB_NOOP(1);

QRY
QRY_RC_DW_VoidwokenChicks_CanStartQuest()
AND
GlobalGetFlag("RC_DW_VC_AllChickensAreDead",0)
THEN
DB_NOOP(1);

//Only start quest if there is still a chicken or chicken ghost left
PROC
Proc_RC_DW_VC_StartQuestSafely((CHARACTERGUID)_player)
AND
QRY_RC_DW_VoidwokenChicks_CanStartQuest()
THEN
PartySetFlag(_player,"QuestUpdate_RC_DW_VoidwokenChicks_StartFoundEggs");
Proc_StartDialog(1,"RC_DW_VC_AD_PlayerHelplessEggReaction",_player);


//Egg already destroyed
PROC
Proc_RC_DW_VC_PlayerFoundEggs((CHARACTERGUID)_player)
AND
GlobalGetFlag("RC_DW_VC_EggBrokenByAttack",1)
AND
PartyGetFlag(_player,"QuestUpdate_RC_DW_VoidwokenChicks_StartFoundEggs",1)
THEN
PartySetFlag(_player,"QuestUpdate_RC_DW_VoidwokenChicks_EggDestroyedByAttack");


//On egg hatch
IF
ItemDestroyed(_corruptedEgg)
AND
DB_RC_DW_VC_CorruptedEggs(_corruptedEgg)
THEN
NOT DB_RC_DW_VC_CorruptedEggs(_corruptedEgg);


//On attacking eggs
IF
AttackedByObject(_corruptedEgg,_charOwner,_char,_,_)
AND
_corruptedEgg != _char
AND
DB_RC_DW_VC_CorruptedEggs(_corruptedEgg)
THEN
Proc_RC_DW_VC_PlayerFoundEggs((CHARACTERGUID)_charOwner);
Proc_SetEggsHostile();


//On hatch flag
IF
GlobalFlagSet("RC_DW_VC_HatchVoidWoken")
THEN
Proc_SetEggsHostile();

IF
ObjectFlagSet("GEN_Voidegg_Hatching",_corruptedEgg,_)
AND
DB_RC_DW_VC_CorruptedEggs(_corruptedEgg)
THEN
Proc_SetEggsHostile();

//Set Eggs hostile
PROC
Proc_SetEggsHostile()
AND
DB_RC_DW_VC_CorruptedEggs(_corruptedEgg)
AND
NOT GetFaction(_corruptedEgg,"Evil NPC")
THEN
SetFaction(_corruptedEgg,"Evil NPC");
//END_REGION

//REGION If corrupted egg hatches but is not in combat (Safety)

IF
StoryEvent(_hatchling,"RC_DW_VC_HatchlingHatched")
THEN
SetCombatGroupID(_hatchling,"RC_DW_VC_HatchedVoidwoken");

IF
StoryEvent(_hatchling,"RC_DW_VC_HatchlingHatched")
AND
CharacterIsInCombat((CHARACTERGUID)_hatchling,0)
THEN
Proc_RC_DW_VC_EngageClosestPlayerInCombat((CHARACTERGUID)_hatchling);

PROC
Proc_RC_DW_VC_EngageClosestPlayerInCombat((CHARACTERGUID)_hatchling)
AND
GetClosestAlivePlayer(_hatchling,_player,_dist)
AND
_dist <= 15
THEN
ProcForceStopDialog(_player);
EnterCombat(_hatchling,_player);
//END_REGION

//REGION Spawn thief when last corrupted egg hatches
IF
CharacterDied(_Egg)
AND
DB_RC_DW_VC_CorruptedEggs(_Egg)
THEN
DB_RC_DW_VC_CorruptedEggHatched(1);
ProcDecreaseCounter("RC_DW_VC_CorruptedEggs");

IF
DB_GlobalCounter("RC_DW_VC_CorruptedEggs",3)
AND
DB_RC_DW_VC_CorruptedEggHatched(1)
THEN
CharacterAppear(S_RC_DW_VC_EggThiefVoidwoken_059fbb9c-0967-481c-a732-faacf13ab9a6,1,"");
//END_REGION

//REGION On Helpless egg pickup
IF
ItemAddedToCharacter(S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,_player)
AND
DB_IsPlayer(_player)
AND
PartyGetFlag(_player,"QuestUpdate_RC_DW_VoidwokenChicks_PickedUpHelplessEgg",0)
THEN
GlobalSetFlag("RC_DW_VC_HatchVoidWoken"); //hatches eggs if not done yet
PartySetFlag(_player,"QuestUpdate_RC_DW_VoidwokenChicks_PickedUpHelplessEgg");
Proc_RC_DW_VC_StartQuestSafely((CHARACTERGUID)_player);

//END_REGION

//REGION Egg eaten
//Eaten
IF
CharacterUsedItem(_player,S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa)
THEN
GlobalSetFlag("RC_DW_VC_EggDestroyed");
PartySetFlag(_player,"QuestUpdate_RC_DW_VoidwokenChicks_HelplessEggBecameFood");
ObjectSetFlag(_player,"RC_DW_VC_AteTheEgg");

//Egg dying AD
IF
ObjectFlagSet("RC_DW_VC_AteTheEgg",_player,_)
THEN
Proc_StartDialog(1,"RC_DW_VC_AD_PlayerHelplessEggReaction",_player);

//END_REGION

//REGION Helpless egg attacked
IF
ItemDestroyed(S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa)
THEN
GlobalSetFlag("RC_DW_VC_EggBrokenByAttack");
GlobalSetFlag("RC_DW_VC_EggDestroyed");
Proc_RC_DW_VC_EggDestroyedByAttackJournal();

//Update Journal
PROC
Proc_RC_DW_VC_EggDestroyedByAttackJournal()
AND
DB_IsPlayer(_player)
AND
PartyGetFlag(_player,"QuestUpdate_RC_DW_VoidwokenChicks_StartFoundEggs",1)
THEN
PartySetFlag(_player,"QuestUpdate_RC_DW_VoidwokenChicks_EggDestroyedByAttack");


IF
AttackedByObject(S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,_,_Attacker,_,_)
AND
S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa != _Attacker
AND
GlobalGetFlag("RC_DW_VC_EggDestroyed",0)
THEN
ObjectSetFlag(S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,"RC_DW_VC_EggAttacked");
Proc_StartDialog(1,"RC_DW_VC_AD_HelplessEgg",S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa);
//END_REGION

//REGION Returning the egg
//If dialogue ends with normal chicken and egg returned
IF
DialogEnded(_dialog,_inst)
AND
DB_RC_DW_VC_Chickens(_chicken,_dialog,_,_,_)
AND
ObjectGetFlag(_chicken,"RC_DW_VC_GaveEgg",1)
THEN
GlobalClearFlag("RC_DW_VC_ChickensAreUpset");
ObjectClearFlag(_chicken,"RC_DW_VC_GaveEgg",0);
//Silence the egg
SetVarInteger(S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,"Bool_PlayAD",0);
Proc_RC_DW_VC_ChickenReactionAD();
Proc_RC_DW_VC_SetCalmedDownBehaviours();

//Chickens no longer panic
PROC
Proc_RC_DW_VC_SetCalmedDownBehaviours()
AND
DB_RC_DW_VC_Chickens(_chicken,_,_,_,_)
AND
// Scared chicken doesn't have GEN_WanderTriggerWithAD script
_chicken != S_RC_DW_VC_Chicken_Scared_81ef9840-7b6e-4d3d-9b60-4c7ed7a286a7
THEN
SetVarInteger(_chicken,"Run",0);
SetVarFloat(_chicken,"WanderDurationMax",10.0);

//Set chicken who got egg on brooding duty
PROC
Proc_RC_DW_VC_SetCalmedDownBehaviours()
AND
DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,_onEgg,_trigger)
AND
CharacterIsDead((CHARACTERGUID)_chicken,0)
AND
ObjectGetFlag(_chicken,"RC_DW_VC_ChickenOnBroodDuty",1)
AND
NOT DB_RC_DW_VC_DoneOnce(1)
THEN
DB_RC_DW_VC_DoneOnce(1);
NOT DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,_onEgg,_trigger);
DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,1,_trigger);
// Make invisible because the egg bouncing all around the place and then teleporting
// (or bouncing again, in case of ItemDragToTrigger) when putting it in the nest looks ridiculous
// Have to do it here already, because if we do it right before dropping, then the egg will still
// be visible for a short while. In case of interruption (the chicken gets killed), the egg is made
// visible again because DB_RC_DW_VC_DroppingEgg(_,_) is set.
SetVisible(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,0);
ItemToInventory((ITEMGUID)S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,_chicken);
DB_RC_DW_VC_DroppingEgg(_chicken,"0_GoingToEggAfterDialog");
ProcCharacterMoveTo(_chicken,TRIGGERGUID_S_RC_DW_VC_HelplessEggLocation_92d4c501-c342-4e25-b08e-32fb7d396ee9,0,"RC_DW_VC_ChickenWalkToEgg");
// Protective mother!
ItemSetOwner((ITEMGUID)S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,_chicken);
DB_IsNotMessingAround(_chicken);
DB_CrimeReaction_DoNotWarn(_chicken);
// Detect when the egg is removed without the chickens noticing (so if you talk about it in the dialog, the hatching chicken can react to it)
TriggerRegisterForItems(TRIGGERGUID_S_RC_DW_VC_NestArea_425c31d0-77d0-4020-a9e4-2d8a338f609f);
DB_RC_DW_VC_NestAreaTriggerInstalled(1);

//Stop wandering if hatching and not scared chicken (scared chicken doesn't have GEN_WanderTriggerWithAD script -> no DefaultWanderInTrigger behaviour)
PROC
Proc_RC_DW_VC_SetCalmedDownBehaviours()
AND
DB_RC_DW_VC_Chickens(_chicken,_,_,_,_)
AND
CharacterIsDead((CHARACTERGUID)_chicken,0)
AND
ObjectGetFlag(_chicken,"RC_DW_VC_ChickenOnBroodDuty",1)
AND
_chicken != S_RC_DW_VC_Chicken_Scared_81ef9840-7b6e-4d3d-9b60-4c7ed7a286a7
THEN
CharacterSetReactionPriority(_chicken,"DefaultWanderInTrigger",0);

PROC
Proc_RC_DW_VC_SetCalmedDownBehaviours()
AND
DB_RC_DW_VC_DoneOnce(_int)
THEN
NOT DB_RC_DW_VC_DoneOnce(_int);
//END_REGION

//REGION Return egg to ghost chicken
//"Gave" egg and only ghost chicken left -> drop in nest
IF
ObjectFlagSet("RC_DW_VC_DropEggForGhostChicken", _Player, _)
AND
QryTakeItemFromMagicPockets((CHARACTERGUID)_Player, S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa, (CHARACTERGUID)_Player)
AND
GetDistanceTo(_Player, TRIGGERGUID_S_RC_DW_VC_NestArea_425c31d0-77d0-4020-a9e4-2d8a338f609f, _Dist)
AND
_Dist <= 8.0
THEN
PartySetFlag(_Player, "QuestUpdate_RC_DW_VoidwokenChicks_ReturnedHelplessEgg");
ItemDrop(S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa);
DB_RC_DW_VC_DroppingEggToNest(1);

IF
ItemRemovedFromCharacter(S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa, _)
AND
DB_RC_DW_VC_DroppingEggToNest(1)
THEN
NOT DB_RC_DW_VC_DroppingEggToNest(1);
ItemDragToTrigger(S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa, TRIGGERGUID_S_RC_DW_VC_HelplessEggLocation_92d4c501-c342-4e25-b08e-32fb7d396ee9);
// Detect when the egg is removed without the chickens noticing (so if you talk about it in the dialog, the hatching chicken can react to it)
TriggerRegisterForItems(TRIGGERGUID_S_RC_DW_VC_NestArea_425c31d0-77d0-4020-a9e4-2d8a338f609f);
DB_RC_DW_VC_NestAreaTriggerInstalled(1);

IF
ObjectFlagSet("RC_DW_VC_DropEggForGhostChicken", _Player, _)
THEN
ObjectClearFlag(_Player, "RC_DW_VC_DropEggForGhostChicken");

//END_REGION

//REGION Return to original panicky behaviour (in case the egg gets stolen again)
PROC
Proc_RC_DW_VC_SetPanicBehaviours()
AND
DB_RC_DW_VC_Chickens(_chicken,_,_,_,_)
AND
// Scared chicken doesn't have GEN_WanderTriggerWithAD script
_chicken != S_RC_DW_VC_Chicken_Scared_81ef9840-7b6e-4d3d-9b60-4c7ed7a286a7
THEN
SetVarInteger(_chicken,"Run",1);
SetVarFloat(_chicken,"WanderDurationMax",5.0);

PROC
Proc_RC_DW_VC_SetPanicBehaviours()
AND
DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,_hatching,_trigger)
AND
CharacterIsDead((CHARACTERGUID)_chicken,0)
AND
_chicken != S_RC_DW_VC_Chicken_Scared_81ef9840-7b6e-4d3d-9b60-4c7ed7a286a7
THEN
CharacterSetReactionPriority(_chicken,"DefaultWanderInTrigger",200);
// In case it was hatching the egg, start walking around (the egg can be returned
// to another chicken afterwards, which then will go to the nest)
Proc_RC_DW_VC_ClearGuardPos(_chicken);
// No longer hatching (so that if this chicken was hatching and dies, no other
// chicken goes to the nest)
NOT DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,_hatching,_trigger);
DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,0,_trigger);

IF
GlobalFlagSet("RC_DW_VC_ChickensAreUpset")
AND
GlobalGetFlag("RC_DW_VC_ChickensKnowEggBecameFood",0)
AND
GlobalGetFlag("RC_DW_VC_ChickensKnowEggIsDestroyed",0)
THEN
Proc_RC_DW_VC_SetPanicBehaviours();
//END_REGION

//REGION Stealing/destroying the egg after returning it

// Logic:
//   1) Stealing/destroying the egg by itself does not change the behaviour of the chickens, only them noticing this will do so (at which point we set the RC_DW_VC_ChickensAreUpset flag)
//   2) They will notice when
//    a) The crime system notices (TODO: cannot hook)
//    b) You talk to the hatching chicken after taking the egg away again (RC_DW_VC_ReturnedEggGone global -- this includes eating,
//       since you can't eat it without taking it away first) or the item is destroyed (RC_DW_VC_EggDestroyed global)
//   3) When the egg is returned, the chickens will calm down and one of them will start hatching it again

//REGION Removing the egg
// The egg was destroyed while a chicken was hatching it
IF
GlobalFlagSet("RC_DW_VC_EggDestroyed")
AND
GlobalGetFlag("RC_DW_VC_EggWasReturned",1)
AND
DB_RC_DW_VC_Chickens(_chicken,_,_,1,_)
THEN
GlobalSetFlag("RC_DW_VC_ReturnedEggGone");

// The egg was removed from under the chicken
IF
ItemLeftTrigger((ITEMGUID)S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,(TRIGGERGUID)S_RC_DW_VC_NestArea_425c31d0-77d0-4020-a9e4-2d8a338f609f,_)
THEN
// Prevents the egg from hatching via its GEN_ItemCheckIfPlayersAway script
// and affects dialogs
GlobalSetFlag("RC_DW_VC_ReturnedEggGone");
//END_REGION

//REGION Put back item before removal was detected
IF
ItemEnteredTrigger((ITEMGUID)S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,(TRIGGERGUID)S_RC_DW_VC_NestArea_425c31d0-77d0-4020-a9e4-2d8a338f609f,_)
AND
DB_GlobalFlag("RC_DW_VC_EggWasReturned")
THEN
GlobalClearFlag("RC_DW_VC_ReturnedEggGone");
// Clear/set flag so the egg script again starts checking until all players are gone (for hatching)
GlobalClearFlag("RC_DW_VC_EggWasReturned");
GlobalSetFlag("RC_DW_VC_EggWasReturned");

IF
ItemEnteredTrigger((ITEMGUID)S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,(TRIGGERGUID)S_RC_DW_VC_NestArea_425c31d0-77d0-4020-a9e4-2d8a338f609f,_)
AND
// Is not set in case the egg was returned to the ghost chicken
DB_RC_DW_VC_Chickens(_chicken,_Dialog,_AD,1,_Trigger)
THEN
ItemSetOwner((ITEMGUID)S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,_chicken);
//END_REGION

//REGION The removal has been detected; can happen by talking to the hatching chicken after removing, by the hatching chicken dying, or by crime detection (last one: TODO)
// RC_DW_VC_ChickensAreUpset is set when
//  a) you tell a chicken the egg was eaten/destroyed (you cannot do this with the hatching chicken while it was hatching)
//  b) you talk to the hatching chicken after removing/stealing the egg
IF
GlobalFlagSet("RC_DW_VC_ChickensAreUpset")
AND
DB_RC_DW_VC_Chickens(_chicken,_Dialog,_AD,1,_Trigger)
THEN
GlobalClearFlag("RC_DW_VC_ReturnedEggGone");
// Stop detecting and the chicken that was hatching no longer hatches (since the egg is gone)
Proc_RC_DW_VC_CleanUpStealBackEgg();
//END_REGION


//REGION Clean up
// Egg hatches -> no more theft detection necessary
IF
GlobalFlagSet("RC_DW_VC_HelplessEggHatches")
THEN
Proc_RC_DW_VC_CleanUpStealBackEgg();


// If the hatching chicken dies and the egg was stolen from under it, the others will notice that the egg is gone
IF
DB_Dead(_chicken)
AND
DB_RC_DW_VC_Chickens(_chicken,_dialog,_ad,1,_trigger)
AND
GlobalGetFlag("RC_DW_VC_ReturnedEggGone",1)
THEN
GlobalSetFlag("RC_DW_VC_ChickensAreUpset");

// Only clean up the part specific to this functionality, the rest is handled by
// the "Current Brooding chicken died" functionality
PROC
Proc_RC_DW_VC_CleanUpStealBackEgg()
AND
DB_RC_DW_VC_Chickens(_chicken,_Dialog,_AD,1,_Trigger)
THEN
GlobalClearFlag("RC_DW_VC_ReturnedEggGone");
ObjectClearFlag(_chicken,"RC_DW_VC_ChickenOnBroodDuty",0);
NOT DB_IsNotMessingAround(_chicken);
NOT DB_CrimeReaction_DoNotWarn(_chicken);

PROC
Proc_RC_DW_VC_CleanUpStealBackEgg()
AND
DB_RC_DW_VC_NestAreaTriggerInstalled(1)
THEN
TriggerUnregisterForItems((TRIGGERGUID)S_RC_DW_VC_NestArea_425c31d0-77d0-4020-a9e4-2d8a338f609f);
NOT DB_RC_DW_VC_NestAreaTriggerInstalled(1);
//END_REGION
//END_REGION


//REGION Set guard position for Brooding Chicken
//Set new guard position
IF
StoryEvent((CHARACTERGUID)_chicken,"RC_DW_VC_ChickenWalkToEgg")
AND
DB_RC_DW_VC_DroppingEgg(_chicken,"0_GoingToEggAfterDialog")
THEN
NOT DB_RC_DW_VC_DroppingEgg(_chicken,"0_GoingToEggAfterDialog");
DB_RC_DW_VC_DroppingEgg(_chicken,"2_Teleport");
TeleportTo(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,TRIGGERGUID_S_RC_DW_VC_HelplessEggLocation_92d4c501-c342-4e25-b08e-32fb7d396ee9);
Proc_RC_DW_VC_SetGuardPos(_chicken);
SetVisible(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,1);
NOT DB_RC_DW_VC_DroppingEgg(_chicken,"2_Teleport");
Proc_RC_DW_VC_SetGuardPos(_chicken);

IF
StoryEvent((CHARACTERGUID)_chicken,"RC_DW_VC_ChickenWalkToEgg")
AND
DB_RC_DW_VC_DroppingEgg(_chicken,"0_GoingToEggAsNewHatchingChicken")
THEN
NOT DB_RC_DW_VC_DroppingEgg(_chicken,"0_GoingToEggAsNewHatchingChicken");
Proc_RC_DW_VC_SetGuardPos(_chicken);

PROC
Proc_RC_DW_VC_SetGuardPos((CHARACTERGUID)_chicken)
AND
GetPosition(_chicken,_x,_y,_z)
THEN
SetVarFloat3(_chicken,"GuardPoint",_x,_y,_z);
CharacterSetReactionPriority((CHARACTERGUID)_chicken,"GuardDefaultPosition",10000); //Chicken will now guard this position

PROC
Proc_RC_DW_VC_ClearGuardPos((CHARACTERGUID)_chicken)
THEN
CharacterSetReactionPriority(_chicken,"GuardDefaultPosition",0);


// Detect stealing the egg after the hatching chicken died
// (independent of the crime system: they will notice the egg is gone if they were
//  walking to it -- even if distracted, they should notice it when continuing;
//  TODO: revisit when hookable crimes are in: simply register a crime for the egg
//    then)
IF
ItemAddedToCharacter(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,_Player)
AND
DB_RC_DW_VC_DroppingEgg(_chicken,_state)
AND
DB_IsPlayer(_Player)
THEN
NOT DB_RC_DW_VC_DroppingEgg(_chicken,_state);
// Stop going to the nest
CharacterPurgeQueue(_chicken);
// Restore dialogs etc that were disabled for the move
ProcClearStoryMove(_chicken);
GlobalSetFlag("RC_DW_VC_ChickensAreUpset");

// And clean up if the chicken is killed while walking to the nest or doing the egg juggle
IF
CharacterDied(_chicken)
AND
DB_RC_DW_VC_DroppingEgg(_chicken,_state)
THEN
SetVisible(ITEMGUID_S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,1);
NOT DB_RC_DW_VC_DroppingEgg(_chicken,_state);
//END_REGION

//REGION Informed ate or destroyed egg, or noticed that egg is gone again after returning it first
IF
GlobalFlagSet("RC_DW_VC_ChickensAreUpset")
THEN
Proc_RC_DW_VC_ChickenReactionAD();
//END_REGION

//REGION Turn Chickens hostile
IF
DialogEnded(_dialog,_Inst)
AND
DB_RC_DW_VC_Chickens(_,_dialog,_,_,_)
AND
DialogGetInvolvedPlayer(_Inst,1,_player)
AND
ObjectGetFlag(_player,"RC_DW_VC_ChickensBecomeHostileTowardsPlayer",1)
AND
DB_RC_DW_VC_Chickens(_chicken,_,_,_,_)
AND
CharacterIsDead(_chicken,0)
THEN
SetCanFight(_chicken,1);
ProcMakeNPCHostile((CHARACTERGUID)_player,(CHARACTERGUID)_chicken);
//END_REGION

//REGION Hatching Helpless egg
IF
GlobalFlagSet("RC_DW_VC_HelplessEggHatches")
THEN
SetCanFight(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,1);
SetOnStage(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,1);
SetOnStage(S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,0);
Proc_RC_DW_VC_HelplessEggHatches();


PROC
Proc_RC_DW_VC_HelplessEggHatches()
AND
DB_RC_DW_VC_Chickens(_chicken,_,_,_,_trigger)
AND
CharacterIsDead(_chicken,0)
THEN
TeleportTo(_chicken,_trigger);
CharacterLookFromTrigger(_chicken,_trigger,1);
CharacterDie(_chicken,0,"DoT");
CreateSurface((CHARACTERGUID)_chicken,"SurfaceBlood",1.0,-1.0);
//END_REGION

//REGION Imprinted -> no more fighting
IF
ObjectFlagSet("RC_DW_VC_IsImprinted",S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,_)
THEN
SetCanFight(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,0);
//END_REGION

//REGION Killing Rooster or Chick
IF
DB_Dead(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d)
THEN
GlobalSetFlag("RC_DW_VC_VoidwokenChickDead");
GlobalClearFlag("RC_DW_VC_ChickNearRooster");

//Journal chick dies
IF
DB_Dead(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d)
AND
DB_IsPlayer(_player)
AND
GlobalGetFlag("RC_DW_VC_HelplessEggHatches",1)
THEN
PartySetFlag(_player,"QuestUpdate_RC_DW_VoidwokenChicks_CloseChickDead");


//Rooster
IF
DB_Dead(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0)
THEN
GlobalSetFlag("RC_DW_VC_MagicRoosterDead");
TriggerUnregisterForCharacter(S_RC_DW_VC_RoosterCheckChickTrigger_33774bdb-125d-4bbf-93fe-542950abba31,S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d);
GlobalClearFlag("RC_DW_VC_ChickNearRooster");


//Journal Rooster dies
IF
DB_Dead(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d)
AND
DB_IsPlayer(_player)
AND
GlobalGetFlag("RC_DW_VC_HelplessEggHatches",1)
THEN
PartySetFlag(_player,"QuestUpdate_RC_DW_VoidwokenChicks_CloseChickDead");


//Get informed about rooster but he is dead
IF
DB_Dead(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0)
AND
DB_Sees(_Player,S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0)
AND
ObjectGetFlag(_Player,"QuestUpdate_RC_DW_VoidwokenChicks_KnowsAboutRooster",1)
THEN
PartySetFlag((CHARACTERGUID)_player,"QuestUpdate_RC_DW_VoidwokenChicks_CloseRoosterDead");

IF
ObjectFlagSet("QuestUpdate_RC_DW_VoidwokenChicks_KnowsAboutRooster",_player,_)
AND
CharacterIsDead(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0,1)
THEN
PartySetFlag((CHARACTERGUID)_player,"QuestUpdate_RC_DW_VoidwokenChicks_CloseRoosterDead");

//END_REGION

//REGION Cat Grave
/*PROC
ProcShovelRewards(_player,"RC_WC_VC_DiggingUpCatGrave")
THEN
SetOnStage(S_RC_DW_VC_CatSkeleton_5f1081e7-c93b-404a-aa49-3717cf12b10f,1);*/
//END_REGION

//REGION Voidwoken Chick enters trigger
IF
CharacterEnteredTrigger(_chick,S_RC_DW_VC_RoosterCheckChickTrigger_33774bdb-125d-4bbf-93fe-542950abba31)
THEN
GlobalSetFlag("RC_DW_VC_ChickNearRooster");

IF
CharacterLeftTrigger(_chick,S_RC_DW_VC_RoosterCheckChickTrigger_33774bdb-125d-4bbf-93fe-542950abba31)
THEN
GlobalClearFlag("RC_DW_VC_ChickNearRooster");
//END_REGION

//REGION Normal or Triple Dialog Rooster
//Start dialog
PROC
PROC_GLOBAL_DialogStartRequested(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0,_player)
THEN
Proc_RC_DW_VC_TripleDialog_RoosterChick(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0,S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,(CHARACTERGUID)_player);


//Triple dialog
PROC
Proc_RC_DW_VC_TripleDialog_RoosterChick((CHARACTERGUID)_rooster,(CHARACTERGUID)_chick,(CHARACTERGUID)_player)
AND
GlobalGetFlag("RC_DW_VC_ChickNearRooster",1)
AND
GlobalGetFlag("RC_DW_VC_VoidwokenChickBroughtToRooster",0)
THEN
DB_RC_DW_VC_CheckedOnce(1);
Proc_StartDialog(0,"RC_DW_VC_TripleDialog_RoosterChick",_rooster,_chick,_player);
ProcFaceEachother(_rooster,_chick);


//Normal dialog
PROC
Proc_RC_DW_VC_TripleDialog_RoosterChick((CHARACTERGUID)_rooster,(CHARACTERGUID)_chick,(CHARACTERGUID)_player)
AND
NOT DB_RC_DW_VC_CheckedOnce(1)
THEN
Proc_StartDialog(0,"RC_DW_VC_MagicRooster",_rooster,_player);

//Clear use once
PROC
Proc_RC_DW_VC_TripleDialog_RoosterChick((CHARACTERGUID)_rooster,(CHARACTERGUID)_chick,(CHARACTERGUID)_player)
AND
DB_RC_DW_VC_CheckedOnce(_int)
THEN
NOT DB_RC_DW_VC_CheckedOnce(_int);
//END_REGION

//REGION On Bringing Chick to Rooster
// Move Peeper close to player so we can easily kill it
IF
DialogStarted("RC_DW_VC_TripleDialog_RoosterChick",_ID)
AND
DB_DialogPlayers(_ID,_Player,_)
THEN
CharacterMoveTo(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,_Player,1,"",0);

IF
GlobalFlagSet("RC_DW_VC_VoidwokenChickBroughtToRooster")
THEN
TriggerUnregisterForCharacter(S_RC_DW_VC_RoosterCheckChickTrigger_33774bdb-125d-4bbf-93fe-542950abba31,S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d);
GlobalClearFlag("RC_DW_VC_ChickNearRooster");
ObjectClearFlag(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"RC_DW_VC_IsImprinted",0);
//ProcCharacterStopFollow(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d);

//Disabled Pet companion script
IF
GlobalFlagSet("RC_DW_VC_VoidwokenChickBroughtToRooster")
AND
DB_IsPlayer(_player)
AND
ObjectGetFlag(_player,"RC_DW_VC_IsOwnerOfChick",1)
THEN
ObjectClearFlag(_player,"RC_DW_VC_IsOwnerOfChick",0);
//END_REGION

//REGION Rooster Look at Player or chest
IF
ObjectFlagSet("RC_DW_VC_RoosterLookAtPlayer",_player,_)
THEN
ObjectClearFlag(_player,"RC_DW_VC_RoosterLookAtPlayer",0);
ProcFaceEachother(_player,S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0);
//END_REGION

//REGION Peeper fight after bringing him back
IF
DialogEnded("RC_DW_VC_TripleDialog_RoosterChick",_ID)
AND
DB_DialogPlayers(_ID,_Player,_)
THEN
Proc_RC_DW_VC_PuttingDownPeeper((CHARACTERGUID)_Player);

// Assassinate
PROC
Proc_RC_DW_VC_PuttingDownPeeper((CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"RC_DW_VC_KillPeeper",1)
THEN
CharacterDie(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,1,"DoT");
Foop(S_RC_DW_VC_MagicRoosterChest_f93a85e3-7588-4e0f-a1af-d1e9c63f6a87);

// Transform
PROC
Proc_RC_DW_VC_TransformPeeper()
AND
GetPosition(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,_X,_Y,_Z)
THEN
PlayEffectAtPosition("RS3_FX_GP_Combat_CorpseExplosion_Blood_01_Medium",_X,_Y,_Z);
Transform(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"QUEST_RC_DW_VC_SmallInsect_f01630e8-de8f-471a-9c36-007d4f50186e",0,1);
ClearTag(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"CHICKEN");
SetTag(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"VOIDSECT");

PROC
Proc_RC_DW_VC_PuttingDownPeeper((CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"RC_DW_VC_DontKillPeeper",1)
THEN
Poof(CHARACTERGUID_S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0);
Proc_RC_DW_VC_TransformPeeper();
Proc_RC_DW_VC_TurnChickHostile();
DB_VC_RoosterFightSpawnCounter(0);

IF
DB_VC_RoosterFightSpawnCounter(_Count)
AND
_Count < 10
AND
IntegerSum(_Count,1,_NewCount)
THEN
NOT DB_VC_RoosterFightSpawnCounter(_Count);
DB_VC_RoosterFightSpawnCounter(_NewCount);
Proc_RC_DW_VC_SpawnPeeperTNGHelper();

PROC
Proc_RC_DW_VC_SpawnPeeperTNGHelper()
AND
GetRandomPositionInTrigger(TRIGGERGUID_S_RC_DW_VC_RoosterCheckChickTrigger_33774bdb-125d-4bbf-93fe-542950abba31,_X,_Y,_Z)
AND
CharacterCreateAtPosition(_X,_Y,_Z,"QUEST_RC_DW_VoidwokenChick_99e5c092-1cd2-4e35-91eb-b8e89fb02c5e",1,_Helper)
THEN
SetCanFight(_Helper,1);
SetCanJoinCombat(_Helper,1);
SetFaction(_Helper,"Evil NPC");

IF
DB_VC_RoosterFightSpawnCounter(10)
THEN
NOT DB_VC_RoosterFightSpawnCounter(10);
//END_REGION

//REGION Turn Chick Hostile and being attacked
//Attacked while imprinted
IF 
AttackedByObject(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,_attackerOwner,_attacker,_,_)
AND
S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d != _attacker
THEN
Proc_RC_DW_VC_OnChickAttacked((CHARACTERGUID)_attacker);


PROC
Proc_RC_DW_VC_OnChickAttacked((CHARACTERGUID)_attacker)
AND
CharacterIsInCombat(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,0)
AND
ObjectGetFlag(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"RC_DW_VC_IsImprinted",1)
THEN
ObjectSetFlag(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"RC_DW_VC_ChickGainedDamage");
SetStoryEvent(_attacker,"RC_DW_VC_ChickFleeFrom");


//On Attack when brought to rooster
PROC
Proc_RC_DW_VC_OnChickAttacked((CHARACTERGUID)_attacker)
AND
CharacterIsInCombat(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,0)
AND
GlobalGetFlag("RC_DW_VC_VoidwokenChickBroughtToRooster",1)
AND
CharacterIsDead(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0,0)
THEN
ObjectSetFlag(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"RC_DW_VC_ChickGainedDamage");
SetStoryEvent(_attacker,"RC_DW_VC_ChickFleeFrom");


//On Attack when not imprinted
PROC
Proc_RC_DW_VC_OnChickAttacked((CHARACTERGUID)_attacker)
AND
CharacterIsInCombat(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,0)
AND
ObjectGetFlag(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"RC_DW_VC_IsImprinted",0)
AND
GlobalGetFlag("RC_DW_VC_VoidwokenChickBroughtToRooster",0)
AND
CharacterIsDead(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0,0)
THEN
SetStoryEvent(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"RC_DW_VC_TurnHostileBeforeImprint");
Proc_RC_DW_VC_TurnChickHostile();


//Turn chick hostile if Father dies when he was returned
IF
DB_Dead(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0)
AND
GlobalGetFlag("RC_DW_VC_VoidwokenChickBroughtToRooster",1)
AND
CharacterIsDead(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,0)
THEN
Proc_RC_DW_VC_TurnChickHostile();


PROC
Proc_RC_DW_VC_TurnChickHostile()
AND
CharacterIsDead(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,0)
THEN
ObjectSetFlag(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"RC_DW_VC_ChickIsHostile");
DialogRequestStop(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d);
SetFaction(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"Evil NPC");
SetCanFight(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,1);
TimerLaunch("RC_DW_VC_ChickHostile",500);

IF
TimerFinished("RC_DW_VC_ChickHostile")
AND
NOT DB_Dead(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d)
THEN
Proc_StartDialog(1,"RC_DW_VC_AD_VoidwokenChick",S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d);
//END_REGION

//REGION Mother resurected
IF
CharacterResurrected(_player)
AND
ObjectGetFlag(_player,"RC_DW_VC_PeepersMother",1)
THEN
SetStoryEvent(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"RC_DW_VC_MotherRez");
//END_REGION


//REGION Peeper follow player
IF
ObjectFlagSet("RC_DW_VC_PeepersMother",_player,_)
THEN
//CharacterAddToCharacter(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,(CHARACTERGUID)_player);
//ProcCharacterFollowCharacter(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,(CHARACTERGUID)_player);
CharacterSetRelationIndivFactionToIndivFaction((CHARACTERGUID)_player,S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,100);
CharacterSetRelationIndivFactionToIndivFaction(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,(CHARACTERGUID)_player,100);

//END_REGION

//REGION Rooster In dialog
IF
DialogStarted(_,_inst)
AND
DialogGetInvolvedNPC(_inst,1,S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0)
THEN
ObjectSetFlag(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0,"RC_DW_VC_InDialog");

IF
DialogEnded(_,_inst)
AND
DialogGetInvolvedNPC(_inst,1,S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0)
THEN
ObjectClearFlag(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0,"RC_DW_VC_InDialog",0);

//END_REGION

//REGION Attacking Rooster or chick, rooster reaction
IF
AttackedByObject(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0,_player,_,_,_)
AND
DB_IsPlayer((CHARACTERGUID)_player)
AND
CharacterIsDead(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0,0)
AND
ObjectGetFlag(S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0,"RC_DW_VC_InDialog",0)
AND
QRY_SpeakerIsAvailableAndInDialogRange(_player,S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0)
THEN
Proc_RC_DW_VC_StartDialogToTeleport((CHARACTERGUID)_player);

PROC
Proc_RC_DW_VC_StartDialogToTeleport((CHARACTERGUID)_player)
THEN
ObjectSetFlag(_player,"RC_DW_VC_PlayerAttackedRoosterOrChick");
DialogRequestStop((CHARACTERGUID)_player);
Proc_StartDialog(0,"RC_DW_VC_MagicRooster",S_RC_DW_VC_MagicRooster_0e367750-1970-4668-a78c-116f0fff8cc0,_player);
//END_REGION

//REGION Rooster Teleports Player
IF
DialogEnded("RC_DW_VC_MagicRooster",_Inst)
AND
DialogGetInvolvedPlayer(_Inst,1,_player)
AND
ObjectGetFlag(_player,"RC_DW_VC_TeleportPlayer",1)
AND
Random(4,_random)
THEN
ObjectClearFlag(_player,"RC_DW_VC_TeleportPlayer",0);
Proc_RC_DW_VC_RoosterRandomTpPlayer((CHARACTERGUID)_player,(INTEGER)_random);


PROC
Proc_RC_DW_VC_RoosterRandomTpPlayer((CHARACTERGUID)_player,(INTEGER)_RandomTrigger)
AND
DB_RC_DW_VC_TeleportTriggers(_tpTrigger,_RandomTrigger)
AND
GetPosition(_player,_x,_y,_z)
THEN
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01",_x,_y,_z);
CharacterDetachFromGroup((CHARACTERGUID)_player);
SetVisible(_player,0);
TeleportTo(_player,_tpTrigger);
CharacterLookFromTrigger(_player,_tpTrigger,1);
DB_RC_DW_VC_TeleportedPlayer(_player);
TimerLaunch("RC_DW_VC_RoosterTeleporter",250);

IF
TimerFinished("RC_DW_VC_RoosterTeleporter")
AND
DB_RC_DW_VC_TeleportedPlayer(_player)
THEN
SetVisible(_player,1);
PlayEffect(_player,"RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01");
ApplyStatus(_Player,"KNOCKED_DOWN",1.0,1);
DB_RC_DW_VC_TeleportedPlayerAD(_player);
NOT DB_RC_DW_VC_TeleportedPlayer(_player);
TimerLaunch("RC_DW_VC_RoosterTeleportAD",2000);

IF
TimerFinished("RC_DW_VC_RoosterTeleportAD")
AND
DB_RC_DW_VC_TeleportedPlayerAD(_player)
THEN
Proc_RC_DW_VC_PlayerTeleportedAD((CHARACTERGUID)_player);

PROC
Proc_RC_DW_VC_PlayerTeleportedAD((CHARACTERGUID)_player)
AND
NOT DB_DialogPlayers(_,_player,_)
THEN
Proc_StartDialog(1,"RC_DW_VC_AD_PlayerGotTeleported",_player);

PROC
Proc_RC_DW_VC_PlayerTeleportedAD((CHARACTERGUID)_player)
THEN
NOT DB_RC_DW_VC_TeleportedPlayerAD(_player);
//END_REGION

//REGION Peeper mother died/rezed in combat
IF
StoryEvent(S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,"RC_DW_VC_PeeperPlayAD")
AND
NOT DB_DialogNPCs(_,S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d,_)
THEN
Proc_StartDialog(1,"RC_DW_VC_AD_VoidwokenChick",S_RC_DW_VC_VoidwokenChick_08b71c62-f1bf-41d7-95e3-488d190f018d);
//END_REGION

//REGION Chickens know egg was eaten or destroyed by attack
IF
GlobalFlagSet("RC_DW_VC_ChickensKnowEggIsDestroyed")
THEN
GlobalSetFlag("RC_DW_VC_ChickensAreUpset");

IF
GlobalFlagSet("RC_DW_VC_ChickensKnowEggBecameFood")
THEN
GlobalSetFlag("RC_DW_VC_ChickensAreUpset");

// RC_DW_VC_ChickensAreUpset is also set when the egg is stolen again after returning it
IF
GlobalFlagSet("RC_DW_VC_ChickensKnowEggIsDestroyed")
AND
ObjectGetFlag(S_RC_DW_VC_Chicken_Scared_Ghost_1e3a6b3d-84cf-4458-b741-71df3c19614b,"GLO_GhostSuck",0)
THEN
ObjectSetFlag(S_RC_DW_VC_Chicken_Scared_Ghost_1e3a6b3d-84cf-4458-b741-71df3c19614b,"RC_DW_VC_SpiritChickenGreeving");

IF
GlobalFlagSet("RC_DW_VC_ChickensKnowEggBecameFood")
AND
ObjectGetFlag(S_RC_DW_VC_Chicken_Scared_Ghost_1e3a6b3d-84cf-4458-b741-71df3c19614b,"GLO_GhostSuck",0)
THEN
ObjectSetFlag(S_RC_DW_VC_Chicken_Scared_Ghost_1e3a6b3d-84cf-4458-b741-71df3c19614b,"RC_DW_VC_SpiritChickenGreeving");
//END_REGION

//REGION Chicken Reaction AD when given egg or informed about egg became food or destoyed
PROC
Proc_RC_DW_VC_ChickenReactionAD()
AND
DB_RC_DW_VC_Chickens(_chicken,_,_ad,_,_)
AND
CharacterIsDead((CHARACTERGUID)_chicken,0)
THEN
Proc_StartDialog(1,_ad,_chicken);
//END_REGION

//REGION Reward location after returning egg
IF
ObjectFlagSet("RC_DW_VC_EggRewardLocation",(CHARACTERGUID)_Player,_)
THEN
TriggerRegisterForCharacter(S_RC_DW_VC_ChickenRewardDigTrigger_d372f610-49e8-4a2d-863a-8a6cae89ff93,_Player);
ProcShowMarker(_Player,"RC_DW_VC_EggRewardMarker");

IF
CharacterEnteredTrigger((CHARACTERGUID)_Player,S_RC_DW_VC_ChickenRewardDigTrigger_d372f610-49e8-4a2d-863a-8a6cae89ff93)
THEN
TriggerUnregisterForCharacter(S_RC_DW_VC_ChickenRewardDigTrigger_d372f610-49e8-4a2d-863a-8a6cae89ff93,_Player);
ProcHideMarker(_Player,"RC_DW_VC_EggRewardMarker");
//END_REGION

//REGION City chickens location
IF
ObjectFlagSet("QuestUpdate_RC_DW_VoidwokenChicks_KnowsAboutRooster",(CHARACTERGUID)_Player,_)
THEN
// Marker is shown as part of journal update
TriggerRegisterForCharacter(S_RC_DW_VC_CityChickensWanderTrigger_72a38ef2-dbab-468b-a9c2-436c0dd8f66c,_Player);

IF
CharacterEnteredTrigger((CHARACTERGUID)_Player,S_RC_DW_VC_ChickenRewardDigTrigger_d372f610-49e8-4a2d-863a-8a6cae89ff93)
THEN
TriggerUnregisterForCharacter(S_RC_DW_VC_ChickenRewardDigTrigger_d372f610-49e8-4a2d-863a-8a6cae89ff93,_Player);
ProcHideMarker(_Player,"RC_DW_VC_CityChickensMarker");
//END_REGION


//REGION All chickens are dead
IF
DB_Dead(_chicken)
AND
DB_RC_DW_VC_Chickens(_chicken,_,_,_,_)
AND
NOT QRY_RC_DW_VC_AnyAliveChickens()
THEN
GlobalSetFlag("RC_DW_VC_AllChickensAreDead");

QRY
QRY_RC_DW_VC_AnyAliveChickens()
AND
DB_RC_DW_VC_Chickens(_chicken,_,_,_,_)
AND
CharacterIsDead((CHARACTERGUID)_chicken,0)
THEN
DB_NOOP(1);
//END_REGION

//REGION On Consuming marges Ghost ending the quest
IF
DB_GoneGhosts(CHARACTERGUID_S_RC_DW_VC_Chicken_Scared_Ghost_1e3a6b3d-84cf-4458-b741-71df3c19614b)
THEN
GlobalSetFlag("RC_DW_VC_MargeGhostGone");

IF
GlobalFlagSet("RC_DW_VC_MargeGhostGone") //Marge ghost gone
AND
GlobalGetFlag("RC_DW_VC_AllChickensAreDead",1) //All chickens dead
AND
GlobalGetFlag("RC_DW_VC_EggWasReturned",0) //Not returned
AND
GlobalGetFlag("RC_DW_VC_ChickensAreUpset",0) //Not informed about ate or destroy
THEN
Proc_RC_DW_VC_EndQuestSafely();

IF
GlobalFlagSet("RC_DW_VC_AllChickensAreDead") //All chickens dead
AND
GlobalGetFlag("RC_DW_VC_MargeGhostGone",1) //Marge ghost gone
AND
GlobalGetFlag("RC_DW_VC_EggWasReturned",0) //Not returned
AND
GlobalGetFlag("RC_DW_VC_ChickensAreUpset",0) //Not informed about ate or destroy
THEN
Proc_RC_DW_VC_EndQuestSafely();


PROC
Proc_RC_DW_VC_EndQuestSafely()
AND
DB_IsPlayer(_player)
AND
ObjectGetFlag(_player,"QuestUpdate_RC_DW_VoidwokenChicks_StartTalkedToChickens",1)
AND
GlobalGetFlag("RC_DW_VC_AllChickensAreDead",1)
THEN
PartySetFlag((CHARACTERGUID)_player,"QuestUpdate_RC_DW_VoidwokenChicks_CloseNoOneLeft");

PROC
Proc_RC_DW_VC_EndQuestSafely()
AND
DB_IsPlayer(_player)
AND
ObjectGetFlag(_player,"QuestUpdate_RC_DW_VoidwokenChicks_StartFoundEggs",1)
AND
GlobalGetFlag("RC_DW_VC_AllChickensAreDead",1)
THEN
PartySetFlag((CHARACTERGUID)_player,"QuestUpdate_RC_DW_VoidwokenChicks_CloseNoOneLeft");


PROC
Proc_RC_DW_VC_EndQuestSafely()
AND
GlobalGetFlag("RC_DW_VC_EggDestroyed",0)
THEN
SetVarInteger(S_RC_DW_VC_HelplessEgg_22a95ce1-5b3f-4ba9-8667-62cd2d38daaa,"Bool_PlayAD",0); //Sillence the egg


//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
