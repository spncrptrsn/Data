Version 1
SubGoalCombiner SGC_AND
INITSECTION
SetVisible(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,0);
CharacterSetArmorPercentage(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,100.0);
CharacterSetMagicArmorPercentage(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,100.0);
CharacterSetHitpointsPercentage(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,50.0);

DB_CoS_AotO_SourceTitan((CHARACTERGUID)CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f);
CharacterSetImmortal(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,1);

SetOnStage(ITEMGUID_S_CoS_AotO_LvBeacon_7704541a-bf3b-44d1-aae4-c49a47b20add,0);
SetOnStage(ITEMGUID_S_CoS_AotO_LvBeacon_Helper_eb805737-302b-4dea-9027-97a9cb7633a7,0);
SetOnStage(CHARACTERGUID_S_CoS_AotO_CaveInLocation_P3_005_b395a4dc-1c59-4467-a1a1-18c34c9c43de,0);


KBSECTION
PROC
Proc_CoS_AotO_PhaseThreeInit()
THEN
GlobalSetFlag("CoS_AotO_Phase_3_Started");


PROC
Proc_CoS_AotO_PhaseThreeInit()
AND
DB_CoS_AotO_LastNemesis((CHARACTERGUID)_Nemesis)
THEN
PlayEffect(_Nemesis,"RS3_FX_GP_ScriptedEvent_CoS_TitanTransform_01_Overlay");
CharacterSetReactionPriority(_Nemesis,"Sleep_InCombat",10000);
DB_CombatCutscene_PrioritizedObject(_Nemesis);
CharacterSetAnimationOverride(_Nemesis,"skill_cast_shout_01_cast");
ProcObjectTimer(_Nemesis,"CoS_AotO_SourceTitanTransformation_Timer",1100);
TeleportTo(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,_Nemesis);
TimerLaunch("CoS_AotO_SourceTitanTransformation_CamShake",600);

IF
TimerFinished("CoS_AotO_SourceTitanTransformation_CamShake")
AND
DB_IsPlayer(_Player)
AND
CharacterIsDead(_Player,0)
THEN
Proc_ShakeCameraForTime(_Player,2000);


PROC
ProcObjectTimerFinished((CHARACTERGUID)_Nemesis,"CoS_AotO_SourceTitanTransformation_Timer")
AND
GetPosition(_Nemesis,_X,_Y,_Z)
THEN
//PlayEffectAtPosition("RS3_FX_Skills_Divine_Cast_Ghosts_Root_01",_X,_Y,_Z);
Proc_CoS_AotO_CreateSourceTitan();



PROC
Proc_CoS_AotO_CreateSourceTitan()
AND
DB_CoS_AotO_LastNemesis((CHARACTERGUID)_Nemesis)
THEN
PlayEffect(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,"RS3_FX_GP_ScriptedEvent_CoS_TitanTransform_01_Titan");
CharacterSetReactionPriority(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,"Sleep_InCombat",10000);
SetOnStage(_Nemesis,0);
TeleportTo(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,_Nemesis);
CharacterSetArmorPercentage(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,100.0);
CharacterSetMagicArmorPercentage(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,100.0);
CharacterSetHitpointsPercentage(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,50.0);
CharacterFlushQueue(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f);
DB_CombatCutscene_PrioritizedObject(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f);
CharacterSetAnimationOverride(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,"resurrect");
//PlayEffect(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,"RS3_FX_Skills_Divine_Cast_Soul_Self_Head_01");
TimerLaunch("CoS_AotO_SourceTitan_Spawned",2500);
TimerLaunch("CoS_AotO_SourceTitan_Spawned_Visiblity",10);

PROC
Proc_CoS_AotO_CreateSourceTitan()
AND
DB_IsPlayer(_Player)
AND
CharacterIsDead(_Player,0)
THEN
CharacterLookAt(_Player,CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f);
SetStoryEvent(_Player,"GEN_FallAndGetUp");

IF
TimerFinished("CoS_AotO_SourceTitan_Spawned_Visiblity")
THEN
SetVisible(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,1);

IF
TimerFinished("CoS_AotO_SourceTitan_Spawned")
AND
DB_CoS_AotO_SourceTitan((CHARACTERGUID)_SourceTitan)
AND
GetClosestAlivePlayer(_SourceTitan,(CHARACTERGUID)_Player,_)
AND
QueryOnlyOnce("CoS_AotO_CheckFirstTimeSourceTitan_Once")
AND
StartDialog_Internal("CoS_AotO_Phase2_SourceTitanTransformation",1,_SourceTitan,_Player,NULL_00000000-0000-0000-0000-000000000000,
		NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,1)
THEN
CharacterSetAnimationOverride(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,"");

IF
DialogEnded("CoS_AotO_Phase2_SourceTitanTransformation",_Id)
AND
DialogGetInvolvedPlayer(_Id,1,_Player)
AND
DB_CoS_AotO_SourceTitan((CHARACTERGUID)_SourceTitan)
THEN
CharacterSetImmortal(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,0);
CharacterSetReactionPriority(_SourceTitan,"Sleep_InCombat",0);
Proc_CombatCutscene_PrioritizedObject_Purge();
JumpToTurn(_Player);



IF
CharacterKilledBy((CHARACTERGUID)_SourceTitan,_Attacker,_)
AND
DB_CoS_AotO_SourceTitan(_SourceTitan)
AND
DB_IsPlayer((CHARACTERGUID)_Attacker)
THEN
SetTag(_Attacker,"GODSLAYER");

//Start Escape
IF
CharacterDying(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f)
AND
DB_CoS_AotO_CaveIns_Phase2(_CaveIn)
THEN
SetStoryEvent(_CaveIn,"TryEnterCombat");
SetOnStage(CHARACTERGUID_S_CoS_AotO_CaveInLocation_P3_005_b395a4dc-1c59-4467-a1a1-18c34c9c43de,1);
SetStoryEvent(CHARACTERGUID_S_CoS_AotO_CaveInLocation_P3_005_b395a4dc-1c59-4467-a1a1-18c34c9c43de,"TryEnterCombat");


IF
CharacterDied(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f)
AND
GetPosition(ITEMGUID_S_CoS_AotO_LvBeacon_Helper_eb805737-302b-4dea-9027-97a9cb7633a7,_X,_Y,_Z)
THEN
CreateSurface(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,"SurfaceNone",3.0,1.0);
CreateSurface(ITEMGUID_S_CoS_AotO_LvBeacon_Helper_eb805737-302b-4dea-9027-97a9cb7633a7,"SurfaceNone",3.0,1.0);
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_HOE_Summoning_01",_X,_Y,_Z);
SetOnStage(ITEMGUID_S_CoS_AotO_LvBeacon_7704541a-bf3b-44d1-aae4-c49a47b20add,1);
ItemSetForceSynch(ITEMGUID_S_CoS_AotO_LvBeacon_7704541a-bf3b-44d1-aae4-c49a47b20add,1);
SetOnStage(ITEMGUID_S_CoS_AotO_LvBeacon_Helper_eb805737-302b-4dea-9027-97a9cb7633a7,1);
ItemSetForceSynch(ITEMGUID_S_CoS_AotO_LvBeacon_Helper_eb805737-302b-4dea-9027-97a9cb7633a7,1);
DB_CoS_AotO_CaveIns_Phase2(CHARACTERGUID_S_CoS_AotO_CaveInLocation_P3_005_b395a4dc-1c59-4467-a1a1-18c34c9c43de);

//REGION Arena Hack to Retain Combat

IF
CharacterDied(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f)
AND
DB_IsPlayer(_Player)
THEN
SetInArena(_Player,1);


IF
CharacterDied(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f)
AND
DB_CoS_AotO_CaveIns_Phase2(_CaveIn)
THEN
SetInArena(_CaveIn,1);

PROC
Proc_CoS_AotO_End()
AND
DB_CoS_AotO_CaveIns_Phase2(_CaveIn)
THEN
SetInArena(_CaveIn,0);

PROC
Proc_CoS_AotO_End()
AND
DB_IsPlayer(_Player)
THEN
CharacterRemoveSummons(_Player,0);
SetInArena(_Player,0);

//Paranoia check
PROC
Proc_CoS_AotO_End()
AND
DB_CoS_AotO_Competitors(_Player)
THEN
SetInArena(_Player,0);

//END_REGION


IF
CharacterDied(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f)
AND
GetClosestAlivePlayer(CHARACTERGUID_S_CoS_AotO_SourceTitan_d230a9d9-e44d-493e-8c69-9570f2fa065f,_Player,_)
AND
StartDialog_Internal("CoS_AotO_Phase3_MaladyTelepathy",1,_Player,NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,
		NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,1)
THEN
DB_CombatCutscene_PrioritizedObject(_Player);

IF
DialogEnded("CoS_AotO_Phase3_MaladyTelepathy",_)
THEN
Proc_CombatCutscene_PrioritizedObject_Purge();

IF
DialogEnded("CoS_AotO_Phase3_MaladyTelepathy",_)
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_CoS_ArenaOfTheOne_Phase_3_Beacon");

//REGION End AotO to Lady Vengeance
IF
CharacterUsedItem(_,ITEMGUID_S_CoS_AotO_LvBeacon_7704541a-bf3b-44d1-aae4-c49a47b20add)
AND
QueryOnlyOnce("CoS_AotO_BeaconUsed")
THEN
Proc_CoS_AotO_End();

//Handling Players
PROC
Proc_CoS_AotO_End()
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
CharacterPurgeQueue(_Player);
SetCanFight(_Player,0);
SetCanJoinCombat(_Player,0);
ProcSetInvulnerable(_Player,1);
CharacterSetImmortal(_Player,1);
CharacterFreeze(_Player);
LeaveCombat(_Player);
Proc_ShakeCameraForTime(_Player,5000);


PROC
Proc_CoS_AotO_End()
THEN
GlobalSetFlag("CoS_AotO_End_LavaCaveIn");
TimerLaunch("CoS_AotO_LadyVengeanceReturn_FadeOut_Timer",1500);


PROC
Proc_CoS_AotO_End()
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
FadeToBlack(_Player,1.0,0,"CoS_AotO_LadyVengeanceReturn_Cutscene_FadeOut");

IF
FadeOutDone(_,"CoS_AotO_LadyVengeanceReturn_Cutscene_FadeOut")
AND
QueryOnlyOnce("CoS_AotO_LadyVengeanceReturn_Cutscene_FadeOut_Once")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
CreateSurface(_Player,"SurfaceNone",2.0,1.0);
FadeToBlack(_Player,0.0,1,"Placeholder");
Proc_CharacterFullRestore(_Player);
CharacterSetAnimationOverride(_Player,"");


IF
FadeOutDone(_,"CoS_AotO_LadyVengeanceReturn_Cutscene_FadeOut")
AND
QueryOnlyOnce("CoS_AotO_LadyVengeanceReturn_PlayMovie")
THEN
Proc_CoS_AotO_LoadReturnToLV();

PROC
Proc_CoS_AotO_LoadReturnToLV()
THEN
GlobalClearFlag("CoS_AotO_Active");
DB_CoS_AfterArenaOfTheOne(1);
GlobalSetFlag("CoS_AfterArenaOfTheOne");

PROC
Proc_CoS_AotO_LoadReturnToLV()
THEN
ProcStartMovie("CS_Rescue");
TimerLaunch("CS_Do_RescueMovieTeleport",0);

IF
TimerFinished("CS_Do_RescueMovieTeleport")
AND
DB_IsPlayer(_Player)
AND
CharacterIsInCombat(_Player,1)
THEN
LeaveCombat(_Player);

IF
TimerFinished("CS_Do_RescueMovieTeleport")
THEN
CharacterTeleportPartiesToTrigger(TRIGGERGUID_S_CoS_LV_HoE_RegionStart_fda0f33f-8438-4057-bdff-5b3c945bdb20,"");

PROC
Proc_CoS_AotO_LoadReturnToLV()
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
CharacterPurgeQueue(_Player);
FadeToBlack(_Player,0.0,0,"Placeholder");
FadeToBlack(_Player,1.25,1,"Placeholder");
SetCanFight(_Player,1);
SetCanJoinCombat(_Player,1);
ProcSetInvulnerable(_Player,0);
CharacterSetImmortal(_Player,0);
CharacterUnfreeze(_Player);
PartySetFlag(_Player,"QuestUpdate_CoS_ArenaOfTheOne_End");
//END_REGION

IF
TextEventSet("aoto_phase3end")
THEN
Proc_CoS_AotO_End();

IF
TextEventSet("aoto_cinematic")
THEN
ProcStartMovie("CS_Rescue");

IF
TextEventSet("aoto_cinematic_1")
THEN
CharacterTeleportPartiesToTriggerWithMovie(TRIGGERGUID_S_CoS_LV_HoE_RegionStart_fda0f33f-8438-4057-bdff-5b3c945bdb20,"","CS_Rescue");
EXITSECTION
ItemSetForceSynch(ITEMGUID_S_CoS_AotO_LvBeacon_7704541a-bf3b-44d1-aae4-c49a47b20add,0);
ItemSetForceSynch(ITEMGUID_S_CoS_AotO_LvBeacon_Helper_eb805737-302b-4dea-9027-97a9cb7633a7,0);
ENDEXITSECTION
ParentTargetEdge "CoS_ArenaOfTheOne"
