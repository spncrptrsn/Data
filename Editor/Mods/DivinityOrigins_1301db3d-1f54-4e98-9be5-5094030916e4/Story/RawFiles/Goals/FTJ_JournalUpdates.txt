Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION FTJ_Seeker

DB_FTJ_Seeker_DissidentMagisterUpdates("QuestUpdate_FTJ_Seeker_AtusaEvent");
DB_FTJ_Seeker_DissidentMagisterUpdates("QuestUpdate_FTJ_Seeker_AtusaLimb");
DB_FTJ_Seeker_DissidentMagisterUpdates("QuestUpdate_FTJ_Seeker_DelorusMet");
DB_FTJ_Seeker_DissidentMagisterUpdates("QuestUpdate_FTJ_Seeker_UnnisMet");
DB_FTJ_Seeker_DissidentMagisterUpdates("QuestUpdate_FTJ_Seeker_UnnisMetAtusa");
DB_FTJ_Seeker_DissidentMagisterUpdates("QuestUpdate_FTJ_Seeker_VasnyaMetStart");

DB_FTJ_Seeker_SeekerUpdates("QuestUpdate_FTJ_Seeker_MeetZaleskar"); 
DB_FTJ_Seeker_SeekerUpdates("QuestUpdate_FTJ_Seeker_ZaleskarShip"); 
DB_FTJ_Seeker_SeekerUpdates("QuestUpdate_FTJ_Seeker_BaharaMetToldSeeker"); 
DB_FTJ_Seeker_SeekerUpdates("QuestUpdate_FTJ_Seeker_MissingLeaders"); 
DB_FTJ_Seeker_SeekerUpdates("QuestUpdate_FTJ_Seeker_GarethMet"); 
DB_FTJ_Seeker_SeekerUpdates("QuestUpdate_FTJ_Seeker_CrucifiedSeekerMet"); 
DB_FTJ_Seeker_SeekerUpdates("QuestUpdate_FTJ_Seeker_SeekerEscape"); 

//END_REGION
//REGION FTJ_Hunted

//DB_FTJ_Hunted_IntermediateUpdateFlag(_HuntedFlag,_UpdateFlag);
DB_FTJ_Hunted_IntermediateUpdateFlag("FTJ_Hunted_GarethHunted","QuestUpdate_FTJ_Hunted_GarethHunted");
DB_FTJ_Hunted_IntermediateUpdateFlag("FTJ_Hunted_BaharaHunted","QuestUpdate_FTJ_Hunted_BaharaHunted");
DB_FTJ_Hunted_IntermediateUpdateFlag("FTJ_Hunted_AlexandarHunted","QuestUpdate_FTJ_Hunted_AlexandarHunted");
DB_FTJ_Hunted_IntermediateUpdateFlag("FTJ_Hunted_IfanHunted","QuestUpdate_FTJ_Hunted_IfanHunted");

//DB_FTJ_Hunted_StoredUpdateFlag(_Player,_UpdateFlag);
//END_REGION
//REGION FTJ_SW_Tyrant

DB_FTJ_SW_Tyrant_ItemUpdate((ITEMGUID)ITEMGUID_S_FTJ_SW_BraccusArmor_Arms_e21fb4cd-2b28-4eaa-a913-153be5f23bb9,"QuestUpdate_FTJ_SW_Tyrant_FoundArms");
DB_FTJ_SW_Tyrant_ItemUpdate((ITEMGUID)ITEMGUID_S_FTJ_SW_BraccusArmor_Legs_0a4896d2-df6d-4c0c-bd79-60315fb77a96,"QuestUpdate_FTJ_SW_Tyrant_FoundLegs");
DB_FTJ_SW_Tyrant_ItemUpdate((ITEMGUID)ITEMGUID_S_FTJ_SW_BraccusArmor_Lowerbody_3ef7e582-a813-40da-a0d4-17b49ee96712,"QuestUpdate_FTJ_SW_Tyrant_FoundLowerBody");
DB_FTJ_SW_Tyrant_ItemUpdate((ITEMGUID)ITEMGUID_S_FTJ_SW_BraccusArmor_Upperbody_3da52544-453d-4bd4-aa95-a2968cf6f647,"QuestUpdate_FTJ_SW_Tyrant_FoundIUpperBody");
DB_FTJ_SW_Tyrant_ItemUpdate((ITEMGUID)ITEMGUID_S_FTJ_ARM_BraccusHelm_Uncured_9e30ecd5-ce72-49d6-a798-c20c8235dd77,"QuestUpdate_FTJ_SW_Tyrant_FoundHelm"); // removed when FTJ_BraccusAxeDemonSpawned is set
//DB_FTlJ_SW_Tyrant_ItemUpdate((ITEMGUID)ITEMGUID_S_FTJ_ARM_BraccusHelm_Cured_92a963b6-d4e0-4dc6-a49d-8b0847bfb7f5,""); // added when FTJ_BraccusAxeDemonSpawned is set

//END_REGION

DB_FTJ_QuestDef_DeadCharacterUpdates((CHARACTERGUID)CHARACTERGUID_S_FTJ_TorturedSeeker_8d839534-a039-4b5c-bfa6-6b88da149eb2,"FTJ_Seeker_VerdasDead");

DB_FTJ_DeadVerdasConditions("QuestUpdate_FTJ_Seeker_AtusaLimb");
DB_FTJ_DeadVerdasConditions("QuestUpdate_FTJ_Seeker_HanMetStart");
DB_FTJ_DeadVerdasConditions("QuestUpdate_FTJ_Seeker_VasnyaMetStart");
DB_FTJ_DeadVerdasConditions("QuestUpdate_FTJ_Seeker_MetVerdas");

//REGION Books

DB_FTJ_BookUpdates(ITEMGUID_S_FTJ_BOOK_AlexandarJournal_000_5c2a4a0e-28bc-4dd1-855b-0fa7f95bb741,"QuestUpdate_CORE_Magisters_VredemanTutor");
DB_FTJ_BookUpdates(ITEMGUID_S_FTJ_SW_CartMagister_LetterDallis_9353f715-5e93-41ef-8b72-721dbda356bb,"QuestUpdate_CORE_Magisters_DallisKnowledge");
DB_FTJ_BookUpdates(ITEMGUID_BOOK_Paper_Sheet_A_000_81088c42-97b3-40de-a9a7-1dda37ba581d,"QuestUpdate_CORE_Magisters_DallisKnowledge");
DB_FTJ_BookUpdates(ITEMGUID_BOOK_Bible_A_000_1ab533fc-02af-43c1-b4d1-0863285bb16b,"QuestUpdate_CORE_Magisters_DallisKnowledge");
DB_FTJ_BookUpdates(ITEMGUID_S_FTJ_SW_CartMagister_LetterDallis_9353f715-5e93-41ef-8b72-721dbda356bb,"QuestUpdate_CORE_Magisters_MagisterBraccus");
DB_FTJ_BookUpdates(ITEMGUID_BOOK_FTJ_TyrantReport_000_64e1bef3-7b8a-458b-832e-ed74874c27c5,"QuestUpdate_CORE_Magisters_MagisterBraccus");

//END_REGION

DB_KilledEvent(CHARACTERGUID_S_GLO_Alexandar_03e6345f-1bd3-403c-80e2-a443a74f6349,"QuestUpdate_CORE_Magisters_AlexandarDead");

//REGION Free_Own Regions
DB_FTJ_Escape_Free_Own_Region((TRIGGERGUID)TRIGGERGUID_S_FTJ_SUB_Chapel_29763898-0722-4f22-bed2-5ec94b837fd3);
DB_FTJ_Escape_Free_Own_Region((TRIGGERGUID)TRIGGERGUID_S_FTJ_SUB_OldHarbour_d6f95039-d76a-48d0-abdf-b8f029cbfe10);
DB_FTJ_Escape_Free_Own_Region((TRIGGERGUID)TRIGGERGUID_S_FTJ_SUB_PrisonGroundFloor_6c759383-8420-4354-ac44-b4fa5b2f7bb5);
DB_FTJ_Escape_Free_Own_Region((TRIGGERGUID)TRIGGERGUID_S_FTJ_SUB_UndergroundTunnel_85d72589-4344-48f8-ab98-98d3926703ba);
DB_FTJ_Escape_Free_Own_Region((TRIGGERGUID)TRIGGERGUID_S_FTJ_SUB_HoldingCells_2cf84586-d155-47be-95bc-372ccd034dc6);
//END_REGION

//REGION Necromancer SJ Pickup Update
DB_FTJ_SW_Necromancers_JarsUpdate((ITEMGUID)ITEMGUID_S_FTJ_SW_SoulJar_GuardUndead1_46ccad9c-a2fe-4004-89ba-f422528b2860,(TRIGGERGUID)TRIGGERGUID_S_FTJ_SW_BraccusVaultProper_2d8c84d5-0f88-47e8-b6ca-f70f43b7d331);
DB_FTJ_SW_Necromancers_JarsUpdate((ITEMGUID)ITEMGUID_S_FTJ_SW_SoulJar_GuardUndead2_40aa483c-fa08-4a95-8d30-99539a0c7137,(TRIGGERGUID)TRIGGERGUID_S_FTJ_SW_BraccusVaultProper_2d8c84d5-0f88-47e8-b6ca-f70f43b7d331);
DB_HasStoryEvent(ITEMGUID_S_FTJ_SW_SoulJar_GuardUndead1_46ccad9c-a2fe-4004-89ba-f422528b2860,"QuestUpdate_FTJ_SW_Necromancers_DealSJ");
DB_HasStoryEvent(ITEMGUID_S_FTJ_SW_SoulJar_GuardUndead2_40aa483c-fa08-4a95-8d30-99539a0c7137,"QuestUpdate_FTJ_SW_Necromancers_DealSJ");
KBSECTION
//REGION Debug
IF
TextEventSet("FTJ_Hunted")
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Hunted_AlexandarHunted");

IF
TextEventSet("InterFTJ_Hunted")
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"FTJ_Hunted_AlexandarHunted");

//END_REGION

//REGION Early FTJ_Escape_Island entries

//If you encountered some FTJ_Escape_Island related entries before starting FTJ_Escape_Island; give them once FTJ_Escape_Island has started

IF
ObjectFlagSet("QuestUpdate_FTJ_Escape_EscapeHan",(CHARACTERGUID)_Player,_Id)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_EnterDunes",0)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Island_EscapeFort");
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Island_EscapeHan");

IF
ObjectFlagSet("QuestUpdate_FTJ_Escape_DallisWarning",(CHARACTERGUID)_Player,_Id)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_EnterDunes",0)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Island_EscapeFort");
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Island_DallisWarning");

IF
ObjectFlagSet("QuestUpdate_FTJ_Escape_VasnyaShelter",(CHARACTERGUID)_Player,_Id)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_EnterDunes",0)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Island_EscapeFort");
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Island_VasnyaShelter");

//If you triggered the early entries at the "right" time (after triggering EnterDunes)

IF
ObjectFlagSet("QuestUpdate_FTJ_Escape_EscapeHan",(CHARACTERGUID)_Player,_Id)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_EnterDunes",1)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Island_EscapeHan");

IF
ObjectFlagSet("QuestUpdate_FTJ_Escape_DallisWarning",(CHARACTERGUID)_Player,_Id)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_EnterDunes",1)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Island_DallisWarning");

IF
ObjectFlagSet("QuestUpdate_FTJ_Escape_VasnyaShelter",(CHARACTERGUID)_Player,_Id)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_EnterDunes",1)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Island_VasnyaShelter");



//END_REGION

//REGION SourceCollar Death Closing

IF
DB_Dead(CHARACTERGUID_S_FTJ_GhettoBlacksmith_175b32ec-f6a1-4aa1-9028-d02d593f092c)
AND
DB_IsPlayer(_Player)
AND
DB_Sees(_Player, CHARACTERGUID_S_FTJ_GhettoBlacksmith_175b32ec-f6a1-4aa1-9028-d02d593f092c)
AND
ObjectGetFlag(_Player, "QuestUpdate_RC_FTJ_SourceCollar_MetBlackSmith", 1)
THEN
PartySetFlag(_Player,"QuestClose_RC_FTJ_SourceCollar_SUB");


IF
DB_Dead(CHARACTERGUID_S_FTJ_SW_CollarRemovingSeeker_437c00b3-6544-44dd-8396-a43848b19a1e)
AND
DB_IsPlayer(_Player)
AND
DB_Sees(_Player, CHARACTERGUID_S_FTJ_SW_CollarRemovingSeeker_437c00b3-6544-44dd-8396-a43848b19a1e)
AND
ObjectGetFlag(_Player, "QuestUpdate_RC_FTJ_SourceCollar_MetCollarRemover", 1)
THEN
PartySetFlag(_Player,"QuestClose_RC_FTJ_SourceCollar_SUB");

//END_REGION

//REGION FTJ_Escape

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_FTJ_SUB_Courtroom_7b9ba26e-b60f-4a09-aba1-728d50b1e133)
AND
DB_IsPlayer(_Player)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Escape_EnterUpperArea");

PROC
Proc_FTJ_Escape_FoundWandUpdate((CHARACTERGUID)_Player)
AND
QuestAccepted(_Player,"FTJ_SW_Shriekers",1)
THEN
UserSetFlag((CHARACTERGUID)_Player,"QuestUpdate_FTJ_Escape_FindWand_Shrieker");

//END_REGION
//REGION FTJ_Seeker

//--- Tarlene - FTJ_SW_SeekerNearSourceTotem
IF
ObjectFlagSet("FTJ_Seeker_SeekerTrueGoal_SetUpdates",_Player,_)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_LucianEncounter",0)
THEN
UserSetFlag((CHARACTERGUID)_Player,"QuestUpdate_FTJ_Seeker_SeekerTrueGoal");

IF
ObjectFlagSet("FTJ_Seeker_SeekerTrueGoal_SetUpdates",_Player,_)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_LucianEncounter",1)
THEN
UserSetFlag((CHARACTERGUID)_Player,"QuestUpdate_FTJ_Seeker_SeekerTrueGoalGod");

IF
ObjectFlagSet("FTJ_SW_LucienGaveBless",(CHARACTERGUID)_Player,_)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Seeker_SeekerTrueGoal",1)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Seeker_SeekerTrueGoalAfterGod");


//--- VERDAS FTJ_Seeker_VerdasDead
IF
ObjectFlagSet("FTJ_TorturedSeeker_HasMet",(CHARACTERGUID)_Player,_)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Seeker_VerdasMet");

IF
ObjectFlagSet("FTJ_Seeker_VerdasDead",(CHARACTERGUID)_Player,_)
AND
QRY_FTJ_seeker_KnowOfVerdas(_Player)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Seeker_VerdasDead");

QRY
QRY_FTJ_seeker_KnowOfVerdas((CHARACTERGUID)_Player)
AND
DB_FTJ_DeadVerdasConditions(_Flag)
AND
ObjectGetFlag(_Player,_Flag,1)
THEN
DB_NOOP(1);



//--- HAN
IF
ObjectFlagSet("FTJ_Seeker_HanMet_SetUpdates",(CHARACTERGUID)_Player,_)
AND
UserGetFlag(_Player,"QuestUpdate_FTJ_Seeker_VerdasMet",0)
AND
ObjectGetFlag(_Player,"FTJ_Seeker_VerdasDead",0)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Seeker_HanMetStart");

IF
ObjectFlagSet("FTJ_Seeker_HanMet_SetUpdates",(CHARACTERGUID)_Player,_)
AND
UserGetFlag(_Player,"QuestUpdate_FTJ_Seeker_VasnyaMetStart",1)
AND
UserGetFlag(_Player,"QuestUpdate_FTJ_Seeker_VerdasMet",0)
AND
ObjectGetFlag(_Player,"FTJ_Seeker_VerdasDead",0)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Seeker_HanVasnyaVerdasMet");

IF
ObjectFlagSet("FTJ_Seeker_HanMet_SetUpdates",(CHARACTERGUID)_Player,_)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Seeker_VerdasMet",1)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Seeker_HanVerdas");

IF
ObjectFlagSet("FTJ_Seeker_HanMet_SetUpdates",(CHARACTERGUID)_Player,_)
AND
ObjectGetFlag(_Player,"FTJ_Seeker_VerdasDead",1)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Seeker_HanVerdas");




//--- FTJ_Seeker_MagsiterOrders
IF
GameBookInterfaceClosed(S_FTJ_SW_MagisterOrders_1ea1d33a-368a-4a76-9da0-7c39e9899803, _Player)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Seeker_MagsiterOrders");

IF
GameBookInterfaceClosed(ITEMGUID_S_FTJ_MessageFromDallis_20f60e2d-7a0a-4366-a20f-c17b7d98a171, _Player)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Escape_DallisWarning");
UserSetFlag(_Player,"QuestUpdate_FTJ_Seeker_DallisWarning");


//--- VASNYA
IF
ObjectFlagSet("FTJ_Seeker_VasnyaMet_SetUpdates",_Player,_)
AND
ObjectGetFlag(_Player,"FTJ_Seeker_VerdasDead",1)
THEN
UserSetFlag((CHARACTERGUID)_Player,"QuestUpdate_FTJ_Seeker_VasnyaDeadVerdas");

IF
ObjectFlagSet("FTJ_Seeker_VasnyaMet_SetUpdates",(CHARACTERGUID)_Player,_)
AND
ObjectGetFlag(_Player,"FTJ_Seeker_VerdasDead",0)
AND
UserGetFlag(_Player,"QuestUpdate_FTJ_Seeker_VerdasMet",1)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Seeker_VasnyaVerdas");

IF
ObjectFlagSet("FTJ_Seeker_VasnyaMet_SetUpdates",(CHARACTERGUID)_Player,_)
AND
ObjectGetFlag(_Player,"FTJ_Seeker_VerdasDead",0)
AND
UserGetFlag(_Player,"QuestUpdate_FTJ_Seeker_VerdasMet",0)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Seeker_VasnyaMetStart");



//--- Dissident & seeker
IF
ObjectFlagSet(_Update,(CHARACTERGUID)_Player,_)
AND
DB_IsPlayer(_Player)
AND
DB_FTJ_Seeker_SeekerUpdates(_Update)
AND
QRY_FTJ_Seeker_KnowAboutDissident(_Player)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Seeker_DissidentSeekers");

IF
ObjectFlagSet(_Update,(CHARACTERGUID)_Player,_)
AND
DB_IsPlayer(_Player)
AND
DB_FTJ_Seeker_DissidentMagisterUpdates(_Update)
AND
QRY_FTJ_Seeker_KnowAboutSeeker(_Player)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Seeker_DissidentSeekers");


QRY
QRY_FTJ_Seeker_KnowAboutDissident((CHARACTERGUID)_Player)
AND
DB_FTJ_Seeker_DissidentMagisterUpdates(_Flag)
AND
ObjectGetFlag(_Player,_Flag,1)
THEN
DB_NOOP(1);

QRY
QRY_FTJ_Seeker_KnowAboutSeeker((CHARACTERGUID)_Player)
AND
DB_FTJ_Seeker_SeekerUpdates(_Flag)
AND
ObjectGetFlag(_Player,_Flag,1)
THEN
DB_NOOP(1);

//END_REGION 
//REGION FTJ_Hunted

// some of FTJ_Hunted's updates need the quest FTJ_Godwoken to be given
IF
ObjectFlagSet(_HuntedFlag,(CHARACTERGUID)_Player,_)
AND
DB_FTJ_Hunted_IntermediateUpdateFlag(_HuntedFlag,_UpdateFlag)
AND
QRY_FTJ_Hunted_HasQuestFTJ_Godwoken(_Player)
THEN
UserSetFlag(_Player,_UpdateFlag);


IF
ObjectFlagSet(_HuntedFlag,(CHARACTERGUID)_Player,_)
AND
DB_FTJ_Hunted_IntermediateUpdateFlag(_HuntedFlag,_UpdateFlag)
AND
NOT QRY_FTJ_Hunted_HasQuestFTJ_Godwoken(_Player)
THEN
DB_FTJ_Hunted_StoredUpdateFlag(_Player,_UpdateFlag);

QRY
QRY_FTJ_Hunted_HasQuestFTJ_Godwoken((CHARACTERGUID)_Player)
AND
DB_IsPlayer(_OtherChar)
AND
CharacterIsInPartyWith(_OtherChar,_Player,1)
AND
QuestAccepted(_OtherChar,"FTJ_Godwoken",1)
THEN
DB_NOOP(1);


IF
ObjectFlagSet("FTJ_SW_LucienGaveBless",(CHARACTERGUID)_Player,_)
AND
DB_FTJ_Hunted_StoredUpdateFlag(_Player,_UpdateFlag)
THEN
UserSetFlag(_Player,_UpdateFlag);

IF
ObjectFlagSet(_UpdateFlag,(CHARACTERGUID)_Player,_)
AND
DB_FTJ_Hunted_StoredUpdateFlag(_Player,_UpdateFlag)
THEN
NOT DB_FTJ_Hunted_StoredUpdateFlag(_Player,_UpdateFlag);


//FTJ_Ifan_DarkFaction_GarethInfo
IF
ObjectFlagSet("QuestUpdate_FTJ_Hunted_GarethHunted",CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,_)
AND
IsTagged(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"AVATAR",1)
THEN
ObjectSetFlag(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"QuestUpdate_FTJ_Ifan_DarkFaction_GarethInfo");
//UserSetFlag(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"QuestUpdate_FTJ_Hunted_IfanHunted");

IF
ObjectFlagSet("QuestUpdate_FTJ_Hunted_BaharaHunted",CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,_)
AND
IsTagged(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"AVATAR",1)
THEN
ObjectSetFlag(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"QuestUpdate_FTJ_Ifan_DarkFaction_GarethInfo");
//UserSetFlag(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"QuestUpdate_FTJ_Hunted_IfanHunted");

IF
ObjectFlagSet("QuestUpdate_FTJ_Hunted_AlexandarHunted",CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,_)
AND
IsTagged(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"AVATAR",1)
THEN
ObjectSetFlag(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"QuestUpdate_FTJ_Ifan_DarkFaction_GarethInfo");
//UserSetFlag(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"QuestUpdate_FTJ_Hunted_IfanHunted");


IF
ObjectFlagSet("FTJ_IfanLoneWolf_SetUpdates",CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,_)
AND
IsTagged(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"AVATAR",1)
THEN
ObjectSetFlag(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"QuestUpdate_FTJ_Ifan_DarkFaction_GarethInfo");

IF
ObjectFlagSet("FTJ_IfanLoneWolf_SetUpdates",CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,_)
AND
IsTagged(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"AVATAR",0)
AND
DB_CompanionAvatarBond(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,_Avatar)
THEN
UserSetFlag(_Avatar,"QuestUpdate_FTJ_COM_Ifan_GarethInfo");
UserSetFlag(_Avatar,"QuestUpdate_FTJ_Hunted_IfanHunted");


// winnebago
//DB_QuestDef_KillCharacterUpdate(CHARACTERGUID_S_GLO_Windego_d783285f-d3be-4cba-8333-db8976cef182,"FJ_FortJoy_Main","QuestUpdate_FTJ_Hunted_WindegoKill");
PROC
Proc_ObjectLeftCombat(CHARACTERGUID_S_GLO_Windego_d783285f-d3be-4cba-8333-db8976cef182,_CombatID)
AND
DB_Dead(CHARACTERGUID_S_GLO_Windego_d783285f-d3be-4cba-8333-db8976cef182)
AND
DB_IsPlayer(_Player)
AND
DB_WasInCombat(_Player,_CombatID)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Hunted_WindegoKill");

PROC
Proc_ObjectLeftCombat(CHARACTERGUID_S_GLO_Windego_d783285f-d3be-4cba-8333-db8976cef182,_CombatID)
AND
DB_Dead(CHARACTERGUID_S_GLO_Windego_d783285f-d3be-4cba-8333-db8976cef182)
AND
DB_IsPlayer(_Player)
AND
DB_CombatCharacters(_Player,_CombatID)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Hunted_WindegoKill");

IF
CharacterKilledBy(CHARACTERGUID_S_GLO_Windego_d783285f-d3be-4cba-8333-db8976cef182,_Player,_)
AND
DB_IsPlayer(_Player)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Hunted_WindegoKill");


//DOSTWO-24348 FTJ_Hunted_TrackHunter
IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Player,_)
AND
DB_FTJ_Hunted_IntermediateUpdateFlag(_,_Flag)
AND
_Flag != "QuestUpdate_FTJ_Hunted_AlexandarHunted"
THEN
UserSetFlag((CHARACTERGUID)_Player,"QuestUpdate_FTJ_Hunted_TrackHunter");

//END_REGION
//REGION FTJ_Voice

IF
ObjectFlagSet("FTJ_SW_LucienGaveBless",(CHARACTERGUID)_Player,_)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Voice_GodMet");

//END_REGION
//REGION FTJ_Godwoken

IF
ObjectFlagSet("FTJ_SW_LucienGaveBless",(CHARACTERGUID)_Player,_)
AND
_Player != CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Godwoken_GodMission");

IF
ObjectFlagSet("FTJ_SW_LucienGaveBless",CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,_)
THEN
ObjectSetFlag(CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,"QuestUpdate_FTJ_Godwoken_GodMission_Lohse");

//END_REGION
//REGION FTJ_OlgoSaheila

IF
ObjectFlagSet("RC_FTJ_OlgoSaheila_RatHint",(CHARACTERGUID)_Player,_)
AND
UserGetFlag(_Player,"QuestUpdate_RC_FTJ_OlgoSaheila_GriffHint",1)
THEN
UserSetFlag(_Player,"QuestUpdate_RC_FTJ_OlgoSaheila_RatHint");


//END_REGION
//REGION FTJ_Shriekers

IF
ItemAddedToCharacter(ITEMGUID_S_FTJ_SW_WitchPurgingWand_10c04dcc-ee41-4bb2-b6b5-fd9920119180,_Player)
AND
DB_IsPlayer(_Player)
THEN
PartySetFlag((CHARACTERGUID)_Player,"QuestUpdate_FTJ_SW_Shriekers_FoundWandRadeka");
Proc_FTJ_Escape_FoundWandUpdate((CHARACTERGUID)_Player);

IF
ItemAddedToCharacter(ITEMGUID_S_FTJ_SW_VaultPurgingWand_fb552404-f2a0-4f7f-80ed-d44bf62dba03,_Player)
AND
DB_IsPlayer(_Player)
THEN
PartySetFlag((CHARACTERGUID)_Player,"QuestUpdate_FTJ_SW_Shriekers_FoundWandVault");
Proc_FTJ_Escape_FoundWandUpdate((CHARACTERGUID)_Player);

IF
CharacterUsedSkillOnTarget(_Player,(CHARACTERGUID)_Shrieker,"Target_SourceDisruption",_,_)
AND
DB_IsPlayer(_Player)
AND
DB_FTJ_SW_SourceTotems(_Shrieker,_,_)
THEN
PartySetFlag((CHARACTERGUID)_Player,"QuestUpdate_FTJ_SW_Shriekers_FirstKill_Shrieker");

IF
CharacterUsedSkillOnTarget(_Player,(CHARACTERGUID)_Shrieker,_Skill,_,_)
AND
DB_SourceVampirism_Skills(_Skill)
AND
DB_IsPlayer(_Player)
AND
DB_FTJ_SW_SourceTotems(_Shrieker,_,_)
THEN
PartySetFlag((CHARACTERGUID)_Player,"QuestUpdate_FTJ_SW_Shriekers_FirstKill_Shrieker");

//END_REGION
//REGION BatteredAndCornered - Check if Gareth is still alive & then trigger two other entries

IF
ObjectFlagSet("QuestUpdate_FTJ_SW_BatteredAndCornered_GarethAfterWeapons",(CHARACTERGUID)_Player,_)
AND
DB_IsPlayer(_Player)
AND
UserGetFlag(_Player,"FTJ_SW_FoundDeadGareth",0)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Escape_Island_FindGareth");
PartySetFlag(_Player,"QuestUpdate_FTJ_SW_Shriekers_GarethHelp");

//END_REGION
//REGION FTJ_SW_Tyrant

IF
GameBookInterfaceClosed(ITEMGUID_BOOK_FTJ_TyrantReport_000_64e1bef3-7b8a-458b-832e-ed74874c27c5,_Player)
THEN
PartySetFlag(_Player,"QuestUpdate_FTJ_SW_Tyrant_FoundReport");


IF
ItemAddedToCharacter(_Piece,_Player)
AND
DB_IsPlayer(_Player)
AND
DB_FTJ_SW_Tyrant_ItemUpdate(_Piece,_Flag)
THEN
PartySetFlag(_Player,_Flag);
PROC_FTJ_SW_Tyrant_CheckHasAllPieces(_Player);

IF
ItemAddedToCharacter(ITEMGUID_S_FTJ_ARM_BraccusHelm_Cured_92a963b6-d4e0-4dc6-a49d-8b0847bfb7f5,_Player)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_SW_Tyrant_FoundHelm",0)
THEN
PartySetFlag(_Player,"QuestUpdate_FTJ_SW_Tyrant_FoundHelmCured");


PROC
PROC_FTJ_SW_Tyrant_CheckHasAllPieces((CHARACTERGUID)_Player)
AND
NOT QRY_FTJ_SW_Tyrant_MissingPieces(_Player)
THEN
PartySetFlag(_Player,"QuestUpdate_FTJ_SW_Tyrant_FoundAll");

QRY
QRY_FTJ_SW_Tyrant_MissingPieces((CHARACTERGUID)_Player)
AND
DB_FTJ_SW_Tyrant_ItemUpdate(_Piece,_)
AND
ItemIsInPartyInventory(_Piece,_Player,0,0)
THEN
DB_NOOP(1);


IF
GlobalFlagSet("FTJ_BraccusAxeDemonSpawned")
THEN
NOT DB_FTJ_SW_Tyrant_ItemUpdate((ITEMGUID)ITEMGUID_S_FTJ_ARM_BraccusHelm_Uncured_9e30ecd5-ce72-49d6-a798-c20c8235dd77,"QuestUpdate_FTJ_SW_Tyrant_FoundHelm");
DB_FTJ_SW_Tyrant_ItemUpdate((ITEMGUID)ITEMGUID_S_FTJ_ARM_BraccusHelm_Cured_92a963b6-d4e0-4dc6-a49d-8b0847bfb7f5,"");

//END_REGION
//REGION Dead character updates

//Dead
IF
CharacterSawCharacter(_Player,_Char)
AND
DB_FTJ_QuestDef_DeadCharacterUpdates(_Char,_Update)
AND
DB_IsPlayer(_Player)
AND
DB_Dead(_Char)
THEN
ObjectSetFlag(_Player,_Update);

IF
CharacterDied(_Char)
AND
DB_FTJ_QuestDef_DeadCharacterUpdates(_Char,_Update)
AND
DB_IsPlayer(_Player)
AND
CharacterCanSee(_Player,_Char,1)
THEN
ObjectSetFlag(_Player,_Update);
//END_REGION
//REGION FTJ_SW_BraccusArmory - Rat hint

IF
ObjectFlagSet("FTJ_SW_BraccusArmory_RatHint_SetUpdate",(CHARACTERGUID)_Player,_)
AND
Qry_UserHasBless(_Player)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_SW_BraccusArmory_RatHintGotBless");

IF
ObjectFlagSet("FTJ_SW_BraccusArmory_RatHint_SetUpdate",(CHARACTERGUID)_Player,_)
AND
NOT Qry_UserHasBless(_Player)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_SW_BraccusArmory_RatHintNoBless");

IF
SkillAdded(_Player, "Target_Bless", _)
AND
UserGetFlag(_Player,"QuestUpdate_FTJ_SW_BraccusArmory_RatHintNoBless",1)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_SW_BraccusArmory_RatHintFoundBless");


QRY
Qry_UserHasBless((CHARACTERGUID)_Player)
AND
CharacterGetReservedUserID(_Player,_ID)
AND
DB_IsPlayer(_Char)
AND
CharacterGetReservedUserID(_Char,_ID)
AND
CharacterHasSkill(_Char,"Target_Bless",1)
THEN
DB_NOOP(1);

//END_REGION

//REGION Undead Necromancer SJ

IF
CharacterEnteredTrigger(_Player, _Trigger)
AND
DB_IsPlayer(_Player)
AND
DB_FTJ_SW_Necromancers_JarsUpdate(_Jar,_Trigger)
AND
CharacterCanSee(_Player,_Jar,1)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_SW_Necromancers_DealSJ");

IF
CharacterUsedItem(_Player,_Jar)
AND
DB_IsPlayer(_Player)
AND
DB_FTJ_SW_Necromancers_JarsUpdate(_Jar,_)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_SW_Necromancers_DealSJ");

//END_REGION

// Origin
//REGION BEAST
IF
ItemUnlocked(_,_Player,ITEMGUID_S_FTJ_ChapelUpperFloorDoorKey_2_144ece86-8427-466d-b87c-722ad238e268)
AND
Qry_FTJ_CarsonKey_BeastInParty(_Player)
THEN
Proc_Beast_CarsonKey_GiveUpdate();


QRY
Qry_FTJ_CarsonKey_BeastInParty((CHARACTERGUID)_Player)
AND
_Player == CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978
THEN
DB_NOOP(1);

QRY
Qry_FTJ_CarsonKey_BeastInParty((CHARACTERGUID)_Player)
AND
CharacterIsInPartyWith(_Player,CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,1)
THEN
DB_NOOP(1);


PROC
Proc_Beast_CarsonKey_GiveUpdate()
AND
DB_Avatars(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978)
AND
ObjectGetFlag(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,"QuestUpdate_FTJ_OriginBeast_CarsonKeyUnused",0)
THEN
ObjectSetFlag(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,"QuestUpdate_FTJ_OriginBeast_CarsonKey");

PROC
Proc_Beast_CarsonKey_GiveUpdate()
AND
NOT DB_Avatars(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978)
AND
DB_IsPlayer(_Player)
AND
DB_CompanionAvatarBond(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_COM_Beast_CarsonKeyUnused",0)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_COM_Beast_CarsonKey");

//Left FTJ without using the key DOSTWO-24286

PROC
Proc_Beast_CarsonKey_GiveUpdate_NotUsed()
AND
NOT DB_Avatars(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978)
AND
DB_IsPlayer(_Player)
AND
DB_CompanionAvatarBond(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_COM_Beast_CarsonKey",0)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_COM_Beast_CarsonKeyUnused");

PROC
Proc_Beast_CarsonKey_GiveUpdate_NotUsed()
AND
DB_Avatars(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978)
AND
ObjectGetFlag(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,"QuestUpdate_FTJ_OriginBeast_CarsonKey",0)
THEN
UserSetFlag(CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,"QuestUpdate_FTJ_OriginBeast_CarsonKeyUnused");

IF
ObjectFlagSet("QuestUpdate_FTJ_Escape_EnterDunes",CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,_)
THEN
Proc_Beast_CarsonKey_GiveUpdate_NotUsed();

//END_REGION
//REGION SEBILLE

IF
CharacterSawCharacter(CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,CHARACTERGUID_S_FTJ_LizardDreamer_ffb80725-25ff-4174-98cb-60a11612e5c4)
AND
IsTagged(CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,"AVATAR",1)
THEN
ObjectSetFlag(CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,"QuestUpdate_FTJ_OriginSebille_DreamerFound");

//END_REGION

//REGION FREE_OWN JOURNAL UPDATE

//Also set in:
//FTJ_UnderGroundPrison
//FTJ_Shrine

IF
CharacterEnteredTrigger(_Player,_Trigger)
AND
DB_IsPlayer(_Player)
AND
DB_FTJ_Escape_Free_Own_Region(_Trigger)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_Free_Own",0)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_EnterUnderground",0)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_DivineStatue_Passage",0)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_Yarrow_KeyUsed",0)
AND
ObjectGetFlag(_Player,"IsInPrison",0)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Escape_Free_Own");

PROC
ProcCrimeTeleportCharacterToPrison((CHARACTERGUID)_Player,(TRIGGERGUID)_)
AND
DB_IsPlayer(_Player)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Escape_Arrested");

//END_REGION

//REGION Give FTJ_SW_CallToArms_FoundWand
IF
ObjectFlagSet("FTJ_SW_HasPurgingWand",(CHARACTERGUID)_Player,_)
AND
DB_IsPlayer(_Player)
AND
UserGetFlag(_Player,"QuestUpdate_FTJ_SW_CallToArms_SuggestPurging",1)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_SW_CallToArms_FoundWand");
//END_REGION

IF
GameBookInterfaceClosed(ITEMGUID_S_FTJ_SW_CartMagister_LetterDallis_9353f715-5e93-41ef-8b72-721dbda356bb,_Player)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_SW_BraccusArmory_FoundMagisterOrders");
UserSetFlag(_Player,"QuestUpdate_FTJ_SW_Shriekers_FoundMagisterOrders");

IF
GameBookInterfaceClosed(_Book,_Player)
AND
DB_FTJ_BookUpdates(_Book,_Update)
THEN
ObjectSetFlag(_Player,_Update);


PROC
ProcRegionEnded("FJ_FortJoy_Main")
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_RC_FTJ_SoulJar_DestroyedSJ",1)
THEN
QuestClose(_Player,"RC_FTJ_SoulJar");


PROC
ProcRegionEnded("FJ_FortJoy_Main")
THEN
GoalCompleted;
EXITSECTION
NOT DB_KilledEvent(CHARACTERGUID_S_GLO_Alexandar_03e6345f-1bd3-403c-80e2-a443a74f6349,"QuestUpdate_CORE_Magisters_AlexandarDead");
ENDEXITSECTION
ParentTargetEdge "FortJoy"
