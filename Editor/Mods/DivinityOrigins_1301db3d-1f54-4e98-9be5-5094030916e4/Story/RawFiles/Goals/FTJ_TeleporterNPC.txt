Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_HasStoryEvent((ITEMGUID)ITEMGUID_S_FTJ_TeleporterNPCQuestItem_c9e1fb97-0c98-4d24-b83c-33755f89b59a,"FTJ_HasTeleportItem");
DB_Dialogs(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,"FTJ_TeleporterNPC");
ProcTriggerRegisterForPlayers(TRIGGERGUID_S_FTJ_TelportPuzzleFirstPlatform_EventTrigger_1c4d9f18-07fb-41f6-b39b-daece074befe);
ItemSetCanInteract(ITEMGUID_S_FTJ_TeleporterHoleEntrance_760b13f5-4762-463e-bb72-2219b42e0c99,0);
ItemToInventory(ITEMGUID_S_FTJ_TeleporterNPCQuestItem_c9e1fb97-0c98-4d24-b83c-33755f89b59a,CHARACTERGUID_S_FTJ_TeleporteQuestrCroc_003_7cf7d4d4-de1a-4ac7-999a-1f128fac3789);
ItemToInventory(ITEMGUID_S_FTJ_DeadTeleporterElf_BodyPart_5c2e999f-1177-4a73-a2bb-a12a0ad23540,CHARACTERGUID_S_FTJ_DeadTeleporterElf_3d4c8c64-9c94-4cf4-8d7c-2ce70f1d06ff);
ItemToInventory(ITEMGUID_S_FTJ_GawinRobe_8fdd6231-e34e-4f73-b13c-ba71a00efe8e,CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d);


DB_GLO_CheckSpeakerNumber(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d, -1.0);


DB_FTJ_TeleporterOnPlatformStates("State_StartTeleportPuzzle");
DB_FTJ_TeleporterOnPlatformStates("State_TeleportPuzzleSecondPhase");
ProcCharacterDisableCrime(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,"FTJCollarless");
ProcCharacterDisableCrime(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,"FTJCollarlessADs");

SetOnStage(ITEMGUID_S_FTJ_TeleporterHoleEntrance_760b13f5-4762-463e-bb72-2219b42e0c99,0);

KBSECTION
IF
ObjectFlagSet("FTJ_TeleporterNPCAquireNewUser",(CHARACTERGUID)_Player,_)
THEN
Proc_StartDialog(0,"FTJ_TeleporterNPC",S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,_Player);

IF
CharacterUsedItem(_Player,ITEMGUID_S_FTJ_DeadTeleporterElf_BodyPart_5c2e999f-1177-4a73-a2bb-a12a0ad23540)
AND
IsTagged((CHARACTERGUID)_Player,"ELF",1)
THEN
ProcShowMarker(_Player,"FTJ_TeleporterElfTreasure");


IF
ItemDestroying(ITEMGUID_S_FTJ_TeleporterTreeTrunk_76c4615a-37bb-48d8-af3d-51690d60a857)
THEN
SetOnStage(ITEMGUID_S_FTJ_TeleporterHoleEntrance_760b13f5-4762-463e-bb72-2219b42e0c99,1);


//Journal Updates
IF
ObjectFlagSet("FTJ_AcceptedTeleport",(CHARACTERGUID)_Player,_)
THEN
ObjectSetFlag(_Player,"QuestAdd_FTJ_Teleporter");
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Teleporter_Start");
UserSetFlag(_Player,"QuestUpdate_FTJ_Escape_TeleportMet");

IF
ObjectFlagSet("FTJ_TeleporterClosed",(CHARACTERGUID)_Player,_)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Teleporter_Alcove");

IF
ObjectFlagSet("FTJ_TeleporterClosed",(CHARACTERGUID)_Player,_)
AND
DB_IsPlayer((CHARACTERGUID)_OtherPlayer)
AND
_Player != _OtherPlayer
AND
PartyGetFlag(_OtherPlayer,"FTJ_TeleporterClosed",0)
THEN
ObjectSetFlag(_OtherPlayer,"QuestUpdate_FTJ_Teleporter_EndOpportunityLost");
ObjectSetFlag(_OtherPlayer,"QuestUpdate_FTJ_Escape_Teleport_Fail");

IF
CharacterUsedSkillOnTarget(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,(CHARACTERGUID)_Player,_Skill,"Teleportation",_)
AND
CharacterGetAbility(_Player,"PainReflection",_Amount)
AND
_Amount > 0
THEN
DB_IgnoreAssault(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d);

IF
CharacterUsedSkillOnTarget(_Player,CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,_Skill,"Teleportation",_)
AND
GlobalGetFlag("FTJ_TeleporterLeaveEntrance",1)
THEN
DB_IgnoreAssault(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d);

IF
AttackedByObject(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,(CHARACTERGUID)_Player,_,_,_)
AND
DB_IsPlayer(_Player)
AND
DB_IgnoreAssault(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d)
THEN
NOT DB_IgnoreAssault(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d);

IF
CharacterLootedCharacterCorpse((CHARACTERGUID)_Player,CHARACTERGUID_S_FTJ_TeleporteQuestrCroc_001_bc1a10a1-51b6-42c5-b517-827565f6512b)
THEN
ProcHideMarker(_Player,"FTJ_TeleporterCroc");

//REGION Check if Player has teleport spell, but not item
IF
DialogStarted("FTJ_TeleporterNPC",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"FTJ_HasTeleportItem",0)
AND
CharacterHasSkill(_Player,"Teleportation_FreeFall",1)
THEN
ObjectSetFlag(_Player,"S_FTJ_HasTeleportSpell");


IF
DialogEnded("FTJ_TeleporterNPC",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"S_FTJ_HasTeleportSpell",1)
THEN
ObjectClearFlag(_Player,"S_FTJ_HasTeleportSpell",0);

//END_REGION

//REGION DOS2EE-3955

IF
SavegameLoaded(_Major,_Minor,_Rev,_Build)
AND
QRY_VersionIsOlderThan(_Major,_Minor,_Rev,_Build,3, 5, 1, 0)
AND
NOT DB_QuestDef_State("FTJ_Escape","Teleport_Fail")
THEN
DB_QuestDef_State("FTJ_Escape","Teleport_Fail");

IF
SavegameLoaded(_Major,_Minor,_Rev,_Build)
AND
QRY_VersionIsOlderThan(_Major,_Minor,_Rev,_Build,3, 5, 1, 0)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Teleporter_EndOpportunityLost",1)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_Teleport_Fail",0)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Teleport_Fail");

IF
SavegameLoaded(_Major,_Minor,_Rev,_Build)
AND
QRY_VersionIsOlderThan(_Major,_Minor,_Rev,_Build,3, 5, 1, 0)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Teleporter_EndFail",1)
AND
ObjectGetFlag(_Player,"QuestUpdate_FTJ_Escape_Teleport_Fail",0)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Teleport_Fail");

//END_REGION

IF
DialogStarted("FTJ_TeleporterNPC",_ID)
AND
ObjectGetFlag(S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,"FTJ_WaitingInAlcove",1)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player,"FTJ_TeleporterClosed",1)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Teleporter_PuzzleStart");
/*
IF
SkillAdded(_Player,"Teleportation_FreeFall",_)
AND
UserGetFlag(_Player,"QuestUpdate_FTJ_Teleporter_Start",1)
THEN
UserSetFlag(_Player,"QuestUpdate_FTJ_Teleporter_Alcove");
*/

IF
ItemAddedToCharacter(ITEMGUID_S_FTJ_TeleporterNPCQuestItem_c9e1fb97-0c98-4d24-b83c-33755f89b59a,_Player)
AND
DB_IsPlayer(_Player)
AND
QueryOnlyOnce("FTJ_Teleporter_GloveTaken")
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Teleporter_DropGlove");

//REGION Player leaves Gawin on first Platform

IF
CharacterLeftTrigger(_Player,TRIGGERGUID_S_FTJ_SUB_HiddenAlcove_ccfb91cd-1de7-4758-9f21-cbb871fee147)
AND
DB_IsPlayer(_Player)
AND
GetClosestPlayer(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,_ClosestPlayer,_)
AND
CharacterIsDead(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,0)
AND
CharacterCanSee(_ClosestPlayer,CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,0)
AND
GetDistanceTo(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,_Player,_Dist)
AND
DB_FTJ_TeleporterOnPlatformStates(_PuzzleState)
AND
GetVarFixedString(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,"currentState",_PuzzleState)
AND
_Dist > 30.0
THEN
TeleportTo(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,TRIGGERGUID_S_FTJ_GawinDeath_Point_ed67b0f1-a238-46c0-8e38-03da8e2087e3);
ItemDestroy(ITEMGUID_S_FTJ_TeleporterTreeTrunk_76c4615a-37bb-48d8-af3d-51690d60a857);
CharacterDie(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,1,"DoT");

//END_REGION



//REGION Quest End

IF
AutomatedDialogEnded("FTJ_AD_TeleporterNPCDeath",_)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"FTJ_TeleporterClosed",1)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Teleporter_End");
ProcHideMarker(_Player,"RC_FTJ_Alcove");

IF
CharacterDied(S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"FTJ_TeleporterClosed",1)
AND
GlobalGetFlag("FTJ_TeleporterEscaped",1)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Teleporter_End");
ProcHideMarker(_Player,"RC_FTJ_Alcove");

IF
CharacterDied(S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d)
AND
DB_IsPlayer(_Player)
AND
ObjectGetFlag(_Player,"FTJ_TeleporterClosed",1)
AND
GlobalGetFlag("FTJ_TeleporterEscaped",0)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Teleporter_EndFail");
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Escape_Teleport_Fail");
ProcHideMarker(_Player,"RC_FTJ_Alcove");
//END_REGION

IF
CharacterDied(S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d)
AND
DB_IsPlayer(_Player)
AND
DB_InRegion(_Player,TRIGGERGUID_S_FTJ_TelportPuzzleFirstPlatform_EventTrigger_1c4d9f18-07fb-41f6-b39b-daece074befe)
AND
CharacterIsInCombat(_Player,0)
AND
QueryOnlyOnce("FTJ_StuckOnPlatformVB_OneShot")
THEN
CharacterLookAt(_Player,ITEMGUID_S_FTJ_TeleporterTreeTrunk_76c4615a-37bb-48d8-af3d-51690d60a857);
StartVoiceBark("FTJ_VB_StuckOnPlatform",_Player);

IF
GlobalFlagSet("FTJ_TeleporterEscaped")
AND
DB_IsPlayer(_Player)
AND
DB_InRegion(_Player,TRIGGERGUID_S_FTJ_TelportPuzzleFirstPlatform_EventTrigger_1c4d9f18-07fb-41f6-b39b-daece074befe)
AND
CharacterIsInCombat(_Player,0)
AND
QueryOnlyOnce("FTJ_StuckOnPlatformVB_OneShot")
THEN
CharacterLookAt(_Player,ITEMGUID_S_FTJ_TeleporterTreeTrunk_76c4615a-37bb-48d8-af3d-51690d60a857);
StartVoiceBark("FTJ_VB_StuckOnPlatform",_Player);

IF
VoiceBarkStarted("FTJ_VB_StuckOnPlatform",_)
AND
DB_IsPlayer(_Player)
AND
DB_InRegion(_Player,TRIGGERGUID_S_FTJ_TelportPuzzleFirstPlatform_EventTrigger_1c4d9f18-07fb-41f6-b39b-daece074befe)
AND
CharacterIsInCombat(_Player,0)
THEN
TimerLaunch("FTJ_SecondTeleporterHoleHint",20000);

IF
TimerFinished("FTJ_SecondTeleporterHoleHint")
AND
DB_IsPlayer(_Player)
AND
DB_InRegion(_Player,TRIGGERGUID_S_FTJ_TelportPuzzleFirstPlatform_EventTrigger_1c4d9f18-07fb-41f6-b39b-daece074befe)
AND
QueryOnlyOnce("FTJ_StuckOnPlatformVBSecondHint_OneShot")
THEN
StartVoiceBark("FTJ_VB_StuckOnPlatform",_Player);

//Fall into the Prison
/*
IF
CharacterUsedItem(_Char,ITEMGUID_S_FTJ_CaveEntranceToPrison_e2cbcf37-16ed-420d-ab04-41c1723bd188)
THEN
CharacterDetachFromGroup(_Char);
TeleportTo(_Char,S_FTJ_CaveEntranceToPrison_Point_48612598-a9f3-4893-ad93-1a9002c243f6,"FTJ_ShortKnockdown",1);
*/

IF
StoryEvent(_Char,"FTJ_ShortKnockdown")
THEN
ApplyStatus(_Char,"KNOCKED_DOWN",1.0,1);

IF
CharacterUsedItem(_Char,ITEMGUID_S_FTJ_CaveEntranceToPrison_e2cbcf37-16ed-420d-ab04-41c1723bd188)
AND
DB_IsPlayer(_Char)
AND
QueryOnlyOnce("FTJ_VB_FallIntoPrison_OneShot")
THEN
ProcObjectTimer(_Char,"FTJ_VB_FallIntoPrison_Timer",2200);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char,"FTJ_VB_FallIntoPrison_Timer")
THEN
StartVoiceBark("FTJ_VB_FallIntoPrison",_Char);

IF
TextEventSet("tpAlcove")
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"QuestAdd_FTJ_Teleporter");
ObjectSetFlag(_Player,"QuestUpdate_FTJ_Teleporter_PuzzleStart");
ObjectSetFlag((CHARACTERGUID)_Player,"FTJ_TeleporterClosed");
TeleportTo(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d,TRIGGERGUID_S_FTJ_TelepoerterTeleportPlayerTo_Point_6ae7f67a-e72e-43e1-a4c4-866c680d4ec9);

IF
CharacterDying(CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d)
THEN
ItemSetStoryItem(ITEMGUID_S_FTJ_GawinRobe_8fdd6231-e34e-4f73-b13c-ba71a00efe8e,0);


IF
ItemDestroyed(ITEMGUID_S_FTJ_TeleporterTreeTrunk_76c4615a-37bb-48d8-af3d-51690d60a857)
THEN
ItemSetCanInteract(ITEMGUID_S_FTJ_TeleporterHoleEntrance_760b13f5-4762-463e-bb72-2219b42e0c99,1);


//REGION Gawin Robe Remove Story Object

IF
ItemRemovedFromCharacter(ITEMGUID_S_FTJ_GawinRobe_8fdd6231-e34e-4f73-b13c-ba71a00efe8e,CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d)
THEN
ItemSetStoryItem(ITEMGUID_S_FTJ_GawinRobe_8fdd6231-e34e-4f73-b13c-ba71a00efe8e,0);

IF
ItemAddedToCharacter(ITEMGUID_S_FTJ_GawinRobe_8fdd6231-e34e-4f73-b13c-ba71a00efe8e,CHARACTERGUID_S_FTJ_TeleporterNPC_4e291594-10ec-4c1f-a126-936e45ff875d)
THEN
ItemSetStoryItem(ITEMGUID_S_FTJ_GawinRobe_8fdd6231-e34e-4f73-b13c-ba71a00efe8e,1);

//END_REGION


PROC
ProcRegionEnded("FJ_FortJoy_Main")
THEN
GoalCompleted;

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "FortJoy"
