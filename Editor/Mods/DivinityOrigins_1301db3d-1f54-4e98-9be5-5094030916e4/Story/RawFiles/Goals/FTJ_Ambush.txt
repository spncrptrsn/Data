Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_AmbushTrigger(S_FTJ_FrogAmbush_Box_01_10df8c5b-5811-41b4-88b7-e7e884ced161,S_FTJ_FrogAmbush_InvisibleHelper_ced520a8-3233-4f4d-89c4-a00323effdcb);
DB_FTJ_FrogAmbush((CHARACTERGUID)S_FTJ_FrogAmbush_Melee_01_747af1e4-d204-4564-9a50-9f1955dd4723);
DB_FTJ_FrogAmbush((CHARACTERGUID)S_FTJ_FrogAmbush_Ranged_02_ffae5e44-ac8a-4f43-ab14-2e684b60d87b);
DB_FTJ_FrogAmbush((CHARACTERGUID)S_FTJ_FrogAmbush_Ranged_03_18d2b17c-a400-4e1d-991f-d1cbb44cfac4);

DB_KillCounter_Internal(CHARACTERGUID_S_FTJ_FrogAmbush_Melee_01_747af1e4-d204-4564-9a50-9f1955dd4723,"FTJ_FrogAmbush");
DB_KillCounter_Internal(CHARACTERGUID_S_FTJ_FrogAmbush_Ranged_02_ffae5e44-ac8a-4f43-ab14-2e684b60d87b,"FTJ_FrogAmbush");
DB_KillCounter_Internal(CHARACTERGUID_S_FTJ_FrogAmbush_Ranged_03_18d2b17c-a400-4e1d-991f-d1cbb44cfac4,"FTJ_FrogAmbush");
DB_KillCounter("FTJ_FrogAmbush",3);


DB_AmbushTrigger(S_FTJ_SW_VWBoss_ProximityBox_01_53804249-93b6-4756-ae5c-f7d954b6e36b,S_FTJ_VoidWokenBoss_InvisibleHelper_6901d2e4-d6d7-4872-8274-c9f5c58e2a9f);
DB_VWBossAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_SW_VWBoss_Mage_01_5cf41c21-bfed-499e-a6fe-6eda7c24b118);
DB_VWBossAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_SW_VWBoss_Mage_02_2f619e60-5cfc-4323-a094-e285ea922903);
DB_VWBossAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_SW_VWBoss_Ranger_01_e8ad5533-b8f0-4c55-a261-4192f5cf1e48);
DB_VWBossAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_SW_VWBoss_Melee_01_961c827b-43d1-43c8-8553-6d1d4c8e8aed);
DB_VWBossAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_SW_VWBoss_Melee_02_8644ff57-7eb3-4ed7-a496-00e977227b53);



DB_Dialogs(CHARACTERGUID_S_FTJ_SpikedTurtle_01_abd3afae-a6e5-452c-a94a-db57826dd082,"FTJ_SpikedTurtle_01");
DB_Dialogs(CHARACTERGUID_S_FTJ_SpikedTurtle_03_fb4618f9-9c61-4640-a32c-e4735783e878,"FTJ_SpikedTurtle_03");
DB_Dialogs(CHARACTERGUID_S_FTJ_SpikedTurtle_04_f37cb16e-027e-4a21-8504-d6cab12d9098,"FTJ_SpikedTurtle_04");

DB_OneShotPlayerTrigger(TRIGGERGUID_S_FTJ_TurtleAmbush_3b293d2c-8098-490d-a357-2d84cbc77e40);
DB_TurtleAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_SpikedTurtle_01_abd3afae-a6e5-452c-a94a-db57826dd082,"FTJ_SpikedTurtle_Small");
DB_TurtleAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_SpikedTurtle_03_fb4618f9-9c61-4640-a32c-e4735783e878,"FTJ_SpikedTurtle_Large");
DB_TurtleAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_SpikedTurtle_04_f37cb16e-027e-4a21-8504-d6cab12d9098,"FTJ_SpikedTurtle_Small");

//REGION Larves ambush
 
DB_AmbushTrigger(TRIGGERGUID_S_FTJ_SW_VoidlingAmbush_Box_823d0c0c-1a16-4824-a54e-8a8fe9f7d9fb,ITEMGUID_S_FTJ_SW_VoidlingAmbushHelper_22f2d0b7-d213-4b22-ba97-90323bec580e);
DB_VoidlingAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_000_d61a5845-383b-4759-9fe3-99f519dec4dc,1);
DB_VoidlingAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_001_eedb56aa-aad1-4de2-8097-3fd7241be1ec,1);
DB_VoidlingAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_002_a8318c72-e603-4a08-b01d-09232110bccc,1);
DB_VoidlingAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_003_53680e8b-a4ee-4b00-9419-3860e91e76e6,3);
DB_VoidlingAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_004_3fe3a69c-97b6-42d5-b1db-bc646a66ab15,3);
DB_VoidlingAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_005_03ed2bcc-3b3b-4e9c-bfd1-54c7f6a1bcaa,3);
DB_VoidlingAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_006_0cf5424e-2183-4c52-980e-de156c31f5e4,3);
DB_VoidlingAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_007_1aa2f181-c36b-4e9e-ae5e-9652fe038824,6);
DB_VoidlingAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_008_8c83992f-328d-405d-bebd-0f5461d027ad,6);
DB_VoidlingAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_009_811f904d-4822-463c-b77e-d658a0fb3380,6);
DB_VoidlingAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_010_7dfba778-1b94-4cf7-8b26-663dfcb760d3,6);
DB_VoidlingAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_011_a01a4838-c65a-452e-bde5-dc7b8e3dca27,6);
DB_VoidlingAmbush((CHARACTERGUID)CHARACTERGUID_S_FTJ_VoidlingAmbush_012_360a68c3-e5f1-4834-aa9e-7dc7497d9301,6);
DB_VoidlingAmbush_KillCount(0);
DB_VoidlingAmbushADInd(1);
DB_KillCounter("FTJ_SW_VoidlingAmbush",13);

//END_REGION
KBSECTION
//REGION CAVERN - FROG AMBUSH
IF
DB_FTJ_FrogAmbush(_Frog)
THEN
SetOnStage(_Frog,0);

PROC
ProcLaunchAmbush(S_FTJ_FrogAmbush_Box_01_10df8c5b-5811-41b4-88b7-e7e884ced161,_Player)
THEN
ProcTriggerUnregisterForPlayers(S_FTJ_FrogAmbush_Box_01_10df8c5b-5811-41b4-88b7-e7e884ced161);

PROC
ProcLaunchAmbush(S_FTJ_FrogAmbush_Box_01_10df8c5b-5811-41b4-88b7-e7e884ced161,_Player)
AND
DB_FTJ_FrogAmbush(_Char)
THEN
CharacterAppear(_Char,1,"dbg_frog");

IF
StoryEvent(_,"dbg_frog")
THEN
DebugText(CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,"dbg_frog");

PROC
ReactOnKillCounter("FTJ_FrogAmbush")
THEN
GlobalSetFlag("FTJ_FrogAmbush_Defeated");
ObjectSetFlag(CHARACTERGUID_S_FTJ_Skeptic_Elf_001_d8902155-d0e3-4dfe-b6dd-d90fd5606f50,"FTJ_CaveElfThankPlayer");

IF
StoryEvent((CHARACTERGUID)_Player,"FTJ_DoFrogAmbushThanks_Dialog")
THEN
Proc_StartDialog(0,"FTJ_CaveElf_001",CHARACTERGUID_S_FTJ_Skeptic_Elf_001_d8902155-d0e3-4dfe-b6dd-d90fd5606f50,_Player);

IF
ObjectEnteredCombat((CHARACTERGUID)_Player,_Id)
AND
DB_FTJ_FrogAmbush(_Frog)
AND
DB_CombatCharacters(_Frog, _CombatID)
AND
_Id == _CombatID
THEN
ObjectSetFlag(_Player,"FTJ_ParticipatedInFrogAmbush");

IF
ObjectFlagSet("FTJ_FrogAmbushRewardXp",(CHARACTERGUID)_Player,_)
THEN
PartyAddExperience(_Player,1,3,5);

//END_REGION

//REGION VoidWoken Boss - Proximity Box

IF 
ObjectEnteredCombat((CHARACTERGUID)_Enemy,_) 
AND 
DB_VWBossAmbush(_Enemy) 
AND 
QueryOnlyOnce("FTJ_SW_CombatSave_VWMerman") 
THEN 
AutoSave();



PROC
ProcLaunchAmbush(S_FTJ_SW_VWBoss_ProximityBox_01_53804249-93b6-4756-ae5c-f7d954b6e36b,_Player)
THEN
SetStoryEvent(S_FTJ_SW_VWBoss_VoidWoken_112f8c17-ea77-4658-ac72-239154772fb8,"CMB_FTJ_SW_VWBoss_ProximityBox_Event");
ProcTriggerUnregisterForPlayers(S_FTJ_SW_VWBoss_ProximityBox_01_53804249-93b6-4756-ae5c-f7d954b6e36b);

IF
DB_VWBossAmbush(_Char)
THEN
SetOnStage(_Char,0);

IF
ObjectEnteredCombat(CHARACTERGUID_S_FTJ_SW_VWBoss_VoidWoken_112f8c17-ea77-4658-ac72-239154772fb8,_)
AND
DB_VWBossAmbush(_Char)
THEN
CharacterAppear(_Char,1,"");
NOT DB_VWBossAmbush(_Char);
//END_REGION


//REGION
IF
StoryEvent((CHARACTERGUID)_Char,"OnSightEvent_SpikedTurtle")
AND
DB_IsPlayer(_Char)
AND
NOT DB_TurtleAmbush_Queue(_)
THEN
Proc_SpikedTurtle_VoidWokening();
DB_TurtleAmbush_Queue(_Char);

PROC
Proc_SpikedTurtle_VoidWokening()
THEN
GlobalSetFlag("FTJ_SpikedTurtles_Transform");

PROC
Proc_SpikedTurtle_VoidWokening()
AND
DB_TurtleAmbush(_Char,_)
THEN
PROC_RemoveDialogFromCharacter(_Char);

PROC
Proc_SpikedTurtle_VoidWokening()
AND
QueryOnlyOnce("FTJ_SpikedTurtle_VoidWokening_Timers")
THEN
TimerLaunch("FTJ_SpikedTurtle_VoidWokening_VB",800);
TimerLaunch("FTJ_SpikedTurtle_VoidWokening",1800);

IF
TimerFinished("FTJ_SpikedTurtle_VoidWokening_VB")
AND
DB_TurtleAmbush_Queue(_Player)
THEN
StartVoiceBark("FTJ_VB_SpikedTurtle_VoidWokening",_Player);

IF
TimerFinished("FTJ_SpikedTurtle_VoidWokening")
AND
DB_TurtleAmbush(_Char,_Name)
THEN
CharacterSetCustomName(_Char,_Name);
NOT DB_TurtleAmbush(_Char,_Name);
ProcSetRelationToPlayers(_Char,0);

IF
AttackedByObject((CHARACTERGUID)_Char,(CHARACTERGUID)_Player,_,_,_)
AND
DB_IsPlayer(_Player)
AND 
NOT DB_TurtleAmbush_Queue(_)
AND
DB_TurtleAmbush(_Char,_)
THEN
GlobalSetFlag("OnSightGlobalFlag_SpikedTurtle");
Proc_SpikedTurtle_VoidWokening();
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_FTJ_TurtleAmbush_3b293d2c-8098-490d-a357-2d84cbc77e40);
DB_TurtleAmbush_Queue(_Player);

//END_REGION

//REGION Voidling Ambush
IF
DB_VoidlingAmbush((CHARACTERGUID)_Voidling,_)
THEN
DB_KillCounter_Internal(_Voidling,"FTJ_SW_VoidlingAmbush");
SetOnStage(_Voidling,0);

PROC
ProcLaunchAmbush(TRIGGERGUID_S_FTJ_SW_VoidlingAmbush_Box_823d0c0c-1a16-4824-a54e-8a8fe9f7d9fb,(CHARACTERGUID)_Player)
THEN
Proc_VoidlingAmbush();

IF
CharacterDied(_Char)
AND
DB_VoidlingAmbush(_Char,_Int)
THEN
Proc_VoidlingAmbush();
NOT DB_VoidlingAmbush(_Char,_Int);

PROC
Proc_VoidlingAmbush()
AND
DB_VoidlingAmbush_KillCount(_Count)
AND
IntegerSum(_Count,1,_NewCount)
THEN
NOT DB_VoidlingAmbush_KillCount(_Count);
 DB_VoidlingAmbush_KillCount(_NewCount);

PROC
Proc_VoidlingAmbush()
AND
DB_VoidlingAmbush_KillCount(_Count)
AND
DB_VoidlingAmbush(_Char,_Count)
THEN
CharacterAppear(_Char,1,"");
GlobalSetFlag("FTJ_VoidlingAmbush_SetVoiceBark");

PROC
ReactOnKillCounter("FTJ_SW_VoidlingAmbush")
AND
DB_LastManInCombatKilledBy(_Player,"FTJ_SW_VoidlingAmbush")
AND
DB_IsPlayer(_Player)
THEN
ProcDefineReflectionDialog("FTJ_SW_RD_VoidlingAmbush",_Player);

IF
ObjectTurnStarted((CHARACTERGUID)_Char)
AND
GlobalGetFlag("FTJ_VoidlingAmbush_SetVoiceBark",1)
AND
DB_IsPlayer(_Char)
THEN
Proc_VoidlingAmbush_SetVoiceBark(_Char);

PROC
Proc_VoidlingAmbush_SetVoiceBark((CHARACTERGUID)_Char)
AND
DB_VoidlingAmbush_SetVoidBark(_Count)
AND
IntegertoString(_Count,_ToString)
AND
StringConcatenate("FTJ_VoidlingAmbush_Phase_",_ToString,_SetFlag)
THEN
GlobalSetFlag(_SetFlag);

PROC
Proc_VoidlingAmbush_SetVoiceBark((CHARACTERGUID)_Char)
AND
DB_VoidlingAmbush_SetVoidBark(_Count)
AND
IntegerSum(_Count,1,_NewCount)
THEN
NOT DB_VoidlingAmbush_SetVoidBark(_Count);
DB_VoidlingAmbush_SetVoidBark(_NewCount);

PROC
Proc_VoidlingAmbush_SetVoiceBark((CHARACTERGUID)_Char)
AND
NOT DB_VoidlingAmbush_SetVoidBark(_)
THEN
DB_VoidlingAmbush_SetVoidBark(1);

PROC
Proc_VoidlingAmbush_SetVoiceBark((CHARACTERGUID)_Char)
THEN
StartVoiceBark("FTJ_SW_VB_VoidlingAmbush",_Char);
GlobalClearFlag("FTJ_VoidlingAmbush_SetVoiceBark");

IF
ObjectTurnStarted((CHARACTERGUID)_Voidling)
AND
DB_VoidlingAmbush(_Voidling,_)
AND
NOT DB_VoidlingAmbushAreadySpoken(_Voidling)
AND
DB_VoidlingAmbushADInd(_Ind)
AND
IntegertoString(_Ind,_StrInd)
AND
StringConcatenate("FTJ_SW_VoidlingComment_",_StrInd,_CommentFlag)
THEN
DB_VoidlingAmbushAreadySpoken(_Voidling);
GlobalSetFlag(_CommentFlag);
DB_FTJ_SW_VoidlingSwampFlag(_CommentFlag);
Proc_StartDialog(1,"FTJ_SW_AD_VoidlingAmbushSwarm",_Voidling);

IF
GlobalFlagCleared(_Flag)
AND
DB_FTJ_SW_VoidlingSwampFlag(_CommentFlag)
AND
DB_VoidlingAmbushADInd(_Ind)
AND
IntegertoString(_Ind,_StrInd)
AND
IntegerSum(_Ind,1,_NewInd)
THEN
NOT DB_FTJ_SW_VoidlingSwampFlag(_CommentFlag);
NOT DB_VoidlingAmbushADInd(_Ind);
DB_VoidlingAmbushADInd(_NewInd);

IF
DB_VoidlingAmbushADInd(9)
THEN
NOT DB_VoidlingAmbushADInd(9);

//END_REGION

PROC
ProcRegionEnded("FJ_FortJoy_Main")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "FortJoy"
