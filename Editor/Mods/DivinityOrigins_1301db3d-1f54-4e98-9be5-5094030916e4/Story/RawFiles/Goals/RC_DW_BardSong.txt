Version 1
SubGoalCombiner SGC_AND
INITSECTION
//http://fileserver-dmz/mediawiki/index.php/General_Boosters#Undertavern_bouncer
TriggerRegisterForCharacter(TRIGGERGUID_S_RC_DW_TavernBard_Highground_0cf13953-676f-47e1-a94b-f0f5c75125cc,CHARACTERGUID_S_RC_DW_Tavern_Bard_928d216f-b498-4035-b869-063f015826f6);
DB_Dialogs(CHARACTERGUID_S_RC_DW_Tavern_Bard_928d216f-b498-4035-b869-063f015826f6,"RC_DW_Tavern_Bard");
DB_Dialogs(CHARACTERGUID_S_RC_DW_Tavern_MusicEnthusiast_02_5a46af31-b6b2-4f27-98c4-9e0cbb8b0931,"RC_DW_Tavern_MusicEnthusiast_02");
DB_Dialogs(CHARACTERGUID_S_RC_DW_Tavern_MusicEnthusiast_80b79b89-2119-42c5-9e43-732169effe8e,"RC_DW_Tavern_MusicEnthusiast_01");

DB_DialogMoneyTransfer(1,"RC_DW_Tavern_Bard",20);
DB_DialogMoneyTransfer(2,"RC_DW_Tavern_Bard",10);


//Race tags
DB_BardSong_RaceTag("HUMAN");
DB_BardSong_RaceTag("DWARF");
DB_BardSong_RaceTag("ELF");
DB_BardSong_RaceTag("LIZARD");
DB_BardSong_RaceTag("UNDEAD");

//Option flags
DB_BardSong_ComposeOptionFlags("RC_DW_BardSong_Composition_A_A");
DB_BardSong_ComposeOptionFlags("RC_DW_BardSong_Composition_A_B");
DB_BardSong_ComposeOptionFlags("RC_DW_BardSong_Composition_B_A");
DB_BardSong_ComposeOptionFlags("RC_DW_BardSong_Composition_B_B");
DB_BardSong_ComposeOptionFlags("RC_DW_BardSong_Composition_C_A");
DB_BardSong_ComposeOptionFlags("RC_DW_BardSong_Composition_C_B");

//Other DB used within KB:
//DB_RC_DW_currentBardSong(_Flag) // ex.: RC_DW_BardSong_InitiateComposition_HUMAN
//DB_RC_DW_currentBardComposition(_Flag) // ex.: RC_DW_BardSong_Composition_A_A_HUMAN
//DB_RC_DW_BardSong_Selected(_Flag) // Remembers which tags already had song(s) composed for them
DB_DeadEvent(CHARACTERGUID_S_RC_DW_Tavern_Bard_928d216f-b498-4035-b869-063f015826f6,"RC_DW_Tavern_Bard_IsDead");
KBSECTION
//REGION set up composition

IF
ObjectFlagSet(_Flag,(CHARACTERGUID)_Char,_Inst)
AND
DB_BardSong_ComposeOptionFlags(_Flag)
THEN
Proc_BardSong_SetCompose(_Flag,_Char);
ObjectClearFlag(_Char,_Flag,_Inst);


PROC
Proc_BardSong_SetCompose((STRING)_Flag,(CHARACTERGUID)_Char)
AND
DB_BardSong_RaceTag(_Tag)
AND
IsTagged(_Char,_Tag,1)
AND
StringConcatenate("_",_Tag,_TagFlag)
THEN
Proc_BardSong_SetCompose_1(_Flag,_TagFlag);


PROC
Proc_BardSong_SetCompose_1((STRING)_Flag,(STRING)_TagFlag)
AND
StringConcatenate(_Flag,_TagFlag,_CompFlag)
THEN
GlobalSetFlag(_CompFlag);
DB_RC_DW_currentBardComposition(_CompFlag);

/*
PROC
Proc_BardSong_SetCompose_1((INTEGER)_Count,(STRING)_Tag)
AND
DB_Bardsong_Composition(_Tag,_CurrentCount)
AND
IntegerSum(_CurrentCount,_Count,_Sum)
THEN
NOT DB_Bardsong_Composition(_Tag,_CurrentCount);
DB_Bardsong_Composition(_Tag,_Sum);

PROC
Proc_BardSong_SetCompose_1((INTEGER)_Count,(STRING)_Tag)
AND
NOT DB_Bardsong_Composition(_Tag,_)
THEN
DB_Bardsong_Composition(_Tag,_Count);
*/

//END_REGION

//REGION set up song

IF
DialogEnded("RC_DW_Tavern_Bard",_)
THEN
Proc_RC_DW_Tavern_Bard_CheckIfToCompose();

PROC 
Proc_RC_DW_Tavern_Bard_CheckIfToCompose()
AND
DB_IsPlayer(_Char)
AND
ObjectGetFlag(_Char,"RC_DW_BardSong_ComposeInitiate",1)
AND
IsTagged(_Char,"LOHSE",0)
AND
DB_BardSong_RaceTag(_Tag)
AND
IsTagged(_Char,_Tag,1)
THEN
Proc_BardSong_SetSong(_Tag);
ObjectClearFlag(_Char,"RC_DW_BardSong_ComposeInitiate",0);

PROC 
Proc_RC_DW_Tavern_Bard_CheckIfToCompose()
AND
DB_IsPlayer(_Char)
AND
ObjectGetFlag(_Char,"RC_DW_BardSong_ComposeInitiate",1)
AND
IsTagged(_Char,"LOHSE",1)
THEN
Proc_BardSong_SetSong("LOHSE");
ObjectClearFlag(_Char,"RC_DW_BardSong_ComposeInitiate",0);


PROC
Proc_BardSong_SetSong((STRING)_Tag)
AND
StringConcatenate("RC_DW_BardSong_InitiateComposition_",_Tag,_SetFlag)
THEN
GlobalSetFlag(_SetFlag);
DB_RC_DW_currentBardSong(_SetFlag);


PROC
Proc_BardSong_SetSong(_)
AND
DB_RC_DW_currentBardSong(_Flag)
AND
NOT DB_RC_DW_BardSong_Selected(_Flag)
THEN
DB_RC_DW_BardSong_Selected(_Flag);



//Keeps track if at least one song has been created and for which race
PROC
Proc_BardSong_SetSong((STRING)_Tag)
AND
StringConcatenate("RC_DW_BardSong_CreatedFor_",_Tag,_SetCreatedSong) //A song for a specific tag
THEN
GlobalSetFlag(_SetCreatedSong);
GlobalSetFlag("RC_DW_BardSong_SongCreated"); 

PROC
Proc_BardSong_SetSong(_)
THEN
CharacterLookAt(CHARACTERGUID_S_RC_DW_Tavern_Bard_928d216f-b498-4035-b869-063f015826f6,TRIGGERGUID_S_RC_DW_Tavern_Bard_LookAtPoint_eca6222b-c1a7-4e2a-a985-fc6bdcbfe20e);

PROC
Proc_BardSong_SetSong(_)
THEN
SetHasDialog(CHARACTERGUID_S_RC_DW_Tavern_Bard_928d216f-b498-4035-b869-063f015826f6,0);
CharacterLookAt(CHARACTERGUID_S_RC_DW_Tavern_Bard_928d216f-b498-4035-b869-063f015826f6,TRIGGERGUID_S_RC_DW_Tavern_Bard_LookAtPoint_eca6222b-c1a7-4e2a-a985-fc6bdcbfe20e);
Proc_StartDialog(1,"RC_DW_AD_BardSong",CHARACTERGUID_S_RC_DW_Tavern_Bard_928d216f-b498-4035-b869-063f015826f6);
GlobalSetFlag("RC_DW_BardSong_NoDialog");

//END_REGION

//REGION clear flags after song

IF
AutomatedDialogEnded("RC_DW_AD_BardSong",_)
AND 
GlobalGetFlag("RC_DW_BardSong_NoDialog",1)
THEN
SetHasDialog(CHARACTERGUID_S_RC_DW_Tavern_Bard_928d216f-b498-4035-b869-063f015826f6,1);
GlobalClearFlag("RC_DW_BardSong_NoDialog");

IF
GlobalFlagSet("RC_DW_Bard_ClearPreviousSong")
AND
NOT DB_RC_DW_currentBardSong(_)
THEN
GlobalClearFlag("RC_DW_Bard_ClearPreviousSong");

IF
GlobalFlagSet("RC_DW_Bard_ClearPreviousSong")
AND
DB_RC_DW_currentBardSong(_Flag)
THEN
GlobalClearFlag(_Flag);
NOT DB_RC_DW_currentBardSong(_Flag);
GlobalClearFlag("RC_DW_Bard_ClearPreviousSong");

IF
GlobalFlagSet("RC_DW_Bard_ClearPreviousChoices")
THEN
GlobalClearFlag("RC_DW_Bard_ClearPreviousChoices");


IF
GlobalFlagSet("RC_DW_Bard_ClearPreviousChoices")
AND
DB_RC_DW_currentBardComposition(_Flag)
THEN
GlobalClearFlag(_Flag);
NOT DB_RC_DW_currentBardComposition(_Flag);
GlobalClearFlag("RC_DW_Bard_ClearPreviousChoices");


//END_REGION

//REGION 

IF
ObjectFlagSet("RC_DW_Tavern_Bard_CheckComposition",_Char,_Inst)
AND
DB_RC_DW_BardSong_Selected(_Flag)
AND
DB_BardSong_RaceTag(_Tag)
AND
IsTagged(_Char,_Tag,1)
AND
StringContains(_Flag,_Tag,1)
THEN
ObjectSetFlag(_Char,"RC_DW_Tavern_Bard_ComposedTag");


IF
DialogEnded("RC_DW_Tavern_Bard",_)
AND
DB_IsPlayer(_Char)
THEN
ObjectClearFlag(_Char,"RC_DW_Tavern_Bard_CheckComposition",0);


//END_REGION

//REGION

IF
AutomatedDialogStarted("RC_DW_AD_BardSong",_)
THEN
CharacterSetAnimationOverride(CHARACTERGUID_S_RC_DW_Tavern_Bard_928d216f-b498-4035-b869-063f015826f6,"Sing_01_Loop");

IF
AutomatedDialogEnded("RC_DW_AD_BardSong",_)
THEN
CharacterSetAnimationOverride(CHARACTERGUID_S_RC_DW_Tavern_Bard_928d216f-b498-4035-b869-063f015826f6,"");

IF
ObjectEnteredCombat(CHARACTERGUID_S_RC_DW_Tavern_Bard_928d216f-b498-4035-b869-063f015826f6,_)
THEN
CharacterSetAnimationOverride(CHARACTERGUID_S_RC_DW_Tavern_Bard_928d216f-b498-4035-b869-063f015826f6,"");

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ReapersCoast"
