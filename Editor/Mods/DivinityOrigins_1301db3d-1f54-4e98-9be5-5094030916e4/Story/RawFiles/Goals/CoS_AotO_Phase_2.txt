Version 1
SubGoalCombiner SGC_AND
INITSECTION
SetOnStage(CHARACTERGUID_S_GLO_Dallis_69b951dc-55a4-44b8-a2d5-5efedbd7d572,0);
SetOnStage(CHARACTERGUID_S_GLO_Braccus_358ba541-0710-43be-8263-87afe1a8d9b5,0);

ProcTriggerRegisterForPlayers(TRIGGERGUID_S_CoS_AotO_StartGodsEvent_Trigger_45972925-2f2d-42d5-a58c-49982809dced);


DB_CoS_AotO_SilentMonks((CHARACTERGUID)CHARACTERGUID_S_CoS_AotO_SilentMonk_001_d64285b4-51d4-4f71-afcb-3f04ec33bc31);
DB_CoS_AotO_SilentMonks(CHARACTERGUID_S_CoS_AotO_SilentMonk_002_bfe682fb-cfe4-423e-9be7-6af5ac934e87);
DB_CoS_AotO_SilentMonks(CHARACTERGUID_S_CoS_AotO_SilentMonk_003_d7655de4-b0dc-4eec-ae55-c9e0064b2b58);
DB_CoS_AotO_SilentMonks(CHARACTERGUID_S_CoS_AotO_SilentMonk_004_6d9b5096-775a-44b0-aad2-f87edefd9b2d);
DB_CoS_AotO_SilentMonks(CHARACTERGUID_S_CoS_AotO_SilentMonk_005_7c6d5355-c8eb-4dd5-9961-1e4a8d4bdafd);
DB_CoS_AotO_SilentMonks(CHARACTERGUID_S_CoS_AotO_SilentMonk_006_34e0f99c-0639-4408-be08-59f1fc7459f1);


DB_CoS_AotO_CompanionOneLiners((CHARACTERGUID)CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978,"CoS_AotO_Beast_EscapeOneLiner");
DB_CoS_AotO_CompanionOneLiners(CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb,"CoS_AotO_Fane_EscapeOneLiner");
DB_CoS_AotO_CompanionOneLiners(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"CoS_AotO_Ifan_EscapeOneLiner");
DB_CoS_AotO_CompanionOneLiners(CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295,"CoS_AotO_Lohse_EscapeOneLiner");
DB_CoS_AotO_CompanionOneLiners(CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f,"CoS_AotO_RedPrince_EscapeOneLiner");
DB_CoS_AotO_CompanionOneLiners(CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c,"CoS_AotO_Sebille_EscapeOneLiner");


//Template Selector - Tag , Template , Global Flag , Dialog
DB_CoS_AotO_CreateGodwokenNemesis_NemesisTemplate("UNDEAD","QUEST_CoS_GodwokenNemesis_Wizard_684ce482-c091-432c-adff-34039d580eb9","CoS_AotO_Amadia_Buff","CoS_AotO_Phase2_GodConfrontation_Amadia");
DB_CoS_AotO_CreateGodwokenNemesis_NemesisTemplate("HUMAN","QUEST_CoS_GodwokenNemesis_Human_2f17616a-b8d0-44df-a5b1-614755e48ac7","CoS_AotO_Rhalic_Buff","CoS_AotO_Phase2_GodConfrontation_Rhalic");
DB_CoS_AotO_CreateGodwokenNemesis_NemesisTemplate("ELF","QUEST_CoS_GodwokenNemesis_Elf_773b0671-b28b-4acb-8015-18c015f7e34f","CoS_AotO_Tir_Buff","CoS_AotO_Phase2_GodConfrontation_Tir");
DB_CoS_AotO_CreateGodwokenNemesis_NemesisTemplate("LIZARD","QUEST_CoS_GodwokenNemesis_Lizard_d4dbc857-7fc4-4e55-8920-64532de49e5e","CoS_AotO_Zorl_Buff","CoS_AotO_Phase2_GodConfrontation_Zorl");
DB_CoS_AotO_CreateGodwokenNemesis_NemesisTemplate("DWARF","QUEST_CoS_GodwokenNemesis_Dwarf_682b3c8d-cdc6-45aa-8e61-fb468284ca7e","CoS_AotO_Duna_Buff","CoS_AotO_Phase2_GodConfrontation_Duna");


//REGION Ability
//Air
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("AirSpecialist","Projectile_EnemyLightningBolt",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("AirSpecialist","ProjectileStrike_EnemyDazingBolt",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("AirSpecialist","Target_EnemyPressureSpike",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("AirSpecialist","Shout_EnemyElectricFence",8);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("AirSpecialist","Storm_EnemyLightning",9);
DB_CoS_AotO_Nemesis_AiHint("AirSpecialist","AiHint_Mage","SwapArchetype_Mage");

//Fire
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("FireSpecialist","Projectile_EnemyFlamingDaggers",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("FireSpecialist","Target_EnemyHaste",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("FireSpecialist","Target_EnemyFireWhip",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("FireSpecialist","Zone_EnemyLaserRay",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("FireSpecialist","Target_EnemySpontCombustion",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("FireSpecialist","ProjectileStrike_EnemyMeteorShower",9);
DB_CoS_AotO_Nemesis_AiHint("FireSpecialist","AiHint_Mage","SwapArchetype_Mage");

//Warrior
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WarriorLore","Cone_EnemyGroundSmash",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WarriorLore","Target_EnemyCripplingBlow",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WarriorLore","Shout_EnemyWhirlwind",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WarriorLore","Target_EnemyFlurry",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WarriorLore","Jump_EnemyPhoenixDive",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WarriorLore","Target_EnemyOverpower",9);

//Poly
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Polymorph","Target_EnemyTentacleLash",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Polymorph","Shout_EnemySteelSkin",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Polymorph","Target_EnemyStripResistance",6);

//Rogue
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RogueLore","Projectile_EnemyThrowingKnife",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RogueLore","Projectile_EnemyChloroform",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RogueLore","Target_EnemyCorruptedBlade",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RogueLore","Jump_EnemyCloakAndDagger",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RogueLore","Target_EnemyDaggersDrawn",8);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RogueLore","Target_EnemyFatality",9);

//Summoning
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Summoning","Target_EnemyHarmony",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Summoning","Target_EnemySoulMate",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Summoning","Target_EnemyTotemFromSurface",6);

//Water
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","Target_EnemyFrostyShell",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","ProjectileStrike_EnemyHailStrike",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","Projectile_EnemyIceFan",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","Target_EnemyWinterBlast",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","Projectile_EnemyChainHeal",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","Cone_EnemySteamLance",8);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","ProjectileStrike_EnemyHailAttack",9);
DB_CoS_AotO_Nemesis_AiHint("WaterSpecialist","AiHint_Mage","SwapArchetype_Mage");

//Necro
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Necromancy","Target_EnemyMosquitoSwarm",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Necromancy","Target_EnemyShacklesOfPain",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Necromancy","Target_EnemyInfect",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Necromancy","Target_EnemyGraspOfTheStarved",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Necromancy","Cone_EnemySilencingStare",8);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Necromancy","Shout_EnemyNecromancerTotems",9);
DB_CoS_AotO_Nemesis_AiHint("EarthSpecialist","AiHint_Mage","SwapArchetype_Mage");

//Earth
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("EarthSpecialist","Projectile_EnemyPyroclasticRock",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("EarthSpecialist","Projectile_EnemyPoisonDart",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("EarthSpecialist","Shout_EnemyReactiveArmor",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("EarthSpecialist","Target_EnemyWormTremor",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("EarthSpecialist","Projectile_EnemyAcidSpores",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("EarthSpecialist","Projectile_EnemyPyroclasticEruption",9);
DB_CoS_AotO_Nemesis_AiHint("EarthSpecialist","AiHint_Mage","SwapArchetype_Mage");

//Ranger
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RangerLore","Projectile_EnemyRicochet",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RangerLore","Jump_EnemyTacticalRetreat",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RangerLore","Projectile_EnemyPinDown",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RangerLore","Projectile_EnemyArrowSpray",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RangerLore","Projectile_EnemySnipe",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RangerLore","ProjectileStrike_EnemyRainOfArrows",9); //DO NOT CHANGE THIS WITHOUT CHANGING THE SAVEGAME HACK - DOS2EE-4759
DB_CoS_AotO_Nemesis_AiHint("RangerLore","AiHint_Ranger","SwapArchetype_Ranged");

//END_REGION

DB_CoS_AotO_GlobalDialogs("CoS_AotO_Phase2_Start");
DB_CoS_AotO_GlobalDialogs("CoS_AotO_BraccusEvent");
DB_CoS_AotO_GlobalDialogs("CoS_AotO_BraccusEvent_AlexandarToDallis");
DB_CoS_AotO_GlobalDialogs("CoS_AotO_Phase2_GodConfrontation_Amadia");
DB_CoS_AotO_GlobalDialogs("CoS_AotO_Phase2_GodConfrontation_Rhalic");
DB_CoS_AotO_GlobalDialogs("CoS_AotO_Phase2_GodConfrontation_Tir");
DB_CoS_AotO_GlobalDialogs("CoS_AotO_Phase2_GodConfrontation_Zorl");
DB_CoS_AotO_GlobalDialogs("CoS_AotO_Phase2_GodConfrontation_Amadia");
DB_CoS_AotO_GlobalDialogs("CoS_AotO_Phase2_GodConfrontation_Duna");
DB_CoS_AotO_GlobalDialogs("CoS_AotO_Phase2_SourceTitanTransformation");
DB_CoS_AotO_GlobalDialogs("CoS_AotO_Phase3_MaladyTelepathy");

//Lower Area Spawn Points
DB_CoS_AotO_Phase_2_LowerAreaSpawn(TRIGGERGUID_S_CoS_AotO_P2_TeleportTrigger_001_e3d919fb-3dbd-4351-807d-73933f66ea2d);
DB_CoS_AotO_Phase_2_LowerAreaSpawn(TRIGGERGUID_S_CoS_AotO_P2_TeleportTrigger_002_63c99ace-12b2-46a9-8fff-ce7da1297530);
DB_CoS_AotO_Phase_2_LowerAreaSpawn(TRIGGERGUID_S_CoS_AotO_P2_TeleportTrigger_003_a7614547-ea05-424e-ac5d-6a7059bed27c);
DB_CoS_AotO_Phase_2_LowerAreaSpawn(TRIGGERGUID_S_CoS_AotO_P2_TeleportTrigger_004_3b2997ab-cd17-4b3c-9f8c-8248672de162);


DB_CoS_AotO_CaveIns_Phase2((CHARACTERGUID)CHARACTERGUID_S_CoS_AotO_CaveInLocation_P3_001_34fd471e-2f9c-4bf7-9b25-7e5ddd2a66f1);
DB_CoS_AotO_CaveIns_Phase2(CHARACTERGUID_S_CoS_AotO_CaveInLocation_P3_002_ff5993db-9d23-4cfc-983f-5bdda0dd3372);
DB_CoS_AotO_CaveIns_Phase2(CHARACTERGUID_S_CoS_AotO_CaveInLocation_P3_003_5e8f40e1-5a4e-4cd0-a46b-b1618357af60);
DB_CoS_AotO_CaveIns_Phase2(CHARACTERGUID_S_CoS_AotO_CaveInLocation_P3_004_66f50553-4446-4ef4-84b3-c3b7ec54d6fd);


Proc_CoS_AotO_PhaseTwo_Setup();
KBSECTION
//REGION Set up

IF
RegionStarted("CoS_Main_Ending") ///////DEBUG
AND
DB_CoS_AotO_PhaseTwoInit(1)
AND
QueryOnlyOnce("Proc_CoS_AotO_PhasTwoInit_Once")
THEN
Proc_CoS_AotO_PhaseTwoInit();
NOT DB_CoS_AotO_PhaseTwoInit(1);


IF
DB_CoS_AotO_PhaseTwoInit(1) ///////DEBUG
AND
QueryOnlyOnce("Proc_CoS_AotO_PhasTwoInit_Once")
THEN
Proc_CoS_AotO_PhaseTwoInit();
NOT DB_CoS_AotO_PhaseTwoInit(1);


PROC
Proc_CoS_AotO_PhaseTwo_Setup()
AND
DB_CoS_AotO_SilentMonks(_SilentMonk)
THEN
SetOnStage(_SilentMonk,0);
SetCanFight(_SilentMonk,0);
SetCanJoinCombat(_SilentMonk,0);


PROC
Proc_CoS_AotO_PhaseTwo_Setup()
AND
DB_CoS_AotO_CaveIns_Phase2((CHARACTERGUID)_CaveIn)
THEN
SetOnStage(_CaveIn,0);

//END_REGION

//REGION Wake up in Ruins


PROC
Proc_CoS_AotO_PhaseTwoInit()
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
CharacterUnfreeze(_Player);
CharacterSetAnimationOverride(_Player,"knockdown_fall");
Proc_ShakeCameraForTime(_Player,9000);
//StartCameraSpline(SPLINEGUID_S_CoS_AotO_PurgingCamera_251e8119-dbc1-4a52-a14a-cff7480a0984,_Player,0.3,1,1,0);

PROC
Proc_CoS_AotO_PhaseTwoInit()
THEN
TimerLaunch("CoS_AotO_PurgingCamera_Timer",4000);

/*
IF
TimerFinished("CoS_AotO_PurgingCamera_Timer")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
StopCameraSpline(SPLINEGUID_S_CoS_AotO_PurgingCamera_251e8119-dbc1-4a52-a14a-cff7480a0984,_Player);
*/
IF
TimerFinished("CoS_AotO_PurgingCamera_Timer")
THEN
TimerLaunch("CoS_AotO_PurgingCamera_FadeDelay",1100);


IF
TimerFinished("CoS_AotO_PurgingCamera_FadeDelay")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
FadeToBlack(_Player,3.0,0,"Proc_CoS_AotO_PhaseTwoInit_FadeOut");


PROC
Proc_CoS_AotO_PhaseTwoInit()
AND
DB_CoS_AotO_Competitors(_Competitor)
AND
NOT DB_IsPlayer(_Competitor)
AND
CharacterIsDead(_Competitor,0)
THEN
CharacterDie(_Competitor,1,"DoT");



PROC
Proc_CoS_AotO_PhaseTwoInit()
AND
DB_CoS_AotO_Defender((CHARACTERGUID)_Char)
THEN
CharacterDie(_Char,0,"DoT");

IF
FadeOutDone(_,"Proc_CoS_AotO_PhaseTwoInit_FadeOut")
AND
QueryOnlyOnce("Proc_CoS_AotO_PhaseTwoInit_FadeOut_Once")
THEN
Proc_CoS_AotO_PhaseTwoInit_1();


PROC
Proc_CoS_AotO_PhaseTwoInit_1()
AND
QueryOnlyOnce("Proc_CoS_AotO_PhaseThreeInit_FadeOut_Once")
THEN
TimerLaunch("Proc_CoS_AotO_PhaseTwoInit_FadeIn_Delay",2500);


IF
TimerFinished("Proc_CoS_AotO_PhaseTwoInit_FadeIn_Delay")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
DB_CoS_AotO_Phase_2_LowerAreaSpawn(_Trigger)
AND
NOT DB_CoS_AotO_Phase_2_LowerAreaSpawn(_Player,_)
AND
NOT DB_CoS_AotO_Phase_2_LowerAreaSpawn(_,_Trigger)
THEN
CharacterRemoveSummons(_Player,0);
TeleportTo(_Player,_Trigger);
DB_CoS_AotO_Phase_2_LowerAreaSpawn(_Player,_Trigger);
NOT DB_CoS_AotO_Phase_2_LowerAreaSpawn(_Trigger);
CharacterSetAnimationOverride(_Player,"");
PartySetFlag(_Player,"QuestUpdate_CoS_ArenaOfTheOne_Phase_2_Start");
SetCanFight(_Player,1);
SetCanJoinCombat(_Player,1);


IF
TimerFinished("Proc_CoS_AotO_PhaseTwoInit_FadeIn_Delay")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
CharacterIsDead(_Player,0)
THEN
FadeToBlack(_Player,3.0,1,"Proc_CoS_AotO_PhaseThreeInit_FadeIn");
Proc_CharacterFullRestore(_Player);
ProcRemovePolymorphs(_Player);
CharacterPurgeQueue(_Player);
PlayAnimation(_Player,"knockdown_getup");



//TEMPLATE TRANSFER AFTER PLAYERS MOVE
IF
TimerFinished("Proc_CoS_AotO_PhaseTwoInit_FadeIn_Delay")
THEN
TimerLaunch("CoS_AotO_PhaseTwo_StartDelay",5500);
ActivatePersistentLevelTemplate(b7407095-373b-4195-bdb8-4874bc3fd58b);

//Reattach to group
IF
TimerFinished("Proc_CoS_AotO_PhaseTwoInit_FadeIn_Delay")
AND
DB_GLO_PartyMembers_RecruiteeAvatarBond((CHARACTERGUID)_Member,(CHARACTERGUID)_Avatar)
THEN
CharacterAttachToGroup(_Member,_Avatar);

IF
TimerFinished("CoS_AotO_PhaseTwo_StartDelay")
THEN
AutoSave();


//One Liners
PROC
PROC_GLOBAL_DialogStartRequested((CHARACTERGUID)_Companion,(CHARACTERGUID)_Avatar)
AND
DB_IsPlayer(_Companion)
AND
DB_IsPlayer(_Avatar)
AND
DB_InRegion(_Avatar,TRIGGERGUID_S_CoS_SUB_AotO_LowerArea_b1f47fb3-e8b4-40d3-9c4f-aac7ca3129fd)
AND
DB_InRegion(_Companion,TRIGGERGUID_S_CoS_SUB_AotO_LowerArea_b1f47fb3-e8b4-40d3-9c4f-aac7ca3129fd)
AND
DB_CoS_AotO_CompanionOneLiners(_Companion,_Dialog)
THEN
DialogRequestStop(_Companion);
Proc_StartDialog(0,_Dialog,_Companion,_Avatar);


//END_REGION


IF
TimerFinished("CoS_AotO_PhaseTwo_StartDelay")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
QueryOnlyOnce("CoS_AotO_VB_AwakenInRubble_Once")
THEN
StartVoiceBark("CoS_AotO_VB_AwakenInRubble",_Player);


IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_S_CoS_AotO_StartGodsEvent_Trigger_45972925-2f2d-42d5-a58c-49982809dced)
AND
QueryOnlyOnce("CoS_AotO_PhaseTwoCombat_TriggerDialog")
THEN
Proc_CoS_AotO_Phase2_StartDialog(_Player);
ProcTriggerUnregisterForPlayers(TRIGGERGUID_S_CoS_AotO_StartGodsEvent_Trigger_45972925-2f2d-42d5-a58c-49982809dced);



PROC
Proc_CoS_AotO_Phase2_StartDialog((CHARACTERGUID)_Player)
AND
StartDialog_Internal("CoS_AotO_Phase2_Start",1,_Player,NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,
		NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,1)
AND
QueryOnlyOnce("CoS_AotO_Phase2_StartDialog_Once")
THEN
Proc_DialogFlagSetup("CoS_AotO_Phase2_Start",_Player);
CharacterUnfreeze(_Player);
DB_CombatCutscene_PrioritizedObject(_Player);



//Give each player their own individual choice dialog
IF
DialogEnded("CoS_AotO_Phase2_Start",_)
THEN
Proc_CoS_AotO_AvatarHasDoneGodConfrontation();
Proc_CoS_AotO_CheckLastNemesisStanding();
SetOnStage(CHARACTERGUID_S_CoS_AotO_CaveInLocation_P3_001_34fd471e-2f9c-4bf7-9b25-7e5ddd2a66f1,1);
SetOnStage(CHARACTERGUID_S_CoS_AotO_CaveInLocation_P3_002_ff5993db-9d23-4cfc-983f-5bdda0dd3372,1);
SetOnStage(CHARACTERGUID_S_CoS_AotO_CaveInLocation_P3_003_5e8f40e1-5a4e-4cd0-a46b-b1618357af60,1);
SetOnStage(CHARACTERGUID_S_CoS_AotO_CaveInLocation_P3_004_66f50553-4446-4ef4-84b3-c3b7ec54d6fd,1);

PROC
Proc_CoS_AotO_AvatarHasDoneGodConfrontation()
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
IsTagged(_Player,"AVATAR",1)
AND
IsSpeakerReserved(_Player,0)
AND
CharacterIsDead(_Player,0)
AND
NOT DB_CoS_AotO_AvatarHasDoneGodConfrontation(_Player)
AND
DB_CoS_AotO_Nemesis(_Nemesis,_Player,_)
AND
NOT DB_QueueNextGodConfrontation(_,_)
THEN
DB_CoS_AotO_AvatarHasDoneGodConfrontation(_Player);
DB_QueueNextGodConfrontation(_Player,_Nemesis);
DB_CombatCutscene_PrioritizedObject(_Nemesis);


//REGION DOSTWO-26557 All Nemesis join esachothers dialogs

IF
DB_CoS_AotO_Nemesis(_Nemesis,_,_)
THEN
SetCombatGroupID(_Nemesis,"CoS_AotO_NemesisCombatGroup");

//END_REGION

IF
ObjectTurnStarted((CHARACTERGUID)_Nemesis)
AND
DB_QueueNextGodConfrontation((CHARACTERGUID)_Player,_Nemesis)
AND
DB_CoS_AotO_Nemesis(_Nemesis,(CHARACTERGUID)_Player,_Dialog)
AND
StartDialog_Internal(_Dialog,1,_Nemesis,_Player,NULL_00000000-0000-0000-0000-000000000000,
		NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,NULL_00000000-0000-0000-0000-000000000000,1)
THEN
NOT DB_QueueNextGodConfrontation(_Player,_Nemesis);

//Player yielded to nemesis, Purge after dialog
IF
DialogEnded(_Dialog,_ID)
AND
DialogGetInvolvedNPC(_ID,1,(CHARACTERGUID)_Nemesis)
AND
ObjectGetFlag(_Nemesis,"CoS_AotO_NemesisPurgePlayerAfterDialog",1)
AND
DB_CoS_AotO_Nemesis(_Nemesis,(CHARACTERGUID)_Player,_Dialog)
THEN
PartySetFlag(_Player,"QuestUpdate_CoS_ArenaOfTheOne_Phase_2_Gods");
CharacterAddSkill(_Nemesis,"Target_Quest_PurgeCharacter");
CharacterUseSkill(_Nemesis,"Target_Quest_PurgeCharacter",_Player,1);
ProcObjectTimer(_Player,"CoS_AotO_NemesisPurgePlayer_Timer",3200);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"CoS_AotO_NemesisPurgePlayer_Timer")
THEN
CharacterDie(_Player,0,"Explode");
ProcObjectTimer(_Player,"CoS_AotO_NemesisPurgePlayerNextTurnDelay_Timer",2000);

//Cosmetic Delay between next dialog starting
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"CoS_AotO_NemesisPurgePlayerNextTurnDelay_Timer")
THEN
Proc_CoS_AotO_AvatarHasDoneGodConfrontation();


IF
DialogEnded(_Dialog,_ID)
AND
DB_CoS_AotO_Nemesis(_Nemesis,_Player,_Dialog)
THEN
Proc_CombatCutscene_PrioritizedObject_Purge();

IF
DialogEnded(_Dialog,_)
AND
DB_CoS_AotO_Nemesis((CHARACTERGUID)_Nemesis,_,_Dialog)
AND
DB_CoS_AotO_Nemesis((CHARACTERGUID)_AllNemesis,_,_)
THEN
CharacterSetArmorPercentage(_AllNemesis,100.0);
CharacterSetMagicArmorPercentage(_AllNemesis,100.0);


IF
DialogEnded(_Dialog,_ID)
AND
DB_CoS_AotO_Nemesis(_Nemesis,_Player,_Dialog)
AND
DialogGetInvolvedNPC(_ID,1,(CHARACTERGUID)_Nemesis)
AND
ObjectGetFlag(_Nemesis,"CoS_AotO_NemesisPurgePlayerAfterDialog",0)
THEN
Proc_CoS_AotO_AvatarHasDoneGodConfrontation();
UserSetFlag(_Player,"QuestUpdate_CORE_Chapter5_COS_GodBattle");

//REGION Create Shadow Links of Godwoken

IF
TextEventSet("nemesis")
AND
CharacterGetHostCharacter((CHARACTERGUID)_Player)
THEN
Proc_CoS_AotO_CreateGodwokenNemesis(_Player);


IF
GlobalFlagSet("CoS_AotO_CreateGodwokenNemesis")
AND
DB_CoS_AotO_Competitors(_Char)
AND
CharacterIsDead(_Char,0)
THEN
Proc_CoS_AotO_CreateGodwokenNemesis(_Char);

//REGION DOSTWO-27369 true race query, else set to undead to match the existing DB query
QRY
QRY_CoS_AotO_GodwokenGetRealRace((CHARACTERGUID)_Godwoken)
AND
CharacterGetRace(_Godwoken,0,_Race)
AND
DB_CoS_AotO_CreateGodwokenNemesis_NemesisTemplate(_Race, _, _, _)
AND
NOT DB_CoS_AotO_GodwokenRealRace(_Godwoken, _Race)
THEN
DB_CoS_AotO_GodwokenRealRace(_Godwoken, _Race);

QRY
QRY_CoS_AotO_GodwokenGetRealRace((CHARACTERGUID)_Godwoken)
AND
NOT DB_CoS_AotO_GodwokenRealRace(_Godwoken, _ )
THEN
DB_CoS_AotO_GodwokenRealRace(_Godwoken, "undead");

//END_REGION

PROC
Proc_CoS_AotO_CreateGodwokenNemesis((CHARACTERGUID)_Godwoken)
AND
QRY_CoS_AotO_GodwokenGetRealRace(_Godwoken)
AND
DB_CoS_AotO_GodwokenRealRace(_Godwoken, _Race)
AND
ObjectGetFlag(_Godwoken,"CoS_AotO_NemesisCreated",0)
AND
DB_CoS_AotO_CreateGodwokenNemesis_NemesisTemplate(_Tag,_NemesisTemplate,_BuffFlag,_Dialog)
AND
_Race == _Tag
AND
GetPosition(_GodWoken,_X,_Y,_Z)
AND
RealSum(_X,5.5,_X2)
AND
NOT DB_CoS_AotO_Nemesis(_,_Godwoken,_)
AND
CharacterCreateAtPosition(_X2,_Y,_Z,_NemesisTemplate,1,_Nemesis)
THEN
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_HOE_Summoning_01",_X2,_Y,_Z);
ObjectSetFlag(_Nemesis,"CoS_AotO_IsNemesis");
CharacterTransformAppearanceTo(_Nemesis,_Godwoken,1,0);
CharacterDisableAllCrimes(_Nemesis);
Proc_CoS_AotO_CreateGodwokenNemesis((CHARACTERGUID)_Godwoken,_Nemesis);
ObjectSetFlag(_Godwoken,"CoS_AotO_NemesisCreated"); //Nemesis is an onlyonce per char
PROC_LoopEffect("RS3_FX_Char_Enemies_ShadowPlayer_01",_Nemesis,"SourceMaterial","CoS_Main_Ending","");
DB_CoS_AotO_Nemesis(_Nemesis,_Godwoken,_Dialog); //used to check Last man standing
ApplyStatus(_Nemesis,"ETHEREAL_SOLES",-1.0,1);
ApplyStatus(_Nemesis,"SOURCEBLOOD",-1.0,1);
CharacterLookAt(_Godwoken,_Nemesis);
CharacterSetCorpseLootable(_Nemesis,0);
ObjectSetFlag(_Nemesis,_BuffFlag); //Apply the Global Flag as a character flag to compare later, 
									//and keep track of which god it is since the Template changes

//REGION Recieved Buff because of the Altar events on the island

//Fall back for others recieving buffs
IF
DialogEnded(_Dialog,_)
AND
DB_CoS_AotO_Nemesis((CHARACTERGUID)_Nemesis,_,_Dialog)
AND
DB_CoS_AotO_CreateGodwokenNemesis_NemesisTemplate(_,_,_BuffFlag,_)
AND
ObjectGetFlag(_Nemesis,_BuffFlag,1)
AND
GlobalGetFlag(_BuffFlag,1)
THEN
ObjectSetFlag(_Nemesis,"CoS_AotO_GodRecieveBuff");


//Apply buff for all of the Same type
IF
ObjectFlagSet("CoS_AotO_GodRecieveBuff",(CHARACTERGUID)_Nemesis,_)
AND
DB_CoS_AotO_Nemesis(_Nemesis,_,_)
AND
DB_CoS_AotO_CreateGodwokenNemesis_NemesisTemplate(_,_,_BuffFlag,_)
AND
ObjectGetFlag(_Nemesis,_BuffFlag,1)
AND
DB_CoS_AotO_Nemesis(_OtherNemesis,_,_)
AND
ObjectGetFlag(_OtherNemesis,_BuffFlag,1)
THEN
ApplyStatus(_OtherNemesis,"ENRAGED",-1.0,1);



//END_REGION

//Make Hostile To All Competitors
IF
DB_CoS_AotO_Nemesis(_Nemesis,_,_)
AND
DB_CoS_AotO_Competitors(_Char)
THEN
CharacterSetRelationIndivFactionToIndivFaction(_Char,_Nemesis,0);
CharacterSetRelationIndivFactionToIndivFaction(_Nemesis,_Char,0);


//Ability
PROC
Proc_CoS_AotO_CreateGodwokenNemesis((CHARACTERGUID)_Godwoken,(CHARACTERGUID)_Nemesis)
AND
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos(_Ability,_Skill,_Int)
AND
CharacterGetAbility(_Godwoken,_Ability,_Val)
AND
_Val >= _Int
THEN
CharacterAddSkill(_Nemesis,_Skill);
Proc_CoS_AotO_CreateGodwokenNemesis_AddAbilityLevel(_Nemesis,_Ability,_Val);

PROC
Proc_CoS_AotO_CreateGodwokenNemesis_AddAbilityLevel((CHARACTERGUID)_Nemesis,(STRING)_Ability,(INTEGER)_Val)
AND
StringConcatenate("_CoS_AotO_AddAbilityLevel_",_Ability,_Name)
AND
GetUUID(_Nemesis,_UUID)
AND
StringConcatenate(_UUID,_Name,_OnlyOnce)
AND
QueryOnlyOnce(_OnlyOnce)
THEN
CharacterAddAbility(_Nemesis,_Ability,_Val);

//Create Ai Hints, set Archetype (event sent to script)
PROC
Proc_CoS_AotO_CreateGodwokenNemesis_AddAbilityLevel((CHARACTERGUID)_Nemesis,(STRING)_Ability,(INTEGER)_Val)
AND
CharacterGetAbility(_Nemesis,_Ability,_Int)
AND
DB_CoS_AotO_Nemesis_AiHint(_Ability,_Tag,_Event)
AND
_Int > 4
 THEN
SetVarFixedString(_Nemesis,"AiHint",_Tag);
SetStoryEvent(_Nemesis,_Event);


//Add GodSlayer if killed a Nemesis
IF
CharacterKilledBy((CHARACTERGUID)_Defender,(CHARACTERGUID)_AttackerOwner,_Attacker)
AND
ObjectGetFlag(_Defender,"CoS_AotO_IsNemesis",1)
AND
DB_IsPlayer(_AttackerOwner)
THEN
SetTag(_AttackerOwner,"GODSLAYER");




//END_REGION


//REGION Last Nemesis Standing


IF
CharacterDying(_Nemesis)
AND
DB_CoS_AotO_Nemesis(_Nemesis,_Godwoken,_Dialog)
THEN
NOT DB_CoS_AotO_Nemesis(_Nemesis,_Godwoken,_Dialog);
CreatePuddle(_Nemesis,"SurfaceSource",10, 120, 3, 5, 0.2);
Proc_CoS_AotO_CheckLastNemesisStanding();

PROC
Proc_CoS_AotO_CheckLastNemesisStanding()
AND
SysCount("DB_CoS_AotO_Nemesis",3,_Amount)
AND
_Amount == 1
AND
DB_CoS_AotO_Nemesis((CHARACTERGUID)_Nemesis,_Godwoken,_)
THEN
DB_CoS_AotO_LastNemesis(_Nemesis);
CharacterResurrect(_Nemesis);
CharacterSetImmortal(_Nemesis,1);

IF
DB_CoS_AotO_LastNemesis(_Nemesis)
AND
CharacterGetHitpointsPercentage(_Nemesis,_Per)
AND
_Per < 55.0
AND
QueryOnlyOnce("CoS_AotO_PhaseThreeInit_Once")
THEN
Proc_CoS_AotO_PhaseThreeInit();

IF
CharacterReceivedDamage(_Nemesis, _, _)
AND
DB_CoS_AotO_LastNemesis(_Nemesis)
AND
CharacterGetHitpointsPercentage(_Nemesis,_Per)
AND
_Per < 55.0
AND
QueryOnlyOnce("CoS_AotO_PhaseThreeInit_Once")
THEN
Proc_CoS_AotO_PhaseThreeInit();

//END_REGION

//REGION DOS2EE-4759 Reset god skills skills

IF
SavegameLoaded(_Major,_Minor,_Rev,_Build)
AND
QRY_VersionIsOlderThan(_Major,_Minor,_Rev,_Build, 3, 6, 0, 1)
AND
NOT DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RangerLore","ProjectileStrike_EnemyRainOfArrows",9)
THEN
Proc_CoS_AotO_CreateGodwokenNemesis_SkillReset();

//Remove skills from old DB
PROC
Proc_CoS_AotO_CreateGodwokenNemesis_SkillReset()
AND
DB_CoS_AotO_Nemesis((CHARACTERGUID)_Nemesis,_,_)
AND
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos(_,_Skill,_)
AND
CharacterHasSkill(_Nemesis,_Skill,1)
THEN
CharacterRemoveSkill(_Nemesis,_Skill);

//Remove old skill DB
PROC
Proc_CoS_AotO_CreateGodwokenNemesis_SkillReset()
AND
QueryOnlyOnce("CoS_AotO_CreateGodwokenNemesis_SkillDataBasePurge_Once")
AND
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos(_Class,_Skill,_Level)
THEN
NOT DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos(_Class,_Skill,_Level);

//Re-define skills
PROC
Proc_CoS_AotO_CreateGodwokenNemesis_SkillReset()
AND
QueryOnlyOnce("CoS_AotO_CreateGodwokenNemesis_SkillDataBasUpdate_Once")
THEN
//Air
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("AirSpecialist","Projectile_EnemyLightningBolt",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("AirSpecialist","ProjectileStrike_EnemyDazingBolt",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("AirSpecialist","Target_EnemyPressureSpike",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("AirSpecialist","Shout_EnemyElectricFence",8);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("AirSpecialist","Storm_EnemyLightning",9);
//Fire
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("FireSpecialist","Projectile_EnemyFlamingDaggers",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("FireSpecialist","Target_EnemyHaste",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("FireSpecialist","Target_EnemyFireWhip",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("FireSpecialist","Zone_EnemyLaserRay",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("FireSpecialist","Target_EnemySpontCombustion",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("FireSpecialist","ProjectileStrike_EnemyMeteorShower",9);
//Warrior
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WarriorLore","Cone_EnemyGroundSmash",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WarriorLore","Target_EnemyCripplingBlow",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WarriorLore","Shout_EnemyWhirlwind",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WarriorLore","Target_EnemyFlurry",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WarriorLore","Jump_EnemyPhoenixDive",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WarriorLore","Target_EnemyOverpower",9);
//Poly
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Polymorph","Target_EnemyTentacleLash",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Polymorph","Shout_EnemySteelSkin",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Polymorph","Target_EnemyStripResistance",6);
//Rogue
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RogueLore","Projectile_EnemyThrowingKnife",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RogueLore","Projectile_EnemyChloroform",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RogueLore","Target_EnemyCorruptedBlade",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RogueLore","Jump_EnemyCloakAndDagger",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RogueLore","Target_EnemyDaggersDrawn",8);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RogueLore","Target_EnemyFatality",9);
//Summoning
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Summoning","Target_EnemyHarmony",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Summoning","Target_EnemySoulMate",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Summoning","Target_EnemyTotemFromSurface",6);
//Water
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","Target_EnemyFrostyShell",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","ProjectileStrike_EnemyHailStrike",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","Projectile_EnemyIceFan",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","Target_EnemyWinterBlast",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","Projectile_EnemyChainHeal",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","Cone_EnemySteamLance",8);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("WaterSpecialist","ProjectileStrike_EnemyHailAttack",9);
//Necro
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Necromancy","Target_EnemyMosquitoSwarm",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Necromancy","Target_EnemyShacklesOfPain",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Necromancy","Target_EnemyInfect",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Necromancy","Target_EnemyGraspOfTheStarved",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Necromancy","Cone_EnemySilencingStare",8);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("Necromancy","Shout_EnemyNecromancerTotems",9);
DB_CoS_AotO_Nemesis_AiHint("EarthSpecialist","AiHint_Mage","SwapArchetype_Mage");
//Earth
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("EarthSpecialist","Projectile_EnemyPyroclasticRock",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("EarthSpecialist","Projectile_EnemyPoisonDart",4);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("EarthSpecialist","Shout_EnemyReactiveArmor",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("EarthSpecialist","Target_EnemyWormTremor",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("EarthSpecialist","Projectile_EnemyAcidSpores",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("EarthSpecialist","Projectile_EnemyPyroclasticEruption",9);
//Ranger
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RangerLore","Projectile_EnemyRicochet",3);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RangerLore","Jump_EnemyTacticalRetreat",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RangerLore","Projectile_EnemyPinDown",5);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RangerLore","Projectile_EnemyArrowSpray",6);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RangerLore","Projectile_EnemySnipe",7);
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos("RangerLore","ProjectileStrike_EnemyRainOfArrows",9);
DB_CoS_AotO_Nemesis_AiHint("RangerLore","AiHint_Ranger","SwapArchetype_Ranged");

//Re-add skills
PROC
Proc_CoS_AotO_CreateGodwokenNemesis_SkillReset()
AND
DB_CoS_AotO_Nemesis((CHARACTERGUID)_Nemesis,_Godwoken,_)
AND
DB_CoS_AotO_CreateGodwokenNemesis_SkillCombos(_Ability,_Skill,_Int)
AND
CharacterGetAbility(_Godwoken,_Ability,_Val)
AND
_Val >= _Int
THEN
CharacterAddSkill(_Nemesis,_Skill);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "CoS_ArenaOfTheOne"
