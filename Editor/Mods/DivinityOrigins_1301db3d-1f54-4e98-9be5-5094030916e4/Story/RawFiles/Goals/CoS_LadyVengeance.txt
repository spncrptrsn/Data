Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Subregion(TRIGGERGUID_S_CoS_SUB_Temples_LVTopDeck_f9679914-8348-4083-9171-1f62bd020ef0,"LV_HoE_SUB_LadyVengeance",0);

DB_Dialogs(ITEMGUID_S_CoS_LV_ShipHead_4129f1cd-a8d4-4439-837d-2fc686216cc5,"CoS_LV_ShipHead");

ProcTriggerRegisterForPlayers(TRIGGERGUID_S_CoS_LV_TarquinAlmiraDialogStart_Box_c8c61662-d96f-4685-b8c8-818bd2fd843f);

Proc_CoS_LadyVengenace_Init();

//CHARACTERGUID_S_CoS_MonolithBoss_Char_c963ad5d-72f7-4c0e-8f08-910ffe11a0a3
//ITEMGUID_FUR_Rich_Chest_A_Steel_001_COS_000_1533d44e-750c-84d5-5c2a-797343ea5ce0
DB_HasStoryEvent(ITEMGUID_S_CoS_Temples_PromiseBreaker_Blade_ad5823d0-2b93-413b-af20-39f72fff3140,"CoS_PromiseBreaker_HasBlade");
DB_HasStoryEvent(ITEMGUID_S_CoS_Temples_PromiseBreaker_Shaft_ede5be02-9cc9-4c3e-b9c7-0ff9bd50f2e9,"CoS_PromiseBreaker_HasShaft");
DB_GiveItemToEvent(ITEMGUID_S_CoS_Temples_PromiseBreaker_Blade_ad5823d0-2b93-413b-af20-39f72fff3140,"CoS_PromiseBreaker_GiveBlade");
DB_GiveItemToEvent(ITEMGUID_S_CoS_Temples_PromiseBreaker_Shaft_ede5be02-9cc9-4c3e-b9c7-0ff9bd50f2e9,"CoS_PromiseBreaker_GiveShaft");
KBSECTION
//REGION Flag to indicate we are in CoS (for dialogs)
PROC
Proc_CoS_LadyVengenace_Init()
THEN
GlobalSetFlag("GLO_Region_CoS");

PROC
ProcRegionEnded("CoS_Main")
THEN
GlobalClearFlag("GLO_Region_CoS");
//END_REGION

//REGION Start Act2m

IF
StoryEvent(_Player,"CoS_LV_RiverStartDialog")
AND
NOT QRY_CoS_LV_Act2mIntro_TryLohse()
AND
NOT QRY_CoS_LV_Act2mIntro_TryAnyAvatar()
THEN
DialogRequestStop((CHARACTERGUID)_Player); //Make sure the spotted player is not in dialogue currently
ProcFaceEachother(_Player, CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e);
Proc_StartDialog(0,"CoS_LV_River",CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e,_Player);

QRY
QRY_CoS_LV_Act2mIntro_TryLohse()
AND
DB_Avatars((CHARACTERGUID)CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295)
AND
QRY_SpeakerIsAvailableAndInDialogRange(CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295, CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e)
AND
QRY_StartDialog(0, "CoS_LV_River", CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e, CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295)
THEN
DB_NoOp(1);

QRY
QRY_CoS_LV_Act2mIntro_TryAnyAvatar()
AND
DB_Avatars((CHARACTERGUID)_Avatar)
AND
QRY_SpeakerIsAvailableAndInDialogRange(_Avatar, CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e)
AND
QRY_StartDialog(0, "CoS_LV_River", CHARACTERGUID_S_GLO_River_f4931ebe-10b0-43c4-9182-640cf052717e, _Avatar)
THEN
DB_NoOp(1);

//END_REGION

//REGION Debug Events
IF
TextEventSet("JahanCoS")
AND
DB_GLO_LevelTraveler("CoS_Main",CHARACTERGUID_S_GLO_Jahan_4d222cfd-49c8-41d3-8cb7-2a2b56232633,_Trigger,_Dialog,_,_,0)
THEN
TeleportTo(CHARACTERGUID_S_GLO_Jahan_4d222cfd-49c8-41d3-8cb7-2a2b56232633,_Trigger);
ProcRemoveAllDialogEntriesForSpeaker(CHARACTERGUID_S_GLO_Jahan_4d222cfd-49c8-41d3-8cb7-2a2b56232633);
DB_Dialogs(CHARACTERGUID_S_GLO_Jahan_4d222cfd-49c8-41d3-8cb7-2a2b56232633,_Dialog);

//END_REGION

//REGION Stop Player if havent talked to Almira / Tarquin
IF
CharacterEnteredTrigger((CHARACTERGUID)_Player,TRIGGERGUID_S_CoS_LV_TarquinAlmiraDialogStart_Box_c8c61662-d96f-4685-b8c8-818bd2fd843f)
AND
DB_IsPlayer(_Player)
AND
QRY_IsAvailableTo(_Player,CHARACTERGUID_S_GLO_Almira_49b466a9-e67f-4e3c-8f66-3636ba7dd54f)
AND
QueryOnlyOnce("CoS_LV_TarquinAlmiraDialogStart_Once")
THEN
Proc_StartDialog(0,"CoS_LV_Almira",CHARACTERGUID_S_GLO_Almira_49b466a9-e67f-4e3c-8f66-3636ba7dd54f,_Player);

IF
CharacterEnteredTrigger((CHARACTERGUID)_Player,TRIGGERGUID_S_CoS_LV_TarquinAlmiraDialogStart_Box_c8c61662-d96f-4685-b8c8-818bd2fd843f)
AND
DB_IsPlayer(_Player)
AND
QRY_IsAvailableTo(_Player,CHARACTERGUID_S_GLO_Tarquin_a3d0cdcc-d0b0-464d-9ad7-43b38baed620)
AND
QueryOnlyOnce("CoS_LV_TarquinAlmiraDialogStart_Once")
THEN
Proc_StartDialog(0,"CoS_LV_Tarquin",CHARACTERGUID_S_GLO_Tarquin_a3d0cdcc-d0b0-464d-9ad7-43b38baed620,_Player);

IF
DialogStarted("CoS_LV_Tarquin",_)
AND
QueryOnlyOnce("CoS_LV_TarquinAlmiraDialogStart_Once")
THEN
DB_NOOP(1);

IF
DialogStarted("CoS_LV_Almira",_)
AND
QueryOnlyOnce("CoS_LV_TarquinAlmiraDialogStart_Once")
THEN
DB_NOOP(1);

//END_REGION


//User Flags for our Cover Stories
//CoS_Temples_AlmiraCoverStory
//CoS_Temples_TarquinCoverStory

//REGION Almira
IF
ItemAddedToCharacter(ITEMGUID_S_CoS_Temples_PromiseBreaker_Blade_ad5823d0-2b93-413b-af20-39f72fff3140,_Player)
AND
DB_IsPlayer(_Player)
AND
PartyGetFlag(_Player,"QuestUpdate_CoS_BreakerForAlmira_FoundBlade",0)
THEN
PartySetFlag(_Player,"QuestUpdate_CoS_BreakerForAlmira_FoundBlade");
Proc_CoS_PromiseBreaker_CheckForParts(_Player);

IF
ItemAddedToCharacter(ITEMGUID_S_CoS_Temples_PromiseBreaker_Shaft_ede5be02-9cc9-4c3e-b9c7-0ff9bd50f2e9,_Player)
AND
DB_IsPlayer(_Player)
AND
PartyGetFlag(_Player,"QuestUpdate_CoS_BreakerForAlmira_FoundShaft",0)
THEN
PartySetFlag(_Player,"QuestUpdate_CoS_BreakerForAlmira_FoundShaft");
Proc_CoS_PromiseBreaker_CheckForParts(_Player);

PROC
Proc_CoS_PromiseBreaker_CheckForParts((CHARACTERGUID)_Player)
AND
QryItemInMagicPockets(_Player,ITEMGUID_S_CoS_Temples_PromiseBreaker_Blade_ad5823d0-2b93-413b-af20-39f72fff3140)
AND
QryItemInMagicPockets(_Player,ITEMGUID_S_CoS_Temples_PromiseBreaker_Shaft_ede5be02-9cc9-4c3e-b9c7-0ff9bd50f2e9)
THEN
PartySetFlag(_Player,"QuestUpdate_CoS_BreakerForAlmira_HaveBothParts");

IF
ItemTemplateCombinedWithItemTemplate("QUEST_Ataraxians_Scythe_2H_A_Blade_e2a186e1-7772-4f10-96e2-fb6329e25d69","QUEST_Ataraxians_Scythe_2H_A_Shaft_398998dd-7776-4fc4-98dd-d9f0f8fc4e0b","QUEST_AtaraxianTablet_PromiseInfo_23edac9b-2517-4fd0-a068-6bc9e60d1bbc",_,_,_Player,_Item)
AND
DB_IsPlayer(_Player)
AND
PartyGetFlag(_Player,"QuestUpdate_CoS_BreakerForAlmira_GotTabletBack",1)
THEN
PartySetFlag(_Player,"QuestUpdate_CoS_BreakerForAlmira_CraftedBreaker");

IF
ObjectFlagSet("CoS_LV_Almira_GiveSkillbook",(CHARACTERGUID)_Player,_)
THEN
ItemTemplateAddTo("BOOK_Skill_Water_BloodStorm_4fd6849b-b2cb-416b-9094-05c55d56c88e",_Player,1);

IF
DialogEnded("CoS_LV_Almira",_)
AND
DB_GlobalFlag("GLO_Almira_ReceivedSwornbreaker")
AND
QueryOnlyOnce("Almira_RemovesPromise")
THEN
Proc_GLO_Almira_RemovePromise();

PROC
Proc_GLO_Almira_RemovePromise()
THEN
ObjectSetFlag(CHARACTERGUID_S_GLO_Almira_49b466a9-e67f-4e3c-8f66-3636ba7dd54f,"GLO_PromiseBreaker_UseSkill");
Proc_RemoveItemFromTemplate(CHARACTERGUID_S_GLO_Almira_49b466a9-e67f-4e3c-8f66-3636ba7dd54f,"QUEST_AtaraxianTablet_PromiseInfo_23edac9b-2517-4fd0-a068-6bc9e60d1bbc","");
Proc_RemoveItemFromTemplate(CHARACTERGUID_S_GLO_Almira_49b466a9-e67f-4e3c-8f66-3636ba7dd54f,"ITEMGUID_QUEST_Ataraxians_Scythe_2H_A_Blade_e2a186e1-7772-4f10-96e2-fb6329e25d69","");
Proc_RemoveItemFromTemplate(CHARACTERGUID_S_GLO_Almira_49b466a9-e67f-4e3c-8f66-3636ba7dd54f,"ITEMGUID_QUEST_Ataraxians_Scythe_2H_A_Shaft_398998dd-7776-4fc4-98dd-d9f0f8fc4e0b","");
Proc_RemoveItemFromTemplate(CHARACTERGUID_S_GLO_Almira_49b466a9-e67f-4e3c-8f66-3636ba7dd54f,"WPN_Ataraxians_Scythe_2H_A_3b45c978-5a42-40b5-a7aa-183852616910","");

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "CouncilOfSeven"
