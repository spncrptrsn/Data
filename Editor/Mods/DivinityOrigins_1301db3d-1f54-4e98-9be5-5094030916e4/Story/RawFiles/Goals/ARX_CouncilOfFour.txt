Version 1
SubGoalCombiner SGC_AND
INITSECTION
// Check Council of Four situation for more details
// ArxRework: https://sites.google.com/a/larian.com/larian-wikisite/dos2-dlc/dos2dlc-quest-design-documentation/council-of-four-arx-rework
// https://docs.google.com/document/d/1GCGB2qRDe94uu23okthZS6Ta1wg2Qn3KqgV4Tjgx9vo/edit#bookmark=id.hstchpjhmhfm
// https://sites.google.com/a/larian.com/projecttest/home/wiki-articles/quest-design-documentation/council-of-four

DB_Dialogs(CHARACTERGUID_S_ARX_KemmMansion_Lady_6437df25-3c12-4a69-96b2-0188afb59255,"ARX_KemmMansion_Lady");
DB_Dialogs(CHARACTERGUID_S_ARX_KemmMansion_Pleb1_47abbff7-3be8-4ab3-873e-b17f2477b736,"ARX_KemmMansion_Pleb1");
DB_Dialogs(CHARACTERGUID_S_ARX_KemmMansion_Pleb2_dd9313c4-32b6-4e05-94b5-a39b1fe289fc,"ARX_KemmMansion_Pleb2");

DB_ARX_KemmMansion_LadyTea("ARX_KemmMansion_LadyTea_A","Quest_Tea_Brand_A_28c858bc-ed95-43eb-85a7-7aadaa53f3de");
DB_ARX_KemmMansion_LadyTea("ARX_KemmMansion_LadyTea_B","Quest_Tea_Brand_B_8075aed3-8e8b-4fb8-aa0f-cc1f22d50555");
DB_ARX_KemmMansion_LadyTea("ARX_KemmMansion_LadyTea_C","Quest_Tea_Brand_C_440b1a0c-508b-4f48-92ae-339af81ad84a");

DB_ARX_KemmMansion_LadyNormalTea("ARX_KemmMansion_LadyTeaSort_A","Quest_Tea_Cup_Brand_A_d01218dd-f4bb-47fd-9847-2012fb7bde3b");
DB_ARX_KemmMansion_LadyNormalTea("ARX_KemmMansion_LadyTeaSort_B","Quest_Tea_Cup_Brand_B_2af8d8ce-ce4a-4fd5-9de7-79dd9a1ea9c4");
DB_ARX_KemmMansion_LadyNormalTea("ARX_KemmMansion_LadyTeaSort_C","Quest_Tea_Cup_Brand_C_3172d889-30ed-405d-a295-e01922a6e77b");

ProcTriggerRegisterForPlayers(TRIGGERGUID_S_ARX_KemmMansion_LadyRoom_aba447c1-df1d-4e70-825c-0c97527b9ab9);

DB_HasStoryEvent(ITEMGUID_S_ARX_KemmVault_WeddingRing_Wife_549aaea3-c37d-4bb4-b662-b9185e1d36db,"ARX_KemmMansion_Lady_HasWeddingRing");
DB_GiveItemToEvent(ITEMGUID_S_ARX_KemmVault_WeddingRing_Wife_549aaea3-c37d-4bb4-b662-b9185e1d36db,"ARX_KemmMansion_Lady_ReceiveWeddingRing");
KBSECTION
//REGION Council of Four

IF
CharacterEnteredTrigger(_,TRIGGERGUID_S_ARX_KemmMansion_LadyRoom_aba447c1-df1d-4e70-825c-0c97527b9ab9)
AND
QueryOnlyOnce("ARX_KemmMansion_LadyRoomEntered")
THEN
GlobalSetFlag("ARX_KemmMansion_LadyRoomEntered");

IF
CharacterUsedItem(_Player,ITEMGUID_S_ARX_KemmMansion_CouncilPainting_d95d8711-757e-44a9-930d-66899b60c1d8)
AND
ObjectGetFlag(_Player,"ARX_KemmMansion_CouncilResolved",0)
AND
NOT DB_ARX_CouncilOfFour_VB(1)
THEN
DB_ARX_CouncilOfFour_VB(1);
StartVoiceBark("ARX_VB_CouncilOfFour",_Player);

IF
CharacterUsedItem(_Player,ITEMGUID_S_ARX_KemmMansion_CouncilPainting_d95d8711-757e-44a9-930d-66899b60c1d8)
AND
ObjectGetFlag(_Player,"ARX_KemmMansion_CouncilResolved",1)
THEN
StartVoiceBark("ARX_VB_CouncilOfFour_Resolved",_Player);

IF
VoiceBarkEnded("ARX_VB_CouncilOfFour",_)
THEN
NOT DB_ARX_CouncilOfFour_VB(1);

IF
VoiceBarkFailed("ARX_VB_CouncilOfFour")
THEN
NOT DB_ARX_CouncilOfFour_VB(1);

IF
VoiceBarkEnded("ARX_VB_CouncilOfFour",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
DB_InRegion(_Player,TRIGGERGUID_S_ARX_KemmMansion_LadyRoom_aba447c1-df1d-4e70-825c-0c97527b9ab9)
AND
CharacterCanSee(CHARACTERGUID_S_RC_ARX_KemmMansion_Lady_6437df25-3c12-4a69-96b2-0188afb59255,_Player,1)
THEN
Proc_StartDialog(0,"ARX_KemmMansion_CouncilOfFour",CHARACTERGUID_S_RC_ARX_KemmMansion_Lady_6437df25-3c12-4a69-96b2-0188afb59255,_Player);

IF
VoiceBarkEnded("ARX_VB_CouncilOfFour",_ID)
AND
DialogGetInvolvedPlayer(_ID,1,(CHARACTERGUID)_Player)
AND
DB_InRegion(_Player,TRIGGERGUID_S_ARX_KemmMansion_LadyRoom_aba447c1-df1d-4e70-825c-0c97527b9ab9)
AND
CharacterCanSee(CHARACTERGUID_S_RC_ARX_KemmMansion_Lady_6437df25-3c12-4a69-96b2-0188afb59255,_Player,1)
AND
QRY_SpeakerIsAvailable(CHARACTERGUID_S_RC_ARX_KemmMansion_Lady_6437df25-3c12-4a69-96b2-0188afb59255)
THEN
Proc_StartDialog(1,"ARX_AD_KemmMansion_CouncilOfFourBlocked",CHARACTERGUID_S_RC_ARX_KemmMansion_Lady_6437df25-3c12-4a69-96b2-0188afb59255);

//END_REGION

//REGION Tea Party
IF
ObjectFlagSet(_Flag,_Player,_)
AND
DB_ARX_KemmMansion_LadyTea(_Flag,_TeaBrand)
THEN
ItemTemplateAddTo(_TeaBrand,_Player,1);
ObjectClearFlag(_Player,_Flag,0);

IF
ObjectFlagSet(_Flag,_Player,_)
AND
DB_ARX_KemmMansion_LadyNormalTea(_Flag,_TeaBrand)
THEN
ItemTemplateAddTo(_TeaBrand,_Player,1);
ObjectClearFlag(_Player,_Flag,0);

//END_REGION

IF
CharacterDied(CHARACTERGUID_S_ARX_KemmMansion_Lady_6437df25-3c12-4a69-96b2-0188afb59255)
THEN
GlobalSetFlag("ARX_KemmMansion_LadyDead");
DialogRequestStop(CHARACTERGUID_S_ARX_KemmMansion_Pleb1_47abbff7-3be8-4ab3-873e-b17f2477b736);
DialogRequestStop(CHARACTERGUID_S_ARX_KemmMansion_Pleb2_dd9313c4-32b6-4e05-94b5-a39b1fe289fc);
SetHasDialog(CHARACTERGUID_S_ARX_KemmMansion_Pleb1_47abbff7-3be8-4ab3-873e-b17f2477b736,0);
SetHasDialog(CHARACTERGUID_S_ARX_KemmMansion_Pleb2_dd9313c4-32b6-4e05-94b5-a39b1fe289fc,0);
ProcCharacterDisappearOutOfSight(CHARACTERGUID_S_ARX_KemmMansion_Pleb1_47abbff7-3be8-4ab3-873e-b17f2477b736,0,0,"",0);
ProcCharacterDisappearOutOfSight(CHARACTERGUID_S_ARX_KemmMansion_Pleb2_dd9313c4-32b6-4e05-94b5-a39b1fe289fc,0,0,"",0);


//REGION Debug

IF
TextEventSet("SetKemmDeadFlags")
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"ARX_Kemm_SawDeadKemm");


IF
TextEventSet("SetArhuMissingFlags")
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"ARX_Kemm_Knows_KemmInvitingArhu");
ObjectSetFlag(_Player,"QuestUpdate_ARX_DisappearanceOfArhu_ArhuIsMissing");


IF
TextEventSet("SetArhuRescuedFlags")
AND
DB_IsPlayer(_Player)
THEN
GlobalSetFlag("ARX_KemmVault_Arhu_InCathedral");
ObjectSetFlag(_Player,"ARX_Kemm_SawDeadKemm");
ObjectSetFlag(_Player,"ARX_KemmVault_SawArhu");

//END_REGION


//REGION Dialog flags


IF
DialogEnded("ARX_KemmMansion_Lady",_)
THEN
GlobalClearFlag("ARX_KemmMansion_Lady_DialogFlow_ToldBadNews");
GlobalClearFlag("ARX_KemmMansion_Lady_DialogFlow_AddressedKemmTopic");
GlobalClearFlag("ARX_KemmMansion_Lady_DialogFlow_BlockTeaQuestion");



//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ARX"
