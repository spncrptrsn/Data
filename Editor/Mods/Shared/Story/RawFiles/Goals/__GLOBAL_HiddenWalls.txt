Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_HiddenWallCount(0);
KBSECTION
//REGION Use event to open hidden wall

IF
GlobalFlagSet(_flag)
AND
DB_HiddenWallEvent((STRING)_flag, (INTEGER)_wallIndex)
THEN
PROC_OpenWall(_WallIndex);

//END_REGION

//REGION Action When Player Uses Item To Open Wall

IF
CharacterUsedItem(_Player, _Item)
AND
_Player.DB_IsPlayer()
AND
DB_HiddenWallItem((ITEMGUID)_Item, (INTEGER)_WallIndex)
THEN
PROC_CommentHiddenEffect(_Player);
PROC_OpenWall(_WallIndex);

//END_REGION

//REGION Action When Player Uses Trigger To Open Wall

IF
DB_HiddenWallTrigger(_trigger, _)
THEN
DB_HW_CharCountInTrigger(_trigger, 0);

IF
CharacterEnteredTrigger(_, _trigger)
AND
DB_HiddenWallTrigger((TRIGGERGUID)_trigger, (INTEGER)_wallIndex)
AND
DB_HW_CharCountInTrigger(_trigger, _current)
AND
IntegerSum(_current, 1, _new)
THEN
NOT DB_HW_CharCountInTrigger(_trigger, _current);
DB_HW_CharCountInTrigger(_trigger, _new);

IF
CharacterLeftTrigger(_, _trigger)
AND
DB_HiddenWallTrigger((TRIGGERGUID)_trigger, (INTEGER)_wallIndex)
AND
DB_HW_CharCountInTrigger(_trigger, _current)
AND
IntegerSubtract(_current, 1, _new)
THEN
NOT DB_HW_CharCountInTrigger(_trigger, _current);
DB_HW_CharCountInTrigger(_trigger, _new);

IF
DB_HW_CharCountInTrigger(_trigger, _current)
AND
_current < 0
THEN
NOT DB_HW_CharCountInTrigger(_trigger, _current);
DB_HW_CharCountInTrigger(_trigger, 0);

IF
DB_HW_CharCountInTrigger(_trigger, _current)
AND
_current > 0
AND
DB_HiddenWallTrigger(_trigger, _wallIndex)
THEN
PROC_OpenWall(_wallIndex);

IF
DB_HW_CharCountInTrigger(_trigger, _current)
AND
_current == 0
AND
DB_HiddenWallTrigger(_trigger, _wallIndex)
THEN
PROC_CloseWall(_wallIndex);


//END_REGION

//REGION Action When Player Uses Skills To Open Wall

IF
StoryEvent(_Wall, "Open")
AND
DB_HiddenWall(_WallIndex, (ITEMGUID)_Wall)
THEN
PROC_OpenWall(_WallIndex);

//END_REGION

//REGION Registering all the hidden walls 

PROC
PROC_Register_HiddenWall((ITEMGUID)_Wall)
AND
NOT DB_HiddenWall(_, (ITEMGUID)_Wall)
AND
DB_HiddenWallCount(_Current)
AND
IntegerSum(_Current, 1, _New)
THEN
NOT DB_HiddenWallCount(_Current);
DB_HiddenWallCount(_New);
DB_HiddenWall((INTEGER)_New, (ITEMGUID)_Wall);
DB_HW_ClosedWalls(_New);

//END_REGION

//REGION Open/Close Hidden walls

PROC
PROC_OpenWall((INTEGER)_WallIndex)
AND
DB_HW_ClosedWalls(_WallIndex)
AND
DB_HiddenWall(_WallIndex, _Wall)
THEN
NOT DB_HW_ClosedWalls(_WallIndex);
PlayEffect(_Wall, "RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01");
SetOnStage(_Wall, 0);

PROC
PROC_CloseWall((INTEGER)_WallIndex)
AND
NOT DB_HW_ClosedWalls(_WallIndex)
AND
DB_HiddenWall(_WallIndex, _Wall)
THEN
DB_HW_ClosedWalls(_WallIndex);
PlayEffect(_Wall, "RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01");
SetOnStage(_Wall, 1);

//END_REGION
EXITSECTION

ENDEXITSECTION
