Version 1
SubGoalCombiner SGC_AND
INITSECTION
// Events to handle between the end of character creation and before the next level starts
KBSECTION
//REGION Disconnects between end of CC and start of game
IF
UserDisconnected(_UserID,_,_UserProfile)
AND
NOT DB_InCharacterCreation(1)
AND
DB_SelectedCC(_Char,_UserProfile)
THEN
PROC_GLO_PartyMembers_Remove(_Char,1);
ClearTag(_Char,"AVATAR");
NOT DB_IsPlayer(_Char);
ProcUnRegisterPlayerTriggers(_Char);
NOT DB_SelectedCC(_Char,_UserProfile);
PROC_RemoveIfGenericOrigin(_Char);

// Generic origins don't appear anywhere in the game if they are not players
// -> if a player that selected one during CC disconnects before the first
// game level starts, set it off-stage again after making it an NPC.
PROC
PROC_RemoveIfGenericOrigin((CHARACTERGUID)_Char)
AND
DB_GenericOrigins(_Char)
THEN
SetOnStage(_Char,0);
//END_REGION

//REGION Prevent teleport race in case of auto-selected origin
// In case a RegionStarted event arrives between starting and ending
// the teleport of the randomly selected origin to the dummy, cancel
// the teleport because the origin was a player already at this time
// and hence it will automatically be teleported to the current level
// already (and the dummy may no longer be a player and be left behind
// in the previous level)
IF
RegionStarted(_)
AND
DB_ChosenOriginWaitingForTeleport(_Player)
THEN
NOT DB_ChosenOriginWaitingForTeleport(_Player);
CharacterPurgeQueue(_Player);
//END_REGION

//REGION Complete
IF
RegionStarted(_Level)
AND
IsGameLevel(_Level, 1)
THEN
GoalCompleted;
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Z_Shared_Campaign_At_End"
