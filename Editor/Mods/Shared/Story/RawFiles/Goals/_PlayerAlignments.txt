Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
PROC
ProcFixPlayerAlignments()
AND
DB_IsPlayer(_Player)
AND
NOT DB_PlayerAlignments_BlockFix(_Player)
AND
IsInArena(_Player,0)
AND
CharacterIsPlayer(_Player,1)
AND
DB_IsPlayer(_OtherPlayer)
AND
NOT DB_PlayerAlignments_BlockFix(_OtherPlayer)
AND
_Player!=_OtherPlayer
AND
IsInArena(_OtherPlayer,0)
AND
CharacterIsPlayer(_OtherPlayer,1)
AND
CharacterGetReservedUserID(_Player,_User)
AND
CharacterGetReservedUserID(_OtherPlayer,_OtherUser)
AND
GetUserProfileID(_User,_UserProfileID)
AND
GetUserProfileID(_OtherUser,_OtherUserProfileID)
THEN
ProcSetPlayerAlignment(_Player,_OtherPlayer,_UserProfileID,_OtherUserProfileID);

PROC
ProcFixPlayerAlignments()
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
NOT DB_PlayerAlignments_BlockFix(_Player)
AND
IsInArena(_Player,0)
AND
DB_IsPlayer((CHARACTERGUID)_OtherPlayer)
AND
NOT DB_PlayerAlignments_BlockFix(_OtherPlayer)
AND
_Player != _OtherPlayer
AND
IsInArena(_OtherPlayer,0)
AND
CharacterGetRelationToCharacter(_Player,_OtherPlayer,_Relation)
THEN
ProcLeavePartyIfRelationLow(_Player,_OtherPlayer,_Relation);

PROC
ProcSetPlayerAlignment((CHARACTERGUID)_Player,(CHARACTERGUID)_OtherPlayer,(STRING)_User,(STRING)_OtherUser)
AND
DB_UserAlign(_User,_OtherUser,_Result)
THEN
CharacterSetRelationIndivFactionToIndivFaction(_Player,_OtherPlayer,_Result);

PROC
ProcSetPlayerAlignment((CHARACTERGUID)_Player,(CHARACTERGUID)_OtherPlayer,(STRING)_User,(STRING)_OtherUser)
AND
NOT DB_UserAlign(_User,_OtherUser,_)
AND
CharacterIsInPartyWith(_Player,_OtherPlayer,1)	
THEN
CharacterSetRelationIndivFactionToIndivFaction(_Player,_OtherPlayer,100);

PROC
ProcSetPlayerAlignment((CHARACTERGUID)_Player,(CHARACTERGUID)_OtherPlayer,(STRING)_User,(STRING)_OtherUser)
AND
NOT DB_UserAlign(_User,_OtherUser,_)
AND
CharacterIsInPartyWith(_Player,_OtherPlayer,0)	
THEN
CharacterSetRelationIndivFactionToIndivFaction(_Player,_OtherPlayer,50);

PROC
ProcLeavePartyIfRelationLow((CHARACTERGUID)_Player,(CHARACTERGUID)_OtherPlayer,(INTEGER)_Relation)
AND
_Relation < 25
AND
CharacterIsInPartyWith(_Player,_OtherPlayer,1)
AND
CharacterGetReservedUserID(_Player,_UserID)
AND
CharacterGetReservedUserID(_OtherPlayer,_OtherUserID)
AND
_UserID != _OtherUserID
THEN
LeaveParty(_OtherUserID);

/***************************************************************************************
	Fix alignments when swapping characters between users or creating new players.
****************************************************************************************/

IF
CharacterReservedUserIDChanged(_,_,_)
THEN
ProcFixPlayerAlignments();

IF 
DB_IsPlayer(_)
THEN
ProcFixPlayerAlignments();

IF 
GlobalFlagSet("SetupUserAlignments")
THEN
ProcFixPlayerAlignments();
GlobalClearFlag("SetupUserAlignments");

IF
CharacterJoinedParty(_)
THEN
ProcFixPlayerAlignments();

/***************************************************************************************
	DB_UserAlign logic
****************************************************************************************/

IF
GameStarted(_,_)
THEN
GlobalSetFlag("SetupUserAlignments");

IF 
UserDisconnected(_User,_,_UserProfileID)
AND
DB_UserAlign(_UserProfileID,_OtherUser,_Result)
THEN
NOT DB_UserAlign(_UserProfileID,_OtherUser,_Result);

IF 
UserDisconnected(_User,_,_UserProfileID)
AND
DB_UserAlign(_OtherUser,_UserProfileID,_Result)
THEN
NOT DB_UserAlign(_OtherUser,_UserProfileID,_Result);

IF 
UserMakeWar(_Source,_Target,1)
AND
GetUserProfileID(_Source,_SourceProfileID)
AND
GetUserProfileID(_Target,_TargetProfileID)
THEN
ProcSetUserAlign(_SourceProfileID,_TargetProfileID,0);
ProcSetUserAlign(_TargetProfileID,_SourceProfileID,0);
ProcFixPlayerAlignments();

IF 
UserMakeWar(_Source,_Target,0)
AND
GetUserProfileID(_Source,_SourceProfileID)
AND
GetUserProfileID(_Target,_TargetProfileID)
THEN
NOT DB_UserAlign(_SourceProfileID,_TargetProfileID,0);
NOT DB_UserAlign(_TargetProfileID,_SourceProfileID,0);
ProcFixPlayerAlignments();

PROC
ProcSetUserAlign((STRING)_SourceProfileID,(STRING)_TargetProfileID,(INTEGER)_NewResult)
AND
DB_UserAlign(_SourceProfileID,_TargetProfileID,_Result)
THEN
NOT DB_UserAlign(_SourceProfileID,_TargetProfileID,_Result);

PROC
ProcSetUserAlign((STRING)_SourceProfileID,(STRING)_TargetProfileID,(INTEGER)_NewResult)
THEN
DB_UserAlign(_SourceProfileID,_TargetProfileID,_NewResult);

/***************************************************************************************
	DB_UserAlign Debugging
****************************************************************************************/

IF 
TextEventSet("useralign")
AND 
DB_UserAlign(_User,_OtherUser,_Result)
AND
DB_IsPlayer(_Player)
AND
StringConcatenate(_User," vs ",_Res)
AND
StringConcatenate(_Res,_OtherUser,_Res2)
AND
StringConcatenate(_Res2,":  ",_Res3)
AND
IntegertoString(_Result,_IntStr)
AND
StringConcatenate(_Res3,_IntStr,_FinalResult)
THEN
DebugText(_Player,_FinalResult);
EXITSECTION

ENDEXITSECTION
