Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
QRY
QRY_SpeakerBlockedBycombat((GUIDSTRING)_Char,(INTEGER)_Ignore)
AND
ObjectIsCharacter(_Char,1)
AND
_Ignore == 0
AND
CharacterIsInCombat((CHARACTERGUID)_Char,1)
THEN
DB_NOOP(1);

QRY
QRY_SpeakerIsAvailable((GUIDSTRING)_Item)
AND
ObjectExists(_Item,1)
AND
ObjectIsItem(_Item,1)
AND
QRY_SpeakerIsAvailable(_Item,0)
THEN
DB_NOOP(1);

QRY
QRY_SpeakerIsAvailable((GUIDSTRING)_Char)
AND
ObjectIsCharacter(_Char,1)
AND
QRY_SpeakerIsAvailable((GUIDSTRING)_Char, 0)
THEN
DB_NOOP(1);

QRY
QRY_SpeakerIsDead((GUIDSTRING)_Char)
AND
ObjectIsCharacter(_Char,1)
AND
CharacterIsDeadOrFeign((CHARACTERGUID)_Char,1)
THEN
DB_NOOP(1);

QRY
QRY_SpeakerIsDead((GUIDSTRING)_Item)
AND
ObjectIsItem(_Item,1)
AND
ItemIsDestroyed((ITEMGUID)_Item,1)
THEN
DB_NOOP(1);

QRY
QRY_HasInteractionDisabled((GUIDSTRING)_Char)
AND
ObjectIsCharacter(_Char,1)
AND
CharacterIsPolymorphInteractionDisabled((CHARACTERGUID)_Char,1)
THEN
DB_NOOP(1);

QRY
QRY_SpeakerIsAvailable((GUIDSTRING)_Char, (INTEGER)_IgnoreCombat)
AND
NOT QRY_SpeakerIsDead(_Char)
AND
IsSpeakerReserved(_Char,0)
AND
NOT QRY_HasInteractionDisabled(_Char)
AND
NOT QRY_SpeakerBlockedBycombat(_Char,_IgnoreCombat)
THEN
DB_NOOP(1);

QRY
QRY_SpeakerIsAvailable(NULL_00000000-0000-0000-0000-000000000000)
THEN
DB_NOOP(1);

QRY
QRY_CharacterIsNotDisabled((CHARACTERGUID)_Character)
AND
HasActiveStatus(_Character,"FROZEN",0)
AND
HasActiveStatus(_Character,"STUNNED",0)
AND
HasActiveStatus(_Character,"FEAR",0)
AND
HasActiveStatus(_Character,"PETRIFIED",0)
AND
HasActiveStatus(_Character,"KNOCKED_DOWN",0)
THEN
DB_NOOP(1);

QRY
QRY_SpeakerIsAvailableForCombat((CHARACTERGUID)_Char)
AND
CharacterIsDeadOrFeign(_Char,0)
AND
IsSpeakerReserved(_Char,0)
THEN
DB_NOOP(1);


QRY
QRY_SpeakerIsAvailableAndInDialogRange((CHARACTERGUID)_Char,(CHARACTERGUID)_SourceCharacter)
AND
CharacterIsDeadOrFeign(_Char,0)
AND
IsSpeakerReserved(_Char,0)
AND
CharacterIsInCombat(_Char,0)
AND
GetDistanceTo(_Char,_SourceCharacter,_Distance)
AND
_Distance < 10.0
THEN
DB_NOOP(1);

QRY
Query_IsPlayerHiding((CHARACTERGUID)_Char)
AND
HasActiveStatus(_Char,"SNEAKING",1)
THEN
DB_NOOP(1);

QRY
Query_IsPlayerHiding((CHARACTERGUID)_Char)
AND
HasActiveStatus(_Char,"INVISIBLE",1)
THEN
DB_NOOP(1);

QRY
QRY_CharacterIsNull((CHARACTERGUID)_Char)
AND
_Char == NULL_00000000-0000-0000-0000-000000000000
THEN
DB_NOOP(1);

//REGION Find a player with petpal to talk to
QRY
QRY_PetPallerInDialogRange((CHARACTERGUID)_Char)
AND
DB_QRYRTN_PetPallerInDialogRange(_Petpaller)
THEN
NOT DB_QRYRTN_PetPallerInDialogRange(_Petpaller);

QRY
QRY_PetPallerInDialogRange((CHARACTERGUID)_Char)
AND
DB_IsPlayer(_Player)
AND
NOT DB_QRYRTN_PetPallerInDialogRange(_)
AND
CharacterHasTalent(_Player,"AnimalEmpathy",1)
AND
QRY_SpeakerIsAvailableAndInDialogRange(_Player,_Char)
THEN
DB_QRYRTN_PetPallerInDialogRange(_Player);

QRY
QRY_PetPallerInDialogRange((CHARACTERGUID)_Char)
THEN
DB_NOOP(1);
//END_REGION
EXITSECTION

ENDEXITSECTION
