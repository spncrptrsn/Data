Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
PROC
Proc_ArenaIncreaseTeamSize((INTEGER)_TeamID)
AND
NOT DB_ArenaTeamNumberOfChampions(_TeamID,_)
THEN
DB_ArenaTeamNumberOfChampions(_TeamID,0);

PROC
Proc_ArenaIncreaseTeamSize((INTEGER)_TeamID)
AND
DB_ArenaTeamNumberOfChampions(_TeamID,_Size)
AND
IntegerSum(_Size,1,_NewSize)
THEN
NOT DB_ArenaTeamNumberOfChampions(_TeamID,_Size);
DB_ArenaTeamNumberOfChampions(_TeamID,_NewSize);

PROC
Proc_ArenaSetupDeathmatch()
AND
DB_ArenaTeams(_TeamID)
THEN
DB_Arena_DeathMatchCounter(_TeamID,0);

IF
CharacterDied(_Champion)
AND
DB_ArenaTeamChampions((CHARACTERGUID)_Champion,(INTEGER)_TeamID)
AND
DB_Arena_DeathMatchCounter(_TeamID,_Int)
AND
IntegerSum(_Int,1,_NewCount)
THEN
NOT DB_Arena_DeathMatchCounter(_TeamID,_Int);
DB_Arena_DeathMatchCounter(_TeamID,_NewCount);
Proc_ArenaCheckTeamDeathMatchLose((INTEGER)_TeamID);

IF
CharacterResurrected(_Champion)
AND
DB_ArenaTeamChampions(_Champion,_TeamID)
AND
DB_Arena_DeathMatchCounter(_TeamID,_Int)
AND
IntegerSubtract(_Int,1,_NewCount)
AND
_NewCount >= 0
THEN
NOT DB_Arena_DeathMatchCounter(_TeamID,_Int);
DB_Arena_DeathMatchCounter(_TeamID,_NewCount);

PROC
Proc_ArenaCheckTeamDeathMatchLose((INTEGER)_TeamID)
AND
DB_Arena_DeathMatchCounter(_TeamID,_Counter)
AND
DB_ArenaTeamNumberOfChampions(_TeamID,_Counter)
THEN
DB_ArenaTeamLostMatch(_TeamID);
Proc_ArenaShowTeamLostMessage(_TeamID,"LostDeathMatch");
Proc_StartArenaCheckTimer();

PROC
Proc_StartArenaCheckTimer()
AND
DB_ArenaTimerAlreadyStarted(1)
THEN
TimerCancel("Timer_ArenaCheckVictory");
TimerLaunch("Timer_ArenaCheckVictory",1500);

PROC
Proc_StartArenaCheckTimer()
AND
NOT DB_ArenaTimerAlreadyStarted(1)
THEN
DB_ArenaTimerAlreadyStarted(1);
TimerLaunch("Timer_ArenaCheckVictory",1500);

IF
TimerFinished("Timer_ArenaCheckVictory")
THEN
NOT DB_ArenaTimerAlreadyStarted(1);
Proc_ArenaCheckLastTeamExist();

PROC
Proc_ArenaCheckLastTeamExist()
AND
SysCount("DB_ArenaTeams",1,_NumberOfTeams)
AND
SysCount("DB_ArenaTeamLostMatch",1,_NumberOfLoosers)
AND
IntegerSubtract(_NumberOfTeams,_NumberOfLoosers,1)
AND
DB_ArenaTeams(_TeamID)
AND
NOT DB_ArenaTeamLostMatch(_TeamID)
AND
NOT DB_ArenaVictoriousTeam(_TeamID)
AND
DB_Arena_DeathMatchCounter(_TeamID,_DeadCount)
AND
DB_ArenaTeamNumberOfChampions(_TeamID,_Counter)
AND
_DeadCount < _Counter
THEN
DB_ArenaVictoriousTeam(_TeamID);

PROC
Proc_ArenaCheckLastTeamExist()
AND
SysCount("DB_ArenaTeams",1,_Count)
AND
SysCount("DB_ArenaTeamLostMatch",1,_Count)
THEN
ShowArenaResult(-1);
Proc_RestartArena();
SetCameraDistanceOverride(19.0);

IF
DB_ArenaVictoriousTeam(_TeamID)
THEN
Proc_ShowVictoryAnimation(_TeamID);
TimerLaunch("Timer_ArenaEnd",5000);
Proc_ArenaShowVictoryMessage((INTEGER)_TeamID,"WinDeathMatch");

IF
DB_ArenaTeamLostMatch(_TeamID)
AND
DB_ArenaTeamChampions(_Champion,_TeamID)
THEN
DebugText(_Champion,"LostDeathMatch");
CharacterFreeze(_Champion);

IF
DB_ArenaVictoriousTeam((INTEGER)_ID)
AND
DB_ArenaTeamChampions(_Champion,_ID)
THEN
CharacterSetImmortal(_Champion,1);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ArenaModWrapper"
