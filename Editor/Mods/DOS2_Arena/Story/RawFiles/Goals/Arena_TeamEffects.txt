Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_ArenaTeamColours(0, "TEAM_BLUE", "TEAM_BLUE_ACTIVE");
DB_ArenaTeamColours(1, "TEAM_RED", "TEAM_RED_ACTIVE");
DB_ArenaTeamColours(2, "TEAM_YELLOW", "TEAM_YELLOW_ACTIVE");
DB_ArenaTeamColours(3, "TEAM_GREEN", "TEAM_GREEN_ACTIVE");


KBSECTION
IF
DB_ArenaTeamChampions(_Champion,_TeamID)
AND
DB_ArenaTeamColours(_TeamID, _Status, _)
AND
NOT DB_Dead(_Champion)
THEN
ApplyStatus(_Champion, _Status, -1.0, 1);

IF
CharacterEnteredRegion(_Character, _)
AND
CharacterIsSummon(_Character, 1)
AND
CharacterGetOwner(_Character, _Champion)
AND
DB_ArenaTeamChampions(_Champion,_TeamID)
AND
DB_ArenaTeamColours(_TeamID, _Status, _)
THEN
ApplyStatus(_Character, _Status, -1.0, 1);

IF
ObjectTurnStarted(_Character)
AND
DB_ArenaTeamColours(_, _Inactive, _Active)
AND
HasActiveStatus(_Character, _Inactive, 1)
THEN
RemoveStatus(_Character, _Inactive);
ApplyStatus(_Character, _Active, -1.0, 1);

IF
ObjectTurnEnded(_Character)
AND
DB_ArenaTeamColours(_, _Inactive, _Active)
AND
HasActiveStatus(_Character, _Active, 1)
THEN
RemoveStatus(_Character, _Active);
ApplyStatus(_Character, _Inactive, -1.0, 1);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ArenaModWrapper"
