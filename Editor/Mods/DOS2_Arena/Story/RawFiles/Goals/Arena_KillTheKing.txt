Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_ArenaKillTheKingStatusTrack("SNEAKING");
DB_ArenaKillTheKingStatusTrack("INVISIBLE");

KBSECTION
PROC
Proc_ArenaSetupKillTheKing()
AND
DB_ArenaTeams(_TeamID)
THEN
DB_ArenaTeamSize(_TeamID,0);
Proc_ArenaCalculateChampionsInTeam(_TeamID);
Proc_ArenaChooseKing(_TeamID);

PROC
Proc_ArenaCalculateChampionsInTeam((INTEGER)_TeamID)
AND
DB_ArenaTeamChampions(_Champion,_TeamID)
THEN
Proc_ArenaAddIndexedChampion(_Champion,_TeamID);

PROC
Proc_ArenaAddIndexedChampion((CHARACTERGUID)_Champion,(INTEGER)_TeamID)
AND
DB_ArenaTeamSize(_TeamID,_Size)
AND
IntegerSum(_Size,1,_NewSize)
THEN
DB_ArenaTeamChampionIndex(_Champion,_TeamID,_Size);
NOT DB_ArenaTeamSize(_TeamID,_Size);
DB_ArenaTeamSize(_TeamID,_NewSize);

PROC
Proc_ArenaChooseKing((INTEGER)_TeamID)
AND
DB_ArenaTeams(_TeamID)
AND
DB_ArenaTeamSize(_TeamID,_Size)
AND
Random(_Size,_Rnd)
AND
DB_ArenaTeamChampionIndex(_Champion,_TeamID,_Rnd)
AND
NOT DB_ArenaTeamKing(_TeamID,_)
THEN
DB_ArenaTeamKing(_TeamID,_Champion);
PROC_LoopEffect("RS3_FX_UI_King_01",_Champion,"ArenaKing","__ANY__","Dummy_FX");
SetTag(_Champion, "AI_PREFERRED_TARGET");

IF
CharacterDying(_Champion)
AND
DB_ArenaTeamKing(_TeamID,_Champion)
AND
DB_ArenaTeamChampions(_OtherChampion,_TeamID)
AND
_OtherChampion != _Champion
AND
CharacterIsDead(_OtherChampion,0)
THEN
CharacterFreeze(_OtherChampion);
CharacterDie(_OtherChampion,0,"None");

IF
CharacterDying(_Champion)
AND
DB_ArenaTeamKing(_TeamID,_Champion)
THEN
PROC_StopLoopEffect(_Champion,"ArenaKing");

IF
CharacterDied(_Champion)
AND
DB_ArenaTeamKing(_TeamID,_Champion)
THEN
NOT DB_ArenaTeamKing(_TeamID,_Champion);
Proc_ArenaShowTeamLostMessage(_TeamID,"LostKillTheKing");
Proc_ArenaCheckKings();

PROC
Proc_ArenaCheckKings()
AND
SysCount("DB_ArenaTeamKing",2,1)
AND
DB_ArenaTeamKing(_TeamID,_Champion)
THEN
DB_ArenaVictoriousTeam(_TeamID);
PROC_StopLoopEffect(_Champion,"ArenaKing");
Proc_ShowVictoryAnimation(_TeamID);
Proc_ArenaShowVictoryMessage(_TeamID,"WonKillTheKing");
TimerLaunch("Timer_ArenaEnd",5000);

IF
DB_ArenaVictoriousTeam(_TeamID)
AND
DB_ArenaTeamChampions(_Champion,_TeamID)
THEN
ProcSetInvulnerable(_Champion,1);
CharacterSetHitpointsPercentage(_Champion,100.0);
CharacterSetMagicArmorPercentage(_Champion,100.0);
CharacterSetArmorPercentage(_Champion,100.0);

IF
CharacterStatusApplied(_Champion,_Status,_)
AND
DB_ArenaKillTheKingStatusTrack(_Status)
AND
DB_ArenaTeamKing(_,_Champion)
THEN
PROC_StopLoopEffect(_Champion,"ArenaKing");

IF
CharacterStatusRemoved(_Champion,_Status,_)
AND
DB_ArenaKillTheKingStatusTrack(_Status)
AND
DB_ArenaTeamKing(_,_Champion)
THEN
PROC_LoopEffect("RS3_FX_UI_King_01",_Champion,"ArenaKing","__ANY__","Dummy_FX");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ArenaModWrapper"
