Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
PROC
Proc_ShowVictoryAnimation(_)
AND
DB_CombatCharacters(_Character, _)
THEN
LeaveCombat(_Character);
CharacterFreeze(_Character);

PROC
Proc_ShowVictoryAnimation((INTEGER)_TeamID)
AND
DB_ArenaTeamChampions(_Champion,_TeamID)
AND
NOT DB_Dead(_Champion)
THEN
PlayAnimation(_Champion,"Victory_01");
PlayAnimation(_Champion,"Victory_02");
PlayEffect(_Champion,"RS3_FX_GP_ScriptedEvent_SoulShout_01");

IF
TimerFinished("Timer_ArenaEnd")
AND
DB_ArenaVictoriousTeam(_TeamID)
THEN
ShowArenaResult(_TeamID);
Proc_RestartArena();
SetCameraDistanceOverride(19.0);

PROC
Proc_ArenaShowVictoryMessage((INTEGER)_TeamID,(STRING)_Mode)
AND
DB_ArenaTeamChampions(_Champion,_TeamID)
THEN
CharacterEnteredSubRegion(_Champion,_Mode);

PROC
Proc_ArenaShowTeamLostMessage((INTEGER)_TeamID,(STRING)_Mode)
AND
DB_ArenaTeamChampions(_Champion,_TeamID)
THEN
CharacterPlayHUDSound(_Champion,"Fight_End_Victory");
CharacterEnteredSubRegion(_Champion,_Mode);

IF 
CharacterKilledBy(_Defender,_Attacker,_AttackerOwner)
AND
DB_ArenaTeamChampions(_Defender,_TeamID)
AND
DB_ArenaTeamChampions(_AttackerOwner,_OtherTeamID)
AND
_TeamID != _OtherTeamID
THEN 
DisplayText(_Attacker,"Arena_KillingBlow");


IF 
CharacterKilledBy(_Defender,_Attacker,_AttackerOwner)
AND
DB_ArenaTeamChampions(_Defender,_TeamID)
AND
DB_ArenaTeamChampions(_AttackerOwner,_OtherTeamID)
AND
_TeamID != _OtherTeamID
THEN
DB_ArenaTeamChampionsKilledBy(_Defender, _AttackerOwner);

//For the case where the character died by e.g. teleporting them into an unowned surface.
IF
CharacterDied(_Defender)
AND
DB_ArenaTeamChampions(_Defender,_TeamID)
AND
NOT DB_ArenaTeamChampionsKilledBy(_Defender, _)
AND
DB_CombatCharacters(_Defender, _ID)
AND
CombatGetActiveEntity(_ID, _Attacker)
AND
ObjectIsCharacter(_Attacker, 1)
AND
CharacterIsSummon((CHARACTERGUID)_Attacker, 1)
AND
CharacterGetOwner((CHARACTERGUID)_Attacker, _AttackerOwner)
AND
DB_ArenaTeamChampions(_AttackerOwner,_OtherTeamID)
AND
_TeamID != _OtherTeamID
THEN 
DisplayText(_Attacker,"Arena_KillingBlow");
DB_ArenaTeamChampionsKilledBy(_Defender, _AttackerOwner);

IF
CharacterDied(_Defender)
AND
DB_ArenaTeamChampions(_Defender,_TeamID)
AND
NOT DB_ArenaTeamChampionsKilledBy(_Defender, _)
AND
DB_CombatCharacters(_Defender, _ID)
AND
CombatGetActiveEntity(_ID, _Attacker)
AND
ObjectIsCharacter(_Attacker, 1)
AND
DB_ArenaTeamChampions((CHARACTERGUID)_Attacker,_OtherTeamID)
AND
_TeamID != _OtherTeamID
THEN 
DisplayText(_Attacker,"Arena_KillingBlow");
DB_ArenaTeamChampionsKilledBy(_Defender, _Attacker);

IF
CharacterDied(_Defender)
AND
DB_ArenaTeamChampionsKilledBy(_Defender, _Attacker)
THEN
ApplyStatus(_Defender, "EXPLODE", -1.0, 1);
ApplyStatus(_Attacker, "INFUSED", 0.0, 1);
PlayEffect(_Attacker, "RS3_FX_GP_Status_SourceInfused_Hit_01");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ArenaModWrapper"
