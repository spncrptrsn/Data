Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_CharacterCreationDummy((CHARACTERGUID)CHARACTERGUID_S_GLO_CharacterCreationDummy_001_da072fe7-fdd5-42ae-9139-8bd4b9fca406);
DB_CharacterCreationDummy((CHARACTERGUID)S_GLO_CharacterCreationDummy_002_361dacdc-4135-4d3f-a9a2-3cad46ca246a);
DB_CharacterCreationDummy((CHARACTERGUID)S_GLO_CharacterCreationDummy_003_dded8c22-b28e-45c1-a074-eb0954602c8a);
DB_CharacterCreationDummy((CHARACTERGUID)S_GLO_CharacterCreationDummy_004_5f93cae7-6c10-4da1-b9a5-0efafc168c8e);

DB_GenericOrigins((CHARACTERGUID)S_Player_GenericOrigin_7b6c1f26-fe4e-40bd-a5d0-e6ff58cef4fe);
DB_GenericOrigins((CHARACTERGUID)S_Player_GenericOrigin2_c451954c-73bf-46ce-a1d1-caa9bbdc3cfd);
DB_GenericOrigins((CHARACTERGUID)S_Player_GenericOrigin3_41a06985-7851-4c29-8a78-398ccb313f39);
DB_GenericOrigins((CHARACTERGUID)S_Player_GenericOrigin4_41a594ed-b768-4289-9f17-59f701cc6910);

DB_CampaignOrigins((CHARACTERGUID)CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f);
DB_CampaignOrigins((CHARACTERGUID)CHARACTERGUID_S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978);
DB_CampaignOrigins((CHARACTERGUID)CHARACTERGUID_S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295);
DB_CampaignOrigins((CHARACTERGUID)CHARACTERGUID_S_Player_RedPrince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f);
DB_CampaignOrigins((CHARACTERGUID)CHARACTERGUID_S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c);
DB_CampaignOrigins((CHARACTERGUID)CHARACTERGUID_S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb);
KBSECTION
IF
GMCampaignModeStarted("Prepare")
AND
DB_CharacterCreationDummy(_Dummy)
THEN
CharacterMakeNPC(_Dummy);
SetOnStage(_Dummy,0);

IF
GMCampaignModeStarted("Prepare")
AND
DB_GenericOrigins(_Origin)
THEN
CharacterMakeNPC(_Origin);
SetOnStage(_Origin,0);

IF
GMCampaignModeStarted("Prepare")
AND
DB_CampaignOrigins(_Origin)
AND
ObjectExists(_Origin,1)
THEN
CharacterMakeNPC(_Origin);
SetOnStage(_Origin,0);


IF
GMCampaignModeStarted("Play")
THEN
NOT DB_GLO_GMCharacterCreation_InitialSetupDone(1);
PROC_GLO_GMCharacterCreation_SetGMChar();

PROC
PROC_GLO_GMCharacterCreation_SetGMChar()
AND
CharacterGetHostCharacter(_GameMasterChar)
AND
CharacterGameMaster(_GameMasterChar,1)
AND
CharacterGetReservedUserID(_GameMasterChar,_GM)
AND
NOT DB_GameMasterCharacter(_GM,_)
THEN
DB_GameMasterCharacter(_GM,_GameMasterChar);
TeleportTo(_GameMasterChar,S_CharOriginDest_c9c5e1d7-1998-4d4e-aacb-3970e8823674);

IF
GMCampaignModeStarted("Play")
AND
DB_GameMasterCharacter(_GM,_GameMasterChar)
AND
GameMasterAddToCharacterCreation(_GameMasterChar,0,_Result)
THEN
DB_GameMasterCharacterCreationStarted(_Result);

IF
CharacterCreationStarted(_)
AND
DB_GLO_GMCharacterCreation_InitialSetupDone(1)
AND
NOT DB_GameMasterCharacterCreationStarted(_)
THEN
PROC_GLO_GMCharacterCreation_SetGMChar();

IF
CharacterCreationStarted(_)
AND
DB_GameMasterCharacter(_GM,_GameMasterChar)
AND
NOT DB_GameMasterCharacterCreationStarted(_)
AND
GameMasterAddToCharacterCreation(_GameMasterChar,0,1)
THEN
DB_GameMasterCharacterCreationStarted(1);

IF
DB_GameMasterCharacterCreationStarted(1)
AND
DB_CharacterCreationDummy(_Dummy)
THEN
SetOnStage(_Dummy,1);
TeleportTo(_Dummy,S_CharOriginDest_c9c5e1d7-1998-4d4e-aacb-3970e8823674);
CharacterMakePlayer(_Dummy, NULL_00000000-0000-0000-0000-000000000000);

IF
DB_GameMasterCharacterCreationStarted(1)
AND
DB_GenericOrigins((CHARACTERGUID)_Org)
THEN
SetOnStage(_Org,1);
TeleportTo(_Org,S_CharOriginDest_c9c5e1d7-1998-4d4e-aacb-3970e8823674);
CharacterMakePlayer(_Org, NULL_00000000-0000-0000-0000-000000000000);

//Copy the campaign characters (IF they exist) to the char creation level
IF
DB_GameMasterCharacterCreationStarted(1)
AND
DB_CampaignOrigins((CHARACTERGUID)_Orig)
AND
ObjectExists(_Orig,1)
THEN
SetOnStage(_Orig,1);
TeleportTo(_Orig,S_CharOriginDest_c9c5e1d7-1998-4d4e-aacb-3970e8823674);
CharacterMakePlayer(_Orig, NULL_00000000-0000-0000-0000-000000000000);

IF
UserDisconnected(_,_,_Profile)
AND
DB_GM_SelectedCharacter(_Profile,_Player)
THEN
NOT DB_GM_SelectedCharacter(_Profile,_Player);

IF
CharacterSelectedInCharCreation(_Char,_User)
AND
GetUserProfileID(_User,_Profile)
THEN
DB_GM_SelectedCharacter(_Profile,_Char);

IF
CharacterCreationFinished((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
AND
DB_GameMasterCharacterCreationStarted(1)
AND
DB_GenericOrigins(_Origin)
AND
NOT DB_GM_SelectedCharacter(_,_Origin)
THEN
CharacterRemoveFromParty(_Origin);
CharacterMakeNPC(_Origin);
SetOnStage(_Origin,0);

IF
CharacterCreationFinished((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
AND
DB_GameMasterCharacterCreationStarted(1)
AND
DB_CampaignOrigins(_Origin)
AND
ObjectExists(_Origin,1)
AND
NOT DB_GM_SelectedCharacter(_,_Origin)
THEN
CharacterRemoveFromParty(_Origin);
CharacterMakeNPC(_Origin);
SetOnStage(_Origin,0);

IF
CharacterCreationFinished((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
AND
DB_GameMasterCharacterCreationStarted(1)
AND
DB_CharacterCreationDummy(_Dummy)
THEN
CharacterRemoveFromParty(_Dummy);
CharacterMakeNPC(_Dummy);
SetOnStage(_Dummy,0);

IF
CharacterCreationFinished((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
THEN
NOT DB_GameMasterCharacterCreationStarted(1);

IF
CharacterCreationFinished((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
THEN
CharacterTeleportPartiesToTrigger(TRIGGERGUID_S_GLO_WaitingSpot_a5b5a134-fe24-4199-aed0-c97d2d40d9de,"GM_GameStarted");

IF
StoryEvent((CHARACTERGUID)_Player,"GM_GameStarted")
THEN
DB_GLO_GMCharacterCreation_InitialSetupDone(1);
DB_IsPlayer(_Player);

//REGION Savegame loading
// GM mode completely re-initialises story every time you load a game. When we finish GM character creation,
// we set DB_GLO_GMCharacterCreation_InitialSetupDone(1). We also set it after a SavegameLoaded() event.

// This means that if reserved user IDs change while DB_GLO_GMCharacterCreation_InitialSetupDone(1) is not set,
// we are in the proces of loading a savegame or character creation is ongoing.
// If character creation was ongoing, the queued ID - character assignments will simply never be used (and be discarded
// when a savegame containing them gets loaded due to the reinitialisation)

IF
SavegameLoaded(_,_,_,_)
THEN
DB_GLO_GMCharacterCreation_InitialSetupDone(1);

IF
SavegameLoaded(_, _, _, _)
THEN
IterateParties("GLO_GM_CheckIsPlayer");

IF
StoryEvent((CHARACTERGUID)_Char, "GLO_GM_CheckIsPlayer")
AND
GetMultiplayerCharacter(_Char,_Char)
THEN
DB_IsPlayer(_Char);
//END_REGION

//REGION Joining after the GM loaded a previous savegame
// A GM can load a savegame in which there were two clients without those two clients
// being connected yet. In that case, we will get CharacterReservedUserIDChanged() events
// after DB_GLO_GMCharacterCreation_InitialSetupDone(1) has been set -> add the players to
// DB_IsPlayer() in this case as well.

// There's no need to remove characters from DB_IsPlayer() when they disconnect:
//  1) those characters are in fact still player-characters (just not controlled right now)
//  2) when reconnecting (or someone else connects), we'll just set DB_IsPlayer() again,
//     which won't do anything
IF
CharacterReservedUserIDChanged(_Char,_,_UserID)
AND
DB_GLO_GMCharacterCreation_InitialSetupDone(1)
AND
_UserID != -65536
AND
// This will fail for characters possessed by the game master, as the main game master
// character is different from the one they possess.
GetMultiplayerCharacter(_Char,_Char)
THEN
DB_IsPlayer(_Char);
ProcFixPlayerAlignments();
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "GMModWrapper"
